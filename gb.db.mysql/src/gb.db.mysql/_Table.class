' Gambas class file

Export
Create Static

Public Procedure Modify(Table As String, Action As String, Optional Symbol As String, Optional Definition As String)

  Action = Upper(Action)
  If (Not ((Action = "DROP") Or (Action = "ADD") Or (Action = "MODIFY") Or (Action = "CHANGE"))) Then Error.Raise("Invalid Action")
  modMain.$Query = "ALTER TABLE " & DB.Quote(Table) & " " & Action & " " & DB.Quote(Symbol)
  If Definition Then modMain.$Query &= " " & Definition
  modMain.RunQuery(modMain.$Query)

End

Public Procedure Add(Table As String, Optional Engine As String, Optional Charset As String)

  'Construc the table statement
  modMain.$Query = "CREATE TABLE " & DB.Quote(Table) & " ("
  modMain.$Query &= modMain.ArrayToString(modMain.$FieldDefinition, False)
  If modMain.$PrimaryKey Then modMain.$Query &= modMain.$PrimaryKey
  If modMain.$Index.Count Then modMain.$Query &= ", " & modMain.ArrayToString(modMain.$Index, False)
  If modMain.$ForeignKey.Count Then modMain.$Query &= ", " & modMain.ArrayToString(modMain.$ForeignKey, False)
  modMain.$Query &= ")"
  If Engine Then modMain.$Query &= " ENGINE = " & Engine
  If Charset Then modMain.$Query &= " DEFAULT CHARSET = " & Charset

  'Clears all the information from this table to be ready for the next one
  modMain.$FieldDefinition.Clear()
  modMain.$PrimaryKey = Null
  modMain.$ForeignKey.Clear()
  modMain.$Index.Clear()

  'Runs the query
  modMain.RunQuery(modMain.$Query)

End

Public Procedure Delete(Tables As String[], IfExists As Boolean)

  modMain.$Query = "DROP TABLE "
  If IfExists Then modMain.$Query &= "IF EXISTS "
  modMain.$Query &= modMain.ArrayToString(Tables, True)
  modMain.RunQuery(modMain.$Query)

End

Public Procedure LoadData(Table As String, File As String)

  modMain.$Query = "LOAD DATA LOCAL INFILE '" & File & "' INTO TABLE " & DB.Quote(Table)
  modMain.RunQuery(modMain.$Query)

End

Public Function Info(Table As String) As String

  modMain.$hResult = modMain.$Connection.Exec("SHOW CREATE TABLE " & DB.Quote(Table))
  Return modMain.$hResult["Create Table"]

End

Public Procedure Insert(Table As String, Fields As String[], Values As String[])

  Dim iCounter As Integer

  modMain.$Query = "INSERT INTO " & DB.Quote(Table)
  If Upper(Fields[0]) = "ALL" Then
    modMain.$Query &= " VALUES ("
  Else
    modMain.$Query &= " (" & modMain.ArrayToString(Fields, True) & ") VALUES ("
  Endif
  'If the Value is string type, we need to use the Apostrophe caracter
  For iCounter = 0 To Values.Count - 1
    If (IsNumber(Val(Values[iCounter]))) Then
      modMain.$Query &= Values[iCounter] & ", "
    Else
      If Upper(Values[iCounter]) = "NULL" Or Upper(Values[iCounter]) = "TRUE" Or Upper(Values[iCounter]) = "FALSE" Then
        modMain.$Query &= Values[iCounter] & ", "
      Else
        modMain.$Query &= "'" & Values[iCounter] & "', "
      Endif
    Endif
  Next
  modMain.$Query = Mid(modMain.$Query, 1, Len(modMain.$Query) - 2) 'Strips the last two characters
  modMain.$Query &= ")"
  modMain.RunQuery(modMain.$Query)

End
