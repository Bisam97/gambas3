' Gambas class file

Export
Create Static

Public Procedure Modify(Table As String, Action As String, Optional Symbol As String, Optional Definition As String)

  Action = Upper(Action)
  modMain.$Query = "ALTER TABLE `" & Table & "` "
  Select (Action)
    Case "DROP"
      modMain.$Query &= Action & " COLUMN `" & Symbol & "`"
    Case "ADD", "MODIFY", "CHANGE"
      modMain.$Query &= Action & " COLUMN `" & Symbol & "` " & Definition
    Case "CHARACTER SET"
      modMain.$Query &= "CONVERT TO CHARACTER SET " & Definition
    Case "ENGINE"
      modMain.$Query &= "ENGINE " & Definition
    Default
      Error.Raise("Invalid Action")
  End Select
  modMain.RunQuery(modMain.$Query)

End

Public Procedure Add(Table As String, Optional Engine As String, Optional Charset As String)

  'Construc the table statement
  If Not Charset Then Charset = modMain.$Connection.MySQL.Charset
  If Not Engine Then Engine = modMain.$Connection.MySQL.Engine
  modMain.$Query = "CREATE TABLE `" & Table & "` ("
  modMain.$Query &= modMain.ArrayToString(modMain.$FieldDefinition, False)
  If modMain.$PrimaryKey Then modMain.$Query &= modMain.$PrimaryKey
  If modMain.$Index.Count Then modMain.$Query &= ", " & modMain.ArrayToString(modMain.$Index, False)
  If modMain.$ForeignKey.Count Then modMain.$Query &= ", " & modMain.ArrayToString(modMain.$ForeignKey, False)
  modMain.$Query &= ") ENGINE = " & Engine & " DEFAULT CHARSET = " & Charset

  'Clears all the information from this table to be ready for the next one
  modMain.$FieldDefinition.Clear()
  modMain.$PrimaryKey = Null
  modMain.$ForeignKey.Clear()
  modMain.$Index.Clear()

  'Runs the query
  modMain.RunQuery(modMain.$Query)

End

Public Procedure Delete(Tables As String[], Optional IfExists As Boolean)

  If Not IfExists Then IfExists = False
  modMain.$Query = "DROP TABLE "
  If IfExists Then modMain.$Query &= "IF EXISTS "
  modMain.$Query &= modMain.ArrayToString(Tables, True)
  modMain.RunQuery(modMain.$Query)

End

Public Procedure LoadData(Table As String, File As String)

  modMain.RunQuery("LOAD DATA LOCAL INFILE '" & File & "' INTO TABLE `" & Table & "`")

End

Public Function Info(Table As String) As String

  Return modMain.$Connection.Exec("SHOW CREATE TABLE `" & Table & "`")!"Create Table"

End

Public Procedure Insert(Table As String, Fields As String[], Values As String[])

  Dim iCounter As Integer

  modMain.$Query = "INSERT INTO `" & Table & "`"
  If Upper(Fields[0]) = "ALL" Then
    modMain.$Query &= " VALUES ("
  Else
    modMain.$Query &= " (" & modMain.ArrayToString(Fields, True) & ") VALUES ("
  Endif
  'If the Value is string type, we need to use the Apostrophe caracter
  For iCounter = 0 To Values.Count - 1
    If (IsNumber(Val(Values[iCounter]))) Then
      modMain.$Query &= Values[iCounter] & ", "
    Else
      If Upper(Values[iCounter]) = "NULL" Or Upper(Values[iCounter]) = "TRUE" Or Upper(Values[iCounter]) = "FALSE" Then
        modMain.$Query &= Values[iCounter] & ", "
      Else
        modMain.$Query &= "'" & Values[iCounter] & "', "
      Endif
    Endif
  Next
  modMain.$Query = Mid(modMain.$Query, 1, Len(modMain.$Query) - 2) 'Strips the last two characters
  modMain.$Query &= ")"
  modMain.RunQuery(modMain.$Query)

End

Public Procedure Rename(Table As String, NewTable As String)

  modMain.RunQuery("RENAME TABLE `" & Table & "` TO `" & NewTable & "`")

End

Public Function Fields(Table As String, Optional Database As String) As String[]

  Dim asFields As New String[]

  If Not Database Then Database = modMain.$Connection.Name
  modMain.$hResult = modMain.$Connection.Exec("SHOW COLUMNS FROM `" & Database & "`.`" & Table & "`")
  For Each modMain.$hResult
    asFields.Add(modMain.$hResult!Field)
  Next
  Return asFields

End

Public Function Charset(Table As String, Optional Database As String) As String

  If Not Database Then Database = modMain.$Connection.Name
  Return modMain.$Connection.Exec("SELECT `a`.`CHARACTER_SET_NAME` FROM `information_schema`.`COLLATIONS` `a`, `information_schema`.`TABLES` `b` WHERE `b`.`TABLE_SCHEMA` = '" & Database & "' AND `b`.`TABLE_NAME` = '" & Table & "' AND `a`.`COLLATION_NAME` = `b`.`TABLE_COLLATION`")!CHARACTER_SET_NAME

End

Public Function Collation(Table As String, Optional Database As String) As String

  If Not Database Then Database = modMain.$Connection.Name
  Return modMain.$Connection.Exec("SELECT `TABLE_COLLATION` FROM `information_schema`.`TABLES` WHERE `TABLE_SCHEMA` = '" & Database & "' AND `TABLE_NAME` = '" & Table & "'")!TABLE_COLLATION

End

Public Function Engine(Table As String, Optional Database As String) As String

  If Not Database Then Database = modMain.$Connection.Name
  Return modMain.$Connection.Exec("SELECT `ENGINE` FROM `information_schema`.`TABLES` WHERE `TABLE_SCHEMA` = '" & Database & "' AND `TABLE_NAME` = '" & Table & "'")!ENGINE

End

Public Procedure Truncate(Table As String, Optional Database As String)

  If Not Database Then Database = modMain.$Connection.Name
  modMain.RunQuery("TRUNCATE TABLE `" & Database & "`.`" & Table & "`")

End
