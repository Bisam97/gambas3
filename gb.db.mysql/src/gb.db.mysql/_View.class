' Gambas class file

Export
Create Static

Public Procedure Modify(View As String, Statement As String)

  Add(View, Statement, True)

End

Public Procedure Add(View As String, Statement As String, Optional {Replace} As Boolean)

  If Not {Replace} Then {Replace} = False
  'TODO: Quote the Statement parameter
  modMain.$Query = "CREATE"
  If {Replace} Then modMain.$Query &= " OR REPLACE"
  modMain.$Query &= " VIEW `" & View & "` AS " & Statement
  modMain.RunQuery(modMain.$Query)

End

Public Procedure Delete(Views As String[], Optional IfExists As Boolean)

  If Not IfExists Then IfExists = False
  modMain.$Query = "DROP VIEW "
  If IfExists Then modMain.$Query &= "IF EXISTS "
  modMain.$Query &= modMain.ArrayToString(Views, True)
  modMain.RunQuery(modMain.$Query)

End

Public Function Info(View As String, Optional Database As String) As String

  If Not Database Then Database = modMain.$Connection.Name
  Return modMain.$Connection.Exec("SHOW CREATE VIEW `" & Database & "`.`" & View & "`")!"Create View"

End
