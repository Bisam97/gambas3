' Gambas class file

Export

Public _Driver As _DatabaseDriver

Property Read Count As Integer
Property Read List As String[]

Private $aTables As New String[]
Private $cTables As New Collection

Public Sub _get(Name As String) As Table

  Dim hTable As Table
  
  Try hTable = $cTables[Name]
  If Error Then 
    hTable = New Table
    hTable._Fill(_Driver, Name)
    $cTables[Name] = hTable
  Endif

  If Not hTable Then Error.Raise("Unknown table")
  Return hTable  
  
End

Public Sub _next() As Table
  
  Dim hTable As Table
  
  If IsNull(Enum.Index) Then Enum.Index = 0
  Try hTable = _get($aTables[Enum.Index])
  If hTable Then
    Inc Enum.Index
    Return hTable
  Else
    Enum.Stop
  Endif
  
End

Public Sub Exist(Name As String) As Boolean
  
  Return $cTables.Exist(Name)

End

Private Function Count_Read() As Integer

  Return $cTables.Count

End

Public Sub Add(Name As String, Optional Type As String) As Table
  
  Dim hTable As Table
  
  Main.CheckNameWith(Name, "table", ".")
  If Me.Exist(Name) Then Error.Raise("Table already exists")
  
  hTable = New Table
  hTable._Create(_Driver, Name)
  hTable.Type = Type
  
  $cTables[Name] = hTable
  $aTables.Add(Name)
  
  Return hTable
  
End

Public Sub Remove(Name As String)
  
  _Driver.DropTable(Name)
  Refresh()
  
End

Public Sub Refresh()

  Dim sTable As String
  
  $cTables.Clear
  $aTables = _Driver.GetTables()
  $aTables.ReadOnly = True
  For Each sTable In $aTables
    $cTables[sTable] = True
  Next
  
End

Private Function List_Read() As String[]

  Return $aTables

End
