' Gambas class file

Export

Property Read Children As String[]
Property Read _Introspection As String

Private $hAppIndex As Integer
Private $sObjectPath As String
Private $sInterface As String
Private $sIntrospection As String
Private $cSignature As New Collection

Public Sub _new((Application) As DBusApplication, ObjectPath As String, Optional Interface As String)
  
  Dim iPos, iPos2 As Integer
  
  $hAppIndex = Application.Index
  $sObjectPath = ObjectPath
  $sInterface = Interface
  
  $sIntrospection = Application._Introspect(ObjectPath)
  If Not $sIntrospection Then Error.Raise("Unknown object")
  
  If Interface Then
    iPos = InStr($sIntrospection, "<interface name=\"" & Interface & "\">")
    If iPos = 0 Then Error.Raise("Unknown interface")
    iPos2 = InStr($sIntrospection, "</interface>", iPos)
    If iPos2 = 0 Then Error.Raise("Introspection error")
    $sIntrospection = Mid$($sIntrospection, iPos, iPos2 - iPos)
  Endif
  
  'Debug $sIntrospection
  
End

Private Sub GetSignature(sSymbol As String) As String
  
  Dim sFind As String
  Dim iPos, iStart, iEnd As Integer
  Dim sSignIn, sSignOut As String
  Dim sDir, sType As String
  Dim sAccess As String
  Dim iPos2 As Integer
  
  sSignIn = $cSignature[sSymbol]
  If sSignIn Then Return sSignIn
  
  iPos = InStr($sIntrospection, " name=\"" & sSymbol & "\"", 1, gb.IgnoreCase)
  If iPos = 0 Then Error.Raise("Unknown symbol")
  
  iStart = RInStr($sIntrospection, "<", iPos)
  If iStart = 0 Then Error.Raise("Malformed introspection")
  iEnd = InStr($sIntrospection, ">", iStart)
  If iEnd = 0 Then Error.Raise("Malformed introspection")
  
  If Mid$($sIntrospection, iStart) Begins "<property " Then
    
    Try sType = Scan(Mid$($sIntrospection, iStart, iEnd - iStart), "*type=\"*\"*")[1]
    If Error Then Error.Raise("Malformed property")

    Try sAccess = Scan(Mid$($sIntrospection, iStart, iEnd - iStart), "*access=\"*\"*")[1]
    If Error Then Error.Raise("Malformed property")
    
    If sAccess = "read" Then
      sType &= ":r"
    Else If sAccess = "write" Then
      sType &= ":w"
    Else If sAccess = "readwrite" Then
      sType &= ":rw"
    Else
      Error.Raise("Malformed property")
    Endif
    
    sType = "P:" & sType
    
  Else If Mid$($sIntrospection, iStart) Begins "<method " Then
    
    If Mid$($sIntrospection, iEnd - 1, 2) <> "/>" Then
  
      iEnd = InStr($sIntrospection, "</method>", iStart)
      
      Do
        iPos = InStr($sIntrospection, "<arg ", iPos)
        If iPos = 0 Or If iPos > iEnd Then Break
        iPos2 = InStr($sIntrospection, ">", iPos + 1)
        If iPos2 = 0 Or If iPos2 > iEnd Then Break
        
        sDir = "in"
        Try sDir = Scan(Mid$($sIntrospection, iPos, iPos2 - iPos), "*direction=\"*\"*")[1]
        Try sType = Scan(Mid$($sIntrospection, iPos, iPos2 - iPos), "*type=\"*\"*")[1]
        If Error Then Break
        
        If sDir = "in" Then
          sSignIn &= sType
        Else
          sSignOut &= sType
        Endif
        
        iPos = iPos2
        
      Loop
      
    Endif
    
    sType = "M:" & sSignIn & ":" & sSignOut
    
  Else
    
    Error.Raise("Unknown symbol")
    
  Endif
  
  $cSignature[sSymbol] = sType
  Return sType
  
  sFind = "<property name=\"" & sSymbol & "\""
  iPos = InStr($sIntrospection, sFind, 1, gb.IgnoreCase)
  If iPos = 0 Then Goto METHOD
    
  iPos += Len(sFind)
  If Not $sInterface Then
    If InStr($sIntrospection, sFind, iPos, gb.IgnoreCase) Then Error.Raise("Ambiguous property")
  Endif

  iPos2 = InStr($sIntrospection, ">", iPos + 1)
  Try sType = Scan(Mid$($sIntrospection, iPos, iPos2 - iPos), "*type=\"*\"*")[1]
  If Error Then Error.Raise("Unknown property")
      
  Try sAccess = Scan(Mid$($sIntrospection, iPos, iPos2 - iPos), "*access=\"*\"*")[1]
  If Error Then Error.Raise("Malformed property")
  
  If sAccess = "read" Then
    sType &= ":r"
  Else If sAccess = "write" Then
    sType &= ":w"
  Else If sAccess = "readwrite" Then
    sType &= ":rw"
  Else
    Error.Raise("Malformed property")
  Endif
    
  sType = "P:" & sType
  $cSignature[sSymbol] = sType
  Return sType

METHOD:

  sFind = "<method name=\"" & sSymbol & "\""
  iPos = InStr($sIntrospection, sFind, 1, gb.IgnoreCase)
  If iPos = 0 Then Error.Raise("Unknown symbol")
  
  iPos += Len(sFind)
  If Not $sInterface Then
    If InStr($sIntrospection, sFind, iPos, gb.IgnoreCase) Then Error.Raise("Ambiguous method")
  Endif
  
  If LTrim(Mid$($sIntrospection, iPos, 1)) <> "/>" Then
  
    iEnd = InStr($sIntrospection, "</method>", iPos)
  
    Do
      iPos = InStr($sIntrospection, "<arg ", iPos)
      If iPos = 0 Or If iPos > iEnd Then Break
      iPos2 = InStr($sIntrospection, ">", iPos + 1)
      If iPos2 = 0 Or If iPos2 > iEnd Then Break
      
      sDir = "in"
      Try sDir = Scan(Mid$($sIntrospection, iPos, iPos2 - iPos), "*direction=\"*\"*")[1]
      Try sType = Scan(Mid$($sIntrospection, iPos, iPos2 - iPos), "*type=\"*\"*")[1]
      If Error Then Break
      
      If sDir = "in" Then
        sSignIn &= sType
      Else
        sSignOut &= sType
      Endif
      
      iPos = iPos2
      
    Loop
    
  Endif
  
  sSignIn = "M:" & sSignIn & ":" & sSignOut
  $cSignature[sSymbol] = sSignIn
  Return sSignIn
  
End

Public Sub _Invoke(sName As String, aArg As Variant[]) As Variant
  
  Dim aSign As String[]
  Dim hApp As DBusApplication
  Dim vVal As Variant
  Dim sSign As String
  Dim bProperty As Boolean
  Dim hVariant As DBusVariant
  
  hApp = DBus._ApplicationCache[$hAppIndex]
  
  sSign = GetSignature(sName)
  bProperty = Left(sSign, 2) = "P:"
  sSign = Mid$(sSign, 3)
  
  aSign = Split(sSign, ":")
  
  If bProperty Then 'Error.Raise("Unknown property")
  
    If IsNull(aArg) Or If aArg.Count = 0 Then
      'Debug "read property " & sName
      If InStr(aSign[1], "r") = 0 Then Error.Raise("Write-only property")
      aArg = New Variant[2]
      aArg[0] = $sInterface
      aArg[1] = sName
      Return hApp.Connection._CallMethod(hApp.Name, $sObjectPath, "org.freedesktop.DBus.Properties", "Get", "ss", "v", aArg)
    Else
      'Debug "write property " & sName
      If InStr(aSign[1], "w") = 0 Then Error.Raise("Read-only property")
      vVal = aArg[0]
      aArg = New Variant[3]
      aArg[0] = $sInterface
      aArg[1] = sName
      hVariant = New DBusVariant
      hVariant.Value = vVal
      aArg[2] = hVariant
      hApp.Connection._CallMethod(hApp.Name, $sObjectPath, "org.freedesktop.DBus.Properties", "Set", "ssv", "", aArg)
    Endif
  
  Else
  
    'Print GetSignature(Param.Name)
    Return hApp.Connection._CallMethod(hApp.Name, $sObjectPath, $sInterface, sName, aSign[0], aSign[1], aArg)
    
  Endif
  
End

Public Sub _unknown(...) As Variant
  
  Dim aArg As Variant[]
  
  If Param.Count Then aArg = Param.All
  Return _Invoke(Param.Name, aArg)
  
End

Public Sub _property() As Boolean
  
  'Return True
  'Debug Param.Name;; GetSignature(Param.Name)
  Return Left(GetSignature(Param.Name)) = "P"
  
End


' We do not use the cached introspection, because new objects may have appeared.
' But new interfaces or new methods are unlikely.

Private Function Children_Read() As String[]

  Dim hApp As DBusApplication
  
  hApp = DBus._ApplicationCache[$hAppIndex]
  $sIntrospection = hApp._Introspect($sObjectPath)
  Return _GetSymbols("node")

End

Private Function _Introspection_Read() As String

  Return $sIntrospection

End

Public Sub _GetSymbols(Optional Which As String) As String[]

  Dim hIntr As CIntrospection
  Dim aSym As New String[]
  Dim sName As String

  hIntr = New CIntrospection($sIntrospection)
  
  While Not hIntr.MoveNext()
    
    If Not hIntr.Open Then Continue
    If hIntr.Level <> 2 Then Continue
    If Which And If hIntr.Node <> Which Then Continue
    
    sName = hIntr.GetAttribute("name")
    If Not sName Then Continue
    If Not Which Then sName = Left(hIntr.Node) & ":" & sName
    aSym.Add(sName)
    
  Wend
  
  Return aSym

End
