' Gambas class file

Static Public Paths As String[]

Private $bMulti As Boolean
Private $bReadOnly As Boolean

Public Sub OpenFile(bMulti As Boolean) As Boolean
  
  $bMulti = bMulti
  $bReadOnly = True
  Return Not Me.ShowModal()
  
End

Public Sub SaveFile() As Boolean
  
  $bMulti = False
  $bReadOnly = False
  Return Not Me.ShowModal()
  
End


Public Sub Form_Open()

  ' DEBUG "Dialog.Title:";; Dialog.Title
  ' DEBUG "Dialog.Path:";; Dialog.Path

  Me.Title = Dialog.Title
  fchChoose.ReadOnly = $bReadOnly
  fchChoose.Multi = $bMulti
  'fchChoose.Root = "/"
  fchChoose.Filter = Dialog.Filter
  If Dialog.Path Then 
    fchChoose.SelectedPath = Dialog.Path
  Endif

End

Public Sub btnOK_Click()

  Dialog.Path = fchChoose.SelectedPath
  If fchChoose.Multi Then Paths = fchChoose.SelectedPaths
  If Not fchChoose.ReadOnly Then 
    If Exist(fchChoose.SelectedPath) Then 
      If Message.Warning(("This file already exists.\n\nDo you want to overwrite it?"), ("&Overwrite"), ("Cancel")) = 2 Then Return
    Endif 
  Endif
  Me.Close(True)

End

Public Sub btnCancel_Click()

  Me.Close

End

Public Sub fchChoose_Activate()

  btnOK.Value = True

End
