' Gambas class file

Export

Public Const _IsControl As Boolean = True
Public Const _IsVirtual As Boolean = True
Public Const _Group As String = "Special"
Public Const _Properties As String = "Name,Picture,Visible=False,PopupMenu{Menu},Tooltip"
Public Const _DefaultEvent As String = "Click"

Public Enum Horizontal, Vertical

Event Click
Event Scroll(Delta As Float, Orientation As Integer)
Event Menu

Property Name As String
Property Picture, Icon As Picture
Property Visible As Boolean
Property PopupMenu As String
Property Text, Tooltip As String

Static Private $iCount As Integer

Private $sName As String
Private $iId As Integer
Private $bVisible As Boolean
Private $hObject As DBusStatusIcon
Private $hIcon As Picture
Private $sMenu As String
Private $sText As String

Public Sub _new()
  
  $hObject = New DBusStatusIcon As "StatusIcon"
  Inc $iCount
  $iId = $iCount
  $sName = Application.Name 
  If $iId > 1 Then $sName &= "-" & CStr($iId)
  TrayIcons._All.Add(Me)
  
End

Private Sub GetServiceName() As String

  Return "org.kde.StatusNotifierItem-" & CStr(Application.Handle) & "-" & CStr($iId)

End

Public Sub Show()
  
  Dim sService As String
  
  If $bVisible Then Return
  
  sService = GetServiceName()
  
  DBus[sService].Register($hObject, "/StatusNotifierItem", ["org.kde.StatusNotifierItem"])
  
  DBus["org.kde.StatusNotifierWatcher"]["/StatusNotifierWatcher", "org.kde.StatusNotifierWatcher"].RegisterStatusNotifierItem(sService)
  
  $bVisible = True
  
End


Public Sub Hide()
  
  If Not $bVisible Then Return

  DBus[GetServiceName()].Unregister($hObject)
  $bVisible = False

End

Public Sub Delete()

  Dim iIndex As Integer
  
  Hide
  
  iIndex = TrayIcons._All.FindByRef(Me)
  If iIndex < 0 Then Return
  
  TrayIcons._All.Remove(iIndex)
  
End


Private Function Visible_Read() As Boolean

  Return $bVisible

End

Private Sub Visible_Write(Value As Boolean)

  If Value Then
    Show
  Else
    Hide
  Endif

End

Private Function Name_Read() As String

  Return $sName

End

Private Sub Name_Write(Value As String)

  $sName = Value

End

Private Function Picture_Read() As Picture

  Return $hIcon

End

Private Sub Picture_Write(Value As Picture)

  $hIcon = Value
  If $bVisible Then DBus[GetServiceName()].Raise($hObject, "org.kde.StatusNotifierItem.NewIcon")

End

Public Sub _Activate()
  
  Raise Click
  
End

Public Sub _ContextMenu(X As Integer, Y As Integer)
  
  Dim hMenu As Menu
  Dim hParent As Control
  
  Try hParent = Object.Parent(Me)
  If hParent Then hMenu = Main.FindMenu(hParent, $sMenu)
  
  If hMenu Then
    hMenu.Popup(X, Y)
  Else
    Raise Menu
  Endif
  
End

Private Function PopupMenu_Read() As String

  Return $sMenu

End

Private Sub PopupMenu_Write(Value As String)

  $sMenu = Value

End

Public Sub _Scroll(Delta As Integer, bVertical As Boolean)
  
  Raise Scroll(Delta / 120, If(bVertical, Vertical, Horizontal))
  
End

Private Function Text_Read() As String

  Return $sText

End

Private Sub Text_Write(Value As String)

  $sText = Value
  If $bVisible Then DBus[GetServiceName()].Raise($hObject, "org.kde.StatusNotifierItem.NewTooltip")

End
