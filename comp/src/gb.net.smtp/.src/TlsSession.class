' Gambas class file

Inherits SmtpSession

Property Read Connected As Boolean

Private $hProcess As Process

Public Sub Connect(hClient As SmtpClient, sHost As String, iPort As Integer)

  If Not sHost Then sHost = "localhost"
  If iPort = 0 Then iPort = 465
  
  Super.Connect(hClient, sHost, iPort)

  $hProcess = Exec [SmtpSession.GetOpenSSLPath(), "s_client", "-quiet", "-starttls", "smtp", "-connect", sHost & ":" & iPort] For Read Write As "Client"
  $hProcess.Blocking = True
  $hProcess.EndOfLine = gb.Windows
  
  Me.Stream = $hProcess

End

Public Sub Disconnect()

  Dim I As Integer
  
  $hProcess.Kill()

  ' Wait for the openssl process death
  For I = 1 To 20
    If Not $hProcess Then Break
    Wait
    Sleep 0.05
  Next

End

Public Sub Client_Kill()

  $hProcess = Null

End

Private Function Connected_Read() As Boolean

  If $hProcess And If $hProcess.State = Process.Running Then Return True

End
