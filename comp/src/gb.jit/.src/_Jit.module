' Gambas module file

Export

Private $sCompiler As String

Private Sub FindCompiler()

  $sCompiler = System.Find("gcc")

End

Public Sub Compile() As String

  Dim sFile As String
  Dim sDir As String
  Dim sName As String
  Dim sPath As String
  Dim hFile As File
  Dim sResult As String
  Dim sPathO As String
  Dim sPathSO As String
  Dim fTime As Float
  
  fTime = Timer
  
  sName = Component.FindFromPath("..")
  sDir = File.Dir(Temp$()) &/ "jit"
  Try Mkdir sDir
  sPath = sDir &/ sName & ".c"
  
  Error "gb.jit: generating "; sPath
  
  hFile = Open sPath For Output Create
  
  
  Print #hFile, "#define NO_CONFIG_H"
  Print #hFile, File.Load("gambas.h");
  Print #hFile, File.Load("jit.h");
  Print #hFile, File.Load("gb.jit.h");

  Print #hFile, "GB_INTERFACE *GB_PTR;"
  Print #hFile, "#define GB (*GB_PTR)"

  Print #hFile, "JIT_INTERFACE *JIT_PTR;"
  Print #hFile, "#define JIT (*JIT_PTR)"
  
  sDir = "../.jit"
  For Each sFile In Dir(sDir, "*.c")
    Print #hFile, File.Load(sDir &/ sFile);
  Next
  
  Close #hFile
  
  sPathO = File.SetExt(sPath, "o")
  sPathSO = File.SetExt(sPath, "so")
  
  FindCompiler
  
  Error "gb.jit: compiling to "; sPathO
  
  'gcc -c -fPIC -o foo.o foo.c
  'Exec [$sCompiler, "-c", "-fPIC", "-o", File.SetExt(sPath, "o"), sPath] To sResult
  Shell $sCompiler & " -c -fPIC -o " & Shell(sPathO) & " " & Shell(sPath) & " 2>&1" To sResult
  If Not Exist(sPathO) Then 
    Error "gb.jit: warning: unable to compile JIT code:"
    Error sResult
    Return
  Endif

  Error "gb.jit: linking to "; sPathSO
  
  'gcc -shared -o libfoo.so foo.o
  'Exec [$sCompiler, "-shared", "-o", File.SetExt(sPath, "so"), File.SetExt(sPath, "o")] To sResult
  Shell $sCompiler & " -shared -o " & Shell(sPathSO) & " " & Shell(sPathO) & " 2>&1" To sResult
  If Not Exist(sPathSO) Then 
    Error "gb.jit: warning: unable to link JIT code:"
    Error sResult
    Return
  Endif
  
  Error "gb.jit: done in "; Format(Timer - fTime, "0.000"); "s"
  
  Return File.SetExt(sPath, "so")
  
End
