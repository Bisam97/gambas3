' Gambas class file

Export
Inherits UserControl

Public Const _Properties As String = "*,Border{Border.*}"
Public Const _Group As String = "View"

Private $hCenter As MapPoint
Private $hMap As New Map As "Map"
Private $hView As DrawingArea
Private $hPan As New Panel(Me)
Private hWatch As New Watcher(Me) As "Watcher"
Private $pCurCenterPx As Point
Private $iX As Integer
Private $iY As Integer
Property Lock As Boolean
Property Border As Integer
Private $bLock As Boolean
Property Read {Map} As Map
Private $hZoomBuffer As New Image
Private $bZoomEffect As Boolean
Private $tmrZoom As New Timer As "tmrZoom"
Private $fZoomEffect As Float
Private $ZX As Integer
Private $ZY As Integer
Private $iZoomWay As Integer
Private fZoomStep As Float
Event Click
Event Draw

Public Sub _new()
  
  $hPan.Arrangement = Arrange.Fill
  $hView = New DrawingArea($hPan) As "View"
  Me.Proxy = $hView
  $hMap.resize($hView.ClientW, $hView.ClientH)
  $hmap._ShowWithEffect = True
  $hView.Tracking = True
  $hView.Padding = True
  $hView.Background = Color.DarkGray
  $tmrZoom.Delay = 30
  
  '$hmap.Padding = 256
  
End

Public Sub SetCenter(hMapPoint As MapPoint)
  
  $hCenter = hMapPoint
  
End

Private Function Map_Read() As Map
  
  Return $hMap
  
End

Public Sub View_Draw()
  
  'Dim $fZoomEffect As Integer
  
  If Not $bZoomEffect Then 
    'Print $iZoomWay
    If $iZoomWay <> 0 Then 
    Paint.DrawImage($hZoomBuffer, 0, 0)
    Endif
    $hMap.Draw
    Raise Draw
  Else
    If $iZoomWay = 1 Then
      'Paint.Scale($fZoomEffect, $fZoomEffect)
      'Paint.Translate(- $fZoomEffect * 1, - $fZoomEffect * 1)
      Paint.DrawImage($hZoomBuffer, ($ZX - $ZX * $fZoomEffect), ($ZY - $ZY * $fZoomEffect), $hZoomBuffer.Width * $fZoomEffect, $hZoomBuffer.Height * $fZoomEffect)
      $fZoomEffect += 0.1
      If $fZoomEffect < 2 Then 
        $tmrZoom.Trigger
      Else
        $bZoomEffect = False
        Draw.Begin($hZoomBuffer)
        Paint.DrawImage($hZoomBuffer, ($ZX - $ZX * $fZoomEffect), ($ZY - $ZY * $fZoomEffect), $hZoomBuffer.Width * $fZoomEffect, $hZoomBuffer.Height * $fZoomEffect)
        Draw.End
        $fZoomEffect = 1
        $tmrZoom.Trigger
      Endif
    Else
      Paint.DrawImage($hZoomBuffer, (Paint.Width - $hZoomBuffer.Width * $fZoomEffect) / 2, (Paint.Height - $hZoomBuffer.Height * $fZoomEffect) / 2, $hZoomBuffer.Width * $fZoomEffect, $hZoomBuffer.Height * $fZoomEffect)
      $fZoomEffect -= 0.04
      If $fZoomEffect > 0.5 Then 
        $tmrZoom.Trigger
      Else
        $bZoomEffect = False
        Draw.Begin($hZoomBuffer)
        Paint.DrawImage($hZoomBuffer, (Paint.Width - $hZoomBuffer.Width * $fZoomEffect) / 2, (Paint.Height - $hZoomBuffer.Height * $fZoomEffect) / 2, $hZoomBuffer.Width * $fZoomEffect, $hZoomBuffer.Height * $fZoomEffect)
        Draw.End
        $fZoomEffect = 1
        $tmrZoom.Trigger
      Endif
    Endif
  Endif
  
End

' Public Sub Watcher_Resize()
'   
'   $hMap.Resize($hView.ClientW, $hView.ClientH)
'   $hView.Refresh
'   'Print $hView.ClientH
' End
' 
' Public Sub Watcher_Move()
'   
'   $hMap.Resize($hView.ClientW, $hView.ClientH)
'   $hView.Refresh
'   'Print $hView.ClientH
' End

Public Sub View_MouseWheel()
  
  Dim hpix As Point
  'Put the current map in buffer
  $ZX = Mouse.X 
  $ZY = Mouse.Y 
  Draw.Begin($hZoomBuffer)
    Draw.FillRect(0, 0, Draw.Width, Draw.Height, Color.Transparent)
    $hmap.Draw
  Draw.End
  
  $iZoomWay = Mouse.Delta
  
  $hMap.Zoom += Mouse.Delta
  
  hPix = New Point($hMap.PixelBox.X + Mouse.X, $hMap.PixelBox.Y + Mouse.Y)
  
  If Mouse.Delta > 0 Then $hMap.Center = Geo.PixelToMapPoint(hpix, $hmap.Zoom) 'Mercator.MetersToMapPointFP(Mercator.PixelsToMetersP(hpix, $hMap.Zoom))
  
  If $hmap.Zoom > 1 And If $hmap.Zoom < 18 Then $bZoomEffect = True
  $hView.Refresh
  
End

Public Sub Map_Refresh()
  
  $hView.Refresh
  
End

Public Sub View_MouseDown()
  
  $hView.Mouse = Mouse.Pointing
  $iX = Mouse.X 
  $iY = Mouse.Y 
  $pCurCenterPx = Geo.MapPointToPixel($hmap.Center, $hmap.Zoom) 'Mercator.MetersToPixelsFP(Mercator.MapPointToMetersLL($hMap.Center), $hMap.Zoom)
  
  Raise MouseDown
  
End

Public Sub View_MouseUp()
  
  Last.Mouse = Mouse.Default
  
  Raise MouseUp
  'Raise MouseUP
  
End

Public Sub View_MouseMove()
  
  Dim hPix As Point
  
  If Mouse.Left And Not $bLock Then
    
    hPix = New Point($pCurCenterPx.X, $pCurCenterPx.Y)
    
    hPix.X += $ix - Mouse.X
    hPix.Y += $iY - Mouse.Y
    $hmap.Center = Geo.PixelToMapPoint(hpix, $hmap.Zoom) 'Mercator.MetersToMapPointFP(Mercator.PixelsToMetersP(hPix, $hMap.Zoom))
    $iZoomWay = 0
    'Print hpix.X, hpix.y
    '$hMapView.CenterLat = hMapPoint.Lat
    '$hMapView.CenterLon = hMapPoint.Lon
    
    $hView.Refresh
  Endif
  
  'Refresh
  'DispatchEvents("_MouseMove")
  Raise MouseMove
  
End

Public Sub View_Arrange()
  
  $hMap.Resize($hView.ClientW, $hView.ClientH)
  $hZoomBuffer.Resize($hView.ClientW, $hView.ClientH)
  $iZoomWay = 0
  $hmap.Refresh
  $hView.Refresh
  
End

Private Function Lock_Read() As Boolean
  
  Return $bLock
  
End

Private Sub Lock_Write(Value As Boolean)
  
  $bLock = Value
  
End

Public Sub Refresh()
  
  $hView.Refresh
  
End

Private Function Border_Read() As Integer
  
  Return $hPan.Border
  
End

Private Sub Border_Write(Value As Integer)
  
  $hPan.Border = Value
  
End

Public Sub tmrZoom_Timer()
  
  'Wait 3
  $hView.Refresh
  
End
