' Gambas class file

Property Key As String
Property Name As String
Property Exec As String
Property Read LocalNames As Collection


Private $sKey As String
Private $sName As String
Private $sExec As String

Event Change((Key) As String, Value As String)

Public Sub _new(Optional (Key) As String, Name As String, {Exec} As String)

  If Key Then $sKey = Key
  If Name Then $sName = Name
  If {Exec} Then $sExec = {Exec}

End

Public Sub Delete()

  Dim hParent As _DesktopFile_Actions = Object.Parent(Me)

  hParent.Remove($sKey)

End

Private Function Key_Read() As String

  Return $sKey

End

Private Sub Key_Write(Value As String)

  Raise Change("Key", Value)

End

Private Function Name_Read() As String

  Return $sName

End

'' Get or set the Name, this is the text for the action
''
'' languages can be set using underscore
Private Sub Name_Write(Value As String)

  $sName = Value
  Raise Change("Name", Value)

End

Private Function Exec_Read() As String

  Return $sExec

End

Private Sub Exec_Write(Value As String)

  $sExec = Value
  Raise Change("Exec", Value)

End

Private Sub GetSet() As Collection

  Return Object.Parent(GetParent())._Data

End

Private Sub GetParent() As _DesktopFile_Actions

  Return Object.Parent(Me)

End

Private Sub ActionString() As String
  
  If $sKey Then Return "[Desktop Action " & $sKey & "]"
  
End

Public Sub SetLocal(Language As String, Text As String)
  
  GetSet()[ActionString()]["Name[" & Language & "]"] = Text
  
End

Private Function LocalNames_Read() As Collection

  Dim cNames As New Collection
  For Each sKey As String In GetSet()[ActionString()].Keys
    If sKey Like "Name\\[*]" Then cNames.Add(GetSet()[ActionString()][sKey], Split(sKey, "[]")[1])
  Next
  Return cNames

End

