' Gambas class file

Property Key As String
Property Name As String
Property Exec As String

Private $sKey As String

Event Change((Key) As String, Value As String)

Public Sub _new(Optional (Key) As String)

  If Key Then $sKey = Key

End

Public Sub Delete()

  Dim hParent As _DesktopFile_Actions = Object.Parent(Me)

  hParent.Remove($sKey)

End

Private Function Key_Read() As String

  Return $sKey

End

Private Sub Key_Write(Value As String)

  Raise Change("Key", Value)

End

Private Function Name_Read() As String

  Return GetSet()[ActionString()][GetKey("Name")]

End

'' Get or set the Name, this is the text for the action
''
'' languages can be set using underscore
Private Sub Name_Write(Value As String)

  Raise Change(GetKey("Name"), Value)

End

Private Function GetKey(Name As String, Optional sLang As String) As String

  Dim i As Integer
  Dim hDesktopFile As DesktopFile = Object.Parent(GetParent())
  Dim $aLang As String[] = hDesktopFile._LangArray()

  If Not sLang Then
    For i = 0 To $aLang.Max
      If GetSet()[ActionString()].Exist(Name & "[" & $aLang[i] & "]") Then
        sLang = "[" & $aLang[i] & "]"
        Break
      Endif
    Next
  Else
    sLang = "[" & sLang & "]"
    If Not GetSet()[ActionString()].Exist(Name & sLang) Then
      sLang = ""
    Endif
  Endif
  Return Name & sLang

End

Private Function Exec_Read() As String

  Return GetSet()[ActionString()]["Exec"]

End

Private Sub Exec_Write(Value As String)

  Raise Change("Exec", Value)

End

Private Sub GetSet() As Collection

  Return Object.Parent(GetParent())._Data

End

Private Sub GetParent() As _DesktopFile_Actions

  Return Object.Parent(Me)

End

Private Sub ActionString() As String

  If $sKey Then Return "[Desktop Action " & $sKey & "]"

End

Public Sub _property(...) As Boolean

  Return True
  
End

Public Sub _unknown(...) As Variant

    If Not Param.Count Then
      Return GetSet()["[Desktop Action " & $sKey & "]"][GetKey(Param.Name)]
    Else
      GetSet()["[Desktop Action " & $sKey & "]"][GetKey(Param.Name)] = Param[0]
    Endif

End
