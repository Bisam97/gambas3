' Gambas class file
Inherits UserControl
Export

Public Const ThisDay As Integer = 0
Public Const FirstDayOfWeek As Integer = 1
Public Const FirstDayOfMonth As Integer = 2
Public Const FirstDayOfYear As Integer = 3

Public Const _Properties As String = "*,HeightForm=200,WidthForm=300,FormatString=%dddd% %dd% %mmmm% %yyyy%,Value,Range,Alignment{Align.*}=Center,Begin{DatePicker.*}=ThisDay"
Public Const _DefaultEvent As String = "Change"
Public Const _DefaultSize As String = "30,4"
Public Const _DrawWith As String = "DatePicker"

'Private $hObserver As Observer

Private $hDraw As DrawingArea
Private $hForm As form
Private $hTextBox As Label
Private $hdatechooser As DateChooser
Private $iWidthForm As Integer
Private $iHeightForm As Integer
Private $sFormat As String
Private $iFin As Integer
Private $iDebut As Integer
Private htimer As timer
Private IsActivate As Boolean
Private hIcon As Picture
Property HeightForm As Integer
Property WidthForm As Integer
Property FormatString As String
Property Value As Date
Property Range As Integer
Property Alignment As Integer
Property Begin As Integer


Event Change()

Public Sub _New()
  Dim hPanel1, hPanel2 As Panel
  
  hTimer = New timer As "hTimer"
  'htimer.delay = 100
  hPanel2 = New Panel(Me)
  hPanel2.Arrangement = Arrange.Horizontal
  hPanel2.Border = Border.Sunken
  $hTextBox = New Label(hPanel2) As "TextBox"
  $hTextBox.Expand = True
  $hTextBox.Background = Color.White
  $hTextBox.Foreground = Color.Black
  $hTextBox.Border = Border.None
  
  hIcon = Picture["icon:/small/calendar"]
  $hDraw = New DrawingArea(hPanel2) As "Draw"
  $hDraw.Width = 24
  
  $hForm = New Form As "LstForm"
  $hForm.Arrangement = Arrange.Fill
  $hForm.Border = False
  $hForm.Type = Form.Combo
  $hForm.Persistent = True
  HPanel1 = New Panel($hForm)
  HPanel1.Arrangement = Arrange.Fill
  hPanel1.Border = Border.Plain
  $hdatechooser = New DateChooser(HPanel1) As "DateChooser"
  Me.WidthForm = 300
  Me.HeightForm = 200
  Me.FormatString = "%dddd% %dd% %mmmm% %yyyy%"
  Me.Begin = ThisDay
  Me.Value = Now()
  Me.Alignment = Align.Center
'  $hObserver = New Observer(Me.Window) As "OBS"
End

Public Sub Draw_Draw()
  Draw.Begin($hDraw)
  Draw.Style.Button(0, -5, 54, Me.Height + 50, False)
  Draw.Picture(hIcon, (28 - hIcon.Width) / 2, (Me.Height - hIcon.Height - 4) / 2)
  Draw.End()
End

Public Sub Draw_Enter()
$hDraw.Refresh
  ' Draw.Begin($hDraw)
  ' Draw.Style.Button(0, -5, 54, Me.Height + 50, True)
  ' Draw.Picture(hIcon, (28 - hIcon.Width) / 2, (Me.Height - hIcon.Height - 4) / 2)
  ' Draw.End()
End

Public Sub Draw_Leave()
  If Not isActivate Then Draw_Draw
End

Public Sub Draw_MouseDown()
  If Not Me.Enabled Then Return
  isActivate = True
  $hForm.Show
End

Public Sub TextBox_MouseDown()
  Draw_MouseDown
End

Public Sub LstForm_DeACtivate()
 htimer.Trigger
End

Public Sub LstForm_Show()
OBS_Move()
  Draw_Enter
End

Public Sub LstForm_Hide()
  Draw_Draw
End

Public Sub hTimer_Timer()
  hTimer.Stop
  Try $hForm.Close
  isActivate = False
End 

Public Sub DateChooser_Activate()
  Try $hForm.Close
  isActivate = False
End


Public Sub Form_Open()
  Display_Text()
End


Public Sub Form_Resize()
    $hForm.Resize($iWidthForm, $iHeightForm)
End

Private Function Value_Read() As Date
  Return $hdatechooser.Value
End

Private Sub Value_Write(d As Date)
  $hdatechooser.Value = d
  Display_Text()
End

Private Function WidthForm_Read() As Integer
  Return $iWidthForm
End

Private Sub WidthForm_Write(Value As Integer)
  $iWidthForm = Value
  Form_Resize
End

Private Function HeightForm_Read() As Integer
  Return $iHeightForm
End

Private Sub HeightForm_Write(Value As Integer)
  $iHeightForm = Value
  Form_Resize
End

Private Function FormatString_Read() As String
  Return $sFormat
End

Private Sub FormatString_Write(Value As String)
  $sFormat = Value
End

Private Function Range_Read() As Integer
  Return $iFin
End

Private Sub Range_Write(Value As Integer)
   $iFin = Value
End

Private Function Begin_Read() As Integer
  Return $iDebut
End

Private Sub Begin_Write(Value As Integer)
   $iDebut = Value
End

Private Function Alignment_Read() As Integer
  Return $hTextBox.Alignment
End

Private Sub Alignment_Write(Value As Integer)
   $hTextBox.Alignment = Value
End

Public Sub TextBox_Change()
  Raise Change()
End

Private Sub Display_Text()
  Dim form As New String[]
  Dim ret As String
  Dim d As Date
  Dim s As String
  d = $hdatechooser.Value
  Select $iDebut
  Case FirstDayOfWeek
    d = DateAdd(d, gb.day, (- WeekDay(d) + 1))
   d = Date(Year(d), Month(d), Day(d), 0, 0, 0)
  Case FirstDayOfMonth
    d = Date(Year(d), Month(d), 1, 0, 0, 0)
   Case FirstDayOfYear
    d = Date(Year(d), 1, 1, 0, 0, 0)
  End Select
 $hdatechooser.Value = d
  
  form = ["yy", "yyyy", "m", "mm", "mmm", "mmmm", "d", "dd", "ddd", "dddd", "h", "hh", "n", "nn", "s", "ss", "u"]
  ret = $sFormat
  ret = Replace$(ret, "%w%", CStr(Week(d)))
  For Each s In form
    ret = Replace$(ret, "%" & s & "%", Format$(d, s))
  Next
  If $iFin Then 
    d = DateAdd(d, gb.Day, $iFin)
    For Each s In form
      ret = Replace$(ret, "%" & s & "2%", Format$(d, s))
    Next
  Endif
  $hTextBox.Text = ret
End

Public Sub DateChooser_Change()
 Display_Text()
End


Public Sub OBS_Move()
   $hdatechooser.Font = Me.Font
    If $hDraw.ScreenY + $hDraw.Height + $hForm.Height < Desktop.Height Then 
      $hForm.Move($hDraw.ScreenX + $hDraw.Width - $iWidthForm, $hDraw.ScreenY + $hDraw.Height)
    Else 
      $hForm.Move($hDraw.ScreenX + $hDraw.Width - $iWidthForm, $hDraw.ScreenY - $hForm.Height)
    Endif
End

Public Sub LstForm_KeyPress() 'Hides the date form and returns the selected value

  If Key.Code = Key.Esc Then DateChooser_Activate()

End
