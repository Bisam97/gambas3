' Gambas class file

Inherits DrawingArea

Event Click

Property Path As String
Property Label As String
Property Picture As Picture

Private $hObs As Observer
Private $sPath As String
Private $sText As String
Private $sLabel As String
Private $hPict As Picture

Public Sub _new()
  
  $hObs = New Observer(Me) As "DirButton"
  Me.Mouse = Mouse.Pointing
  
End

Private Sub Ellipsize(sText As String, hFont As Font, W As Integer) As String
  
  Dim sCar As String = "â€¦"
  Dim iPos As Integer
  
  If Not sText Or If W <= 0 Then Return
  If hFont.TextWidth(sText) <= W Then Return sText
  
  For iPos = 1 To String.Len(sText)
    If hFont.TextWidth(String.Left(sText, iPos) & sCar) > W Then Break
  Next
  
  Return String.Left(sText, iPos - 1) & sCar
  
End

Public Sub DirButton_Draw()

  Dim DS As Integer
  Dim X As Float
  
  DS = Desktop.Scale
  
  If $hPict Then
    Paint.DrawPicture($hPict, DS \ 2, CInt(Paint.H - $hPict.H) \ 2)
    X = $hPict.W + DS
  Else
    Paint.Background = Color.LightForeground
    Paint.LineWidth = 1.5
    Paint.MoveTo(DS / 2, Paint.H / 2 - DS / 2)
    Paint.LineTo(DS, Paint.H / 2)
    Paint.LineTo(DS / 2, Paint.H / 2 + DS / 2)
    Paint.LineCap = Paint.LineCapRound
    Paint.Stroke
    X = DS * 1.75
  Endif
  
  Paint.Background = Style.ForegroundOf(Me)

  If Me.Hovered And If Not Me.Design Then 
    'Paint.Background = Color.SetAlpha(Paint.Background, 128)
    Paint.DrawTextShadow($sText, X, 0, Paint.W - X, Paint.H, Align.Left)
    'Paint.Background = Color.SetAlpha(Paint.Background, 0)
  Endif
  Paint.DrawText($sText, X, 0, Paint.W - X, Paint.H, Align.Left)
  
End

Public Sub DirButton_Enter()
  
  Me.Refresh
  
End

Public Sub DirButton_Leave()
  
  Me.Refresh
  
End

Public Sub DirButton_Font()
  
  UpdateSize
  
End

Private Sub GetText() As String

  If $sLabel Then Return $sLabel
  Return File.Name($sPath)
  
End

Private Sub UpdateSize()

  Dim W As Integer
  Dim sText As String
  Dim WA As Integer
  
  sText = GetText()

  If $hPict Then
    WA = Desktop.Scale * 1.25 + $hPict.W
  Else 
    WA = Desktop.Scale * 2
  Endif
  
  W = Min(WA + Me.Font.TextWidth(sText), Desktop.Scale * 32)
  
  $sText = Ellipsize(sText, Me.Font, W - WA)
  If $sText <> sText Then
    Me.Tooltip = sText
  Else
    Me.Tooltip = ""
  Endif
  
  Me.W = W

End

Public Sub DirButton_MouseDown()
  
  If Me.Design Or If Me.Parent.Design Then Return
  Raise Click
  
End

Private Function Path_Read() As String

  Return $sPath

End

Private Sub Path_Write(Value As String)

  $sPath = Value
  UpdateSize

End

Private Function Label_Read() As String

  Return $sLabel

End

Private Sub Label_Write(Value As String)

  $sLabel = Value

End

Private Function Picture_Read() As Picture

  Return $hPict

End

Private Sub Picture_Write(Value As Picture)

  $hPict = Value
  UpdateSize

End
