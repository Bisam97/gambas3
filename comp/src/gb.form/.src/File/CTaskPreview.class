' Gambas class file

Inherits Task

Private $sDir As String
Private $iSize As Integer
Private $aPreview As String[]
Private $iMaxFileSize As Integer

Public Sub _new(sDir As String, iSize As Integer, iMaxFileSize As Integer, aPreview As String[])
  
  $sDir = sDir
  $iSize = iSize
  $iMaxFileSize = iMaxFileSize
  If $iMaxFileSize = 0 Then $iMaxFileSize = 4194304
  $aPreview = aPreview
  
End

Private Sub PrintIcon(sFile As String, hImage As Image)

  Dim hIcon As Image
  Dim sTemp As String

  hIcon = New Image(hImage.W + 4, hImage.H + 4, Color.Transparent)
  Paint.Begin(hIcon)
  Paint.AntiAlias = False
  Paint.LineWidth = 2
  Paint.Rectangle(0, 0, hIcon.W, hIcon.H)
  Paint.Background = Color.Gray
  Paint.Stroke
  Paint.End
  hIcon.DrawImage(hImage, 2, 2)
  
  sTemp = File.SetExt(Temp$(), "png")
  hIcon.Save(sTemp)
  Print sFile; "\t"; sTemp
    
End

Public Sub Main()

  Dim sFile As String
  Dim sExt As String
  Dim sPath As String
  Dim hImage As Image
  Dim hSvgImage As SvgImage

  Application.Priority += 10

  For Each sFile In $aPreview
    
    sPath = $sDir &/ sFile
    sExt = LCase(File.Ext(sFile))
    
    If sExt = "jpg" Or If sExt = "jpeg" Or If sExt = "png" Or If sExt = "gif" Or If sExt = "bmp" Or If sExt = "xpm" Then
      
      If $iMaxFileSize > 0 And If Stat(sPath).Size > $iMaxFileSize Then Goto IGNORE_FILE
      
      Try hImage = Image.Load(sPath)
      If Error Then Goto IGNORE_FILE
      
      If Not (hImage.Width = $iSize And hImage.Height = $iSize) Then 
        If hImage.Width > hImage.Height Then 
          hImage = hImage.Stretch($iSize, ($iSize * hImage.Height) \ hImage.Width)
        Else
          hImage = hImage.Stretch(($iSize * hImage.Width) \ hImage.Height, $iSize)
        Endif
      Endif
      
      PrintIcon(sFile, hImage)
      Continue
    
    Else If sExt = "svg" Or If sExt = "svgz" Then
      
      Try hSvgImage = SvgImage.Load(sPath)
      If Error Then Goto IGNORE_FILE
      
      If hSvgImage.Width > hSvgImage.Height Then 
        hSvgImage.Resize($iSize, $iSize * hSvgImage.Height / hSvgImage.Width)
      Else
        hSvgImage.Resize($iSize * hSvgImage.Width / hSvgImage.Height, $iSize)
      Endif

      hImage = New Image(hSvgImage.Width, hSvgImage.Height, Color.Transparent)
      Paint.Begin(hImage)
      hSvgImage.Paint()
      Paint.End
    
      PrintIcon(sFile, hImage)
      Continue 
      
    Endif
    
  IGNORE_FILE:
      
    Print sFile; "\t"
      
  Next
  
  Print "."
  
  Do
    Sleep 3600
  Loop
  
End
