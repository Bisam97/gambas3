' Gambas class file

Inherits Task

Class Hash

Private $sDir As String
Private $iSize As Integer
Private $aPreview As String[]
Private $iMaxFileSize As Integer
Private $sCache As String

Public Sub _new(sDir As String, iSize As Integer, iMaxFileSize As Integer, aPreview As String[], sTempDir As String)
  
  $sDir = sDir
  $iSize = iSize
  $iMaxFileSize = iMaxFileSize
  If $iMaxFileSize = 0 Then $iMaxFileSize = 4194304
  $aPreview = aPreview
  $sCache = sTempDir &/ "gb.form/thumbnails"
  Main.MkDir($sCache)
  
End

Private Sub PrintIcon(sFile As String, hImage As Image, sThumb As String)

  Dim hIcon As Image
  Dim X As Integer
  Dim Y As Integer

  If hImage.W > hImage.H Then
    hImage = hImage.Stretch($iSize, ($iSize * hImage.H) \ hImage.W)
  Else
    hImage = hImage.Stretch(($iSize * hImage.W) \ hImage.H, $iSize)
  Endif
  
  X = ($iSize - hImage.W) \ 2
  Y = ($iSize - hImage.H) \ 2

  hIcon = New Image($iSize, $iSize)
  
  Paint.Begin(hIcon)
  Paint.DrawImage(hImage, X, Y)
  
  'hRect = Paint.StretchImage(hImage, 0, 0, $iSize, $iSize)
  
  Paint.Rectangle(X + 0.5, Y + 0.5, hImage.W - 1, hImage.H - 1)
  Paint.Background = Color.LightForeground
  Paint.Stroke
  Paint.End
  
  Try hIcon.Save(sThumb)
    
End

Private Sub GetThumbnailPath(sPath As String) As String
  
  Try Return $sCache &/ Hash.Sha256(File.Load(sPath)) & "." & CStr($iSize) & ".png"
  
End

Public Sub Main()

  Dim sFile As String
  Dim sExt As String
  Dim sPath As String
  Dim hImage As Image
  Dim hSvgImage As SvgImage
  Dim sThumb As String

  Application.Priority += 10

  For Each sFile In $aPreview
    
    sPath = $sDir &/ sFile
    sExt = LCase(File.Ext(sFile))
    
    If $iMaxFileSize > 0 And If Stat(sPath).Size > $iMaxFileSize Then
      Print sFile; "\t"
      Continue
    Endif
      
    sThumb = GetThumbnailPath(sPath)
    If Exist(sThumb) Then
      Print sFile; "\t"; sThumb
      Continue
    Endif
    
    If sExt = "jpg" Or If sExt = "jpeg" Or If sExt = "png" Or If sExt = "gif" Or If sExt = "bmp" Or If sExt = "xpm" Or If sExt = "webp" Then
      
      Try hImage = Image.Load(sPath)
      If Not Error Then PrintIcon(sFile, hImage, sThumb)
    
    Else If sExt = "svg" Or If sExt = "svgz" Then
      
      Try hSvgImage = SvgImage.Load(sPath)
      If Not Error Then
      
        If hSvgImage.Width > hSvgImage.Height Then 
          hSvgImage.Resize($iSize, $iSize * hSvgImage.Height / hSvgImage.Width)
        Else
          hSvgImage.Resize($iSize * hSvgImage.Width / hSvgImage.Height, $iSize)
        Endif
  
        hImage = New Image(hSvgImage.Width, hSvgImage.Height)
        Paint.Begin(hImage)
        hSvgImage.Paint()
        Paint.End
        
        PrintIcon(sFile, hImage, sThumb)
      Endif
        
    Endif
    
    Print sFile; "\t";
    If Exist(sThumb) Then Print sThumb;
    Print
    
  Next
  
  Print "."
  
  Do
    Sleep 3600
  Loop
  
End
