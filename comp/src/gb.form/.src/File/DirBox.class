' Gambas class file

Export
Inherits UserControl

Public Const _Properties As String = "*,Path,Action,Title,Border=True"
Public Const _DefaultEvent As String = "Click"
Public Const _DefaultSize As String = "32,4"
Public Const _Similar As String = "TextBox"
Public Const _Group As String = "Chooser"

'' Raised when a directory has been selected.
Event Click

'' Raised when the contents of the control has changed.
Event Change

'' Return or set the selected directory.
Property Value, Path As String

'' Return or set if the control has a border.
Property Border As Boolean

'' Return or set the title of the directory selection dialog.
Property Title As String

'' @{since 3.20}
''
'' Return or set the placeholder tdext.
Property Placeholder As String

Private $hButtonBox As ButtonBox
Private $sTitle As String

Public Sub _new()
  
  $hButtonBox = New ButtonBox(Me) As "Button"
  Me.Proxy = $hButtonBox
  $hButtonBox.Picture = Picture["icon:/small/directory"]
  $hButtonBox.ReadOnly = True
  $hButtonBox.ClearButton = True
  
End

Private Function Border_Read() As Boolean

  Return $hButtonBox.Border

End

Private Sub Border_Write(Value As Boolean)

  $hButtonBox.Border = Value

End

Private Function Value_Read() As String

  Return $hButtonBox.Text

End

Private Sub Value_Write(Value As String)

  $hButtonBox.Text = Value

End

Public Sub Button_Click()

  Dim sSaveTitle As String
  Dim bCancel As Boolean
  
  sSaveTitle = Dialog.Title
  
  Dialog.Title = $sTitle
  Dialog.Path = $hButtonBox.Text
  
  bCancel = Dialog.SelectDirectory()
  
  Dialog.Title = sSaveTitle
  
  If Not bCancel Then
    $hButtonBox.Text = Dialog.Path
    Raise Click
  Endif
  
End

Public Sub Button_Change()
  
  Raise Change
  
End

Private Function Title_Read() As String

  Return $sTitle

End

Private Sub Title_Write(Value As String)

  $sTitle = Value

End

Private Function Placeholder_Read() As String

  Return $hButtonBox.Placeholder

End

Private Sub Placeholder_Write(Value As String)

  $hButtonBox.Placeholder = Value

End
