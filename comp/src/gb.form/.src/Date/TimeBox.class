' Gambas class file

'Export
Inherits ButtonBox

Public Const _Properties As String = "*,-Picture,-Text,-Alignment,Precision{TimeBox.*}=Minute"

Public Enum {Minute}, {Second}, Millisecond

Property Value As Date
Property Precision As Integer

Private $iPrec As Integer
Private $hObserver As Observer

Public Sub _new()
  
  $hObserver = New Observer(Me) As "TimeBox"
  
  Me.Picture = Picture["icon:/16/clock"]
  Me.Alignment = Align.Right
  UpdateMask
  
End

Private Sub UpdateMask()
  
  Dim sMask As String
  Dim vVal As Date
  
  vVal = Value_Read()
  
  Select Case $iPrec
    Case {Minute}
      sMask = Format(Time(11, 11, 11), gb.ShortTime)
    Case {Second}
      sMask = Format(Time(11, 11, 11), gb.LongTime)
    Case {Millisecond}
      sMask = Format(Time(11, 11, 11), gb.LongTime) & ".000"
  End Select
  
  Me.Editor.Mask = Replace(sMask, "1", "0")  
  
  Value_Write(vVal)
  
End


Private Function Value_Read() As Date

  Return Val(Me.Text)

End

Private Sub Value_Write(Value As Date)

  Dim sFormat As String
  
  Select Case $iPrec
    Case {Minute}
      sFormat = Format(Time(11, 22, 33), gb.ShortTime)
    Case {Second}
      sFormat = Format(Time(11, 22, 33), gb.LongTime)
    Case {Millisecond}
      sFormat = Format(Time(11, 22, 33), gb.LongTime) & ".uu"
  End Select
  
  sFormat = Replace(sFormat, "11", "hh")
  sFormat = Replace(sFormat, "22", "nn")
  sFormat = Replace(sFormat, "33", "ss")
  
  If IsNull(Value) Then Value = Time
  Me.Text = Format(Value, sFormat)

End

Public Sub TimeBox_Click()
  
  Value_Write(Null)
  Stop Event
  
End

Private Function Precision_Read() As Integer

  Return $iPrec

End

Private Sub Precision_Write(Value As Integer)

  If Value < {Minute} Or If Value > {Millisecond} Or If Value = $iPrec Then Return
  
  $iPrec = Value
  UpdateMask

End
