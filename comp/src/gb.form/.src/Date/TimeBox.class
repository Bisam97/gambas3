' Gambas class file

'Export
Inherits ButtonBox

Public Const _Properties As String = "*,-Picture,-Text,ShowSecond,ShowMillisecond"

Property Value As Date
Property ShowSecond As Boolean
Property ShowMillisecond As Boolean

Private $bAMPM As Boolean
Private $bPM As Boolean
Private $bSecond As Boolean
Private $bMillisecond As Boolean
Private $hObserver As Observer

Public Sub _new()
  
  $hObserver = New Observer(Me) As "TimeBox"
  
  Me.Picture = Picture["icon:/16/clock"]
  UpdateMask
  
End

Private Sub UpdateMask()
  
  Dim sMask As String
  
  If $bSecond Then
    sMask = Format(Time(11, 11, 11), gb.LongTime)
  Else
    sMask = Format(Time(11, 11, 11), gb.ShortTime)
  Endif
  
  If $bMillisecond Then sMask &= ".000"
  
  $bAMPM = Format(Now, "AM/PM")
  
  Me.Editor.Mask = Replace(sMask, "1", "0")  
  
End


Private Function Value_Read() As Date

  Return Val(Me.Text)

End

Private Sub Value_Write(Value As Date)

  Dim sFormat As String
  
  If $bSecond Then
    sFormat = Format(Time(11, 22, 33), gb.LongTime)
  Else
    sFormat = Format(Time(11, 22, 33), gb.ShortTime)
  Endif
  sFormat = Replace(sFormat, "11", "hh")
  sFormat = Replace(sFormat, "22", "nn")
  sFormat = Replace(sFormat, "33", "ss")
  
  If $bMillisecond Then sFormat &= ".uu"

  Me.Text = Format(Value, sFormat)

End

Private Function ShowSecond_Read() As Boolean

  Return $bSecond

End

Private Sub ShowSecond_Write(Value As Boolean)

  $bSecond = Value
  UpdateMask

End

Private Function ShowMillisecond_Read() As Boolean

  Return $bMillisecond

End

Private Sub ShowMillisecond_Write(Value As Boolean)

  $bMillisecond = Value
  UpdateMask

End

Public Sub TimeBox_Click()
  
  Stop Event
  
End
