' Gambas class file

Private Const ROUND_SIZE As Integer = 8
Private Const PADDING As Integer = 8
Private Const BACKGROUND_COLOR As Integer = &HFFFFDF&
Private Const BACKGROUND_COLOR2 As Integer = &HBFBFA7& 

Static Private $iDelay As Integer = 5000

Private $hCtrl As Control

Private $sText As String
Private $DX As Integer
Private $DY As Integer
Private $AH As Integer
Private $iAlign As Integer
Private $hIcon As Picture

Private Sub GetTopLevel(hCtrl As Control) As Window
  
  Dim hWin As Window

  If Not hCtrl Then Return  
  hWin = hCtrl.Window
  Do
    If hWin.TopLevel Then Return hWin
    hWin = hWin.Parent.Window
  Loop
  
End

Public Sub Run(sMsg As String, hCtrl As Control, hIcon As Picture, DX As Integer, DY As Integer)
  
  Dim DS As Integer = Desktop.Scale
  Dim hWin As Window
  Dim X, Y, W, H As Integer
  Dim hScreen As Screen
  
  'Form_Hide
  
  $sText = Replace(sMsg, "\n", "<br>")
  $hIcon = hIcon
  $hCtrl = hCtrl
  $AH = DS * 3
  
  If DX = -1 And If DY = -1 Then
    $DX = hCtrl.W \ 2
    $DY = hCtrl.H \ 2
  Else
    $DX = DX
    $DY = DY
  Endif

  hWin = GetTopLevel($hCtrl)
  If Not hWin.Modal Then hWin.Show
  
  hScreen = Screens[hWin.Screen]
  
  With Me.Font.RichTextSize($sText, DS * 32)
    W = .W
    H = .H
  End With
  If $hIcon Then W += DS + $hIcon.W
  W += DS * 3 + 1
  H += DS * 2 + $AH + 1
  
  X = $hCtrl.ScreenX + $DX - W \ 2
  Y = $hCtrl.ScreenY + $DY - H
  $iAlign = Align.Top
  
  If X + W > hScreen.W Then X = hScreen.W - W
  If Y < 0 Then 
    Y = $hCtrl.ScreenY + $DY
    $iAlign = Align.Bottom
  Endif
  
  Me.Resize(W, H)
  
  timClose.Delay = $iDelay
  timClose.Start
  
  Me.ShowPopup(X, Y)
  $hCtrl.SetFocus
  
End

Public Sub GetControl() As Control
  
  If Me.Visible Then Return $hCtrl
  
End

Static Public Sub GetDelay() As Integer
  
  Return $iDelay
  
End

Static Public Sub SetDelay(iDelay As Integer)

  $iDelay = iDelay
  
End

Public Sub dwgBorder_Draw()

  Dim X, Y, W, H As Integer
  Dim DS As Integer
  Dim XA As Integer
  Dim YA As Integer

  DS = Desktop.Scale
  
  If Align.IsTop($iAlign) Then
    X = 0
    Y = 0
    W = Paint.W - 1
    H = Paint.H - $AH - 1
  Else
    X = 0
    Y = $AH
    W = Paint.W - 1
    H = Paint.H - $AH - 1
  Endif
  
  XA = $hCtrl.ScreenX + $hCtrl.W \ 2 - Me.ScreenX
  
  Paint.Background = Color.TooltipBackground
  
  GoSub DRAW_PATH
  Paint.Fill()
  
  Paint.Background = Color.SetAlpha(Color.TooltipForeground, 128)
  Paint.Translate(0.5, 0.5)
  Paint.LineWidth = 1
  GoSub DRAW_PATH
  Paint.Stroke()
  Paint.Translate(-0.5, -0.5)
  
  Paint.FillRect(XA + 1, YA, $AH - 1, 1, Color.TooltipBackground)
  
  X = DS
  Y = DS
  If Align.IsBottom($iAlign) Then Y += $AH
  
  If $hIcon Then
    Paint.DrawPicture($hIcon, X, Y)
    X += $hIcon.W + DS
  Endif
  
  Paint.Background = Color.TooltipForeground
  Paint.DrawRichText($sText, X, Y, Paint.W - X - DS)
  
  Return
  
DRAW_PATH:

  Paint.Rectangle(X, Y, W, H, DS * 2)
  
  If Align.IsTop($iAlign) Then
    YA = Paint.H - $AH - 1
    Paint.MoveTo(XA, YA)
    Paint.LineTo(XA + $AH, YA)
    Paint.LineTo(XA, YA + $AH)
    Paint.LineTo(XA, YA)
  Else
    YA = $AH
    Paint.MoveTo(XA, YA)
    Paint.LineTo(XA + $AH, YA)
    Paint.LineTo(XA, YA - $AH)
    Paint.LineTo(XA, YA)
  Endif

  Return

End

Public Sub dwgBorder_MouseDown()

  Me.Close

End

Public Sub timClose_Timer()

  Me.Close

End

Public Sub Form_Close()

  timClose.Stop

End

Public Sub Form_KeyPress()

  Me.Close
  Stop Event

End
