' Gambas class file

Export
Inherits UserContainer

Public Const _Properties As String = "*,Count{Range:1;256}=1,Index,Text,TextFont,Picture,Border"
Public Const _Similar As String = "TabStrip"
Public Const _IsMultiContainer As Boolean = True
Public Const _DefaultEvent As String = "Click"

Event Click

Property Count As Integer
Property Index As Integer
Property Text As String
Property Picture As Picture
Property Font As Font
Property TextFont As Font
Property Border As Boolean

Private $hView As GridView
Private $aCont As New _IconPanelContainer[]

Private $iCurrent As Integer = -1
Private $hPanel As Panel

Private Const MIN_WIDTH_PANEL As Integer = 20

Public Sub _new()

  Me.Arrangement = Arrange.Horizontal
  Me.Spacing = True

  $hView = New GridView(Me) As "View"
  '$hView.Orientation = Arrange.Horizontal
  '$hView.IconLines = 2
  $hView.Width = Desktop.Scale * MIN_WIDTH_PANEL
  $hView.Mouse = Mouse.Pointing
  $hView.Columns.Count = 1
  $hView.Mode = Select.Single
  $hView.Grid = False
  '$hView.ScrollBar = Scroll.None
  
  $hPanel = New Panel(Me) As "Panel"
  $hPanel.Expand = True
  $hPanel.Arrangement = Arrange.Fill

  'Me._Container = $hPanel
  Me.Count = 1
  'Me.Index = 0
  'Me.Border = True
  Me.Arrangement = Arrange.None
  Me.Spacing = False

End

Private Function Count_Read() As Integer

  Return $aCont.Count

End

Public Sub _UpdateIconViewWidth()
  
  Dim iInd As Integer
  Dim W, H, WC As Integer
  
  W = Desktop.Scale * MIN_WIDTH_PANEL
  
  For iInd = 0 To $aCont.Max
    
    WC = $hView.Font.TextWidth($hView[iInd, 0].Text) + Desktop.Scale * 4
    If $hView[iInd, 0].Picture Then WC += $hView[iInd, 0].Picture.Width + Desktop.Scale
    W = Max(W, WC)
    
    H = Max(H, $hView.Font.TextHeight($hView[iInd, 0].Text))
    If $hView[iInd, 0].Picture Then H = Max(H, $hView[iInd, 0].Picture.Height)
    
    $hView[iInd, 0].Padding = Desktop.Scale
    
  Next
  
  $hView.Rows.H = H + Desktop.Scale * 2
  
  $hView.W = Min(W, Me.Width / 2)
  
End

Public Sub Panel_Arrange()
  
  _UpdateIconViewWidth()
  
End


Private Sub Count_Write(iCount As Integer)

  Dim iInd As Integer
  Dim hToolbar As _IconPanelContainer
  Dim hCont As Container
  Dim sText As String
  Dim iCurrent As Integer

  If iCount < 1 Then Error.Raise("Bad argument")
  If iCount = $aCont.Count Then Return

  If iCount < $aCont.Count Then

    For iInd = $aCont.Max To iCount Step -1
      hToolbar = $aCont[iInd]
      If hToolbar.Children.Count Then Error.Raise("IconPanel container is not empty")
    Next

    iCurrent = $iCurrent
    
    Object.Lock(Me) '' FIXME: Interpreter crash if this is removed!
    For iInd = $aCont.Max To iCount Step -1
      $aCont[iInd].Delete
      $hView.Rows.Remove(iInd)
      If iInd <= iCurrent Then Dec iCurrent
    Next
    Object.Unlock(Me)

    $aCont.Remove(iCount, -1)
    
    $iCurrent = -1
    Index_Write(iCurrent)
    
  Else

    $hView.Rows.Count = iCount

    For iInd = $aCont.Count To iCount - 1
      hToolbar = New _IconPanelContainer($hPanel) As "_IconPanelContainer"
      'hToolbar.Text = "Toolbar " & CInt(iInd)
      $aCont.Add(hToolbar)
      '$hView.Add(iInd, "Item " & CStr(iInd))
      With $hView[iInd, 0]
        .Text = "Item " & CStr(iInd)
      End With
      _UpdateIconViewWidth
    Next
    'ME.Container = hCont

    'ME.Index = iCount - 1
    Index_Write(iCount - 1)

  Endif

End

Private Function Index_Read() As Integer

  Return $iCurrent

End

Private Sub Index_Write(iIndex As Integer)

  If iIndex < 0 Or iIndex >= $aCont.Count Then Error.Raise("Bad index")

  If iIndex = $iCurrent Then Return

  Me._Container = $aCont[iIndex]
  $iCurrent = iIndex
  $hView.Rows[$iCurrent].Selected = True

  UpdateContainer
  Raise Click

End

Private Sub UpdateContainer()

  Dim hToolBar As _IconPanelContainer
  Dim iInd As Integer
  Dim bCurrent As Boolean

  For iInd = 0 To $aCont.Max

    $aCont[iInd].Visible = iInd = $iCurrent

  Next

End

Private Function Text_Read() As String

  Return $aCont[$iCurrent].Text

End

Private Sub Text_Write(sText As String)

  $aCont[$iCurrent].Text = sText

End

Private Function Picture_Read() As Picture

  Return $aCont[$iCurrent].Picture

End

Private Sub Picture_Write(hPict As Picture)

  $aCont[$iCurrent].Picture = hPict

End

Public Function _get(Index As Integer) As _IconPanelContainer

  If Index < 0 Or Index >= $aCont.Count Then Error.Raise("Bad index")
  Return $aCont[Index]

End

Private Function Font_Read() As Font

  Return Super.Font

End

Private Sub Font_Write(hFont As Font)

  Dim hToolbar As _IconPanelContainer

  Super.Font = hFont

  For Each hToolbar In $aCont
    hToolBar.Text = hToolBar.Text
  Next

End

Private Function Border_Read() As Boolean

  Return $hPanel.Border <> Border.None

End

Private Sub Border_Write(bBorder As Boolean)

  $hPanel.Border = If(bBorder, Border.Sunken, Border.None)

End

Private Function TextFont_Read() As Font

  Return $hView.Font

End

Private Sub TextFont_Write(Value As Font)

  $hView.Font = Value
  
End

Public Sub _GetView() As GridView
  
  Return $hView
  
End

Public Sub _GetIndex(hCont As _IconPanelContainer) As Integer
  
  Return $aCont.Find(hCont)
  
End

Public Sub View_Select()
  
  Index_Write($hView.Row)
  
End

Public Sub View_MouseWheel()
  
  If Mouse.Delta < 0 And If $iCurrent < $aCont.Max Then
    Index_Write($iCurrent + 1)
  Else If Mouse.Delta > 0 And If $iCurrent > 0 Then
    Index_Write($iCurrent - 1)
  Endif
  
End
