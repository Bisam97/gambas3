' Gambas class file

Public DirView As DirView 
Public FileView As FileView

Event Change
Event {Select}
Event GetIcon(Path As String)
Event ShowHidden
'EVENT ShowHiddenChange

Private $bView As Boolean
Private $sActivate As String
Private $aFilter As String[]
Private $bFile As Boolean
Private $aBookmark As Array
Private $iMaxWidth As Integer

Static Private $aHistory As New String[]

Class Settings

Public Sub _new(Optional bFile As Boolean)

  DirView = dvwChoose
  FileView = fvwChoose

  SetRoot()  

  $bFile = bFile
  If Not bFile Then Return

  btnView_Click
  btnView.Visible = False
  txtFile.Visible = True
  cmbFilter.Visible = True
  SetMulti(False)
  SetFont(Me.Font)
  SetFilter()
  
End


Public Sub cmbDir_Change()

  $sActivate = cmbDir.Text
  dvwChoose.Current = cmbDir.Text

End

Public Sub cmbDir_Click()

  'cmbDir_Activate

End


Public Sub dvwChoose_Click()

  If fvwChoose.Visible Then fvwChoose.Dir = dvwChoose.Current

  AddToHistory(dvwChoose.Current)
  Object.Lock(cmbDir)
  cmbDir.Text = dvwChoose.Current
  Object.Unlock(cmbDir)

  ' IF $sActivate THEN
  '   IF $sActivate = dvwChoose.Current THEN
  '     IF cmbDir.Find($sActivate) = 0 THEN cmbDir.Add($sActivate)
  '   ENDIF
  '   $sActivate = ""
  ' ENDIF

  If txtFile.Visible And If txtFile.ReadOnly Then
    txtFile.Text = ""
  Endif

  Raise Change

End


Public Sub Form_Open()

  'DirView = dvwChoose
  'FileView = fvwChoose

  cmbDir.Text = dvwChoose.Current
  If $bFile Then txtFile.SetFocus
  Raise Change

End

Public Sub btnView_Click()

  $bView = Not $bView
  fvwChoose.Visible = $bView
  btnDetailed.Visible = $bView
  If $bView Then
    fvwChoose.Dir = dvwChoose.Current
    splChoose.Layout = [1, 2]
  Endif

End

Public Sub btnDetailed_Click()

  fvwChoose.ShowDetailed = Not fvwChoose.ShowDetailed
  If fvwChoose.ShowDetailed Then
    btnDetailed.Picture = Picture["icon:/small/view-icon"]
  Else
    btnDetailed.Picture = Picture["icon:/small/view-detail"]
  Endif

End

Public Sub btnParent_Click()

  dvwChoose.Current = File.Dir(dvwChoose.Current)

End

Private Sub AddToHistory(sDir As String)
  
  Dim iInd As Integer
  
  iInd = $aHistory.Find(sDir)
  If iInd >= 0 Then $aHistory.Remove(iInd)
  $aHistory.Add(sDir)
  While $aHistory.Count > 16
    $aHistory.Remove(0)
  Wend

  Object.Lock(cmbDir)
  cmbDir.List = $aHistory  
  Object.Unlock(cmbDir)
  
End

Public Sub SetDir(sDir As String)

  If panBookmark.Visible Then FindBestBookmark(sDir)
    
  dvwChoose.Current = sDir

End

Public Function GetDir() As String

  Return dvwChoose.Current

End

Public Function GetShowDetailed() As Boolean

  Return fvwChoose.ShowDetailed

End

Public Sub SetShowDetailed(bShowDetailed As Boolean)

  If fvwChoose.ShowDetailed <> bShowDetailed Then btnDetailed_Click

End

Public Function GetFileVisible() As Boolean

  Return fvwChoose.Visible

End

Public Sub SetFileVisible(bVisible As Boolean)

  If fvwChoose.Visible <> bVisible Then btnView_Click

End

Public Function GetShowHidden() As Boolean

  Return fvwChoose.ShowHidden

End


Public Sub btnShowHidden_Click()

  fvwChoose.ShowHidden = btnShowHidden.Value
  dvwChoose.ShowHidden = fvwChoose.ShowHidden
  Raise ShowHidden
'   IF fvwChoose.ShowHidden THEN
'     btnShowHidden.Picture = Picture["img/16/hide-hidden.png"]
'   ELSE
'     btnShowHidden.Picture = Picture["img/16/show-hidden.png"]
'   ENDIF

End


Public Sub SetShowHidden(bShowHidden As Boolean)

  btnShowHidden.Value = bShowHidden

  'IF fvwChoose.ShowHidden <> bShowHidden THEN btnShowHidden_Click

End

Public Function GetMulti() As Boolean

  Return fvwChoose.Mode = Select.Multiple

End

Public Sub SetMulti(bMulti As Boolean)

  fvwChoose.Mode = If(bMulti, Select.Multiple, Select.Single)

End

Public Function GetPath() As String

  Dim sPath As String
  'Dim sExt As String

  If GetMulti() Then Return

  If txtFile.Text Then 
    sPath = dvwChoose.Current &/ txtFile.Text
    ' If chkExt.Value Then 
    '   Try sExt = Scan(Split($aFilter[cmbFilter.Index], ";,", Chr$(34), True)[0], "\\*.*")[0]
    '   If sExt Then sPath = File.SetExt(sPath, sExt)
    ' Endif
    Return sPath
  Endif

End

Public Sub SetPath(sPath As String)
  
  If txtFile.ReadOnly Then
    SetDir(sPath)
  Else
    SetDir(File.Dir(sPath))
  Endif

  If GetMulti() Then Return 
  
  'DEBUG dvwChoose.Current;; File.Dir(sPath)
  'IF dvwChoose.Current = File.Dir(sPath) THEN
  fvwChoose.Current = File.Name(sPath)
  If txtFile.ReadOnly Then
    txtFile.Text = fvwChoose.Current
  Else 
    txtFile.Text = File.Name(sPath)
  Endif
  'ENDIF
  
End


Public Function GetPaths() As String[]

  Dim aPath As New String[]
  Dim sFile As String

  If GetMulti() Then

    For Each sFile In fvwChoose.Selection
      aPath.Add(dvwChoose.Current &/ sFile)
    Next

  Endif

  Return aPath

End


Public Sub fvwChoose_Select()

  'IF txtFile.Visible THEN 
  txtFile.Text = fvwChoose.Current
  If $bFile Then Raise Change
  
End

Public Sub SetShowDirectory(bShow As Boolean)

  fvwChoose.ShowDirectory = bShow

End

Public Function GetShowDirectory() As Boolean

  Return fvwChoose.ShowDirectory

End


Public Sub SetFont(hFont As Font)

  'fvwChoose.Font = hFont
 ' DEC fvwChoose.Font.Grade

End

Public Sub fvwChoose_Activate()

  If IsDir(dvwChoose.Current &/ fvwChoose.Current) Then
    dvwChoose.Current &/= fvwChoose.Current
  Else
    Raise {Select}
  Endif

End

Public Sub SetFilter(Optional aFilter As String[])

  Dim iInd As Integer

  cmbFilter.Clear
  $aFilter = New String[]

  If aFilter Then

    If aFilter.Count And 1 Then
      aFilter.Remove(aFilter.Max)
    Endif

    For iInd = 0 To aFilter.Max Step 2
      If aFilter[iInd] = "*" Then Continue 
      $aFilter.Add(aFilter[iInd])
      cmbFilter.Add(aFilter[iInd + 1] & " (" & aFilter[iInd] & ")")
    Next

  Endif

  $aFilter.Add("*")
  cmbFilter.Add(("All files (*)"))

  cmbFilter.Index = 0
  cmbFilter.Visible = cmbFilter.Count > 1

End

Public Sub SetFilterIndex(iInd As Integer)
  
  If iInd < 0 Then iInd = cmbFilter.Count - 1
  Try cmbFilter.Index = iInd
  
End

Public Sub GetFilterIndex() As Integer
  
  Return cmbFilter.Index
  
End


Public Function GetFilter() As String[]

  Dim iInd As Integer
  Dim aFilter As New String[]

  For iInd = 0 To $aFilter.Max
    aFilter.Add($aFilter(iInd))
    aFilter.Add(cmbFilter[iInd].Text)
  Next

  Return aFilter

End


Public Sub cmbFilter_Click()

  fvwChoose.Filter = Split($aFilter[cmbFilter.Index], ";,", Chr$(34), True)

End

Public Sub dvwChoose_Activate()

  If Not $bFile Then Raise {Select}

End

Public Sub fvwChoose_Icon(Path As String)

  Raise GetIcon(Path)    

End

Public Sub dvwChoose_Icon(Path As String)

  Raise GetIcon(Path)    

End


Public Sub SetReadOnly(bReadOnly As Boolean)
  
  If Not $bFile Then Return 
  txtFile.ReadOnly = bReadOnly
  
End

Public Sub GetReadOnly() As Boolean
  
  If Not $bFile Then Return
  Return txtFile.ReadOnly
  
End

Public Sub SetPicture(hPict As Picture)

  dvwChoose.Picture = hPict
  
End 

Public Sub GetPicture() As Picture
  
  Return dvwChoose.Picture
  
End

Public Sub txtFile_GotFocus()

  If Not txtFile.ReadOnly Then txtFile.SelectAll

End

Private Sub GetRealLink(sLink As String) As String
  
  If Left(sLink) <> "/" Then sLink = Mid$(sLink, 2)
  Return sLink  
  
End


Public Sub AddBookmark(sLink As String, Optional sName As String, sIcon As String)

  Dim hPanel As HBox
  Dim hIcon As PictureBox
  Dim hVBox As VBox
  Dim hText As Label
  Dim sTooltip As String
  
  If Not sName Then sName = File.Name(sLink)
  If Not sIcon Then sIcon = "icon:/small/bookmark"

  sTooltip = GetRealLink(sLink)
  
  hPanel = New HBox(lstBookmark)
  hPanel.Padding = 2
  hPanel.Spacing = 6
  hPanel.Tag = sLink
  hPanel.ToolTip = sTooltip
  hPanel.Height = hPanel.Font.Height() * 2 + 8

  hIcon = New PictureBox(hPanel)
  hIcon.Picture = Picture[sIcon]
  hIcon.Alignment = Align.Center
  hIcon.Resize(20, 16)
  hIcon.ToolTip = sTooltip

  hVBox = New VBox(hPanel)
  hVBox.Expand = True

  hText = New Label(hVBox)
  'hText.Expand = TRUE
  hText.AutoResize = True
  hText.Expand = True
  'hText.Alignment = Align.TopNormal
  hText.Text = sName  
  hText.ToolTip = sTooltip
  'hText.Background = Color.Yellow
  hText.Font.Bold = True
  'hText.Font.Grade = 3

  $iMaxWidth = Max($iMaxWidth, hText.Font.Width(sName))

  If Left(sLink) <> "$" Then
    hText = New Label(hVBox)
    'hText.Expand = TRUE
    hText.AutoResize = True
    hText.Expand = True
    'hText.Alignment = Align.TopNormal
    hText.Text = Replace(sTooltip, System.User.Home, "~")
    'hText.Font.Grade = -1

    $iMaxWidth = Max($iMaxWidth, hText.Font.Width(hText.Text))
  Endif
  
End

Public Sub RemoveBookmark(sLink As String)
  
  Dim hPanel As Container
  
  For Each hPanel In lstBookmark.Children
    If hPanel.Tag Then hPanel.Delete
  Next
  
End


Private Sub LoadBookmark()
  
  Dim hSettings As Settings 
  Dim iInd As Integer
  Dim aBookmark As String[]

  If Not Components["gb.settings"] Then Return
  
  hSettings = New Settings("gb.form")
  
  For iInd = 1 To hSettings["/Bookmarks/Count", 0]
    aBookmark = hSettings["/Bookmarks/Bookmark" & CStr(iInd)]
    AddBookmark(aBookmark[0], aBookmark[1])
  Next

End


Private Sub SaveBookmark()
  
  Dim hSettings As Settings 
  Dim iInd As Integer  
  Dim hPanel As Container
  
  If Not Components["gb.settings"] Then Return
  
  hSettings = New Settings("gb.form")

  hSettings.Clear("/Bookmarks")
  
  For Each hPanel In lstBookmark.Children
    If Left(hPanel.Tag) <> "/" Then Continue
    If Not hPanel.Visible Then Continue
    Inc iInd
    hSettings["/Bookmarks/Bookmark" & CStr(iInd)] = [CStr(hPanel.Tag), File.Name(hPanel.Tag)]
  Next
  
  hSettings["/Bookmarks/Count"] = iInd

  hSettings.Save
    
End


Private Sub FillBookmark()
  
  Dim iInd As Integer
  Dim sLink As String
  Dim sName As String
  Dim sIcon As String
  
  lstBookmark.Clear
  $iMaxWidth = 0
  
  AddBookmark("$" & System.User.Home, ("Home"), "icon:/small/home")
  If Component.IsLoaded("gb.desktop") Then
    AddBookmark("$" & Desktop.Path, ("Desktop"), "icon:/small/desktop")
  Endif
  AddBookmark("$/", ("System"), "icon:/small/system")

  If $aBookmark Then
    For iInd = 0 To $aBookmark.Max
      sName = ""
      sIcon = ""
      If IsObject($aBookmark[iInd]) Then
        sLink = $aBookmark[iInd][0]
        Try sName = $aBookmark[iInd][1]
        Try sIcon = $aBookmark[iInd][2]
      Else 
        sLink = $aBookmark[iInd]
      Endif
      AddBookmark("#" & sLink, sName, sIcon)
    Next
  Endif 

  LoadBookmark
  
  panBookmark.Dimension = Min($iMaxWidth + 52, Me.Width / 2)

  SelectBookmark
  
End


Public Sub ShowBookmark(bShow As Boolean)

  panBookmark.Visible = bShow
  FillBookmark  
  'FindBookmark
  
End

Public Sub IsBookmarkVisible() As Boolean
  
  Return panBookmark.Visible
  
End

Private Sub SelectBookmark()
  
  Dim hPanel As Container
  Dim hIcon As PictureBox
  Dim sRoot As String

  If Not panBookmark.Visible Then Return
  
  sRoot = dvwChoose.Root
  If Not sRoot Then sRoot = System.User.Home
  
  For Each hPanel In lstBookmark.Children
    If GetRealLink(hPanel.Tag) = sRoot Then 
      For Each hIcon In hPanel.Children
        dvwChoose.Picture = hIcon.Picture
        Break
      Next
      lstBookmark.Select(hPanel)
      Return
    Endif
  Next
  
End

Private Sub FindBookmark(sLink As String) As Container
  
  Dim hPanel As Container
  
  For Each hPanel In lstBookmark.Children
    If GetRealLink(hPanel.Tag) = sLink Then Return hPanel
  Next  
  
End


Public Sub lstBookmark_Click()

  Dim sLink As String = lstBookmark.Current.Tag

  SetRoot(GetRealLink(sLink))
  btnRemove.Enabled = Left(sLink) = "/"
  
End


Public Sub SetRoot(Optional sRoot As String)
  
  If sRoot = dvwChoose.Root Then Return  
  dvwChoose.Root = sRoot
  
  SelectBookmark
  
End

Public Sub GetRoot() As String
  
  Return dvwChoose.Root
  
End

Public Sub SetPrivateBookmarks(aBookmark As Array)
  
  $aBookmark = aBookmark
  FillBookmark    
  
End

Public Sub GetPrivateBookmarks() As Array
  
  Return $aBookmark
  
End

Public Sub btnAdd_Click()

  Dim sLink As String = dvwChoose.Current
  
  If FindBookmark(sLink) Then Return 
  AddBookmark(sLink)
  SaveBookmark

End

Public Sub btnRemove_Click()

  lstBookmark.Current.Delete
  SetRoot()
  SaveBookmark

End

Public Sub GetSettings() As Variant[]
  
  If Not $bFile Then 
    Return [CVariant(dvwChoose.Root), dvwChoose.Current, $bView, splChoose.Layout, btnShowHidden.Value, fvwChoose.Settings, Not panBookmark.Hidden]
  Else
    Return [CVariant(dvwChoose.Root), dvwChoose.Current, fvwChoose.Current, $bView, splChoose.Layout, btnShowHidden.Value, fvwChoose.Settings, GetFilterIndex(), Not panBookmark.Hidden]
  Endif 

End

Public Sub SetSettings(aVar As Variant[])
  
  If Not $bFile Then 
  
    Try SetRoot(aVar[0])
    Try dvwChoose.Current = aVar[1]
    Try $bView = Not aVar[2]
    If Not Error Then btnView_Click
    splChoose.Layout = aVar[3]
    Try btnShowHidden.Value = aVar[4]
    Try fvwChoose.Settings = aVar[5] 
    Try panBookmark.Hidden = Not aVar[6]
    
  Else
  
    Try SetRoot(aVar[0])
    Try dvwChoose.Current = aVar[1]
    Try fvwChoose.Current = aVar[2]
    Try $bView = Not aVar[3]
    If Not Error Then btnView_Click
    Try splChoose.Layout = aVar[4]
    Try btnShowHidden.Value = aVar[5]
    Try fvwChoose.Settings = aVar[6]
    Try SetFilterIndex(aVar[7])
    Try panBookmark.Hidden = Not aVar[8]
    
  Endif

End

Public Sub btnNewDir_Click()

  dvwChoose.NewFolder

End

Private Sub FindBestBookmark(sDir As String)
  
  Dim hPanel As Container
  Dim sBest As String
  Dim iScore As Integer
  Dim aBook As String[]
  Dim aDir As String[]
  Dim iInd As Integer
  
  aDir = Split(sDir, "/")
  
  For Each hPanel In lstBookmark.Children
    aBook = Split(GetRealLink(hPanel.Tag), "/")
    For iInd = 0 To Min(aDir.Max, aBook.Max)
      If aBook[iInd] <> aDir[iInd] Then Break
    Next
    'DEBUG "Comp:";; aBook.Join("|");; aDir.Join("|");; "=";; iInd
    If iInd > iScore And If iInd > aBook.Max Then   
      iScore = iInd
      sBest = hPanel.Tag
    Endif
  Next  
  
  If iScore <= 1 Then sBest = "$/"
  
  For Each hPanel In lstBookmark.Children
    If hPanel.Tag = sBest Then 
      'DEBUG sDir; " -> "; hPanel.Tag
      lstBookmark.Select(hPanel)
      Break 
    Endif
  Next  
  
  
End

Public Sub btnRefresh_Click()

  dvwChoose.Reload
  fvwChoose.Reload
  
End

Public Sub splChoose_Resize()

  'DEBUG splChoose.Layout  

End

Public Sub SetSplitter(bOn As Boolean)
  
  btnView.Visible = bOn
  
End

Public Sub GetSplitter() As Boolean
  
  Return btnView.Visible
  
End

Public Sub Reload()
  
  btnRefresh_Click
  
End

' Public Sub ShowAutomaticExtension(bShow As Boolean)
'   
'   chkExt.Visible = bShow
'   
' End
' 
' Public Sub SetAutomaticExtension(bVal As Boolean)
'   
'   chkExt.Value = bVal
'   
' End
