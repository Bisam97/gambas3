' Gambas class file

Export

Static Property Debug As Boolean
Static Property Read List As String[]

Static Private $bInit As Boolean
Static Private $aIconPath As String[]
Static Private $cIconMap As Collection
Static Private $aList As String[]
Static Private $aSizeDec As Integer[] = [256, 128, 64, 48, 32, 24, 22, 16]
Static Private $aSizeInc As Integer[] = [16, 22, 24, 32, 48, 64, 128, 256]
Static Private $bDebug As Boolean

Class _DefaultStock

Static Private Sub SearchDesktop()
  
  Dim sOutput As String
  
  If Application.Theme Then Return
  
  If Application.Env["KDE_FULL_SESSION"] Then
    If Application.Env["KDE_SESSION_VERSION"] = "4" Then
      Application.Theme = "kde4"
    Else
      Application.Theme = "kde"
    Endif
  Else If Application.Env["GNOME_DESKTOP_SESSION_ID"] Then 
    Application.Theme = "gnome"
  Else 
    Shell "xprop -root XFCE_DESKTOP_WINDOW" To sOutput
    If sOutput Then Application.Theme = "xfce"
  Endif
  
Catch
  
End

Static Private Sub LoadMap(sMap As String)
  
  Dim hFile As File
  Dim sLig As String
  Dim aMap As String[]
  Dim cMap As New Collection
  
  If $cIconMap.Exist(sMap) Then Return
  
  hFile = Open "map" &/ sMap & ".map"
  While Not Eof(hFile)
    Line Input #hFile, sLig
    sLig = Trim(sLig)
    If InStr("';#", Left(sLig)) Then Continue
    aMap = Scan(sLig, "* *")
    If aMap.Count >= 2 Then
      cMap[aMap[0]] = aMap[1]
    Endif
  Wend
  Close #hFile
  
  $cIconMap[sMap] = cMap
  
End

Static Private Sub AddPath(sMap As String, sPath As String, Optional sPattern As String)
  
  Dim aDir As String[]
  
  If Not Exist(sPath) Then Return
  
  If Not sPattern Then
    aDir = Dir(sPath, "[1-9]*", gb.Directory) 
    If aDir.Count > 0 And If aDir[0] Like "*[1-9]x[1-9]*" Then
      sPattern = "&1x&1!&2" 
    Else
      sPattern = "&2!&1"
    Endif
  Endif
  
  sPath &/= sPattern
  
  If sMap Then sPath = sMap & ":" & sPath
  If Not $aIconPath.Exist(sPath) Then 
    $aIconPath.Add(sPath)
    If $bDebug Then Debug sPath
  Endif
  
End

Static Private Sub InitTheme()
  
  Dim hFile As File
  Dim sLig As String
  Dim aMap As String[]
  Dim sStyle As String
  Dim sIconPath As String
  Dim sPath As String
  Dim cMap As Collection
  Dim sTheme As String
  
  If Not $bDebug Then
    If Application.Env["GB_STOCK_DEBUG"] Then $bDebug = True
  Endif
  
  $aIconPath = New String[]
  $cIconMap = New Collection
  
  SearchDesktop
  
  Select Case Application.Theme
      
    Case "gnome"
      
      Try Exec ["gconftool-2", "-g", "/desktop/gnome/interface/icon_theme"] To sTheme
      
      If sTheme Then 
        sTheme = Trim(sTheme)
        GetAllThemePath("gnome", ["~/.icons", "/usr/share/icons"], sTheme)
      Endif
      
      AddPath("gnome", "/usr/share/icons/gnome")
      AddPath("gnome", "/usr/share/icons/hicolor")
      'AddPath("gnome", "/usr/X11R6/share/icons/hicolor")
      
    Case "kde"
      
      Try Exec ["kde-config", "--path", "icon"] To sIconPath
      If sIconPath Then 
        
        sIconPath = Trim(sIconPath)
        
        Try hFile = Open "~/.kde/share/config/kdeglobals"
        If Not Error Then
          While Not Eof(hFile)
            Line Input #hFile, sLig
            If sLig = "[Icons]" Then 
              While Not Eof(hFile)
                Line Input #hFile, sLig
                If Left(sLig) = "[" Then Break
                If sLig Begins "Theme=" Then 
                  sStyle = Mid$(sLig, 7)
                  Break
                Endif 
              Wend
              Break
            Endif 
          Wend
          Close #hFile
          
          If sStyle Then
            GetAllThemePath("kde", Split(sIconPath, ":"), sStyle)
            'For Each sPath In Split(sIconPath, ":")
            '  AddPath("gnome", sPath &/ sStyle)
            'Next
          Endif 
          
        Endif
        
        For Each sPath In Split(sIconPath, ":")
          AddPath("kde", sPath &/ "crystalsvg") ' default theme hardcoded in KDE sources
        Next
        
      Endif
      
    Case "kde4"
      
      Try Exec ["kde4-config", "--path", "icon"] To sIconPath
      If sIconPath Then 
        
        sIconPath = Trim(sIconPath)
        
        Try hFile = Open "~/.kde4/share/config/kdeglobals"
        If Not Error Then
          While Not Eof(hFile)
            Line Input #hFile, sLig
            If sLig = "[Icons]" Then 
              While Not Eof(hFile)
                Line Input #hFile, sLig
                If Left(sLig) = "[" Then Break
                If sLig Begins "Theme=" Then 
                  sStyle = Mid$(sLig, 7)
                  Break
                Endif 
              Wend
              Break
            Endif 
          Wend
          Close #hFile
          
          If sStyle Then
            GetAllThemePath("kde4", Split(sIconPath, ":"), sStyle)
          Endif 
          
        Endif
        
        For Each sPath In Split(sIconPath, ":")
          AddPath("kde4", sPath &/ "oxygen") ' default theme hardcoded in KDE sources
        Next
        
        AddPath("kde4", "/usr/share/icons/hicolor")
        
      Endif
      
    Case "xfce"
      
      Try hFile = Open "~/.config/xfce4/mcs_settings/gtk.xml"
      If Not Error Then 
        While Not Eof(hFile)
          Line Input #hFile, sLig
          If sLig Like "*<option*name=\"Net/IconThemeName\"*" Then 
            Try sTheme = Trim(Scan(sLig, "*<option*value=\"*\"*")[2])
            Break
          Endif          
        Wend
        Close #hFile
      Endif
      
      If sTheme Then 
        GetAllThemePath("gnome", ["~/.icons", "/usr/share/icons"], sTheme)
        'GetAllThemePath("gnome", "/usr/X11R6/share/icons", sTheme)
      Endif 
      
      AddPath("gnome", "/usr/share/icons/gnome")
      
  End Select 
  
  AddPath("", "stock", "&2")
  $aIconPath.Add("#")
  
  $bInit = True
  
End

Static Public Function GetSize(Size As String) As Integer
  
  Dim iSize As Integer
  
  Select Case Size
    Case "small"
      iSize = 16 'CInt(Desktop.Scale * 5 / 8 + 0.5) * 4
    Case "medium"
      iSize = 22 'CInt(Desktop.Scale * 5 / 8 * 1.5 + 0.5) * 4 
    Case "large"
      iSize = 32 'CInt(Desktop.Scale * 5 / 8 + 0.5) * 8
    Case "huge"
      iSize = 48 'CInt(Desktop.Scale * 5 / 8 * 1.5 + 0.5) * 8
    Case Else 
      Try iSize = CInt(Size)
  End Select
  
  Return iSize
  
End

Static Private Sub GetScalablePath(sPath As String) As String
  
  If InStr(sPath, "&1x&1") Then Return Replace(sPath, "&1x&1", "scalable")
  If InStr(sPath, "&1") Then Return Replace(sPath, "&1", "scalable")
  
End

Static Private Sub LoadIcon(sTemplate As String, sFile As String, iSize As Integer, Optional iRealSize As Integer) As Picture
  
  Dim hImage As Image
  Dim hPict As Picture
  Dim sDir As String
  Dim sName As String
  Dim sPath As String
  
  For Each sFile In Split(sFile, ";")
    
    sPath = Subst(sTemplate, CStr(iSize), File.Dir(sFile)) &/ File.Name(sFile)
    sName = File.Name(sPath)
    sDir = File.Dir(sPath)
  
    If iRealSize Then
      
      sName = File.SetExt(sName, "svg")
      Try hPict = PictureFromSvg(SvgImage.Load(sDir &/ sName), iRealSize)
      If Not Error Then Return hPict
      
      sName = File.SetExt(sName, "png")
      Try hImage = Image.Load(sDir &/ sName)
      If Not Error Then Return hImage.Stretch(iRealSize, iRealSize).Picture
      
    Else
      
      sName = File.SetExt(sName, "png")
      Try hImage = Image.Load(sDir &/ sName)
      If Not Error Then Return hImage.Picture
      
      sName = File.SetExt(sName, "svg")
      Try hPict = PictureFromSvg(SvgImage.Load(sDir &/ sName), iSize)
      If Not Error Then Return hPict
      
    Endif
    
  Next
  
End

Static Public Function _get(Key As String) As Picture
  
  Dim sPrefix As String
  Dim sPath As String
  Dim hPict As Picture
  Dim iPos As Integer
  Dim iSize As Integer
  Dim sSize As String
  Dim iTry As Integer
  Dim hImage As Image
  Dim sTemplate As String
  Dim sFile As String
  Dim sMap As String
  Dim sImagePath As String
  Dim sDirPattern As String
  
  If Not $bInit Then InitTheme
  
  iSize = 16
  iPos = InStr(Key, "/")
  If iPos Then 
    sSize = Left$(Key, iPos - 1)
    Key = Mid$(Key, iPos + 1)
    iSize = GetSize(sSize)
  Endif
  
  hPict = Picture[CStr(iSize) &/ Key]
  If hPict Then Return hPict
  
  For Each sPath In $aIconPath
    
    If sPath = "#" Then
      
      If Not Stock.List.Exist(Key) Then Break
      
      Component.Load("gb.form.stock")
      sFile = Key
      
      Try hPict = _DefaultStock.LoadImage(Key, iSize).Picture
      If Not Error Then Goto __RETURN
      Try hPict = PictureFromSvg(_DefaultStock.LoadSvgImage(Key), iSize)
      If Not Error Then Goto __RETURN
      
    Else
      
      iPos = InStr(sPath, ":")
      If iPos Then 
        sMap = Left$(sPath, iPos - 1)
        LoadMap(sMap)  
        sFile = $cIconMap[sMap][Key]
        If Not sFile Then sFile = Key
        sPath = Mid$(sPath, iPos + 1)
      Else 
        sFile = Key
      Endif
      sDirPattern = Replace(File.Name(sPath), "!", "/")
      
      sPath = File.Dir(sPath)
      If Not Exist(sPath) Then Continue
      
      If Left(sPath) = "/" Then   
        sTemplate = sPath &/ sDirPattern
      Else 
        sTemplate = sPath &/ "&1"
      Endif 
      
      hPict = LoadIcon(sTemplate, sFile, iSize)
      If hPict Then Goto __RETURN
      
      hPict = LoadIcon(GetScalablePath(sTemplate), sFile, iSize)
      If hPict Then Goto __RETURN
      
      For Each iTry In $aSizeInc
        If iTry < iSize Then Continue
        hPict = LoadIcon(sTemplate, sFile, iTry, iSize)
        If hPict Then Goto __RETURN
      Next
      
      For Each iTry In $aSizeDec
        If iTry >= iSize Then Continue
        hPict = LoadIcon(sTemplate, sFile, iTry, iSize)
        If hPict Then Goto __RETURN
      Next
        
    Endif
    
  Next
  
__RETURN:

  If Not hPict Then
    Error "warning: unable to load "; CStr(iSize) &/ Key
    hPict = PictureFromSvg(SvgImage.Load("img/unknown.svg"), iSize)
  Endif

  If hPict Then
    Picture[CStr(iSize) &/ Key] = hPict
    Return hPict
  Endif
  
End

Static Private Function List_Read() As String[]
  
  If Not $aList Then 
    $aList = Split(File.Load("map/map"), "\n", "", True)
  Endif 
  
  Return $aList  
  
End

Static Private Sub GetInheritance(aPath As String[], sTheme As String) As String[]
  
  Dim aInheritance As String[] = [sTheme]
  Dim sPath As String
  Dim sLine As String
  
  For Each sPath In aPath
  
    If Exist(sPath &/ sTheme &/ "index.theme") Then 
    
      For Each sLine In Split(File.Load(sPath &/ sTheme &/ "index.theme"), "\n")
        
        If InStr(sLine, "Inherits") Then 
          For Each sTheme In Split(Scan(sLine, "*=*")[1])
            aInheritance.Insert(GetInheritance(aPath, sTheme))
          Next
          Break
        Endif
        
      Next
      
      Break
      
    Endif
    
  Next
  
  Return aInheritance
  
End

Static Private Sub GetAllThemePath(sMap As String, aPath As String[], sTheme As String)
  
  Dim sPath As String
  
  For Each sTheme In GetInheritance(aPath, sTheme)
    For Each sPath In aPath
      AddPath(sMap, sPath &/ sTheme)
    Next
  Next
  
End

Static Private Function PictureFromSvg(hSvg As SvgImage, iSize As Integer) As Picture
  
  Dim hImage As Image
  
  hImage = New Image(iSize, iSize, Color.Transparent)
  Paint.Begin(hImage)
  Paint.Scale(iSize / hSvg.Width, iSize / hSvg.Height)
  hSvg.Paint()
  Paint.End
  
  Return hImage.Picture
  
End

Static Private Function Debug_Read() As Boolean

  Return $bDebug

End

Static Private Sub Debug_Write(Value As Boolean)

  $bDebug = Value

End
