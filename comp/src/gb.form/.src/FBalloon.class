' Gambas class file

Private Const ROUND_SIZE As Integer = 8

Private ARROW_HEIGHT As Integer

Private $hWatcher As Watcher
Private $hWatcher2 As Watcher
Private $hCtrl As Control

Private $X As Integer
Private $Y As Integer
Private $DX As Integer
Private $DY As Integer
Private $dShow As Float
Private $sOld As String
Private $bInside As Boolean
Private $iDelay As Integer = 5000
Private $hRound As Image

Private Sub DrawRoundRect(X As Integer, Y As Integer, W As Integer, H As Integer)

  Dim hRound As Image
  
  If Not $hRound Then $hRound = Image.Load("img/round.png")
  
  hRound = $hRound
  Draw.Image(hRound, X, Y)
  hRound = hRound.Flip()
  Draw.Image(hRound, X + W - ROUND_SIZE, Y)
  hRound = hRound.Mirror()
  Draw.Image(hRound, X + W - ROUND_SIZE, Y + H - ROUND_SIZE)
  hRound = hRound.Flip()
  Draw.Image(hRound, X, Y + H - ROUND_SIZE)

  Draw.FillStyle = Fill.Solid
  Draw.LineStyle = Line.None
  
  Draw.FillColor = &FFFFDF&
  Draw.Rect(X + ROUND_SIZE, Y, W - ROUND_SIZE * 2, ROUND_SIZE)
  Draw.Rect(X, Y + ROUND_SIZE, W, H - ROUND_SIZE * 2)
  Draw.Rect(X + ROUND_SIZE, Y + H - ROUND_SIZE, W - ROUND_SIZE * 2, ROUND_SIZE)
  Draw.LineStyle = Line.Solid
  Draw.ForeColor = &H808080& '&H808070&
  Draw.Line(X + ROUND_SIZE, Y, X + W - ROUND_SIZE - 1, Y)
  Draw.Line(X + ROUND_SIZE, Y + H - 1, X + W - ROUND_SIZE, Y + H - 1)
  Draw.Line(X, Y + ROUND_SIZE, X, Y + H - ROUND_SIZE - 1)
  Draw.Line(X + W - 1, Y + ROUND_SIZE, X + W - 1, Y + H - ROUND_SIZE - 1)
  
End

Private Sub GetTopLevel(hCtrl As Control) As Window
  
  Dim hWin As Window

  If Not hCtrl Then Return  
  hWin = hCtrl.Window
  Do
    If hWin.TopLevel Then Return hWin
    hWin = hWin.Parent.Window
  Loop
  
End


Private Sub MakeBorder(Optional W As Integer, H As Integer)
  
  Dim hPict As Picture
  Dim hImage As Image
  Dim SX, SY, CX, CY As Integer
  Dim X, Y, X1, Y1, X2, Y2, X3, Y3, Y4 As Integer
  Dim hWin As Window
  Dim sNew As String
  Dim hCtrl As Control
  
  $X = $hCtrl.ScreenX
  $Y = $hCtrl.ScreenY

  If $DX < 0 Then
    SX = $X + $hCtrl.W \ 2
  Else 
    SX = $X + $DX
  Endif 
  If $DY < 0 Then 
    SY = $Y + $hCtrl.H \ 2
  Else 
    SY = $Y + $DY
  Endif
  
  'DEBUG $hCtrl;; "SX =";; SX;; "SY =";; SY
  
  hWin = GetTopLevel($hCtrl)
  'DEBUG hWin
  
  CX = hWin.ScreenX + hWin.W \ 2
  CY = hWin.ScreenY + hWin.H \ 2
  
  'DEBUG "CX =";; CX;; "CY =";; CY
'   IF SX < (Desktop.W \ 2) THEN 
'     X = Max(0, SX - ME.W \ 2 - 32)
'   ELSE 
'     X = Min(Desktop.W - ME.W, SX - ME.W \ 2 + 32)
'   ENDIF
' 
  If W <= 0 Then W = Me.W
  If H <= 0 Then H = Me.H

  If SX < CX And SX >= W Then 
    X = SX - W \ 2 - ARROW_HEIGHT
  Else 
    X = SX - W \ 2 + ARROW_HEIGHT
  Endif

  X = Max(4, Min(Desktop.W - W - 4, X))

  'IF SY < (Desktop.H \ 2) THEN 
  
  'DEBUG SY;; CY;; ME.H
  
  If (SY >= CY And SY < (Desktop.H - H)) Or SY < H Then 
    Y1 = 0
    Y4 = ARROW_HEIGHT
    Y2 = Y4 + 1
    Y = SY
  Else 
    Y1 = H - 1
    Y4 = H - ARROW_HEIGHT - 1
    Y2 = Y4 - 1
    Y = SY - H
  Endif
  
  Y = Max(4, Min(Desktop.H - H - 4, Y))
  
  'DEBUG "X =";; X;; "Y =";; Y;; "W =";; W;; "H ="; H

  Y3 = Y2
  
  X1 = Min(Max(16, SX - X), W - 16)
  'X2 = W \ 2 - 12
  'X3 = X2 + 24
  If X1 < (W \ 2) Then 
    X2 = X1
  Else
    X2 = X1 - ARROW_HEIGHT
  Endif
  X2 = Max(X2, 16)
  X3 = X2 + ARROW_HEIGHT

  Me.Move(X, Y, W, H)
  
  sNew = ARROW_HEIGHT & " " & Me.W & " " & Me.H & " " & X1 & " " & Y1 & " " & X2 & " " & Y2 & " " & X3 & " " & Y3
  If sNew <> $sOld Then

    'DEBUG sNew
    'ME.Hide

    hPict = New Picture(Me.W, Me.H)
  
    Draw.Begin(hPict)
    
    Draw.FillRect(0, 0, Draw.W, Draw.H, Color.White)
    DrawRoundRect(0, ARROW_HEIGHT, Me.W, Me.H - ARROW_HEIGHT * 2)

    Draw.FillStyle = Fill.Solid
    Draw.LineStyle = Line.None
    
    Draw.FillColor = &H808080& '&808070&
    'DrawInternalRect(0, ARROW_HEIGHT, ME.W, ME.H - ARROW_HEIGHT * 2)
    Draw.Polygon([X1, Y1, X2, Y2, X3, Y3])

    Draw.FillColor = &FFFFDF&
    'DrawInternalRect(1, ARROW_HEIGHT + 1, ME.W - 2, ME.H - (ARROW_HEIGHT + 1) * 2)
    Draw.Polygon([X1, Y1, X2, Y2, X3, Y3])

    Draw.LineStyle = Line.Solid
    Draw.Foreground = &FFFFDF&
    Draw.Line(X1, Y1, X2 + 1, Y4)
    Draw.Line(X1, Y1, X3 - 1, Y4)
    Draw.Foreground = &H808080& '&808070&
    Draw.Line(X1, Y1, X2, Y4)
    Draw.Line(X1, Y1, X3, Y4)
  
    Draw.End
    
    hImage = hPict.Image
    hImage.Replace(Color.White, Color.Transparent)
    'hImage.MakeTransparent()
    hPict = hImage.Picture
  
    Me.Picture = hPict
    
    $sOld = sNew
    
  Endif 
  
  hCtrl = Application.ActiveControl
  
  If Not Me.Visible Then 
    hWin = Application.ActiveWindow
    Me.Show
    ' If hWin Then 
    '   Debug hWin
    '   hWin.Show
    '   Wait
    '   hWin.Show
    ' Endif
  Else  
    Form_Show
    Me.Raise
  Endif
  
  Try hCtrl.SetFocus
  
  $dShow = Timer
  
End

Public Sub Run(sMsg As String, hCtrl As Control, hIcon As Picture, DX As Integer, DY As Integer)
  
  Dim X, Y, W, H As Integer
  Dim hWin As Window
  
  'IF hCtrl <> $hCtrl THEN 
  '  DEBUG
  '  ME.Hide
  'ELSE
    Form_Hide
  'ENDIF
  
  txtMessage.W = Max(hCtrl.W / 2, 192)
  txtMessage.Text = Replace(sMsg, "\n", "<br>")
  txtMessage.Adjust
  If txtMessage.W < 64 Then txtMessage.W = 64
  'txtMessage.Adjust
  'DEBUG txtMessage.W

  $hCtrl = hCtrl
  ARROW_HEIGHT = Min(16, $hCtrl.H \ 4 + 8)
  $DX = DX
  $DY = DY

  hWin = GetTopLevel($hCtrl)
  If Not hWin.Modal Then hWin.Show
  
  $hWatcher = New Watcher(hWin) As "Watcher"
  $hWatcher2 = New Watcher($hCtrl) As "Watcher"

  If hIcon Then 
    imgIcon.Picture = hIcon
    imgIcon.Resize(hIcon.Width, hIcon.Height)
    imgIcon.Show
    W = txtMessage.W + 24 + imgIcon.W + 8
    H = Max(txtMessage.H, imgIcon.H) + 24 + ARROW_HEIGHT * 2
    imgIcon.Move(12, 12 + ARROW_HEIGHT)
    txtMessage.Move(imgIcon.X + imgIcon.W + 8, ARROW_HEIGHT + 12) ', ME.W - 32 - imgIcon.W - 8)
  Else
    imgIcon.Hide
    W = txtMessage.W + 24
    H = txtMessage.H + 24 + ARROW_HEIGHT * 2
    txtMessage.Move(12, ARROW_HEIGHT + 12) ', ME.W - 32)
  Endif

  MakeBorder(W, H)
  
End


Public Sub Form_Show()

  'DEBUG
  'timCheck.Enabled = FALSE
  'GetTopLevel($hCtrl).Show
  timCheck.Delay = timCheck.Delay
  timCheck.Enabled = True

End

Public Sub Form_Hide()

  'DEBUG
  timCheck.Enabled = False
  $hWatcher = Null
  $hWatcher2 = Null

End

Public Sub Watcher_Hide()
  
  'Debug
  Me.Hide
  
End

' PUBLIC SUB Form_Deactivate()
'   
'   ME.Hide
'   
' END

Public Sub txtMessage_MouseDown()

  'Debug 
  Me.Hide

End

Public Sub Form_MouseDown()

  'Debug 
  Me.Hide 

End

Public Sub Watcher_Move()
  
  If Abs($hCtrl.ScreenX - $X) >= 4 Or Abs($hCtrl.ScreenY - $Y) >= 4 Then
    MakeBorder      
  Endif
  
End


Public Sub timCheck_Timer()

  Dim hWin As Window

  'DEBUG

  If $bInside Then 
    'Debug
    Me.Hide
    Return 
  Endif 

  If (Timer - $dShow) > ($iDelay / 1000) Then 
    'Debug
    Me.Hide
    Return 
  Endif
  
  hWin = GetTopLevel($hCtrl)
  If Not hWin.TopOnly Then
    If GetTopLevel(Application.ActiveWindow) <> hWin Then 
      'Debug
      Me.Hide
      Return
    Endif
  Endif
  
  If Abs($hCtrl.ScreenX - $X) >= 4 Or Abs($hCtrl.ScreenY - $Y) >= 4 Then
    MakeBorder
  Endif
  
End

Public Sub GetControl() As Control
  
  If Me.Visible Then Return $hCtrl
  
End


Public Sub GetDelay() As Integer
  
  Return $iDelay
  
End

Public Sub SetDelay(iDelay As Integer)

  $iDelay = iDelay
  
End

