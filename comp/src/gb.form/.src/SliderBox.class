' Gambas class file

Export
Inherits UserControl

Public Const _Properties As String = "*,Picture,MinValue=0,MaxValue=100,Step=1,DefaultValue,Value"
Public Const _DefaultEvent As String = "Change"
Public Const _DefaultSize As String = "32,4"
Public Const _Similar As String = "Slider"

Event Change

Property Value As Integer
Property MinValue As Integer
Property MaxValue As Integer
Property Step As Integer
Property DefaultValue As Integer
Property Font As Font
Property Picture As Picture

Private $hSlider As Slider
Private $hSpinBox As SpinBox
Private $iStep As Integer = 1
Private $iDefault As Integer
Private $hPictureBox As PictureBox
Private $hWatcher As Watcher

Public Sub _new()
  
  Me._Arrangement = Arrange.Horizontal
  Me._Spacing = True
  
  $hSlider = New Slider(Me) As "Slider"
  $hSlider.Expand = True
  $hSlider.PageStep = 1
  $hSlider.Orientation = ScrollBar.Horizontal
  
  $hSpinBox = New SpinBox(Me) As "SpinBox"
  $hSpinBox.Alignment = Align.Right
  
  $hWatcher = New Watcher(Me) As "UserControl"
  
  UpdateSpinBoxWidth
  
End

Public Sub UserControl_Resize()
  
  UpdateSpinBoxWidth
  
End

Private Function Value_Read() As Integer

  Return $hSlider.Value

End

Private Sub Value_Write(Value As Integer)

  $hSlider.Value = Value
  $hSpinBox.Value = Value

End

Private Function MinValue_Read() As Integer

  Return $hSlider.MinValue

End

Private Sub UpdateSpinBoxWidth()

  Static bUpdating As Boolean

  Dim DS As Integer = Desktop.Scale
  Dim W As Integer
  Dim MW As Integer

  If bUpdating Then Return 
  bUpdating = True

  MW = DS * 8
  If $hPictureBox Then MW += $hPictureBox.W
  
  W = $hSpinBox._GetIdealWidth(True)
  If Me.W < MW + W Then W = $hSpinBox._GetIdealWidth(False)
  
  $hSpinBox.W = W
  
  bUpdating = False

End

Private Sub MinValue_Write(Value As Integer)

  $hSlider.MinValue = Value
  $hSpinBox.MinValue = Value
  UpdateSpinBoxWidth

End

Private Function MaxValue_Read() As Integer

  Return $hSlider.MaxValue

End

Private Sub MaxValue_Write(Value As Integer)

  $hSlider.MaxValue = Value
  $hSpinBox.MaxValue = Value
  UpdateSpinBoxWidth

End

Public Sub Slider_Change()
  
  $hSpinBox.Value = $hSlider.Value
  Raise Change
  
End

Public Sub SpinBox_Change()
  
  $hSlider.Value = $hSpinBox.Value
  
End

Private Function Step_Read() As Integer

  Return $iStep

End

Private Sub Step_Write(Value As Integer)

  $iStep = Value
  $hSlider.PageStep = Value

End

Public Sub SpinBox_MouseWheel()
  
  If Not Me.Enabled Then Return
  $hSpinBox.Value += If(Mouse.Forward, $iStep, -$iStep)
  Stop Event
  
End

Private Function DefaultValue_Read() As Integer

  Return $iDefault

End

Private Sub DefaultValue_Write(Value As Integer)

  $iDefault = Value

End

Public Sub SpinBox_DblClick()
  
  $hSlider.Value = $iDefault
  Stop Event
  
End


Private Function Font_Read() As Font

  Return Super.Font

End

Private Sub Font_Write(Value As Font)

  Super.Font = Value
  UpdateSpinBoxWidth

End

Public Sub SpinBox_KeyPress()

  If Key.Code = Key.Esc Then
    $hSlider.Value = $iDefault
    Stop Event
  Endif
  
End

Private Function Picture_Read() As Picture

  If $hPictureBox Then Return $hPictureBox.Picture

End

Private Sub Picture_Write(Value As Picture)

  If Value Then
    If Not $hPictureBox Then 
      $hPictureBox = New PictureBox(Me)
      $hPictureBox.Lower
      $hPictureBox.Mode = PictureBox.Contain
      $hPictureBox.W = Desktop.Scale * 4
      $hPictureBox.Padding = Desktop.Scale \ 2
    Endif
    $hPictureBox.Picture = Value
  Else
    If $hPictureBox Then 
      $hPictureBox.Delete
      $hPictureBox = Null
    Endif
  Endif

End
