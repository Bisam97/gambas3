' Gambas module file

' TODO: Remove URL, JSON, JSONCollection and use 'gb.util.web' instead. Do that later!

Export

' Property Protocol As String
' Property User As String
' Property Password As String
' Property Host As String
' Property Port As Integer
' Property Path As String
' Property Read Query As _URL_Query
' Property EncodedQuery As String
' Property Fragment As String
' 
' Private $sProtocol As String
' Private $sUser As String
' Private $sPassword As String
' Private $sHost As String
' Private $iPort As Integer
' Private $sPath As String
' Private $sEncodedQuery As String
' 'Private $cQuery As Collection
' 'Private $cVoidQuery As Collection
' Private $sFragment As String
' 
' Public _Dirty As Boolean

Public Sub Quote(Path As String) As String
  
  Dim iInd As Integer
  Dim sRes As String
  Dim sCar As String
  
  For iInd = 1 To Len(Path)
    sCar = Mid$(Path, iInd, 1)
    If sCar = " " Then 
      sCar = "+"
    Else If IsLetter(sCar) Or If IsDigit(sCar) Or If InStr("-._~,$!", sCar) Then 
    Else 
      sCar = "%" & Hex$(Asc(sCar), 2)
    Endif
    sRes &= sCar
  Next

  Return sRes
  
End

Public Sub Encode(Path As String) As String
  
  Return Quote(Path)
  
End


Public Sub UnQuote(Path As String) As String
  
  Dim iInd As Integer
  Dim sRes As String
  Dim sCar As String
  
  For iInd = 1 To Len(Path)
    sCar = Mid$(Path, iInd, 1)
    If sCar = "%" Then 
      sCar = Chr$(Val("&H" & Mid$(Path, iInd + 1, 2)))
      iInd += 2
    Else If sCar = "+" Then 
      sCar = " "
    Endif
    sRes &= sCar
  Next

  Return sRes  
  
End

Public Sub Decode(Path As String) As String
  
  Return UnQuote(Path)
  
End


Private Sub HandleQuery(URL As String, bClear As Boolean, Field As String, Value As String) As String
  
  Dim I, iPos As Integer
  Dim sElt As String
  Dim aQuery As String[]
  
  iPos = InStr(URL, "?")
  If iPos = 0 Then 
    If bClear Then
      Return URL
    Else
      Return URL & "?" & {Quote}(Field) & "=" & {Quote}(Value)
    Endif
  Endif
  
  aQuery = Split(Mid$(URL, iPos + 1), "&")
  URL = Left$(URL, iPos - 1)
  
  Field = {Quote}(Field)
  Value = {Quote}(Value)
  
  For I = 0 To aQuery.Max
    sElt = aQuery[I]
    If sElt = Field Or If sElt Begins Field & "=" Then
      aQuery.Remove(I)
      Break
    Endif
  Next
  
  If Not bClear Then
    If Value Then
      aQuery.Add(Field & "=" & Value)
    Else
      aQuery.Add(Field)
    Endif
  Endif
  
  If aQuery.Count Then
    Return URL & "?" & aQuery.Join("&")
  Else
    Return URL
  Endif
  
End

' TODO: Use IsMissing()

Public Sub SetQuery(URL As String, Field As String, Value As String) As String
  
  Return HandleQuery(URL, False, Field, Value)
  
End

Public Sub UnsetQuery(URL As String, Field As String) As String
  
  Return HandleQuery(URL, True, Field, "")
  
End



