' Gambas module file

Export 

Property Read Fields As String[]
Property Read Cookies As Collection
Property Read Method As String
Property Read Path As String
Property Read Query As String
Property Read Files As Collection
Property Delimiter As String
Property Read InternetExplorer As Integer
Property Read Get As _RequestGet
'Property Read Post As _RequestPost

Private $cCookies As Collection
Private $cVal As Collection
Private $aField As String[]
Private $iMaxLength As Integer
Private $sBuffer As String
Private $bEOF As Boolean
Private $sDelimiter As String = "&"
Private $iIE As Integer
Private $bDecoded As Boolean
Private $bPost As Boolean

Private Sub Decode()

  If $bDecoded Then Return
  $bPost = CGI["REQUEST_METHOD"] = "POST"
  $bDecoded = True
  
End

Public Sub _init()
  
  Dim sAgent As String = CGI["HTTP_USER_AGENT"]
  Dim aScan As String[] = Scan(sAgent, "*; MSIE *;*")
  Try $iIE = Val(aScan[1])
  
End

Public Sub _get(Key As String) As String

  Dim sVal As String
  
  If $cVal And If $cVal.Exist(Key) Then Return $cVal[Key]
  
  Decode
  
  If $bPost Then
    sVal = _RequestPost[Key]
    If sVal Then Return sVal
  Endif
  
  Return _RequestGet[Key]
  
End

Public Sub _put(Value As String, Key As String)
  
  If Not $cVal Then $cVal = New Collection
  $cVal[Key] = Value
  
End

Private Function Fields_Read() As String[]

  Dim sField As String

  If Not $aField Then
    If Not $bPost Then
      $aField = _RequestGet.Fields
    Else
      $aField = _RequestGet.Fields.Copy()
      For Each sField In _RequestPost.Fields
        If Not $aField.Exist(sField) Then $aField.Add(sField)
      Next
    Endif
  Endif
  
  Return $aField

End

Private Function Cookies_Read() As Collection

  Dim sCookie As String
  Dim aCookie As String[]

  If Not $cCookies Then
    
    $cCookies = New Collection
    
    For Each sCookie In Split(CGI["HTTP_COOKIE"], ";")
      sCookie = Trim(sCookie)
      aCookie = Split(sCookie, "=")
      $cCookies[aCookie[0]] = aCookie[1]
    Next
 
  Endif

  Return $cCookies

End


Private Function Method_Read() As String

  Return CGI["REQUEST_METHOD"]  

End

Private Function Path_Read() As String

  Return CGI["PATH_INFO"]  

End

Private Function Query_Read() As String

  Return CGI["QUERY_STRING"]  

End

Private Function Files_Read() As Collection

  Return _RequestPost.Files

End

Public Sub Exist(Field As String) As Boolean
  
  Fields_Read()
  Return $aField.Exist(Field)
  
End


Private Function Delimiter_Read() As String

  Return $sDelimiter  

End

Private Sub Delimiter_Write(Value As String)

  $sDelimiter = Value

End

Private Function InternetExplorer_Read() As Integer

  Return $iIE  

End

Private Function Get_Read() As _RequestGet

  Return _RequestGet

End
