' Gambas module file

Export 

Property Read Fields As String[]
Property Read Cookies As Collection
Property Read Method As String
Property Read Path As String
Property Read Query As String
Property Read Files As Collection
Property Delimiter As String
Property Read InternetExplorer As Integer

Private $cCookies As Collection
Private $cVal As Collection
Private $cFiles As Collection
Private $aField As New String[]
Private $iMaxLength As Integer
Private $sBuffer As String
Private $bEOF As Boolean
Private $sDelimiter As String = "&"
Private $iIE As Integer

Private Sub ReadLine() As String
  
  Dim iPos As Integer
  Dim sLine As String
  Dim sData As String

  Do

    iPos = InStr($sBuffer, "\r\n")
    If iPos Then 
      sLine = Left($sBuffer, iPos - 1)
      $sBuffer = Mid$($sBuffer, iPos + 2)
      Break
    Endif
    
    If $iMaxLength = 0 Then
      $bEOF = True
      sLine = $sBuffer
      $sBuffer = ""
      Break
    Endif
    
    sData = Read Min(4096, $iMaxLength)
    $iMaxLength -= Len(sData)
    $sBuffer &= sData
    
  Loop

  Return sLine
  
End


Private Sub DecodeURL(sUrl As String, cVal As Collection)
  
  Dim sElt As String
  Dim aElt As String[]
  Dim sKey As String
  Dim sVal As String
  Dim iPos As Integer
  Dim iPos2 As Integer
  Dim sCar As String

  sUrl = Trim(sUrl)
  
  For Each sElt In Split(sUrl, $sDelimiter)
  
    If Not sElt Then Continue
  
    aElt = Split(sElt, "=")
    If aElt.Count = 2 Then
      sKey = CGI.Decode(aElt[0])
      If Not sKey Then Continue
      sVal = CGI.Decode(aElt[1])
    Else 
      sKey = CGI.Decode(aElt[0])
      If Not sKey Then Continue
      sVal = ""
    Endif
    
    If Not $aField.Exist(sKey) Then
      $aField.Add(sKey)
      cVal[sKey] = sVal
    Else If sVal Then 
      'IF TypeOf(cVal[sKey]) = gb.String THEN 
      '  cVal[sKey] = [CStr(cVal[sKey]), sVal]
      'ELSE  
      '  cVal[sKey].Add(sVal)
      'ENDIF
      cVal[sKey] &= "\n" & sVal
    Endif
  
  Next  
 
End


Private Sub DecodeQueryForm()
  
  Dim sStr As String
  Dim sType As String
  Dim sLine As String
  Dim aElt As String[]
  Dim sElt As String
  Dim sName As String
  Dim sFileName As String
  Dim sValue As String
  Dim iLength As Integer
  Dim bFile As Boolean
  Dim bFirst As Boolean
  Dim hFile As File
  
  $cVal = New Collection
  $cFiles = New Collection

  If CGI["REQUEST_METHOD"] = "GET" Then
    DecodeURL(CGI["QUERY_STRING"], $cVal)  
    Return
  Endif

  If CGI["REQUEST_METHOD"] <> "POST" Then Return
  
  File.In.EndOfLine = gb.Windows
  
  sType = CGI["CONTENT_TYPE"]
  $iMaxLength = CGI["CONTENT_LENGTH"]
  'DEBUG sType
  
  If sType = "application/x-www-form-urlencoded" Then 
    sStr = Read $iMaxLength
    'LINE INPUT sStr
    DecodeURL(sStr, $cVal)
    Return
  Endif
    
  If Not (sType Begins "multipart/form-data") Then Return
  
  'SHELL "echo MaxLength = " & CStr($iMaxLength) & " >> /var/log/thttpd/debug.log" WAIT
  'sType = Scan(sType, "*boundary=*")[1]
  
  sType = ReadLine()
  'IF sLine <> sType THEN RETURN
  
  While Not $bEOF
  
    sLine = ReadLine()
    
    If sLine Begins "Content-Disposition:" Then 
      bFile = False
      sFileName = ""
      aElt = Split(sLine, ";", Chr$(34))
      For Each sElt In aElt
        sElt = Trim(sElt)
        'SHELL "echo " & Quote.Shell(sElt) & " >> /var/log/thttpd/debug.log" WAIT
        If sElt Begins "name=" Then 
          sName = Split(sElt, "=", Chr$(34))[1]
        Else If sElt Begins "filename=" Then
          bFile = True
          sFileName = Split(sElt, "=", Chr$(34))[1]
        Endif
      Next
      Continue
    Endif
    
    If sLine Then Continue

    If bFile Then 
    
      sValue = sFileName
      If sValue Then
        sFileName = Temp$()
        $cFiles[sName] = sFileName
        
        hFile = Open sFileName For Create
  
        bFirst = True    
        Do
          sLine = ReadLine()
          If Left(sLine, Len(sType)) = sType Then Break
          If Not bFirst Then Print #hFile, "\r\n";
          Print #hFile, sLine;
          bFirst = False
        Loop
        
        Close #hFile
      Else
        Do
          sLine = ReadLine()
          If Left(sLine, Len(sType)) = sType Then Break
        Loop
      Endif

      'SHELL "echo filename = " & Quote.Shell(sFileName) & " >> /var/log/thttpd/debug.log" WAIT

    Else
    
      sValue = ""
      Do
        sLine = ReadLine()
        If Left(sLine, Len(sType)) = sType Then Break
        If sValue Then sValue &= "\r\n"
        sValue &= sLine
      Loop
      
      'SHELL "echo value = " & Quote.Shell(sValue) & " >> /var/log/thttpd/debug.log" WAIT

    Endif
    
    If sName Then 
      If Not $aField.Exist(sName) Then 
        $aField.Add(sName)
        $cVal[sName] = sValue
      Else 
        ' IF TypeOf($cVal[sName]) = gb.String THEN 
        '   $cVal[sName] = [CStr($cVal[sName]), sValue]
        ' ELSE  
        '   $cVal[sName].Add(sValue)
        ' ENDIF
        $cVal[sName] &= "\n" & sValue
      Endif
    Endif
      
    If Right(sLine, 2) = "--" Then Break

  Wend 
    
  'SHELL "echo =DONE= >> /var/log/thttpd/debug.log" WAIT

End

Public Sub _init()
  
  Dim sAgent As String = CGI["HTTP_USER_AGENT"]
  Dim aScan As String[] = Scan(sAgent, "*; MSIE *;*")
  Try $iIE = Val(aScan[1])
  
End


Public Sub _get(Key As String) As String
  
  If Not $cVal Then DecodeQueryForm
  Return $cVal[Key]
  
End

Public Sub _put(Value As Variant, Key As String)
  
  If Not $cVal Then $cVal = New Collection
  $cVal[Key] = Value
  
End

Private Function Fields_Read() As String[]

  If Not $cVal Then DecodeQueryForm
  Return $aField

End

Private Function Cookies_Read() As Collection

  Dim sCookie As String
  Dim aCookie As String[]

  If Not $cCookies Then
    
    $cCookies = New Collection
    
    For Each sCookie In Split(CGI["HTTP_COOKIE"], ";")
      sCookie = Trim(sCookie)
      aCookie = Split(sCookie, "=")
      $cCookies[aCookie[0]] = aCookie[1]
    Next
 
  Endif

  Return $cCookies

End


Private Function Method_Read() As String

  Return CGI["REQUEST_METHOD"]  

End

Private Function Path_Read() As String

  Return CGI["PATH_INFO"]  

End

Private Function Query_Read() As String

  Return CGI["QUERY_STRING"]  

End

Private Function Files_Read() As Collection

  Return $cFiles  

End

Public Sub Exist(Field As String) As Boolean
  
  Return $aField.Find(Field) >= 0
  
End


Private Function Delimiter_Read() As String

  Return $sDelimiter  

End

Private Sub Delimiter_Write(Value As String)

  $sDelimiter = Value

End

Private Function InternetExplorer_Read() As Integer

  Return $iIE  

End
