' Gambas class file

Export

Property Coordinates As Float[] Use $aCoordinates
Property ColorStops As Variant[] Use $aColorStops

Property Type As Integer Use $iType

Public Enum Linear, Radial

Public Sub _new(Optional Mode As Integer = 0, Optional (Coordinates) As Integer[])

  $iType = Mode
  $aCoordinates = Coordinates
  
End

'' Add a single gradient point , Pos is a positional number between 0 and 1 , Color is either a gb Color value or a CSS color value.
Public Sub AddColorStop(Pos As Float, {Color} As Variant)
  
  If TypeOf({Color}) = gb.Integer Then {Color} = WebControl._GetColor({Color})
  If Not $aColorStops Then $aColorStops = New Variant[]
  $aColorStops.Add([Pos, {Color}])
  
End

'' Add a single gradient point , Pos is a positional number between 0 and 1 , Color is either a gb Color value or a CSS color value.
Public Sub AddColorStops(Positions As Float[], Colors As Variant[])
  
  For c As Integer = 0 To Colors.Max
    AddColorStop(Positions[c], Colors[c])
  Next
  
End

Public Sub _call() As String
  
  If Not $aCoordinates Then Error.Raise("No Coordinates")
  If Not $aCoordinates.Count Then Error.Raise("No Coordinates")
  If Not $aColorStops Then Error.Raise("No ColorStops")
  Dim sText As String = "gw.gradient.getvar(" & JS($iType) & "," & JS($aCoordinates) & "," & JS($aColorStops) & ")"
  Return sText
  
End
