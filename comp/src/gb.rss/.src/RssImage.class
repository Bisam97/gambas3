' Gambas class file

Export

Public Url As String
Public Title As String
Public {Link} As String
Public Width As Integer
Public Height As Integer
Public Description As String

Public Sub _Write(hWriter As XmlWriter)
  If Not Url Or If Not Title Or If Not {Link} Then Error.Raise(("Url, Title and Link must be set in RssImage"))
  With hWriter
    .StartElement("image")
      .Element("url", Url)
      .Element("title", Title)
      .Element("link", {Link})
      If Width Then .Element("width", Str$(Width))
      If Height Then .Element("height", Str$(Height))
      If Description Then .Element("description", Description)
    .EndElement()
  End With
End

Public Sub _Read(hReader As XmlReader)
  Dim iDepth As Integer = hReader.Depth

  hReader.Read()
  While Rss._NotClosed(hReader, iDepth)
    Select Case hReader.Node.Name
      Case "url"
        Url = Rss._GetText(hReader)
      Case "title"
        Title = Rss._GetText(hReader)
      Case "link"
        {Link} = Rss._GetText(hReader)
      Case "width"
        Width = CInt(Rss._GetText(hReader))
      Case "height"
        Height = CInt(Rss._GetText(hReader))
      Case "description"
        Description = Rss._GetText(hReader)
      Default
        Error.Raise(Subst$(("Unexpected element '&1' in <image>"), hReader.Node.Name))
    End Select
    hReader.Read()
  Wend
End
