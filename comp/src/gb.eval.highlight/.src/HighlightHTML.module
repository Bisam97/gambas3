' Gambas module file

'Fast

Public Const STYLE_HTML As Integer = 1
Public Const STYLE_WEB As Integer = 2

Private Enum TAG_HTML, TAG_CODE, TAG_SCRIPT, TAG_STYLE, TAG_CODE_MARKUP, TAG_COMMENT, TAG_VARIABLE

Private Sub AnalyzeCode(sText As String) As String
  
  Dim iInd As Integer
  Dim iPos As Integer
  Dim iLen As Integer
  
  If Not sText Then Return
  
  Highlight.Analyze(sText, True)
  
  iPos = 0
  For iInd = 0 To Highlight.Symbols.Max
    
    If iPos < Highlight.Positions[iInd] Then 
      Highlight.Add(Highlight.Normal, Highlight.Positions[iInd] - iPos)
      iPos = Highlight.Positions[iInd]
    Endif
    
    iLen = String.Len(Highlight.Symbols[iInd])
    Highlight.Add(Highlight.Types[iInd], iLen)
    iPos += iLen
    
  Next

  If iPos < String.Len(Highlight.TextAfter) Then 
    Highlight.Add(Highlight.Normal, String.Len(Highlight.TextAfter) - iPos)
  Endif
  
  Return Highlight.TextAfter

End

Private Sub IsMarkup(sStr As String, iPos As Integer, sMarkup As String) As Boolean
  
  Dim sTest As String = String.Mid$(sStr, iPos, Len(sMarkup))
  
  If sTest = sMarkup Or If sTest = (Left(sMarkup, -1) & " ") Then Return True
  
End


Public Sub Run(iStyle As Integer)

  Dim iState As Integer
  Dim iNextState As Integer
  Dim iSaveState As Integer
  Dim iInd As Integer
  Dim J As Integer
  Dim sText As String
  Dim sOrgText As String
  Dim sTextAfter As String
  Dim sCar As String
  Dim iPos As Integer
  Dim bMarkup As Boolean
  Dim bQuoteMarkup As Boolean
  Dim bQuote As Boolean
  Dim bLimit As Boolean
  Dim bCode As Boolean
  Dim iTag As Integer
  Dim iCount As Integer
  Dim iCodeStart As Integer
  Dim bScript, bStyle As Boolean
  Dim sMarkup As String
  Dim bWebComment As Boolean
  Dim sStr As String
  Dim bCodeMarkup As Boolean
  Dim sBufferAfter As String

  iState = Highlight.State
  iTag = Highlight.Tag
  sText = Highlight.Text
  sOrgText = sText

  'PRINT "Highlight:";; iState;; iTag;; sText

  Select Case iStyle
  
    Case STYLE_HTML, STYLE_WEB

      Select Case iTag
        Case TAG_HTML
          bMarkup = iState = Highlight.Keyword
        Case TAG_CODE
          bCode = True
          iCodeStart = 1
        Case TAG_SCRIPT
          bScript = True
        Case TAG_STYLE
          bStyle = True
      End Select
    
      For iInd = 1 To String.Len(sText)
    
        'If iInd Mod 10000 = 0 Then Print iInd
    
        iNextState = iState
        sCar = String.Mid$(sText, iInd, 1)
        'sCar = String.Left(sText)
        'sText = String.Mid$(sText, 2)
    
        If bCode And If Not bCodeMarkup Then
        
          If bQuote Then
          
            If sCar = Chr$(34) Then 
              bQuote = False
            Endif
          
          Else If sCar = Chr$(34) Then
          
            bQuote = True
          
          Else If sCar = "'" Then
            
            Break
            
          Else If String.Mid$(sText, iInd, 2) = "%>" Then
            
            bCode = False
            
            Select Case iTag
              Case TAG_COMMENT
                sBufferAfter &= String.Mid$(sText, iCodeStart, iInd - iCodeStart) & "%>"
                GoSub ADD_TEXT_AFTER
                Highlight.Add(Highlight.Comment, iInd - iCodeStart + 2)
              Case TAG_VARIABLE
                sBufferAfter &= String.Mid$(sText, iCodeStart, iInd - iCodeStart) & "%>"
                GoSub ADD_TEXT_AFTER
                Highlight.Add(Highlight.Datatype, iInd - iCodeStart + 2)
              Case Else
                sBufferAfter &= AnalyzeCode(String.Mid$(sText, iCodeStart, iInd - iCodeStart))
                GoSub ADD_TEXT_AFTER
                Highlight.Add(Highlight.Preprocessor, 2)
                sBufferAfter &= "%>"
                GoSub ADD_TEXT_AFTER
            End Select
            
            Inc iInd
            Highlight.AlternateState = False
            iState = iSaveState 'Highlight.Normal
            iNextState = iState
          
          Endif
          Continue
          
        Else If bScript Then
          
          If String.Mid$(sText, iInd, 9) = "</script>" Then
            bScript = False
            Highlight.AlternateState = False
            Highlight.Add(Highlight.Keyword, 9)
            iInd += 8
            sBufferAfter &= "</script>"
            GoSub ADD_TEXT_AFTER
            iState = iSaveState 'Highlight.Normal
            iNextState = iState
          Else
            sBufferAfter &= sCar
            GoSub ADD_TEXT_AFTER
            Highlight.Add(iState)
            iState = iNextState
          Endif
          Continue

        Else If bStyle Then
          
          If String.Mid$(sText, iInd, 8) = "</style>" Then
            bStyle = False
            Highlight.AlternateState = False
            Highlight.Add(Highlight.Keyword, 8)
            iInd += 7
            sBufferAfter &= "</style>"
            GoSub ADD_TEXT_AFTER
            iState = iSaveState 'Highlight.Normal
            iNextState = iState
          Else
            sBufferAfter &= sCar
            GoSub ADD_TEXT_AFTER
            Highlight.Add(iState)
            iState = iNextState
          Endif
          Continue

        Else If iStyle = STYLE_WEB And If String.Mid$(sText, iInd, 2) = "<%" Then
        
          bWebComment = False
          bCodeMarkup = False
          
          If String.Mid$(sText, iInd + 2, 2) = "--" Then
            iCount = 4
            bWebComment = True
            iTag = TAG_COMMENT
            iNextState = Highlight.Comment
          Else If String.Mid$(sText, iInd + 2, 1) = "=" Then
            iCount = 3
            iTag = TAG_CODE
            iNextState = Highlight.Normal
          Else If String.Mid$(sText, iInd + 2, 1) = "!" Then
            iCount = 3
            iTag = TAG_VARIABLE
            iNextState = Highlight.Datatype
          Else 
            iCount = 2
            iTag = TAG_CODE
            iNextState = Highlight.Normal
          Endif
          
          Highlight.AlternateState = True
          Highlight.Add(If(iTag = TAG_CODE, Highlight.Preprocessor, iNextState), iCount)
          sBufferAfter &= String.Mid$(sText, iInd, iCount)
          GoSub ADD_TEXT_AFTER
          iInd += iCount - 1
          
          iSaveState = iState
          iState = iNextState
          
          bCode = True
          iCodeStart = iInd + 1
          Continue
          
        Else If iStyle = STYLE_WEB And If String.Mid$(sText, iInd, 2) = "<<" Then
        
          bWebComment = False
          bMarkup = True
          bCodeMarkup = True
          sMarkup = ""
          iCount = 2
          iTag = TAG_CODE_MARKUP
          iNextState = Highlight.Preprocessor
          
          'Highlight.AlternateState = True
          Highlight.Add(iNextState, iCount)
          sBufferAfter &= String.Mid$(sText, iInd, iCount)
          GoSub ADD_TEXT_AFTER
          iInd += iCount - 1
          
          iSaveState = iState
          iState = iNextState
          
          bCode = True
          iCodeStart = iInd + 1
          Continue
          
        Else If bMarkup Then
    
          If bQuoteMarkup Then
            If sCar = Chr$(34) Then 
              bQuoteMarkup = False
              iNextState = Highlight.Operator
            Endif
          Else If bCodeMarkup And If sCar = ">" And If String.Mid$(sText, iInd + 1, 1) = ">" Then
            bMarkup = False
            bCodeMarkup = False
            bCode = False
            Highlight.Add(Highlight.Preprocessor, 2)
            Inc iInd
            sBufferAfter &= ">>"
            GoSub ADD_TEXT_AFTER
            Highlight.AlternateState = False
            iState = iSaveState 'Highlight.Normal
            iNextState = iState
            Continue
          Else If sCar = ">" Then
            bMarkup = False
            If bCode Then
              bCode = False
              Highlight.AlternateState = True
            Endif
            If sMarkup Then
              Highlight.Add(Highlight.Keyword, 1)
              sBufferAfter &= sCar
              GoSub ADD_TEXT_AFTER
              iSaveState = iState
              iState = Highlight.Normal
              iNextState = Highlight.Normal
              If sMarkup = "script" Then
                bScript = True
              Else If sMarkup = "style" Then
                bStyle = True
              Endif
              Highlight.AlternateState = True
              Continue
            Else
              iState = Highlight.Keyword
              iNextState = Highlight.Normal
            Endif
          Else If sCar = " " Then
            iNextState = Highlight.Operator
          Else If sCar = "=" Then
            iNextState = Highlight.String
          Else If sCar = Chr$(34) Then 
            bQuoteMarkup = True
          Else If bCode And If sCar = "%" And If String.Mid$(sText, iInd + 1, 1) = ">" Then
            bMarkup = False
            bCodeMarkup = False
            bCode = False
            Highlight.Add(Highlight.Preprocessor, 2)
            Inc iInd
            sBufferAfter &= "%>"
            GoSub ADD_TEXT_AFTER
            Highlight.AlternateState = False
            iState = iSaveState 'Highlight.Normal
            iNextState = iState
            Continue
          Endif
    
        Else
    
          Select Case iState
            Case Highlight.Normal
              If sCar = "<" Then
                If String.Mid$(sText, iInd, 4) = "<!--" Then
                  iState = Highlight.Comment
                  iNextState = Highlight.Comment
                Else If String.Mid$(sText, iInd, 9) = "<![CDATA[" Then
                  iState = Highlight.Symbol
                  iNextState = Highlight.Symbol
                Else
                  iState = Highlight.Keyword
                  iNextState = Highlight.Keyword
                  bMarkup = True
                  sMarkup = ""
                  sStr = String.Mid$(sText, iInd, 5)
                  If IsMarkup(sText, iInd, "<body>") Or If IsMarkup(sText, iInd, "<head>") Or If IsMarkup(sText, iInd, "<html>") Then
                    bLimit = True
                  Else If IsMarkup(sText, iInd, "<script>") Then
                    sMarkup = "script"
                  Else If IsMarkup(sText, iInd, "<style>") Then
                    sMarkup = "style"
                  Endif
                Endif
              Else If sCar = "&" Then
                iPos = String.InStr(sText, ";", iInd)
                If iPos = 0 Or iPos = iInd + 1 Then
                  iState = Highlight.Error
                Else
                  For J = iInd + 1 To iPos - 1
                    sCar = String.Mid$(sText, J, 1)
                    If IsLetter(sCar) Then Continue
                    If IsDigit(sCar) Then Continue
                    If InStr("_#", sCar) Then Continue
                    Break
                  Next
                  If J = iPos Then
                    Highlight.Add(Highlight.Number, iPos - iInd + 1)
                    sBufferAfter &= String.Mid$(sText, iInd, iPos - iInd + 1)
                    GoSub ADD_TEXT_AFTER
                    iInd = iPos
                    Continue
                  Else
                    iState = Highlight.Error
                  Endif
                Endif
              Endif
            Case Highlight.Comment
              If sCar = ">" And If iInd > 2 And If String.Mid$(sText, iInd - 2, 2) = "--" Then
                iNextState = Highlight.Normal
              Endif
            Case Highlight.Symbol
              If sCar = ">" And If iInd > 2 And If String.Mid$(sText, iInd - 2, 2) = "]]" Then
                iNextState = Highlight.Normal
              Endif
          End Select
    
        Endif
    
        sBufferAfter &= sCar
        GoSub ADD_TEXT_AFTER
        Highlight.Add(iState)
        iState = iNextState
    
      Next
    
      If bCode Then
        If bMarkup Then
          iNextState = Highlight.Normal
          iTag = TAG_HTML
        Else
          sBufferAfter &= AnalyzeCode(String.Mid$(sText, iCodeStart))
          GoSub ADD_TEXT_AFTER
          iTag = TAG_CODE
        Endif
      Else If bScript Then
        iTag = TAG_SCRIPT
      Else If bStyle Then
        iTag = TAG_STYLE
      Else
        iTag = TAG_HTML
        If iNextState <> Highlight.Comment And If iNextState <> Highlight.Symbol Then
          If bMarkup Then 
            iNextState = Highlight.Keyword
          Else 
            iNextState = Highlight.Normal
          Endif
        Endif
      Endif
      
      Highlight.Text = sTextAfter & sBufferAfter
      
  End Select 

  Highlight.State = iNextState
  Highlight.Tag = iTag
  Highlight.ShowLimit = bLimit
  Return
  
ADD_TEXT_AFTER:

  If Len(sBufferAfter) > 1024 Then
    sTextAfter &= sBufferAfter
    sBufferAfter = ""
  Endif
  Return

End
