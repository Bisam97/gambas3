' Gambas class file

Export
Create Static

Inherits WebContainer

Public Const _IsForm As Boolean = True
Public Const _HiddenControls As String = "WebControl,WebForm"
Public Const _Properties As String = "*,Title"

Property Title As String

Private $sTitle As String

Static Public Sub Main()

  Dim sPath As String
  Dim hClass As Class
  Dim hForm As WebForm
  Dim iPos As Integer
  Dim sClass As String
  Dim hStat As ClassStat
  
  sPath = Mid$(Request.Path, 2)
  
  If Not sPath Then
    
    hForm = Application.Startup.AutoCreate()
    
  Else
    iPos = InStr(sPath, "/")
    If iPos = 0 Then
      sClass = sPath
      sPath = ""
    Else
      sClass = Left(sPath, iPos - 1)
      sPath = Mid$(sPath, iPos + 1)
    Endif
    
    Try hStat = Class.Stat(sClass)
    If Not hStat Then Goto NOT_FOUND
    If LCase(hStat.Parent) <> "webform" Then Goto NOT_FOUND
    
    Try hClass = Class.Load(sClass)
    If Not hClass Then Goto NOT_FOUND
    
    hForm = hClass.AutoCreate()
    
  Endif
  
  If sPath Then
    If sPath Begins "style:" And If sPath Ends ".css" Then
      hForm.RenderStyleSheet
    Else If Exist("../.public/" &/ sPath) Then
      Response.SendFile("../.public/" &/ sPath)
    Endif
  Else
    hForm.Render
  Endif
  
  Return
  
NOT_FOUND:

  Response.Status = "404 NotFound"
  Response.Begin
  Print "<html><body><h1>404 NotFound</h1></body></html>"
  Response.End
  
End

Public Sub _BeforeRender()
  
  Response.Buffered = True
  Response.Begin
  
  Header.Form = Me
  Header.__Render()
  'Print Header.ToString();
  Header.Form = Null
  
  Print "<body>"
  
  Super._BeforeRender()
  
End


Public Sub _AfterRender()
  
  Super._AfterRender()

  Print "</body>"
  Print "</html>"
  
  Response.End
  
End

Public Sub Render()
  
  Dim hForm As WebForm
  
  hForm = Main.CurrentForm
  Main.CurrentForm = Me
  
  Me._BeforeRender()
  Me._Render()
  Me._AfterRender()
  
  Main.CurrentForm = hForm
  
End

Public Sub RenderStyleSheet()
  
  Response.Buffered = True
  Response.ContentType = "text/css;charset=utf-8"
  Response.Begin
  
  Print File.Load("style.css");
  
  Me._RenderStyleSheet()
  
  If Exist("../style.css") Then Print File.Load("../style.css");
  
  Response.End
  
End

Public Sub RenderJavascript()
  
  
  
End


Private Function Title_Read() As String

  Return $sTitle

End

Private Sub Title_Write(Value As String)

  $sTitle = Value

End
