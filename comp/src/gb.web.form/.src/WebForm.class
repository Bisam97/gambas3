' Gambas class file

Export
Create Static

Inherits WebContainer

Public Const _IsForm As Boolean = True
Public Const _HiddenControls As String = "WebControl,WebForm"
Public Const _Properties As String = "*,Title"
Public Const _DefaultEvent As String = "Open"

Static Public _InExec As Integer

Static Private $aJavascript As New String[]

Event Open
Event Event

Property Title As String

Public _FromName As New Collection
Public _HasRefresh As Boolean

Private $sTitle As String

Static Public Sub Main()

  Dim sPath As String
  Dim hClass As Class
  Dim hForm As WebForm
  Dim iPos As Integer
  Dim sClass As String
  Dim hStat As ClassStat
  
  sPath = Mid$(Request.Path, 2)
  
  If Not sPath Then
    
    hForm = Application.Startup.AutoCreate()
    
  Else If sPath Begins "style:" And sPath Ends ".css" Then
    
    RenderStyleSheet
    Return
    
  Else If sPath Begins "lib:" And sPath Ends ".js" Then
    
    RenderJavascript
    Return
    
  Else If Exist("../.public/" &/ sPath) Then
    
    Response.SendFile("../.public/" &/ sPath)
    Return
    
  Else
    
    iPos = InStr(sPath, "/")
    If iPos = 0 Then
      sClass = sPath
      sPath = ""
    Else
      sClass = Left(sPath, iPos - 1)
      sPath = Mid$(sPath, iPos + 1)
    Endif
    
    Try hStat = Class.Stat(sClass)
    If Not hStat Then Goto NOT_FOUND
    If LCase(hStat.Parent) <> "webform" Then Goto NOT_FOUND
    
    Try hClass = Class.Load(sClass)
    If Not hClass Then Goto NOT_FOUND
    
    hForm = hClass.AutoCreate()
    
  Endif
  
  hForm._InitProperties
  If sPath = "x" Then
    hForm._Exec(JSON.Decode(Request["c"]))
  Else
    hForm.Render
  Endif
  
  Return
  
NOT_FOUND:

  Response.Status = "404 NotFound"
  Response.Begin
  Print "<html><body><h1>404 NotFound</h1></body></html>"
  Response.End
  
End

Public Sub Render()
  
  'Dim hForm As WebForm
  
  'hForm = Main.CurrentForm
  'Main.CurrentForm = Me
  
  Response.Buffered = True
  Response.Begin
  
  Header.Form = Me
  Header.__Render()
  'Print Header.ToString();
  Header.Form = Null
  
  Print "<body onload=\"gw.raise("; JS(Me.Name); ",null,'open');\">"
  
  Print "<script type=\"text/javascript\" src=\""; Application.Root &/ "/lib:" & Application.Version & ".js"; "\"></script>"
  
  'Print "<style type=\"text/css\">"
  'Me._RenderStyleSheet
  'Print "</style>"
  
  Me._BeforeRender()
  Me._Render()
  Me._AfterRender()
  
  Print "</body>"
  Print "</html>"
  
  Response.End
  
  'Main.CurrentForm = hForm
  
End

Static Private Sub RenderStyleSheet()
  
  Response.Buffered = True
  Response.ContentType = "text/css;charset=utf-8"
  Response.Begin
  
  Print File.Load("style.css");
  
  If Exist("../style.css") Then Print File.Load("../style.css");
  
  Response.End
  
End

Static Private Sub RenderJavascript()
  
  Response.ContentType = "text/javascript;charset=utf-8"
  Response.Begin
  
  Print "$root = "; JS(Application.Root)
  Print File.Load("lib.js")
  
  Response.End
  
End


Private Function Title_Read() As String

  Return $sTitle

End

Private Sub Title_Write(Value As String)

  $sTitle = Value
  Me._SetProperty("Title", Value)

End

Static Public Sub Print(Text As String)
  
  $aJavascript.Add("console.log(" & JS(Text) & ");")
  
End

' Static Public Sub _AddReply(sStr As String)
'   
'   $aJavascript.Add(sStr)
'   
' End


Public Sub _Exec(aCmd As Variant[])
  
  Dim hCtrl As WebControl
  Dim iOldInExec As Integer = _InExec
  
  Inc _InExec
  
  Raise Event
  
  Select Case aCmd[0]
    
    Case "raise"
      If aCmd[1] Then
        hCtrl = WebControl.FromId(_FromName[aCmd[1]])
      Else
        hCtrl = Me
      Endif
      
      Object.Raise(hCtrl, aCmd[2], aCmd[3])
    
  End Select
  
  _InExec = iOldInExec
  
  Response.Begin
  Print $aJavascript.Join("\n")
  If _HasRefresh Then
    Me._RefreshReply()
  Endif
  Response.End
  
Catch
  
  _InExec = iOldInExec
  
  Response.Begin
  Print "console.log("; JS(Error.Where & ": " & Error.Text); ");"
  Response.End
  
End
