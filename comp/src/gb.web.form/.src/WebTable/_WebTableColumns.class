' Gambas class file

Export

Property Count As Integer

Event _Fake

Private $aColumns As New _WebTableColumn[]

Private Sub GetTable() As WebTable
  
  Return Object.Parent(Me)
  
End


Private Function Count_Read() As Integer

  Return $aColumns.Count

End

Private Sub Count_Write(Value As Integer)

  Dim iOldCount As Integer = $aColumns.Count
  Dim I As Integer
  
  If Value = iOldCount Then Return
  
  Try $aColumns.Resize(Value)
  If Error Then Error.Raise("Bad argument")
  
  If Value > iOldCount Then
    For I = iOldCount To Value - 1
      $aColumns[I] = New _WebTableColumn As "Column"
      $aColumns[I]._Column = I
    Next
  Endif
  
  GetTable()._SetProperty("#count", Value)

End

Public Sub _Render()
  
  Dim hCol As _WebTableColumn
  Dim iMode As Integer
  
  Print "<thead><tr>";
  
  iMode = GetTable().Mode
  If iMode Then
    Print "<th><div>";
    If iMode = Select.Multiple Then
      Print "<input type=\"checkbox\"";
      Print GetTable()._GetUpdateJS("onchange", "!!", "this.checked");
      If GetTable()._IsEverythingSelected() Then Print " checked";
      Print ">"
    Endif
    Print "</div></th>"
  Endif
  
  For Each hCol In $aColumns
    Print "<th";
    If hCol.Expand Then Print " width=\"100%\"";
    Print ">";
    Print "<div";
    hCol._PrintAlignment()
    Print ">";
    Print Html(hCol.Text);
    Print "</div></th>"
  Next
  
  Print "</tr></thead>"
  
End

Public Sub _get(Index As Integer) As _WebTableColumn
  
  Return $aColumns[Index]
  
End
