' Gambas class file

Export
Inherits WebControl

Public Const _Properties As String = "*,Border=True,ReadOnly,Text"
Public Const _DrawWith As String = "TextBox"
Public Const _DefaultSize As String = "24,4"

Event Change
Event Completion(Text As String)

Property Text As String
Property ReadOnly As Boolean

Private $sText As String
Private $bReadOnly As Boolean

Private Function Text_Read() As String

  Return $sText

End

Private Sub Text_Write(Value As String)

  If $sText = Value Then Return
  
  $sText = Value
  Me._SetProperty("Text", Value)
  Raise Change

End

Public Sub Clear()
  
  Text_Write("")
  
End


Public Sub _BeforeRender()
  
  Print "<input type=\"text\""; Me._GetClassId(); Me._GetUpdateJS("onchange", "text", "this.value");
  Me._RenderStyleSheet()
  If Not Me.Enabled Then Print " disabled";
  If $bReadOnly Then Print " readonly";
  If $sText Then Print " value=\""; Html($sText); "\"";
  Print ">"
  
  If Object.CanRaise(Me, "Completion") Then
    WebForm._AddJavascript("gw.autocomplete(" & JS(Me.Name) & ");")
  Endif
  
End


Public Sub _AfterRender()
  
  'Raise Render
  'Print "</input>"
  
End

Private Function ReadOnly_Read() As Boolean

  Return $bReadOnly

End

Private Sub ReadOnly_Write(Value As Boolean)

  $bReadOnly = Value
  Me._SetProperty("ReadOnly", Value)

End

Public Sub _UpdateProperty(sProp As String, vValue As Variant)
  
  If sProp = "text" Then Try Me.Text = vValue
  
End

Public Sub CompleteWith(CompletionList As String[])
  
  WebForm._AddReply("gw.autocompletion = " & JSON.Encode(CompletionList) & ";")
  
End
