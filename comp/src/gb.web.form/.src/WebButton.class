' Gambas class file

Export
Inherits WebControl

Public Const _Properties As String = "*,Enabled=True,Text,Image{ImagePath}"
Public Const _DrawWith As String = "Button"
Public Const _DefaultEvent As String = "Click"

Event Click

Property Text As String
Property Image As String
Property Enabled As Boolean

Private $sText As String
Private $sImage As String
Private $bDisabled As Boolean

Private Function Text_Read() As String

  Return $sText

End

Private Sub Text_Write(Value As String)

  $sText = Value
  Me._SetProperty("Text", Value)

End

Private Function Image_Read() As String

  Return $sImage

End

Private Sub Image_Write(Value As String)

  $sImage = Value
  Me._SetProperty("Image", Value)

End

Public Sub _BeforeRender()
  
  Print "<button id=\""; Me.Name; "\""; Me._GetEventJS("onclick", "click");
  Me._RenderStyleSheet()
  If $bDisabled Then Print " disabled";
  Print ">";
  
End


Public Sub _Render()
  
  If $sImage Then 
    If $sText Then
      Print "<img class=\"button-image\" src=\""; Html(Application.Root &/ $sImage); "\">";
    Else
      Print "<img src=\""; Html(Application.Root &/ $sImage); "\">";
    Endif
  Endif
  Print Html($sText);
  
End

Public Sub _AfterRender()
  
  Raise Render
  Print "</button>"
  
End

Private Function Enabled_Read() As Boolean

  Return Not $bDisabled

End

Private Sub Enabled_Write(Value As Boolean)

  $bDisabled = Not Value
  Me._SetProperty("Enabled", Value)

End
