' Gambas class file

Public Key As String
Public Text As String
Public Display As String
Public DefaultShortcut As String
Public Shortcut As String
Public Icon As Picture
Public HasShortcut As Boolean

Private $sOldShortcut As String

Private Sub UnquoteShortcut(sStr As String) As String
  
  sStr = Replace(sStr, "&&", Chr$(1))
  sStr = Replace(sStr, "&", "")
  sStr = Replace(sStr, Chr$(1), "&")
  
  Return sStr
  
End

Public Sub _new(sKey As String, bHasShortcut As Boolean, cSlot As Collection)

  Dim sPicture As String
  Dim W As Integer

  Key = sKey
  Text = Tr$(cSlot["Text"])
  Display = UnquoteShortcut(Text)
  DefaultShortcut = cSlot["Shortcut"]
  HasShortcut = bHasShortcut
  Shortcut = MAction.GetShortcut(Key)
  If Not Shortcut Then Shortcut = DefaultShortcut
  $sOldShortcut = Shortcut
  
  sPicture = cSlot["Picture"]
  If Left(sPicture) = "$" Then
    W = Stock.GetSize("small")
    Try Icon = Picture.Load("control" &/ LCase(Mid$(sPicture, 2)) & ".png").Image.Stretch(W, W).Picture
  Else
    If Not (sPicture Like "icon:/*") Then sPicture = "../" &/ sPicture
    Try Icon = Picture[sPicture]
  Endif
  
End

Public Sub Save()
  
  Dim hAction As Object
  
  If Not HasShortcut Then Return
  If Shortcut = $sOldShortcut Then Return
  
  If Shortcut = DefaultShortcut Then
    MAction.SetShortcut(Key, "")
  Else
    MAction.SetShortcut(Key, Shortcut)
  Endif
  
  hAction = Action[Key]
  
  hAction.Shortcut = Shortcut
  Action["." & Key].Shortcut = Shortcut
  
  $sOldShortcut = Shortcut
  
End

Public Sub _compare(hAction As CAction) As Integer
  
  Return String.Comp(Display, hAction.Display)
  
End
