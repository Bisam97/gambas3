' Gambas class file

Public Key As String
Public Text As String
Public Display As String
Public DefaultShortcut As String
Public Shortcut As String
Public Icon As Picture
Public IconPath As String
Public HasShortcut As Boolean

Private $sOldShortcut As String

Private Sub UnquoteShortcut(sStr As String) As String
  
  sStr = Replace(sStr, "&&", Chr$(1))
  sStr = Replace(sStr, "&", "")
  sStr = Replace(sStr, Chr$(1), "&")
  
  Return sStr
  
End

Public Sub _new(sKey As String, bHasShortcut As Boolean, cSlot As Collection, hMerge As CAction)

  Dim sPicture As String
  Dim W As Integer

  'Debug sKey;; bHasShortcut;; cSlot["Picture"]

  Key = sKey
  
  Text = Tr$(cSlot["Text"])
  If hMerge And If Not Text Then Text = hMerge.Text
  Display = UnquoteShortcut(Text)

  DefaultShortcut = cSlot["Shortcut"]
  If hMerge And If Not DefaultShortcut Then DefaultShortcut = hMerge.DefaultShortcut
  
  HasShortcut = bHasShortcut
  If hMerge And If Not HasShortcut Then HasShortcut = hMerge.HasShortcut
  
  Shortcut = MAction.GetShortcut(Key)
  If Not Shortcut Then Shortcut = DefaultShortcut
  $sOldShortcut = Shortcut
  
  sPicture = cSlot["Picture"]
  
  If Left(sPicture) = "$" Then
    W = Stock.GetSize("small")
    IconPath = "control" &/ LCase(Mid$(sPicture, 2)) & ".png"
    Try Icon = Picture[IconPath].Image.Stretch(W, W).Picture
  Else
    If Not (sPicture Like "icon:/*") Then sPicture = "../" &/ sPicture
    IconPath = sPicture
    Try Icon = Picture[IconPath]
  Endif
  
  If hMerge Then
    If Not sPicture Or If Left(sPicture) = "$" And hMerge.IconPath Then
      IconPath = hMerge.IconPath
      Icon = hMerge.Icon
    Endif
  Endif
  
End

Public Sub Save()
  
  Dim hAction As Object
  
  If Not HasShortcut Then Return
  If Shortcut = $sOldShortcut Then Return
  
  If Shortcut = DefaultShortcut Then
    MAction.SetShortcut(Key, "")
  Else
    MAction.SetShortcut(Key, Shortcut)
  Endif
  
  hAction = Action[Key]
  
  hAction.Shortcut = Shortcut
  Action["." & Key].Shortcut = Shortcut
  
  $sOldShortcut = Shortcut
  
End

Public Sub _compare(hAction As CAction) As Integer
  
  Return String.Comp(Display, hAction.Display)
  
End
