' Gambas class file

Public Key As String
Public Text As String
Public Display As String
Public DefaultShortcut As String
Public Shortcut As String
Public Icon As Picture

Private $sOldShortcut As String

Private Sub UnquoteShortcut(sStr As String) As String
  
  sStr = Replace(sStr, "&&", Chr$(1))
  sStr = Replace(sStr, "&", "")
  sStr = Replace(sStr, Chr$(1), "&")
  
  Return sStr
  
End

Public Sub _new(sLine As String)

  Dim aLine As String[] = Split(sLine, Chr$(9))
  Dim sPicture As String

  Key = aLine[0]
  Text = Tr$(aLine[1])
  Display = UnquoteShortcut(Text)
  DefaultShortcut = aLine[2]
  Shortcut = MShortcut.GetShortcut(Key)
  If Not Shortcut Then Shortcut = DefaultShortcut
  $sOldShortcut = Shortcut
  
  sPicture = aLine[3]
  If Not (sPicture Like "icon:/*") Then sPicture = "../" &/ sPicture
  Icon = Picture[sPicture]
  
End

Public Sub Save()
  
  Dim hAction As Object
  
  If Shortcut = $sOldShortcut Then Return
  
  If Shortcut = DefaultShortcut Then
    MShortcut.SetShortcut(Key, "")
  Else
    MShortcut.SetShortcut(Key, Shortcut)
  Endif
  
  hAction = Action[Key]
  
  hAction.Shortcut = Shortcut
  Action["." & Key].Shortcut = Shortcut
  
  $sOldShortcut = Shortcut
  
End

Public Sub _compare(hAction As CAction) As Integer
  
  Return String.Comp(Display, hAction.Display)
  
End
