' Gambas class file

Public Enum ICON_SMALL, ICON_MEDIUM, ICON_LARGE

Public ToolbarKey As String
'Public ToolbarText As String
Public $bDesign As Boolean

Static Private $cRegister As New Collection
Static Private $hExpanderBg As Picture

Private Const SEPARATOR_SIZE As Integer = 3

'Private $hWatcher As Watcher
'Private $hWatcherParent As Watcher
Private $hObserverParent As Observer
Private $bLock As Boolean
Private $bHidden As Boolean
Private $bVertical As Boolean
Private $bReallyVertical As Boolean
Private $bAutoResize As Boolean
Private $bConfigure As Boolean
Private $bInside As Boolean
Private $bPressed As Boolean
'Private $iSeparatorSize As Integer
Private $sDropAction As String
Private $iIconSize As Integer
Private $iButtonWidth As Integer

Private $bOpened As Boolean

'Private Const $bShowHandle As Boolean = True
Private $iHandleSize As Integer

Private $iLock As Integer
'Private $iArrangement As Integer

Private $aHidden As Control[]

Public Sub _new()
  
  $iHandleSize = 1 'Max(4, Desktop.Scale)
  $iButtonWidth = Desktop.Scale * 4
  dwgHandle.Width = $iHandleSize
  $hObserverParent = New Observer(Me.Parent.Parent) As "Container"
  $bAutoResize = True
  UpdateHandle
  
End


Public Sub IsVertical() As Boolean
  
  Return $bReallyVertical
  
End

Public Sub SetVertical(bVertical As Boolean)
  
  $bReallyVertical = bVertical
  Update
  
End

Public Function GetContainer() As Container
  
  Return panToolBar
  
End

Private Function GetToolBarWidth(Optional bPadding As Boolean) As Integer
  
  Dim hCtrl As Control
  Dim W As Integer
  Dim WH As Integer
  
  If $bReallyVertical Then 
  
    ' For Each hCtrl In panToolBar.Children
    '   If Not hCtrl.Visible Then Continue
    '   W = Max(W, hCtrl.W)
    ' Next
    ' 
    ' W = Max(W, $iButtonWidth)
    
    W = $iButtonWidth
    
  Else 
  
    If dwgHandle.Visible Then WH = dwgHandle.W
  
    For Each hCtrl In panToolBar.Children
      If hCtrl.Visible Then 
        If hCtrl.Expand Then Return GetFreeWidth() - WH
        W += hCtrl.W
      Endif
    Next
    
  Endif
  
  If bPadding Then W += panToolBar.Padding * 2 
  Return W
  
End

Private Sub GetToolBarHeight(Optional bPadding As Boolean) As Integer
  
  Dim hCtrl As Control
  Dim X, Y, W, H As Integer
  Dim HH As Integer

  If $bReallyVertical Then
  
    If dwgHandle.Visible Then HH = dwgHandle.H
  
    For Each hCtrl In panToolBar.Children
      If hCtrl.Visible Then 
        If hCtrl.Expand Then Return GetFreeHeight() - HH
        H += hCtrl.H
      Endif
    Next
    
    ' If dwgHandle.Visible Then Y = dwgHandle.H * 2
    ' 
    ' For Each hCtrl In panToolBar.Children
    '   If hCtrl.Visible Then 
    '     If X >= GetMaxWidth() Then
    '       X = 0
    '       Y += H
    '       H = 0
    '     Endif
    '     W += hCtrl.W
    '     H = Max(H, hCtrl.H)
    '     If hCtrl.Expand Then W = GetMaxWidth()
    '     X += W
    '   Endif
    ' Next
    ' 
    ' H = Y + H
    
  Else
  
    ' For Each hCtrl In panToolBar.Children
    '   If hCtrl.Visible Then 
    '     If X >= GetFreeWidth() Then
    '       X = 0
    '       Y += H
    '       H = 0
    '     Endif
    '     W += hCtrl.W
    '     If Not hCtrl Is Separator Then
    '       H = Max(H, hCtrl.H)
    '       Debug hCtrl.Name;; hCtrl.H
    '     Endif
    '     If hCtrl.Expand Then 
    '       X = GetFreeWidth()
    '     Else
    '       X += W
    '     Endif
    '   Endif
    ' Next
    ' 
    ' H = Y

    ' For Each hCtrl In panToolBar.Children
    '   If Not hCtrl.Visible Then Continue
    '   H = Max(H, hCtrl.H)
    ' Next
    H = $iButtonWidth
    
  Endif
  
  If bPadding Then H += panToolBar.Padding * 2
  Return H
  
End

Private Sub GetMaxWidth() As Integer
  
  Dim W As Integer = Me.Parent.Width
  If sepToolbar.Visible Then Dec W
  Return W
  
End


Private Function GetFreeWidth() As Integer
  
  Return Me.Parent.Parent.ClientW
  
End

Private Function GetFreeHeight() As Integer
  
  Return Me.Parent.Parent.ClientH
  
End

Private Sub SetArrangement()
  
  Dim hToolbar As ToolBar = Me.Parent
  
  If $bDesign Then Return
  hToolBar.Padding = 0
  hToolbar.Arrangement = If($bReallyVertical, Arrange.Vertical, Arrange.Horizontal) 'Arrange.Row 'If($bVertical, Arrange.Column, Arrange.Row)
  
End

Private Sub Update()

  Dim W, H As Integer
  Dim XH, YH, WH, HH As Integer 
  Dim XP, YP, WP, HP As Integer 

  If Not Object.IsValid(Me.Parent) Then Return
  If Not Object.IsValid(Me.Parent.Parent) Then Return
  If $iLock Then Return
  If $bLock Then Return
  $bLock = True
  
  ToolBar(Me.Parent).Arrangement = Arrange.None
  CheckSeparators

  If Not $bReallyVertical Then
  
    If System.RightToLeft Then
      XH = Me.W - $iHandleSize
      YH = 0
      XP = 0
    Else
      XH = 0
      YH = 0
      XP = $iHandleSize
    Endif
    YP = 0
    WH = $iHandleSize
    HH = Me.H
    
  Else
  
    XH = 0
    YH = 0
    WH = Me.W
    HH = $iHandleSize
    XP = 0
    YP = $iHandleSize
  
  Endif
  
  If Not dwgHandle.Visible Then
    WH = 0
    HH = 0
    XP = 0
    YP = 0
  Endif

  If $bDesign Then
  
    If dwgHandle.Visible Then
      dwgHandle.Move(XH, YH, WH, HH)
      If Not $bReallyVertical Then
        panToolBar.Move(XP, YP, Me.W - WH, Me.H)
      Else
        panToolBar.Move(XP, YP, Me.W, Me.H - HH)
      Endif
    Else
      panToolBar.Move(XP, YP, Me.W, Me.H)
    Endif
    
  Else

    panToolBar.Show
    
    If Not $bReallyVertical Then
      If $bAutoResize Then
        W = Max($iHandleSize, Min(GetToolBarWidth() + WH, GetFreeWidth()))
        'H = Max(GetToolBarHeight(True), panToolBar.H)
      Else 
        W = Max($iHandleSize, GetToolBarWidth() + WH)
      Endif
      H = GetToolBarHeight(True)
    Else
      If $bAutoResize Then
        'W = Max(GetToolBarWidth(True), GetMaxWidth())
        H = Max($iHandleSize, Min(GetToolBarHeight() + HH, GetFreeHeight()))
      Else 
        'W = GetToolBarWidth(True)
        H = Max($iHandleSize, GetToolBarHeight() + HH)
      Endif
      W = GetToolBarWidth(True)
    Endif
    'W += $iHandleSize
    
    If dwgHandle.Visible Then dwgHandle.Move(XH, YH, WH, HH)
    If Not $bReallyVertical Then
      panToolBar.Move(XP, YP, W - WH)
    Else
      panToolBar.Move(XP, YP, panToolBar.W, H - HH)
    Endif

  Endif
  
  If Not $bReallyVertical Then
    sepToolbar.Move(0, Me.H - 1, Me.W, 1)
  Else
    sepToolbar.Move(Me.W - 1, 0, 1, Me.H)
  Endif
  
  'dwgHandle.Refresh
  
  $bLock = False

  If sepToolbar.Visible Then 
    If $bReallyVertical Then
      Inc W
    Else
      Inc H
    Endif
  Endif
  
  'Debug panToolBar.X;; panToolBar.Y;; panToolBar.W;; panToolBar.H;; "/";; W;; H
  'If panToolBar.Children.Count Then Debug "=>";; panToolBar.Children[0].X;; panToolBar.Children[0].Y
  
  If Not $bDesign Then 
    Me.Parent.Resize(W, H)
  Endif
  
  SetArrangement
  
End

Public Sub Form_Resize()
  
  Update
  
End

 
Public Sub Container_Arrange()
  
  Update
  
End


Public Sub panToolbar_BeforeArrange()

  If $bDesign Then Return
  Update

End

Public Function IsHidden() As Boolean
  
  Return $bHidden
  
End

Public Sub SetHidden(bHidden As Boolean)
  
  If bHidden = $bHidden Then Return
  $bHidden = bHidden
  Update
  Action.Raise(Me.Parent)
  
End

' PUBLIC SUB dwgHandle_DblClick()
' <
'   SetHidden(NOT $bHidden)
' 
' END

Public Sub dwgHandle_Draw()

  Dim iFlag As Integer

  If $bInside Then iFlag += Draw.Hover
  If Not Me.Enabled Then iFlag += Draw.Disabled

  'Draw.Style.Button(1, 1, dwgHandle.W - 2, dwgHandle.H - 2, $bPressed And $bInside, Not Me.Enabled)
  'Draw.Style.Handle(1, 1, dwgHandle.W - 2, dwgHandle.H - 2, Not $bVertical, iFlag)
  'If $bInside Then
  If $bInside Or If $bConfigure Then
    Draw.FillRect(0, 0, dwgHandle.W, dwgHandle.H, Color.Merge(Color.LightForeground, Color.TextBackground, 0.3))
  Else
    Draw.FillRect(0, 0, dwgHandle.W, dwgHandle.H, Color.LightForeground)
  Endif
  'Endif
  'Draw.Style.Separator(0, 0, dwgHandle.W, dwgHandle.H, $bVertical, Not Me.Enabled)
  'Draw.FillX = 0
  'Draw.FillY = 0
  'Draw.Style.Separator(1, 1, dwgHandle.W - 2, dwgHandle.H - 2, $bVertical, Not Me.Enabled)
  
End

Private Sub UpdateHandle()
  
  'If $bInside Or If $bConfigure Then
  '  dwgHandle.Background = Color.Merge(Color.Background, Color.Foreground, 0.3)
  'Else
  '  dwgHandle.Background = Color.Default
  'Endif
  dwgHandle.Refresh
  
End


Public Sub dwgHandle_Enter()

  If $bDesign Then Return
  $bInside = True
  UpdateHandle

End

Public Sub dwgHandle_Leave()

  If $bDesign Then Return
  $bInside = False
  UpdateHandle

End

Public Sub dwgHandle_MouseDown()

  If $bDesign Then Return
  $bPressed = True
  dwgHandle.Refresh
  'SetHidden(NOT $bHidden)

End

Public Sub dwgHandle_MouseUp()

  If $bDesign Then Return
  $bPressed = False
  If $bInside Then 
    dwgHandle.Refresh
    If ToolbarKey Then Action._ConfigureToolbar(Me.Parent)
  Endif
  'SetHidden(NOT $bHidden)

End

Public Sub IsAutoResize() As Boolean
  
  Return $bAutoResize
  
End

Public Sub SetAutoResize(bAutoResize As Boolean)
  
  $bAutoResize = bAutoResize
  Update
  
End

Private Sub SetObserverOn(hCtrl As Control)
  
  Dim hObs As Observer
  Dim hCont As Container
  
  hObs = New Observer(hCtrl) As "Item"
  If Not hCtrl Is Container Then Return
  
  hCont = hCtrl
  For Each hCtrl In hCont.Children
    SetObserverOn(hCtrl)
  Next
  
End


Private Sub InitToolbar()
  
  Dim hCtrl As Control
  Dim hCont As Container
  Dim hObs As Observer
  Dim sAction As String
  Dim aList As String[]
  Dim aReparent As Control[]
  
  If Not ToolBarKey Then Return
  
  Try aList = FindToolbar().DefaultConfig
  If Error Then Return
  
  ' Debug panToolBar.Children.Count
  ' Debug System.Backtrace.Join("\n")
  
  aReparent = New Control[]
  For Each hCtrl In panToolBar.Children
    If hCtrl.Action Or If hCtrl Is Separator Then
      SetObserverOn(hCtrl)
      sAction = hCtrl.Action
      If sAction Then
        If Left(sAction) = "." Then sAction = Mid$(sAction, 2)
        If Not aList.Exist(sAction) Then aReparent.Add(hCtrl)
      Endif
    Endif
  Next
  
  For Each hCtrl In aReparent
    hCtrl.Reparent(panHide)
    hCtrl.Show
  Next
  
End

Public Sub FindControl(sAction As String, Optional bHiddenOnly As Boolean) As Control

  Dim hCtrl As Control
  
  If Not bHiddenOnly Then 
  
    For Each hCtrl In panToolBar.Children
      If hCtrl.Action = sAction Or If hCtrl.Action = ("." & sAction) Then Return hCtrl
    Next
  
  Endif
  
  For Each hCtrl In panHide.Children
    If hCtrl.Action = sAction Or If hCtrl.Action = ("." & sAction) Then Return hCtrl
  Next
  
End


Public Sub Form_Open()

  'Debug panToolBar.Children.Count

  If Not $bDesign Then
    $bOpened = True
    InitToolbar
    RegisterToolbar
    LoadConfig(True)
  Endif

End

' Public Sub panToolBar_Menu()
' 
'   Debug "Toolbar menu"
' 
' End
' 
' Public Sub Item_Menu()
'   
'   Debug "Item menu"
'   
' End

Private Sub FindToolbar() As CToolbar
  
  MAction.InitAction
  Return MAction.Toolbars[ToolbarKey]
  
End


Public Sub SetConfigureMode(bConfigure As Boolean)

  Dim hChild As Control
  Dim hObs As Observer
  Dim hToolButton As ToolButton
  Dim hColor As ColorInfo

  If $bConfigure = bConfigure Then Return
  
  $bConfigure = bConfigure
  
  ' For Each hChild In panToolBar.Children
  '   hChild.Drop = $bConfigure
  ' Next
  ' For Each hChild In panHide.Children
  '   hChild.Drop = $bConfigure
  ' Next
  Me.Drop = $bConfigure
  
  panToolBar.Enabled = Not $bConfigure
  If $bConfigure Then
    'hColor = Color[Color.LightBackground]
    'hColor.Saturation /= 2
    'Me.Background = dwgHandle.Background 'hColor.Color
  Else
    'Me.Background = Color.Default
  Endif
  UpdateHandle
  dwgHandle.Enabled = Not $bConfigure

  Lock()
  If $bConfigure Then
    $aHidden = New Control[]
    For Each hChild In panToolBar.Children
      If hChild Is Separator Then Continue
      If Not hChild.Visible Then 
        $aHidden.Add(hChild)
        hChild.Show
      Endif
    Next
    ' For Each hChild In panHide.Children
    '   If hChild Is Separator Then Continue
    '   If Not hChild.Visible Then 
    '     $aHidden.Add(hChild)
    '     'hChild.Show
    '   Endif
    ' Next
  Else
    For Each hChild In $aHidden
      hChild.Hide
    Next
    $aHidden = Null
    SaveConfig
    'panToolBar.Refresh
  Endif
  Unlock()
  
End

Public Sub Form_Drag()
  
  If Not $bConfigure Or If Drag.Format <> MAction.MIME Then
    HideWhere(True)
    Stop Event
    Return
  Endif
  
End

Private Sub WaitALittle(iCount As Integer)
  
  Dim eTime As Float
  Dim eWait As Float

  eTime = Timer
  'Wait
  eWait = (0.01 / iCount) - (eTime - Timer)
  If eWait > 0 Then Sleep eWait
  
End


Private Sub HideWhere(bAnim As Boolean)
  
  Dim D As Integer
  
  If Not panWhere.Visible Then Return
  
  If $bVertical Then
    D = panWhere.H
  Else
    D = panWhere.W
  Endif
  
  panWhere.Border = Border.None
  If bAnim Then
    For D = D - 1 To 1 Step -2
      If $bVertical Then
        panWhere.H = D
      Else
        panWhere.W = D
      Endif
      WaitALittle(D)
    Next
  Endif
  panWhere.Hide
  
End

Private Sub ShowWhere(sAction As String, bAnim As Boolean, Optional hCtrl As Control)

  Dim D As Integer
  Dim DM As Integer
  Dim W, H As Integer
  
  If Not hCtrl Then
    If sAction And If sAction <> "|" And If sAction <> "~" Then
      hCtrl = FindControl(sAction)
      If Not hCtrl Then Return
    Endif
  Endif
  
  If hCtrl Then
    W = hCtrl.W
    H = hCtrl.H
    panWhere.Move(hCtrl.X, hCtrl.Y)
  Else
    If $bVertical Then
      W = GetToolBarHeight()
      H = SEPARATOR_SIZE
    Else
      W = SEPARATOR_SIZE
      H = GetToolBarHeight()
    Endif
  Endif
  
  If $bVertical Then
    DM = H
    panWhere.W = W
  Else
    DM = W
    panWhere.H = H
  Endif  
  
  panWhere.Show
  If bAnim Then
    For D = 1 To DM - 1 Step 2
    'For D = DM To DM
      If $bVertical Then
        panWhere.H = D
      Else
        panWhere.W = D
      Endif
      WaitALittle(DM)
    Next
  Endif
  If $bVertical Then
    panWhere.H = DM
  Else
    panWhere.W = DM
  Endif
  panWhere.Border = Border.Plain
  
End

Public Sub Form_DragMove()
  
  Dim hCtrl As Control
  Dim D As Integer
  Dim bBefore As Boolean
  Dim bAnim As Boolean
  Dim X, Y As Integer
  
  If Not $bConfigure Or If Drag.Format <> MAction.MIME Then
    HideWhere(True)
    Stop Event
    Return
  Endif

  hCtrl = panToolBar.Find(Drag.X - panToolBar.X, Drag.Y - panToolBar.Y)
  If Not hCtrl Then
    Stop Event
    Return
  Endif
  
  While hCtrl.Parent <> panToolBar
    hCtrl = hCtrl.Parent
  Wend

  If hCtrl = panWhere Then Return
  
  X = Drag.X - panToolBar.X - hCtrl.X
  Y = Drag.Y - panToolBar.Y - hCtrl.Y
  
  If $bVertical Then
  
    bBefore = Y <= (hCtrl.H \ 2)
    'If Not bBefore And If Drag.Y > ((hCtrl.H * 2) \ 3) Then Return
    
  Else
  
    bBefore = X <= (hCtrl.W \ 2)
    'If Not bBefore And If Drag.X > ((hCtrl.W * 2) \ 3) Then Return
    
  Endif
  
  If bBefore Then
    If panWhere.Next = hCtrl Then Return
  Else
    If hCtrl.Next = panWhere Then Return
  Endif
  
  bAnim = Not panWhere.Visible
  'panWhere.Hide
  'HideWhere(False)
  
  If bBefore Then
    panWhere.Next = hCtrl
  Else
    panWhere.Next = hCtrl.Next
    hCtrl.Next = panWhere
  Endif
  
  ShowWhere(Drag.Paste(MAction.MIME), bAnim)
  
End

Private Sub OnMouseDrag(hCtrl As Control)
  
  Dim iArr As Integer
  Dim sAction As String
  
  While hCtrl.Parent <> panToolBar
    hCtrl = hCtrl.Parent
    'Debug "-> "; hCtrl
  Wend
  
  If Not $bConfigure Then Return
  
  sAction = hCtrl.Action
  If Left(sAction) = "." Then sAction = Mid$(sAction, 2)
  If Not sAction Then
    If hCtrl Is ToolBarExpander Then
      sAction = "~"
    Else If hCtrl Is Separator
      sAction = "|"
    Else
      Return
    Endif
  Endif

  $bLock = True
  'Lock()
  DragStart(True)
  panWhere.Next = hCtrl.Next
  hCtrl.Next = panWhere
  ShowWhere(sAction, False, hCtrl)
  hCtrl.Hide
  'Unlock()
  
  FToolBarConfig.DragAction(hCtrl, sAction)
  $bLock = False
  
End

Public Sub Item_MouseDown()
  
  If $bConfigure Then 
    Stop Event
  Else If Mouse.Right Then
    If Not Application.ActiveControl Is TextBox Then
      FToolBarConfig.OpenMenu(Me.Parent)
      Stop Event
    Endif
  Endif
  
End

Public Sub Item_MouseDrag()
  
  OnMouseDrag(Last)
  
End

Public Sub Form_Drop()

  panWhere_Drop

End

Public Sub DragStart(bOutside As Boolean)
  
  $sDropAction = ""
  If bOutside Then
    panWhere.Resize(SEPARATOR_SIZE, SEPARATOR_SIZE)
    panWhere.Reparent(panToolBar)
  Endif
  
End


Public Sub DragEnd(bAnim As Boolean)
  
  Dim hSep As Separator
  Dim hObs As Observer
  Dim hCtrl As Control
  
  If $sDropAction Then
  
    If $sDropAction = "|" Then
      AddSeparator(panWhere)
    Else If $sDropAction = "~" Then
      AddExpander(panWhere)
    Else
      hCtrl = FindControl($sDropAction)
      If hCtrl Then 
        ShowControl(hCtrl)
        hCtrl.Show
        hCtrl.Next = panWhere.Next
      Endif
    Endif
    Update
    
  Endif

  HideWhere(bAnim)
  panWhere.Reparent(Me)
  
End

Public Sub panWhere_Drag()

  If Not $bConfigure Or If Drag.Format <> MAction.MIME Then
    Stop Event
    Return
  Endif

End

Private Sub AddSeparator(Optional hNext As Control)
  
  Dim hSep As Separator
  Dim hObs As Observer
  Dim W, H As Integer

  Lock()
  
  If $bVertical Then
    W = GetToolBarHeight()
    H = SEPARATOR_SIZE
  Else
    W = SEPARATOR_SIZE
    H = GetToolBarHeight()
  Endif

  hSep = New Separator(panToolBar)
  hSep.Next = hNext
  
  hSep.Resize(W, H)
  hSep.Drop = True
  hObs = New Observer(hSep) As "Item"
  
  Unlock()
  
End

Public Sub ToolBarExpander_Draw()
  
  If $bConfigure Then
    If Not $hExpanderBg Then $hExpanderBg = Picture["img/expander.png"]
    Draw.Tile($hExpanderBg, 0, (Last.H - $hExpanderBg.H) / 2, Last.W, $hExpanderBg.H)
  Endif
  
End


Private Sub AddExpander(Optional hNext As Control)
  
  Dim hExp As ToolBarExpander
  Dim hObs As Observer
  Dim W, H As Integer

  Lock()
  
  If $bVertical Then
    W = GetToolBarHeight()
    H = SEPARATOR_SIZE
  Else
    W = SEPARATOR_SIZE
    H = GetToolBarHeight()
  Endif

  hExp = New ToolBarExpander(panToolBar) As "ToolBarExpander"
  hExp.Next = hNext
  hExp.Resize(W, H)
  hExp.Drop = True
  hObs = New Observer(hExp) As "Item"
  
  Unlock()
  
End


Public Sub panWhere_Drop()
  
  $sDropAction = Drag.Paste(MAction.MIME)
  
End

Public Sub SetText(sText As String)
  
  Me.Text = sText
  If $bDesign Then Return
  If sText Then
    dwgHandle.ToolTip = Subst$(("Configure &1 toolbar"), sText)
  Else
    dwgHandle.ToolTip = ("Configure main toolbar")
  Endif
  
End

Private Sub RegisterToolbar()
  
  Dim aSlot As FToolBar[]
  
  If Not ToolbarKey Then Return
  
  'Debug ToolbarKey

  If Not $cRegister.Exist(ToolbarKey) Then
    $cRegister[ToolbarKey] = New FToolbar[]
  Endif
  
  aSlot = $cRegister[ToolbarKey]
  If Not aSlot.Exist(Me) Then 
    aSlot.Add(Me)
  Endif
  
End

Private Sub UnregisterToolbar()
  
  Dim aSlot As FToolBar[]
  Dim iInd As Integer

  If Not ToolbarKey Then Return

  'Debug ToolbarKey

  aSlot = $cRegister[ToolbarKey]
  If aSlot Then
    iInd = aSlot.Find(Me)
    If iInd >= 0 Then 
      aSlot.Remove(iInd)
    Endif
  Endif
  
End


Public Sub SetKey(sKey As String)
  
  If ToolbarKey = sKey Then Return
  
  If $bOpened Then UnregisterToolbar
  ToolbarKey = sKey
  If $bOpened Then 
    InitToolbar
    RegisterToolbar
  Endif
  'dwgHandle.Visible = ToolbarKey
  Update
  
End


Public Sub Form_Close()

  UnregisterToolbar

End

Private Sub CalcIconSize(iIconSize As Integer) As Integer
  
  Select Case iIconSize
    Case ICON_MEDIUM
      Return Stock.GetSize("medium")
    Case ICON_LARGE
      Return Stock.GetSize("large")
    Default
      Return Stock.GetSize("small")
  End Select
  
End

Private Sub GetIcon(sIcon As String) As Picture
  
  Dim aIcon As String[]
  Dim hIcon As Picture
  Dim iSize As Integer
  
  iSize = CalcIconSize($iIconSize)
  
  If sIcon Begins "icon:/" Then
    aIcon = Split(sIcon, "/")
    aIcon[1] = iSize
    hIcon = Picture[aIcon.Join("/")]
  Else
    hIcon = Picture[sIcon]
    If hIcon Then
      hIcon = hIcon.Image.Stretch(iSize, iSize).Picture
    Endif
  Endif
  
  Return hIcon
  
End

Private Sub ResizeControl(hCtrl As Control, iDim As Integer)
  
  Dim hToolButton As Object
  Dim sAction As String
  Dim hAction As CAction

  If $bReallyVertical Then
    hCtrl.W = iDim
  Else
    hCtrl.H = iDim
  Endif
  
  If hCtrl Is ToolButton Or If hCtrl Is MenuButton 
    If hCtrl.Action Then  
      hToolButton = hCtrl
      If Not hToolButton.Text Then 
        If hCtrl Is ToolButton Then
          hCtrl.Resize(iDim, iDim)
        Else
          hCtrl.Resize(iDim + Desktop.Scale * 2, iDim)
        Endif
      Endif
      sAction = hCtrl.Action
      If Left$(sAction) = "." Then sAction = Mid$(sAction, 2)
      hAction = MAction.Actions[sAction]
      If hAction And If hAction.IconPath Then
        hToolButton.Picture = GetIcon(hAction.IconPath)
      Endif
    Endif
  Endif
  
End

Private Sub CheckSeparators()
  
  Dim hLast As Control
  Dim hChild As Control
  Dim aDelete As New Control[]
  
  If $bDesign Then Return
  
  For Each hChild In panToolBar.Children
    If hChild Is Separator Then 
      hChild.Resize(SEPARATOR_SIZE, SEPARATOR_SIZE)
      If FToolBarConfig.WillBeDeleted(hChild) Then Continue
      hChild.Show
    Endif
  Next

  If $bConfigure Then Return

  For Each hChild In panToolBar.Children
  
    If Not hChild.Visible Then Continue
    If Not hLast Or If hLast Is Separator Then 
      If hChild Is Separator Then
        hChild.Hide
        Continue
      Endif
    Endif
    hLast = hChild
  
  Next
  
  If hLast And If hLast Is Separator Then hLast.Hide
  
End


Public Sub LoadConfig(Optional bInit As Boolean)
  
  Dim hToolbar As ToolBar = Me.Parent
  Dim hCtrl As Control
  Dim aConfig As String[]
  Dim sAction As String
  Dim iInd As Integer
  Dim bEnabled As Boolean
  Dim bVisible As Boolean
  Dim iDim As Integer
  
  If Not ToolbarKey Then Return
  If $bDesign Then Return
  
  'Print Me; ": Loading config for";; ToolbarKey
  
  MAction.InitAction

  Select Case Settings["/gb.form.mdi/Toolbars/" &/ ToolbarKey &/ "Size"]
    Case "Medium"
      $iIconSize = ICON_MEDIUM
      iDim = Desktop.Scale * 5
    Case "Large"
      $iIconSize = ICON_LARGE
      iDim = Desktop.Scale * 7
    Case Else
      $iIconSize = ICON_SMALL
      iDim = Desktop.Scale * 4
  End Select
  $iButtonWidth = iDim
  
  aConfig = Settings["/gb.form.mdi/Toolbars/" &/ ToolbarKey &/ "Layout"]
  If Not aConfig Then 
    If $iIconSize = ICON_SMALL Then
      If bInit Then Goto RAISE_CONFIGURE
    Endif
    Try aConfig = FindToolbar().DefaultConfig
    If Not aConfig Then Goto RAISE_CONFIGURE
  Endif
  
  bEnabled = panToolBar.Enabled
  panToolBar.Enabled = True
  panToolBar.Hide
  Lock()
  
  iInd = 0
  While iInd < panToolBar.Children.Count
    hCtrl = panToolBar.Children[iInd]
    ResizeControl(hCtrl, iDim)
    If hCtrl.Action Then
      hCtrl.Reparent(panHide)
    Else If hCtrl Is Separator Or If hCtrl Is ToolBarExpander Then 
      hCtrl.Delete
    Else
      Inc iInd
    Endif
  Wend
  
  For Each hCtrl In panHide.Children
    ResizeControl(hCtrl, iDim)
  Next

  For Each sAction In aConfig
    
    'Print "Action "; sAction
    If sAction = "|" Then
      'Print "--> AddSeparator()"
      AddSeparator()
    Else If sAction = "~" Then
      AddExpander()
    Else If Left(sAction) = "$" Then
      hCtrl = Me[Mid$(sAction, 2)]
      If hCtrl Then
        'Print "--> Reparent to panToolbar"
        hCtrl.Reparent(panToolBar)
        hCtrl.Raise
      Endif
    Else
      hCtrl = FindControl(sAction)
      If hCtrl Then
        'Print "--> Reparent to panToolbar"
        'bVisible = hCtrl.Visible
        hCtrl.Reparent(panToolBar)
        hCtrl.Raise
        If bInit Then hCtrl.Show
        'If hCtrl.Visible <> bVisible Then Debug "**** VISIBLE CHANGED FOR "; sAction
      Endif
    Endif
    
  Next
  
  ' For Each hCtrl In panToolBar.Children
  '   If hCtrl Is Separator Then Continue
  '   If hCtrl.Action Then Continue
  '   hCtrl.Raise
  ' Next
  
  panToolBar.Enabled = bEnabled
  panToolBar.Show
  
  Unlock()

RAISE_CONFIGURE:  
  
  hToolbar._RaiseConfigure
  
End

Public Sub SaveConfig(Optional bReset As Boolean)
  
  Dim aConfig As String[]
  Dim aOldConfig As String[]
  Dim hCtrl As Control
  Dim sAction As String
  Dim hForm As FToolBar
  Dim bChange As Boolean
  Dim sVal As String
  
  If Not ToolbarKey Then Return
  If $bDesign Then Return
  If Not Object.IsValid(Me.Parent.Parent) Then Return

  If Not bReset Then
    aConfig = New String[]
    For Each hCtrl In panToolBar.Children
      If hCtrl.Action Then
        sAction = hCtrl.Action
        If Left(sAction) = "." Then sAction = Mid$(sAction, 2)
        aConfig.Add(sAction)
      Else If hCtrl Is Separator
        aConfig.Add("|")
      Else If hCtrl Is ToolBarExpander Then
        aConfig.Add("~")
      Else If hCtrl.Visible Then
        aConfig.Add("$" & hCtrl.Name)
      Endif
    Next
    If aConfig.Join(",") = FindToolbar().DefaultConfig.Join(",") Then aConfig = Null
  Endif
  
  ' Print Me; ": Saving config for "; ToolbarKey; ": ";
  ' If aConfig Then
  '   Print aConfig.Join(",")
  ' Else
  '   Print "NULL"
  ' Endif
  'Print "Default config is ";; FindToolbar().DefaultConfig.Join(",")

  sVal = Choose($iIconSize + 1, "", "Medium", "Large")
  If sVal <> Settings["/gb.form.mdi/Toolbars/" &/ ToolbarKey &/ "Size"] Then
    Settings["/gb.form.mdi/Toolbars/" &/ ToolbarKey &/ "Size"] = sVal
    bChange = True
  Endif
  
  If Not bChange Then
    bChange = True
    aOldConfig = Settings["/gb.form.mdi/Toolbars/" &/ ToolbarKey &/ "Layout"]
    If Not aOldConfig And If Not aConfig Then
      bChange = False
    Else If aOldConfig And If aConfig And If aOldConfig.Join(",") = aConfig.Join(",") Then 
      bChange = False
    Endif
  Endif
  
  If Not bChange Then Return
  
  Settings["/gb.form.mdi/Toolbars/" &/ ToolbarKey &/ "Layout"] = aConfig
  Settings.Save
  
  For Each hForm In $cRegister[ToolbarKey]
  
    If Not bReset And If hForm = Me Then Continue
    hForm.LoadConfig
  
  Next
  
End

Private Sub Lock()
  
  Dim hToolbar As Toolbar 
  
  Inc $iLock
  If $iLock = 1 Then
    '$iArrangement = panToolBar.Arrangement
    hToolbar = Me.Parent
    hToolbar.Arrangement = Arrange.None
  Endif
  
End

Private Sub Unlock()
  
  If $iLock <= 0 Then Return
  Dec $iLock
  If $iLock = 0 Then
    Update
  Endif
  
End

Public Sub HideControl(hCtrl As Control)
  
  panToolBar.Enabled = True
  hCtrl.Reparent(panHide)
  panToolBar.Enabled = False
  
End

Public Sub ShowControl(hCtrl As Control)
  
  panToolBar.Enabled = True
  hCtrl.Reparent(panToolBar)
  panToolBar.Enabled = False
  
End

Public Sub SetDesign()
  
  $bDesign = True
  dwgHandle.ToolTip = ""
  dwgHandle.Mouse = Mouse.Default
  
End

Public Sub GetIconSize() As Integer

  Return $iIconSize
  
End

Public Sub GetRealIconSize() As Integer
  
  Return CalcIconSize(GetIconSize())
  
End


Public Sub SetIconSize(iSize As Integer)

  If $iIconSize = iSize Then Return
  $iIconSize = iSize
  Inc Application.Busy
  SaveConfig
  LoadConfig
  Dec Application.Busy
  
End


Public Sub HasSeparator() As Boolean
  
  Return sepToolbar.Visible
  
End

Public Sub SetSeparator(bVisible As Boolean)
  
  If sepToolbar.Visible = bVisible Then Return
  sepToolbar.Visible = bVisible
  Update
  
End


Public Sub Form_Menu()

  FToolBarConfig.OpenMenu(Me.Parent)

End
