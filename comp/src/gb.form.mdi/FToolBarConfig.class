' Gambas class file

Private $hToolbar As ToolBar
Private $hForm As FToolBar
Private $aDefault As String[]
Private $hInside As DrawingArea

Public Sub Run(hToolbar As ToolBar) 
  
  Me.Show
  SetCurrentToolbar(hToolbar)
  
End

Private Sub SetCurrentToolbar(hToolbar As ToolBar)

  'Debug hToolbar;; "-";; $hToolbar
  
  If hToolbar = $hToolbar Then Return
  
  If $hToolbar Then
    $hForm.SetConfigureMode(False)
    $hToolbar = Null
    $hForm = Null
  Endif
  
  If Not hToolbar Then Return
  
  $hToolbar = hToolbar
  $hForm = $hToolbar._Container.Parent

  If $hToolbar.Text Then 
    lblTitle.Text = Subst(("&1 toolbar"), $hToolbar.Text)
  Else
    lblTitle.Text = ("Main toolbar")
  Endif
  
  InitAction
  $hForm.SetConfigureMode(True)

End


Public Sub btnClose_Click()

  Me.Close

End

Public Sub AddAction(sAction As String)
  
  Dim hItem As Control

  'Debug sAction

  If Left(sAction) = "." Then sAction = Mid$(sAction, 2)
  
  For Each hItem In svwAction.Children
    If hItem.Tag = sAction Then
      hItem.Show
      Return
    Endif
  Next
  
  hItem = New DrawingArea(svwAction) As "Item"
  hItem.Tag = sAction
  hItem.Resize(Desktop.Scale * 32, Desktop.Scale * 5)
  
End


Private Sub InitAction()
  
  Dim hCtrl As Control
  Dim hItem As DrawingArea
  Dim sAction As String
  
  'For Each hCtrl In svwAction.Children
  '  hCtrl.Delete
  'Next
  svwAction.Children.Clear
  svwAction.Arrangement = Arrange.None
  
  AddAction("|")
  
  'Print $hToolbar.Children.Count;; $hToolbar._Container.Children.Count
  For Each sAction In MAction.Toolbars[$hForm.ToolbarKey].List
  
    If Not MAction.Actions.Exist(sAction) Then Continue
    hCtrl = $hForm.FindControl(sAction, True)
    If Not hCtrl Then Continue
    
    AddAction(sAction)
    
  Next
  
  svwAction.Arrangement = Arrange.Vertical
  
End


Public Sub Item_Enter()
  
  $hInside = Last
  $hInside.Refresh
  
End

Public Sub Item_Leave()
  
  $hInside = Null
  Last.Refresh
  
End

Public Sub Item_Draw()
  
  Dim hItem As DrawingArea = Last
  Dim hAction As CAction
  Dim hIcon As Picture
  Dim sText As String
  Dim W As Integer
  
  If hItem.Tag Then hAction = MAction.Actions[hItem.Tag]
  
  'If $hInside = hItem Then 
  Draw.Style.Button(0, 0, hItem.W, hItem.H, Not hItem.Enabled)
  If hAction Then
    If hAction.Icon Then 
      Draw.Picture(hAction.Icon, 4, (hItem.H - hAction.Icon.H) / 2)
      W = hAction.Icon.W
    Else
      W = Stock.GetSize("small")
    Endif
    Draw.Text(hAction.Display, W + 8, 0, hItem.W - W - 12, hItem.H, Align.Normal)
  Else
    W = Stock.GetSize("small")
    Draw.Rect(4 + W \ 2 - 2, (hItem.H - W) \ 2, 4, W)
    Draw.Text(("Separator"), W + 8, 0, hItem.W - W - 12, hItem.H, Align.Normal)
    'Draw.Style.Separator(8, (hItem.H - W) / 2, W - 4, W, False)
  Endif
  
End

Public Sub DragAction(hCtrl As Control, sAction As String)
  
  Dim hAction As CAction 
  Dim bOutside As Boolean
  Dim hDest As Control
  Dim bAnim As Boolean

  If Not sAction Then Return

  bOutside = hCtrl.Parent = svwAction
  ' If bOutside Then
  '   If sAction <> "|" Then hCtrl.Hide
  ' Else
  '   hCtrl.Hide
  ' Endif

  If sAction Then hAction = MAction.Actions[sAction]
  
  $hForm.DragStart(bOutside)
  If hAction Then 
    Drag.Icon = hAction.Icon
  Else
    Drag.Icon = Null
  Endif

  hDest = Drag(hCtrl, sAction, "text/gambas-x-action")
  
  $hForm.DragEnd(bAnim)
  
  If hDest Then
  
    If bOutside Then
    
      If hDest = panAction Then
      Else
        If hCtrl.Tag <> "|" Then 
          hCtrl.Hide
        Endif
      Endif
    
    Else
    
      If hDest = panAction Then
        If hCtrl.Action Then
          $hForm.HideControl(hCtrl)
        Else
          hCtrl.Delete
        Endif
        bAnim = True
      Else
        If Not hCtrl.Action Then
          hCtrl.Delete
        Endif
      Endif
    
    Endif
    
  Else
  
    If Not bOutside Then
      hCtrl.Show
    Endif
    bAnim = True
    
  Endif
  
End

Public Sub Item_MouseDrag()
  
  Dim hItem As DrawingArea = Last
  
  '$hInside = Null
  'hItem.Refresh
  DragAction(hItem, hItem.Tag)
  
End

Public Sub Form_Close()

  SetCurrentToolbar(Null)

End


Public Sub Form_Open()

  MAction.InitAction
  Me.Center

End


Public Sub panAction_Drag()

  If Drag.Format <> "text/gambas-x-action" Or If Drag.Source.Parent = svwAction Then
    Stop Event
    Return
  Endif
  
  Drag.Show(panAction)

End

Public Sub panAction_Drop()

  Dim sAction As String = Drag.Data
  
  If sAction <> "|" Then AddAction(sAction)

End


Public Sub btnReset_Click()

  If Message.Question(("Do you really want to reset the toolbar?"), ("Reset"), ("Cancel")) = 1 Then
    $hForm.SaveConfig(True)
    InitAction
  Endif

End

Public Sub btnUndo_Click()

  $hForm.LoadConfig
  InitAction

End
