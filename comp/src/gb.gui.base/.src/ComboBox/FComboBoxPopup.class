' Gambas class file

Private Const MAX_ITEMS As Integer = 10
Private $aList As String[]
Private $sFind As String
Private $hCombo As ComboBox
Private $bDown As Boolean
Private $bFixBreeze As Boolean

Public Sub _new()

  If Component.IsLoaded("gb.qt5") Or If Component.IsLoaded("gb.qt6") Then
    If Style.Name = "breeze" Then
      $bFixBreeze = True
    Endif
  Endif
  
End

Public Sub UpdateComboSize(Optional aList As String[])

  Dim W, H As Integer
  Dim WM As Integer
  Dim DY As Integer
  
  If aList Then $aList = aList
  
  Me.Arrangement = Arrange.None
  
  gvwPopup.Rows.Count = $aList.Count
  W = $hCombo.W '- Style.FrameWidth * 2
  
  If $bFixBreeze And If $hCombo.ReadOnly Then W -= 2
  
  WM = gvwPopup.Columns[0].W '+ 2
  If $aList.Count > MAX_ITEMS Then WM += Style.ScrollbarSize
  
  W = Max(W, WM)
  H = (gvwPopup.Font.TextHeight(" ") + 6) * Min(MAX_ITEMS, $aList.Count) + dwgFrame.Padding * 2
  
  Me.Arrangement = Arrange.Fill
  
  W = Min(W, Screens[$hCombo.Window.Screen].AvailableWidth)
  
  If Not $bDown Then
    DY = Me.H - H
  Endif
  
  Me.Resize(W, H)
  If DY Then Me.Y += DY
  
  gvwPopup.Refresh
  
End

Public Sub Open(aList As String[], iIndex As Integer, hCtrl As ComboBox) As Integer
  
  Dim iRow As Integer
  Dim X, Y, W, H As Integer
  Dim hScreen As Screen
  
  hScreen = Screens[hCtrl.Window.Screen]
  
  $aList = aList
  
  dwgFrame.Padding = Style.FrameWidth
  
  gvwPopup.Columns.Count = 1
  'gvwPopup.Columns[0].Expand = True
  gvwPopup.Columns[0].W = -1
  
  $hCombo = hCtrl
  UpdateComboSize()
  
  X = hCtrl.ScreenX '+ Style.FrameWidth
  If $bFixBreeze And If $hCombo.ReadOnly Then Inc X
  
  W = Me.W
  H = Me.H
  
  If hCtrl.ScreenY + hCtrl.H + H > hScreen.AvailableHeight Then 
    Y = hCtrl.ScreenY - H
    $bDown = False
  Else 
    Y = hCtrl.ScreenY + hCtrl.H - 1
    $bDown = True
  Endif
  
  If X + W > hScreen.AvailableX + hScreen.AvailableWidth Then X = hScreen.AvailableX + hScreen.AvailableWidth - W
  If X < hScreen.AvailableX Then X = hScreen.AvailableX
  
  Try gvwPopup.Row = iIndex
  
  iRow = Me.ShowPopup(X, Y) - 1
  $hCombo = Null
  
  $aList = Null
  
  Return iRow
  
End

Public Sub gvwPopup_Data(Row As Integer, (Column) As Integer)

  Try gvwPopup.Data.Text = $aList[Row]
  If gvwPopup.Rows.Count <> $aList.Count Then UpdateComboSize

End

Public Sub gvwPopup_MouseMove()

  Dim iRow As Integer

  If Mouse.X < 0 Or If Mouse.X >= gvwPopup.ClientW Then Return
  
  iRow = gvwPopup.RowAt(Mouse.Y)
  If iRow >= 0 Then 
    gvwPopup.Rows[iRow].EnsureVisible
    gvwPopup.Row = iRow
  Endif

End

' Public Sub gvwPopup_MouseWheel()
'   
'   gvwPopup_MouseMove
'   
' End

Public Sub gvwPopup_MouseUp()
  
  Me.Close(gvwPopup.Row + 1)
  
End

Public Sub Form_Activate()

  gvwPopup.SetFocus

End

Private Sub FindItem()

  Main.FindItem(gvwPopup, $sFind, $aList)
  
End

Public Sub Form_KeyPress()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Then
    Me.Close(gvwPopup.Row + 1)
  Else If Key.Code = Key.Escape Then
    Me.Close
  Else If Key.Code = Key.Backspace Then
    If $sFind Then
      $sFind = String.Left($sFind, -1)
      FindItem
    Endif
  Else If Key.Text Then
    $sFind &= Key.Text
    FindItem
  Endif

End

Public Sub dwgFrame_Draw()

  If $bDown Then
    Style.PaintBox(0, -Style.FrameWidth * 2, Paint.W, Paint.H + Style.FrameWidth * 2, Style.Normal, Color.TextBackground)
    Paint.FillRect(0, 0, Paint.W, 1, Color.LightForeground)
  Else 
    Style.PaintBox(0, 0, Paint.W, Paint.H + Style.FrameWidth * 2, Style.Normal)
    Paint.FillRect(0, Paint.H - 1, Paint.W, 1, Color.LightForeground)
  Endif

End
