' Gambas class file

Export
Inherits DrawingArea

Public Const _Properties As String = "*,-Arrangement,-Spacing,-Margin,-Indent,-Invert,-NoBackground,-Tablet,-Cached,-Focus"
Public Const _DefaultEvent As String = "MouseDown"
Public Const _DefaultSize As String = "16,16"
Public Const _IsContainer As Boolean = False
Public Const _Group As String = "Form"

Private $hObs As Observer


Public Sub _new()
  
  $hObs = New Observer(Me) As "DrawingArea"
  
End

Public Sub DrawingArea_Draw()
  
  Dim iCol As Integer
  Dim D As Integer
  
  If Me.Foreground = Color.Default Then
    iCol = Color.LightForeground
  Else
    iCol = Me.Foreground
  Endif
  
  If Paint.W = 1 Or If Paint.H = 1 Then
    Paint.FillRect(Paint.X, Paint.Y, Paint.W, Paint.H, iCol)
  Else 
    D = 2 + Desktop.Scale \ 2
    If Paint.W > Paint.H Then
      If Paint.W > D Then
        Paint.FillRect(Paint.X + D \ 2, Paint.Y + CInt(Paint.H) \ 2, Paint.W - D, 1, iCol)
      Endif
    Else
      If Paint.H > D Then
        Paint.FillRect(Paint.X + CInt(Paint.W) \ 2, Paint.Y + D \ 2, 1, Paint.H - D, iCol)
      Endif
    Endif
  Endif
  
End
