' Gambas class file

Export
Inherits ScrollArea

Public Const _Properties As String = "*,Arrangement{Arrange.None;Horizontal;Vertical;Row;Column;Fill},Spacing,Margin,Padding{Range:0;63},Indent"
Public Const _DefaultEvent As String = "Draw"
Public Const _DefaultSize As String = "24,24"
Public Const _Similar As String = "DrawingArea,ScrollView"

Static Private $bGTK2 As Boolean

Private $hCont As Panel
Private $hObs As Observer
Private $hTimer As Timer
Private $bLock As Boolean
Private $hScrollTo As Control

Static Public Sub _init()
  
  $bGTK2 = Component.IsLoaded("gb.gtk")
  
End

Public Sub _OnBackgroundChange()

  Dim hDrawingArea As DrawingArea

  If Not $bGTK2 Then Return  
  hDrawingArea = $hCont.Parent
  hDrawingArea.Background = Style.BackgroundOf(hDrawingArea)
  
End

Public Sub _new()
  
  $hObs = New Observer(Me._Container) As "Observer"
  $hCont = New Panel(Me) As "Panel"
  $hCont.Move(0, 0)
  
  Me._Container = $hCont
  $hTimer = New Timer As "AutoResizeContents"
  $hTimer.Trigger
  
  _OnBackgroundChange()
  
End

Public Sub Panel_BeforeArrange()
  
  AutoResizeContents
  
End

Public Sub Panel_Arrange()
  
  AutoResizeContents
  
End

Public Sub TimerArrange_Timer()
  
  AutoResizeContents
  
End

Public Sub AutoResizeContents_Timer()

  Dim W, H As Integer
  Dim hChild As Control
  Dim iSaveArr As Integer
  Dim D As Integer

  'If Me.Name = "Brown" Then Debug $bLock;; System.Backtrace.Join(" ")
  
  If $bLock Then Return
  
  $bLock = True
  
  Do
    
    W = 0
    H = 0
    
    For Each hChild In $hCont.Children
      If Not hChild.Visible Then Continue
      If hChild.Ignore Then Continue
      W = Max(W, hChild.X + hChild.W)
      H = Max(H, hChild.Y + hChild.H)
    Next
    
    If Me.Margin Then
      D = Me.Padding
      If D = 0 Then D = Desktop.Scale
      W += D
      H += D
    Endif
  
    Select Case Me.Arrangement
      Case Arrange.Horizontal, Arrange.TopBottom
        H = Me.ClientH
      Case Arrange.Vertical, Arrange.LeftRight
        W = Me.ClientW
    End Select
    
    'If Me.Name = "lstRecent" Then Debug "W =";; W;; "H =";; H
    If W = Me.ScrollW And If H = Me.ScrollH Then Break
    
    'If Me.Name = "lstRecent" Then Debug "ResizeContents:";; W;; H
    Me.ResizeContents(W, H)
    
  Loop
  
  W = Max(Me.ClientW, W)
  H = Max(Me.ClientH, H)

  If $hCont.W <> W Or If $hCont.H <> H Then
    'If Me.Name = "Brown" Then Debug "Resize cont "; W;; H
    $hCont.Resize(W, H)
    iSaveArr = $hCont.Arrangement
    $hCont.Arrangement = Arrange.None
    $hCont.Arrangement = iSaveArr
  Endif
  
  $bLock = False
  
  'If Me.Name = "svwSelection" Then Debug Me.ScrollW;; Me.ScrollH
  If $hScrollTo Then
    If Object.IsValid($hScrollTo) And If $hScrollTo.Parent = Me Then
      If Me.EnsureVisible($hScrollTo.X, $hScrollTo.Y, $hScrollTo.W, $hScrollTo.H) Then $hScrollTo = Null
    Endif
  Endif
  
End

Private Sub AutoResizeContents()

  $hTimer.Trigger

End

Public Sub Panel_MouseDown()
  
  Me.SetFocus
  'Me.DrawingArea_MouseWheel
  
End

Public Sub Panel_MouseWheel()
  
  Me.SetFocus
  Me.DrawingArea_MouseWheel
  
End

Public Sub Observer_Arrange()
  
  AutoResizeContents
  
End

Public Sub ScrollTo(Child As Control)

  $hScrollTo = Child
  AutoResizeContents
  
End
