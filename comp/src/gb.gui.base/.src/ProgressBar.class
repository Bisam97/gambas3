' Gambas class file

Export

Inherits UserControl

Public Const _Properties As String = "*,Action,Label=True"
Public Const _Group As String = "Form"
Public Const _DefaultEvent As String = "MouseDown"
Public Const _DefaultSize As String = "24,4"

Property Label As Boolean
Property Value As Float

Private $hDrawingArea As DrawingArea
Private $fValue As Float
Private $bLabel As Boolean = True

Public Sub _new()
  
  $hDrawingArea = New DrawingArea(Me) As "DrawingArea"
  
End


Private Function Label_Read() As Boolean

  Return $bLabel

End

Private Sub Label_Write(Value As Boolean)

  $bLabel = Value
  $hDrawingArea.Refresh

End

Private Function Value_Read() As Float

  Return $fValue

End

Private Sub Value_Write(Value As Float)

  $fValue = Max(0, Min(1, Value))
  $hDrawingArea.Refresh

End

Public Sub DrawingArea_Draw()
  
  Dim F As Integer = Style.FrameWidth
  Dim W As Float
  Dim iCol As Integer
  
  W = (Paint.W - F * 2) * $fValue
  
  iCol = Color.Merge(Color.Background, Color.LightForeground)
  If Not Me.Enabled Then iCol = Color.Desaturate(iCol)
  Paint.FillRect(F, F, Paint.W - F * 2, Paint.H - F * 2, iCol)
  
  iCol = Color.SelectedBackground
  If Not Me.Enabled Then iCol = Color.Desaturate(iCol)
  Paint.FillRect(F, F, W, Paint.H - F * 2, iCol)
  
  Style.PaintPanel(0, 0, Paint.W, Paint.H, Border.Sunken)
  'Style.PaintPanel(F, F, W, Draw.H - F * 2, Border.Raised)
  
  If $bLabel Then

    Paint.Save
    Paint.Rectangle(F, F, W, Paint.H - F * 2)
    Paint.Clip
    Paint.Background = Color.SelectedForeground
    Paint.DrawText(Format($fValue, "0%"), 0, 0, Paint.W, Paint.H, Align.Center)
    Paint.Restore

    Paint.Save
    Paint.Rectangle(F + W, F, Paint.W - F * 2 - W, Paint.H - F * 2)
    Paint.Clip
    Paint.Background = Color.Foreground
    Paint.DrawText(Format($fValue, "0%"), 0, 0, Paint.W, Paint.H, Align.Center)
    Paint.Restore

  Endif
  
End

