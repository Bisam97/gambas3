' Gambas class file

Private $iType As Integer

Public Sub _New(Value As Integer)
  
  $iType = Value
End


Public Sub Draw(CX As Integer, CY As Integer, CW As Integer, CH As Integer)  
  Dim oSerie As _CSerie
  Dim cnt As Integer
  Dim fMaxVal As Float
  Dim itmp As Float
  Dim i, j As Integer
  Dim f As Float
  Dim YHeadersW As Integer
  Dim iTwoLetterHeight As Integer = draw.Font.Height() * 2 * Chart._fProportionnal
  Dim iTabPos As Integer
  Dim iXUnit As Integer
  Dim iYUnit As Integer
  Dim ariPolyLine As New Integer[]
  Dim aroPolyLines As New Object[]
  Dim iStaticSpace As Integer = 5 * Chart._fProportionnal
  Dim hRect As New CRect
  Dim hUnits As CPoint
  draw.Font.Size = 11 * Chart._fProportionnal
  
  Select Case $iType
    Case ChartType.Lines, ChartType.LinesSymbols
      fMaxVal = MTools.GetChartMaxValue(Chart)
    Case ChartType.LinesStacked
      fMaxVal = MTools.GetChartMaxCumulateValue(Chart)
    Case ChartType.LinesPercent
      fMaxVal = 100
  End Select
  cnt = draw.TextWidth(fMaxVal & "00")
    'Get the max width needed for draw the Y labels
  If Chart.YAxe.Visible Then 
    YHeadersW = CX + iStaticSpace + cnt + iStaticSpace * 2
  Else
    YHeadersW = CX + iStaticSpace * 2
  Endif
  
  'Set the graph square position value
  If Chart.XAxe.Visible Then 
    hRect.Top = CY + iTwoLetterHeight
    hRect.Bottom = CY + CH - iTwoLetterHeight
  Else
    hRect.Top = CY + iTwoLetterHeight
    hRect.Bottom = CY + CH - iStaticSpace * 2
  Endif
  hRect.Left = YHeadersW
  hRect.Right = CX + CW - YHeadersW / 2
  
  'Draw the background
  Draw.ForeColor = Color.LightGray
  Draw.FillColor = Color.LightGray
  Draw.FillStyle = Fill.Solid
  Draw.Rect(hRect.Left, hRect.Top, hRect.Right - hRect.Left, hRect.Bottom - hRect.Top)
  Draw.ForeColor = Color.Black
  
  
  hUnits = MTools.DrawChartAxes(Chart, hRect, 0, fMaxVal)
  
 If Chart.Count < 2 Then Return 
  Draw.FillStyle = Fill.Solid
  
  For j = 0 To Chart.CountDataSets - 1
    ariPolyLine = New Integer[]
    ariPolyLine.Resize(Chart.Count * 2)
    aroPolyLines.Add(ariPolyLine)
    
  Next  
  
  For i = 0 To Chart.Count - 1
  f = 0
  fMaxVal = 0  
    For j = 0 To Chart.CountDataSets - 1
      fMaxVal += Chart[j][i]
    Next
    For j = 0 To Chart.CountDataSets - 1
      Select Case $iType
        Case ChartType.LinesPercent
          f += Chart[j][i] / fMaxVal * 100
        Case ChartType.LinesStacked
          f += Chart[j][i]
        Case Else
          f = Chart[j][i]
      End Select 
      aroPolyLines[j][i * 2] = hRect.Left + (hUnits.X * i)
      aroPolyLines[j][i * 2 + 1] = hRect.Bottom - f * hUnits.Y
    Next
    

  Next
  
  For i = 0 To aroPolyLines.Max 'TO 0 STEP -1
  Draw.LineWidth = 2
  Draw.ForeColor = Chart.Colors[i]
  Draw.LineStyle = Line.Solid
    draw.Polyline(aroPolyLines[i])  
  Next
  
  If $iType = ChartType.LinesSymbols Then 
  
    For i = 0 To aroPolyLines.Max
    
      For j = 0 To aroPolyLines[i].Max Step 2
        Draw.LineWidth = 1
        Draw.FillColor = Chart.Colors[i]
        Draw.ForeColor = Color.DarkGray
        MTools.DrawSymbol(i, aroPolyLines[i][j], aroPolyLines[i][j + 1])
      
      Next
    
    Next
  
  Endif
  
  
End
