' Gambas module file

Public Sub Main()

    Dim hProc As Process
    Dim hHarness As New TestHarness

    hProc = Exec ["gbx3", "-s", "SampleTest", Application.Path] For Read
    hHarness.Read(hProc, "SampleTest")

    With hHarness.Current
        Dim sLine As String
        Print "Transcript of the TAP stream:"
        Print
        Print String$(80, "*")
        Print
        For Each sLine In .Lines
            Print sLine
        Next
        Print
        Print String$(80, "*")
        Print

        Print "Test";; .Name;; IIf(.Success, "PASSED", "FAILED");; "("; Format$(DateDiff(.Started, .Ended, gb.Second), "0.00s"); ")"
        If .Run <> .Plan[1] Then Print "Planned";; .Plan[1];; "tests but ran";; .Run
        If .Failed Then
            Dim sFail As String
            Print "Failed";; .Failed;; "out of";; .Run;; "tests:"
            For Each sFail In .Failures
                Print sFail
            Next
        Endif
        If .BailedOut Then Print "Bailed out with message";; .BailMessage
        If .Bonus Then Print "Passed";; .Bonus;; "additional tests marked as TODO"
    End With

End
