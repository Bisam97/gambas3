' Gambas class file

Export

Inherits MIMEMessage

''Returns the ID used to list the message as per the LIST command
' Property Read ID As Integer
Private $msgid As Integer

Private $oPOPClient As Pop3Client

''Returns the content of the message. It is cached after the first request.
Property Read Text As String
Private $sText As String

''Returns the size of the message. It is cached after the first request.
Property Read Size As Integer
Private $iSize As Integer

''Returns the unique id of the message. It is cached after the first request.
Property Read UniqueID As String
Private $sUID As String

Public Sub _new(msgid As Integer, $oClient As Pop3Client)

  If $oClient.Debug Then $oClient._PrintDebug("Creating new message instance for " & msgid)

  $msgid = msgid

  $oPOPClient = $oClient

  Super._new(Me.Text)

End

Private Function Text_Read() As String

  Dim sResponse As String

  If $sText Then Return $sText

  sResponse = $oPOPClient.Exec("RETR " & $msgid, True)

  If Pop3Client.isPositive(sResponse) Then
    sResponse = Pop3Client.StripOK(sResponse)

    $sText = sResponse

    Return sResponse

  Endif

End

Private Function Size_Read() As Integer

  Dim sResponse As String

  If $iSize Then Return $iSize

  sResponse = $oPOPClient.Exec("LIST " & $msgid, False)

  If Pop3Client.isPositive(sResponse) Then
    sResponse = Pop3Client.StripOK(sResponse)

    $iSize = CInt(Split(sResponse, " ")[1])

    Return $iSize

  Endif

End

''Deletes the message from the mailbox
Public Function Delete() As Boolean

  Dim sResponse As String

  sResponse = $oPOPClient.Exec("DELE " & $msgid)

  Return Pop3Client.isPositive(sResponse)

End

''Returns _Lines_ from the tom of the message
Public Function Top(Lines As Integer) As String

  Dim sResponse As String

  sResponse = $oPOPClient.Exec(Subst("TOP &1 &2", $msgid, Lines), True)

  If Pop3Client.isPositive(sResponse) Then
    sResponse = Pop3Client.StripOK(sResponse)

    Return sResponse

  Endif

End

Private Function UniqueID_Read() As String

  Dim Response As String

  If $sUID Then Return $sUID

  Response = $oPOPClient.Exec("UIDL " & $msgid, False)

  If Pop3Client.isPositive(Response) Then
    Response = Pop3Client.StripOK(Response)

    $sUID = Split(Response, " ")[1]

    Return $sUID

  Endif

End

''Clears the cached data
Public Sub Refresh()

  $iSize = 0
  $sText = ""
  $sUID = ""

End

Public Sub _free()

  $oPOPClient = Null

End
