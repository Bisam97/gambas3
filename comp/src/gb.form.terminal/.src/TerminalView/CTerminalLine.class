' Gambas class file

Public Text As String
Public Length As Integer
Public Attr As New Short[]

Public Sub GetAttr(hAttr As TerminalAttr, X As Integer)
  
  Dim iAttr As Short
  
  Try iAttr = Attr[X]
  
  hAttr.Background = iAttr And 15
  If hAttr.Background = 15 Then hAttr.Background = -1
  hAttr.Foreground = Lsr(iAttr And &HF0, 4)
  If hAttr.Foreground = 15 Then hAttr.Foreground = -1
  hAttr.Bold = iAttr And &H100
  hAttr.Dim = iAttr And &H200
  hAttr.Reverse = iAttr And &H400
  hAttr.Underscore = iAttr And &H800
  hAttr.Blink = iAttr And &H1000
  
End

Public Sub SetAttr(hAttr As TerminalAttr, X As Integer, Optional L As Integer = 1, Optional bInsert As Boolean)
  
  Dim iAttr As Integer
  Dim aAttr As Short[]
  
  iAttr = (hAttr.Background And 15) + Lsl(hAttr.Foreground And 15, 4)
  If hAttr.Bold Then iAttr += &H100
  If hAttr.Dim Then iAttr += &H200
  If hAttr.Reverse Then iAttr += &H400
  If hAttr.Underscore Then iAttr += &H800
  If hAttr.Blink Then iAttr += &H1000
  
  If iAttr = 0 And If X + L >= Attr.Count Then
    Attr.Resize(X)
  Else
    If bInsert Then
      aAttr = New Short[L]
      aAttr.Fill(iAttr)
      Attr.Insert(aAttr, X)
    Else
      If Attr.Count < X + L Then Attr.Resize(X + L)
      Attr.Fill(iAttr, X, L)
    Endif
  Endif
  
End

