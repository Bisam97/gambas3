' Gambas class file

Fast Unsafe

Public Text As String
Public Length As Integer

Private Attr As New Long[]
Private AttrLen As New Byte[]

Public NewLine As Boolean = True

Private $bCompressed As Boolean

Private Sub Uncompress()

  Dim I As Integer
  Dim iAttr As Long
  Dim iCount As Integer
  Dim aAttr As New Long[]
  Dim aTemp As Long[]

  For I = 0 To Attr.Max
    iAttr = Attr[I]
    iCount = AttrLen[I] + 1 '(Lsr(iAttr, 9) And 127) + 1
    aTemp = New Long[iCount]
    aTemp.Fill(iAttr)
    aAttr.Insert(aTemp)
  Next
  
  'If aAttr.Count > Length Then Stop
  
  Attr = aAttr
  AttrLen = Null
  $bCompressed = False

End

Private Sub Compress(bFull As Boolean)
  
  Dim I As Integer
  Dim iAttr As Long
  Dim iOldAttr As Long
  Dim iCount As Integer
  Dim aAttr As New Long[]
  Dim aAttrLen As New Byte[]
  
  For I = 0 To Min(Length - 1, Attr.Max)
    
    iAttr = Attr[I]
    If iAttr = iOldAttr And If iCount < 256 Then
      Inc iCount
      Continue
    Endif
    
    If iCount Then 
      aAttr.Add(iOldAttr)
      aAttrLen.Add(iCount - 1)
    Endif
    iCount = 1
    iOldAttr = iAttr
    
  Next
  
  If iCount Then 
    aAttr.Add(iOldAttr)
    aAttrLen.Add(iCount - 1)
  Endif
  
  If bFull And If Attr.Count < Length Then
    iCount = Length - Attr.Count
    While iCount >= 256
      aAttr.Add(0)
      aAttrLen.Add(255)
      iCount -= 256
    Wend
    If iCount Then
      aAttr.Add(0)
      aAttrLen.Add(iCount - 1)
    Endif
  Endif
  
  $bCompressed = True
  
  Attr = aAttr
  AttrLen = aAttrLen
  
End

Public Sub SetAttr(hAttr As TerminalAttr, X As Integer, L As Integer, bInsert As Boolean)
  
  Dim iAttr As Long
  Dim aAttr As Long[]
  Dim aTemp As Long[]
  
  iAttr = hAttr._ToValue()
  
  If $bCompressed Then Uncompress()
  
  aAttr = Attr
  
  If iAttr = 0 And If X >= aAttr.Count Then
    'aAttr.Resize(X)
  Else
    If bInsert Then
      If aAttr.Count < X Then aAttr.Resize(X)
      aTemp = New Long[L]
      aTemp.Fill(iAttr)
      aAttr.Insert(aTemp, X)
    Else
      If aAttr.Count < X + L Then aAttr.Resize(X + L)
      aAttr.Fill(iAttr, X, L)
    Endif
  Endif
  
End

Public Sub GetAttr() As Long[]
  
  If Not $bCompressed Then Compress(False)
  Return Attr
  
End

Public Sub GetFullAttr() As Long[]
  
  If $bCompressed Then Uncompress()
  Compress(True)
  Return Attr
  
End

Public Sub GetAttrLength() As Byte[]
  
  Return AttrLen
  
End

Public Sub InitAttr(aInit As Long[], aInitLen As Byte[])

  Dim L As Integer
  Dim I As Integer
  Dim iAttr As Long
  Dim iCount As Integer
  Dim C As Integer
  Dim aAttr As Long[]
  Dim aTemp As Long[]
  
  L = Length
  aAttr = New Long[]

  For I = 0 To aInit.Max
    
    If L = 0 Then Break
    
    iAttr = aInit[0]
    iCount = aInitLen[0] + 1
    C = Min(L, iCount)
    aTemp = New Long[C]
    aTemp.Fill(iAttr)
    aAttr.Insert(aTemp)
    
    L -= C
    If C = iCount Then
      aInit.Remove(0)
      aInitLen.Remove(0)
    Else
      aInitLen[0] -= C
    Endif
    
  Next
  
  For I = aAttr.Max DownTo 0
    If aAttr[I] Then Break
  Next

  If I < aAttr.Max Then aAttr.Remove(I + 1, -1)
  
  Attr = aAttr
  AttrLen = Null
  $bCompressed = False
  
End


Public Sub Clear()
  
  Text = ""
  Length = 0
  NewLine = True
  Attr.Clear
  AttrLen = Null
  $bCompressed = False
  
End

' Public Sub _Dump()
'   
'   Dim I As Integer
'   Dim hAttr As New TerminalAttr
'   Dim iLen As Integer
'   
'   Compress(False)
'   Print "<"; Length; "> ";
'   For I = 0 To Attr.Max
'     iLen = hAttr._FillFrom(Attr[I])
'     Print Hex(hAttr._ToValue(), 16);; "["; iLen; "]";;
'   Next
'   Print
'   
' End
' 
