' Gambas class file

Inherits ReportControl
Export 

Private $hPic As Image
Private $sPath As String

Property Path As String

Private Function Path_Read() As String

  Return $sPath

End

Private Sub Path_Write(Value As String)

  If Exist(Value) Then
    $sPath = Value
    $hPic = Image.Load(Value)
  Else 
    Error.Raise("Cannot find this file !")
  Endif
  
End

Public Sub _GetSizeHints(AvailableW As Float, AvailableH As Float, TotalWidth As Float, TotalHeight As Float) As TSizeHint

  Dim hMyHints As New TSizeHint
  Dim h As Float = Me._Height
  Dim w As Float = Me._Width
  Dim Scale As Float
  If h > 0 Or w > 0 Then
    
    If h > w Then 
      Scale = $hpic.Width / $hpic.Height
      hMyHints.Width = (h * Scale) + (2 * Me._Padding)
      hMyHints.Height = h + (2 * Me._Padding)
    Else 
      Scale = $hpic.Height / $hpic.Width
      hMyHints.Height = (w * Scale) + (2 * Me._Padding)
      hMyHints.Width = w + (2 * Me._Padding)
    Endif
  Else 
    hMyHints.Height = AvailableH + (2 * Me._Padding)
    hMyHints.Width = AvailableW + (2 * Me._Padding)
  Endif
  
  Return hMyHints
  
End

Public Sub _Draw(Page As Integer, X As Integer, Y As Integer, hControl As TControl, VirtualId As Integer)
    
    $hPic = $hPic.Stretch(hControl.RealWidth * MRTools.ReportZoom, hControl.RealHeight * MRTools.ReportZoom)
    Paint.Brush = Paint.Image($hpic, 0, 0)
    
    Paint.Rectangle((x + hControl.RealLeft + MRTools.UnitsToPixels(Me.Padding)) * MRTools.ReportZoom, (y + hControl.RealTop + MRTools.UnitsToPixels(Me.Padding)) * MRTools.ReportZoom, hControl.RealWidth * MRTools.ReportZoom, hControl.RealHeight * MRTools.ReportZoom)
    Paint.Fill
    
    'Draw.Picture($hPic, (x + hControl.RealLeft + MRTools.UnitsToPixels(Me.Padding)) * MRTools.ReportZoom, (y + hControl.RealTop + MRTools.UnitsToPixels(Me.Padding)) * MRTools.ReportZoom, hControl.RealWidth * MRTools.ReportZoom, hControl.RealHeight * MRTools.ReportZoom)

End


