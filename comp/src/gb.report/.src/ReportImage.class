' Gambas class file
Export
Inherits ReportControl

Public Const _Properties As String = "*,Image{Image}"
Public Const _Similar As String = "ReportTextLabel"
Private $hPic As Image

Property Image As Image

Public Sub _GetSizeHints(AvailableW As Float, AvailableH As Float, TotalWidth As Float, TotalHeight As Float) As TSizeHint

  Dim hMyHints As New TSizeHint
  Dim h As Float = Me._Height
  Dim w As Float = Me._Width
  Dim Scale As Float
  If h > 0 Or w > 0 Then
    
    If h > w Then 
      Scale = $hpic.Width / $hpic.Height
      hMyHints.Width = (h * Scale) + (2 * Me._Padding)
      hMyHints.Height = h + (2 * Me._Padding)
    Else 
      Scale = $hpic.Height / $hpic.Width
      hMyHints.Height = (w * Scale) + (2 * Me._Padding)
      hMyHints.Width = w + (2 * Me._Padding)
    Endif
  Else 
    
    If h > AvailableH Then 
      hMyHints.Height = AvailableH + (2 * Me._Padding)
    Else
      hMyHints.Height = h + (2 * Me._Padding)
    Endif
    If w > AvailableW Then
      hMyHints.Width = AvailableW + (2 * Me._Padding)
    Else
      hMyHints.Width = w + (2 * Me._Padding)
    Endif
    
  Endif
  
  Return hMyHints
  
End

Public Sub _Paint(Page As Integer, X As Float, Y As Float, hControl As TControl, VirtualId As Integer)
    
    Dim ix, iy As Float
    Dim hBrush As PaintBrush
    
    ix = x + hControl.RealLeft + MTools.UnitsToPixels(Me._Padding)
    iy = y + hControl.RealTop + MTools.UnitsToPixels(Me._Padding)
    
    '$hPic = $hPic.Stretch(hControl.RealWidth, hControl.RealHeight)
    
    hBrush = Paint.Image($hpic)
    hBrush.Translate(ix, iy)
    hBrush.Scale(hControl.RealWidth / $hPic.Width, hControl.RealHeight / $hPic.Height)
    Paint.Brush = hBrush
    Paint.Rectangle(ix, iy, hControl.RealWidth, hControl.RealHeight)
    Paint.Fill
    
    'Draw.Picture($hPic, (x + hControl.RealLeft + MRTools.UnitsToPixels(Me.Padding)) * MRTools.ReportZoom, (y + hControl.RealTop + MRTools.UnitsToPixels(Me.Padding)) * MRTools.ReportZoom, hControl.RealWidth * MRTools.ReportZoom, hControl.RealHeight * MRTools.ReportZoom)

End


Private Function Image_Read() As Image

  Return $hPic

End

Private Sub Image_Write(Value As Image)

  $hPic = Value
  If Left(Me.Width, 1) = "0" Then Me.Width = $hpic.Width & " px"
  If Left(Me.Height, 1) = "0" Then Me.Height = $hpic.Height & " px"
  

End
