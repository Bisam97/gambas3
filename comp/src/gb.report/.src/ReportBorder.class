' Gambas class file
Export

Static Public Const None As Integer = 0
Static Public Const Solid As Integer = 1

Public Top As Boolean = True
Public {Left} As Boolean = True
Public Bottom As Boolean = True
Public {Right} As Boolean = True
Public Width As String = "1 px"
Public Color As Integer 
Public Style As Integer

Static Private Sub ResetBorder(hReportBorder As ReportBorder) As Boolean
  
  With hReportBorder
    .Bottom = False
    .Top = False
    .Left = False
    .Right = False
  End With
  
  Return True
  
End


Static Public Function _get(sValue As String) As ReportBorder

  Dim sElt As String
  Dim hReportBorder As New ReportBorder
  Dim bReset As Boolean
  
  If Not sValue Then Return hReportBorder
  
  hReportBorder.Style = ReportBorder.Solid
  
  For Each sElt In Split(sValue, ",", "[]", True, True)
  
    Select Case UCase(sElt)
    
      Case "TOP"
        If Not bReset Then bReset = ResetBorder(hReportBorder)
        hReportBorder.Top = True
      Case "BOTTOM"
        If Not bReset Then bReset = ResetBorder(hReportBorder)
        hReportBorder.Bottom = True
      Case "LEFT"
        If Not bReset Then bReset = ResetBorder(hReportBorder)
        hReportBorder.Left = True
      Case "RIGHT"
        If Not bReset Then bReset = ResetBorder(hReportBorder)
        hReportBorder.Right = True
      
      Case Else
        If sElt Begins "&H" Then
          hReportBorder.Color = Val(sElt)
        Else If IsDigit(Left$(sElt)) Then 
          hReportBorder.Width = sElt
        Endif
        
    End Select
    
  Next
  
  Return hReportBorder
  
End

Public Function ToString() As String
  
  Dim aValue As New String[]
  
  If Me.Style = ReportBorder.None Then Return 'aValue.Add("None")
  'If Me.Style = ReportBorder.Solid Then aValue.Add("Solid")
  aValue.Add(Me.Width)
  If Me.Top Then aValue.Add("Top")
  If Me.Bottom Then aValue.Add("Bottom")
  If Me.Left Then aValue.Add("Left")
  If Me.Right Then aValue.Add("Right")
  aValue.Add("&H" & Hex(Me.Color, 6))
  Return aValue.Join()
  
End
