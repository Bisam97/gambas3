' Gambas class file

Export
Inherits ReportFrame

Public Const _Properties As String = "*,Text,Alignment{Align.*}"
Public Const _Similar As String = "ReportControl"
Property Text As String
Property Alignment As Integer
Property WordWrap As Boolean

Private $bWordWrap As Boolean
Private $sText As String
Private $iAlignment As Integer = Align.TopNormal
Private $fZoom As Float

Private Function WordWrap_Read() As Boolean

  Return $bWordWrap

End

Private Sub WordWrap_Write(Value As Boolean)

  $bWordWrap = Value

End

Private Function Text_Read() As String

  Return $sText

End

Private Sub Text_Write(Value As String)

  $sText = Value

End

Private Function Alignment_Read() As Integer

  Return $iAlignment

End

Private Sub Alignment_Write(Value As Integer)

  $iAlignment = Value

End

Public Sub _GetSizeHints(AvailableW As Float, AvailableH As Float, TotalWidth As Float, TotalHeight As Float) As TSizeHint

  Dim hMyHints As New TSizeHint
  Dim fTextHeight As Float

  hMyHints = Super._GetSizeHints(AvailableW, AvailableH, TotalWidth, TotalWidth)

  If Me.Autoresize Then

    'La largeur est fix√©e par l'utilisateur
    'Calcule de la hauteur
    If Not IsNull(Me.Font) Then Paint.Font = Me.Font
    fTextHeight = MTools.PixelsToUnits(Paint.RichTextHeight($sText, MTools.UnitsToPixels(hMyHints.Width)))
    hMyHints.Height = Max(hMyHints.Height, Me.Border._Top + Me.Padding._Top + fTextHeight + Me.Padding._Bottom + Me.Border._Bottom)
  Endif

  Return hMyHints

End

Public Sub _Paint(Page As Integer, X As Float, Y As Float, hControl As TControl, VirtualId As Integer)

  Dim iX, iY, iW, iH As Integer

  Paint.Brush = Paint.Color(Me.BorderColor)
  If Me.Tag = "*" Then Stop

  'Set the Font if it is initialized
  If Not IsNull(Me.Font) Then Paint.Font = Me.Font

  iX = (x + hControl.RealLeft + MTools.UnitsToPixels(Me.Padding._Left + Me.Border._Left))
  iY = (y + hControl.RealTop + MTools.UnitsToPixels(Me.Padding._Top + Me.Border._Top))
  iW = (hControl.RealWidth - MTools.UnitsToPixels(Me.Padding._Left + Me.Padding._Right + Me.Border._Left + Me.Border._Right))
  iH = (hControl.RealHeight - MTools.UnitsToPixels(Me.Padding._Top + Me.Padding._Bottom + Me.Border._Top + Me.Border._Bottom))

  Paint.RichText($sText, iX, iY, iW, iH, $iAlignment)

End
