' Gambas class file

Static Private $cInfo As Collection
Static Private $cKey As Collection
Static Private $cFields As Collection
Static Private $cTable As Collection
Static Private $cBlob As Collection

Static Public Sub _init()
  
  Try DB.Open
  Reset()
  
End


Static Public Function _get(sTable As String, sField As String, Optional bCreate As Boolean) As DataField
  
  Dim hTable As Table
  Dim hField As Field
  Dim iType As Integer
  Dim bReadOnly As Boolean
  Dim hInfo As DataField
  Dim sKey As String
  Dim cInfoTable As Collection
  
  sKey = sField
  If bCreate Then sKey &= "."
  
  If Not $cInfo.Exist(sTable) Then $cInfo[sTable] = New Collection
  cInfoTable = $cInfo[sTable]
  
  If Not cInfoTable.Exist(sKey) Then

    hTable = DB.Tables[sTable]
    hField = hTable.Fields[sField]

    hInfo = New DataField
    hInfo.FromField(hField, hTable)
    If bCreate Then hInfo.ReadOnly = False
    
    cInfoTable[sKey] = hInfo
    
  Endif
  
  Return cInfoTable[sKey]
  
End


Static Public Function GetDefaultValue(sTable As String, sField As String) As Variant
  
  Return DataConnection[sTable, sField].Default
  
End

Static Public Function GetPrimaryKey(sTable As String) As String[]
  
  If Not $cKey.Exist(sTable) Then
    $cKey[sTable] = DB.Tables[sTable].PrimaryKey
  Endif
  
  Return $cKey[sTable]  
  
End

Static Public Function GetFields(sTable As String) As String[]

  Dim aField As String[]
  Dim hField As Field
  
  If Not $cFields.Exist(sTable) Then
    aField = New String[]
    For Each hField In DB.Tables[sTable].Fields
      aField.Add(hField.Name)
    Next
    $cFields[sTable] = aField
  Endif
  
  Return $cFields[sTable]  
  
End

Static Public Function ExistTable(sTable As String) As Boolean
  
  If Not $cTable.Exist(sTable) Then
    $cTable[sTable] = DB.Tables.Exist(sTable)
  Endif
  Return $cTable[sTable]  
  
End


Static Public Sub Reset(Optional sTable As String)
  
  Dim hDataField As DataField
  
  If sTable Then
    If $cInfo Then 
      $cInfo.Remove(sTable)
      $cKey.Remove(sTable)
      $cFields.Remove(sTable)
      $cTable.Remove(sTable)
    Endif
  Else
    $cInfo = New Collection
    $cKey = New Collection
    $cFields = New Collection
    $cTable = New Collection
  Endif
  
End

Static Public Sub CheckTable(sTable As String)
  
  If DB.Tables.Exist(sTable) Then Return
  Reset(sTable)
  
End
