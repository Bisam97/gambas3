' Gambas class file

Private Sub _PrintTitle()

  If Main.Path Then
    Print Html(Main.Path);
  Else
    Print "/";
  Endif

End

Private Sub _PrintLink()

  Dim aPath As String[]
  Dim sElt As String
  Dim sPath As String
  
  aPath = Split(Main.Path, "/", "", True)
  For Each sElt In aPath
    sPath &= "/" & sElt
    Print "<a href=\""; Application.Root &/ sPath; "\">"; "/"; sElt; "</a>";
  Next
  
End

Private Sub _PrintPage()

  Dim sPath As String
  Dim sData As String
  Dim iPos As Integer

  If Main.Edit Then
    sPath = Main.GetPagePath()  
    If Exist(sPath) Then sData = File.Load(sPath)
    If Not sData Then sData = OldWiki.GetPage(Main.Path)
    Print sData;
    Return
  Endif

  If Main.Image Then
    Print "<img src=\"" & Application.Root &/ Main.Path & "?v\" />";
    Return
  Endif
  
  sPath = Main.SolvePath(Main.Path)
  
  If Not Main.ExistPage(sPath) Then
    Print "<div class=\"unknown\">"; ("This page does not exist.")
    If Session.Id Then Print "<p>"; ("Click on the <b>Create</b> button to create it!")
    Print "</div>"
    Return
  Endif
  
  sData = LTrim(File.Load(Main.GetPagePathFrom(sPath)))
  If Main.Symbol Then
    If sData Begins "# " Then
      iPos = InStr(sData, "\n")
      If iPos = 0 Then iPos = Len(sData)
      sData = Mid$(sData, iPos + 1)
    Endif
    sData = "# " & Main.GetSymbolTitle() & sData
  Endif
  Print WikiMarkdown.ToHTML(sData);
  
Catch
  
  Print "<pre>"; Html(Error.Text); " at line #"; WikiMarkdown.Line + 1; ":\n\n"; Html(WikiMarkdown.Current); "\n\nStack backtrace:\n\n"; Error.Backtrace.Join("\n"); "</pre>";

End

Private Sub _PrintParent()

  Dim sPath As String

  sPath = Request.Path
  If sPath Then sPath = File.Dir(sPath)
  Print Application.Root &/ sPath;
  If Main.Lang <> Main.DEFAULT_LANG Then Print "?l="; Main.Lang;

End

Private Sub _PrintLang()

  Dim sLang As String
  
  Print "<select id=\"lang\" name=\"l\" onchange=\"document.form.submit();\">"

  For Each sLang In Main.Languages.Copy().Sort()
    Print "<option value=\""; sLang; "\"";
    If sLang = Main.Lang Then Print " selected";
    Print ">"; Html(sLang); "</option>"
  Next

  Print "</select>"  

End
