' Gambas class file

Public Name As String
Public Title As String
Public Version As String
Public Description As String
Public Authors As String[]
Public Icon As String

Private Sub GetProjectPath(sPath As String) As String
  
  Do
    If Exist(sPath &/ ".project") Then Return sPath
    sPath = File.Dir(sPath)
    If sPath = "/" Then Break
  Loop
  
Catch
  
End

Public Sub _new(sPath As String)
  
  Dim hFile As File
  Dim sIcon As String
  Dim sLine As String
  Dim iPos As Integer
  Dim sKey As String
  Dim sVal As String
  
  sPath = GetProjectPath(sPath)
  If Not sPath Then Return
  
  Name = File.Name(sPath)
    
  hFile = Open sPath &/ ".project"
  
  While Not Eof(hFile)
    Line Input #hFile, sLine
    sLine = Trim(sLine)
    If Not sLine Then Continue 
    If Left(sLine) = "#" Then Continue

    iPos = InStr(sLine, "=")
    If iPos = 0 Then Continue

    sKey = Lower$(Trim(Left$(sLine, iPos - 1)))
    sVal = Trim(Mid$(sLine, iPos + 1))

    Select Case sKey
    
      Case "title"
        Title = sVal

      Case "version"
        Version = sVal
        
      Case "description"
        Description = Unquote(sVal)

      Case "authors"
        Authors = Split(Unquote(sVal), "\n")

      Case "icon"
        sIcon = sVal

    End Select
  
  Wend
  
  Close #hFile

  If sIcon Then
    Icon = sPath &/ sIcon 'Project.StretchIcon(sPath &/ sIcon, 58)
  Else
    Icon = "img/logo/logo-32.png"
  Endif

End

Public Sub GetTitle() As String
  
  If Title Then Return Title
  Return Name
  
End

' Public Sub GetIcon(Optional iSize As Integer) As Image
'   
'   Dim hImage As Image = Image.Load(Icon)
'   
'   If iSize Then hImage = hImage.Stretch(iSize, iSize)
'   Return hImage
'   
' End
