' Gambas class file
Private $hEditor As Editor
Private $iX As Integer
Private $iY As Integer
Private $sHelpTheme As String
Public MouseOver As Boolean = False

Public Sub _New()
  
  $sHelpTheme = "<span style=background:green>&1<br><font color=green>Kind: </font>&2<br>&3</span><hr>&4"
  

  'Config.LoadWindow(ME, "/FOutput")
  Settings.Read(Me)
  
  btnUndock.Value = Settings["/FHelp/Undock"]


  
End

Public Sub Form_Open()

  If Not Me.Parent Then 
    Settings.Read(Me)
    'DEBUG "Read: "; ME.X;; ME.Y
  Endif
  
End

Private Sub ShowHelp()
  Dim iX, iY As Integer
  Dim hCont As Container
  
  If Not FDebugInfo.IsHelpVisible() Then
    Me.Background = Color.LightBackground
    Me.Ignore = True
    If Me.parent <> Null Then 
      Me.Reparent(Null)
      Wait 0.1
    Endif
    Me.Height = Min(txtHelp.TextHeight + 15, Desktop.Height / 4)
    Me.Width = Desktop.Width / 3
    Me.Padding = 2
    
    iX = $hEditor.ScreenX + $iX + 15
    If iX + Me.Width > Desktop.Width Then iX = Desktop.Width - Me.Width
    iY = $hEditor.ScreenY + $iY + $hEditor.LineHeight + 15
    If iY + Me.Height > Desktop.Height Then iY = $hEditor.ScreenY + $iY - $hEditor.LineHeight - Me.Height - 15
    Me.Move(iX, iY)
    Me.Visible = True

    '$hEditor.SetFocus
  Else
    If Me.Parent = Null Then FDebugInfo.SetHelpVisible
  Endif 
  
End


Public Sub ShowKeywordHelp(hEditor As Editor, sName As String, Optional sName2 As String, iX As Integer, iY As Integer) As Boolean
   Dim sPath As String
   Dim sSignature, sKind, sDef As String
  $hEditor = hEditor
  $iX = iX
  $iY = iY
  sSignature = "<b>" & sName & "</b>"
  sKind = "Gambas Keyword"
  'txtHelp.RichText = "<b>" & sName & "</b>"
  'txtHelp.RichText &= "<br><font color=green>Kind: </font>Gambas Keyword: "
  ' ShowWords(sName)
  If sName2 Then
    If Not ShowKeywordHelp(sName2) Then Return
  Endif

  If Right(sName) = "$" Then sName = Left$(sName, -1)

  sPath = GetPath("help/lang" &/ LCase(sName)) & ".html"
  If Not Exist(sPath) Then Return True
  
  txtHelp.RichText = Subst($sHelpTheme, sSignature, sKind, sDef, GetHelp(sPath))
  'txtHelp.RichText &= GetHelp(sPath)
  
  
  
  
  ' ShowHelp()
  
Catch
  Return True
  
End


' Private Sub ShowWords(sWord As String)
' Dim s As String
' s = $hEditor.Lines[$hEditor.PosToLine($iY)].Text
' Try $hEditor.ShowWord($hEditor.PosToLine($iY), InStr(s, sWord, 0, gb.IgnoreCase) - 1, Len(sWord))
' 
'   
' End


Public Sub ShowSymbolHelp(hEditor As Editor, hSymbol As CSymbolInfo, iX As Integer, iY As Integer)
  Dim sLink As String
  Dim sName As String
  Dim sComp As String
  Dim hClass As Class
  Dim sClass As String
  Dim sSignature, sKind, sDef As String
  $iX = iX
  $iY = iY
  $hEditor = hEditor
  sClass = hSymbol.Class
  ' ShowWords(hSymbol.Name)
  sSignature = IIf(hSymbol.IsStatic(), "STATIC", "") & " " & IIf(hSymbol.NotPublic, "PRIVATE", "PUBLIC") & " " & 
      Replace(hSymbol.GetDescription(), hSymbol.Name, "<b>" & hSymbol.Name & "</b>")
      'txtHelp.RichText &= "<br><font color=green>Kind: </font><b>"
      Select Case LCase(hSymbol.Kind)
        Case "v"
          sKind = "Variable"
        Case "p"
          sKind = "Read/Write Property"
        Case "r"
          sKind = "Read only Property"
        Case "m"
          sKind = "Sub/Function"
        Case "c"
          sKind = "Constant"
        Case "t"
          sKind = "Form control"
          sClass = hSymbol.Type
        Case Else
          sKind = hSymbol.Kind

      End Select
      
      'txtHelp.RichText &= "</b>"
      
  If hSymbol.LineNumber Then
    'Definition
    sDef = Subst("Def: <a href=\"file://&1.&2\">&1.&2</a> ", sClass, hSymbol.LineNumber)
    'Afficher l'aide Locale
    txtHelp.RichText = Subst($sHelpTheme, sSignature, sKind, sDef, GetLocalHelp(hSymbol))
  
  Else
  'Symbole externe
          'Afficher le formulaire
    If hSymbol.Kind = "t" Then sDef = Subst("Def: <a href=\"form://&1.&2\">&1.&2</a>", hSymbol.Class, hSymbol.name)

    'Filiation
    sdef &= Subst("<br><font color=blue>&1</font> > <Font color=red>&2</font>", CComponent.Classes[sClass].Component, sClass)

    sName = LCase(IIf(hSymbol.Kind = "t", sClass, hSymbol.Name))
    
    If Right(sName) = "$" Then sName = Left(sName, -1)
    If hSymbol.Component = "gb" And If hSymbol.Class = "." Then
      sLink = "help/lang"
    Else
      sLink = "help/comp"
      sComp = CComponent.Classes[sClass].Component
      If sComp = "gb.qt4" Or If sComp = "gb.gtk" Or If sComp = "gb.gui" Then sComp = "gb.qt"
      sLink &= "/" & sComp
      If hSymbol.Class <> "." Then sLink &= "/" & LCase(sClass)
    Endif
  
    sLink &/= sName
    
    If Exist(GetPath(FormatName(sLink)) & ".html")
      sLink = GetPath(FormatName(sLink)) & ".html"
    Else
      sLink = "http://gambasdoc.org" &/ sLink & "?v3&" & GetLanguage()
    Endif
     txtHelp.RichText = Subst($sHelpTheme, sSignature, sKind, sDef, GetHelp(sLink))
  Endif
  
  ' ShowHelp()
  
End
  

Public Sub ShowClassHelp(hEditor As Editor, sComponent As String, sName As String, iX As Integer, iY As Integer)
  Dim sSignature, sKind, sDef As String
  $hEditor = hEditor
  $iX = iX
  $iY = iY
  ' ShowWords(sname)
  'Description de la Classe
  sSignature = "<b>" & sName & "</b>"
  sKind = "Class"
  sdef = Subst("<br><font color=blue>&1</font>", sComponent)
  txtHelp.RichText = Subst($sHelpTheme, sSignature, sKind, sDef, "")
  ' ShowHelp()
  
End



Private Function GetHelp(sFile As String) As String
  Dim aHelp As String[]

  If Exist(sFile) Then
    ahelp = Scan(File.Load(sFile), "*<div class=\"title\">*</body>*")
    Return aHelp[1]
  Endif
  
  Catch
    Return "</br>Error"
  
End

Public Function ShowDatatypeHelp(hEditor As Editor, sName As String, iX As Integer, iY As Integer) As Boolean

  Dim sPath As String
    Dim sSignature, sKind, sDef As String
  $iX = iX
  $iY = iY
  $hEditor = hEditor
  ' ShowWords(sName)
  sPath = GetPath("help/lang/type" &/ LCase(sName)) & ".html"
  If Not Exist(sPath) Then Return True

  sSignature = "<b>" & sName & "</b>"
  sKind = "DataType"
  txtHelp.RichText = Subst($sHelpTheme, sSignature, sKind, sDef, GetHelp(sPath))
  ' ShowHelp()

End

Private Sub GetPath(sPath As String) As String
  
  sPath = Replace(sPath, "/gb+gui/", "/gb+qt/")
  sPath = Replace(sPath, "/gb+gtk/", "/gb+qt/")
  Return System.Path &/ "share/gambas" & CStr(System.Version) &/ "help" &/ sPath
  
End

Private Sub FormatName(sName As String) As String

  sName = LCase(sName)
  sName = Replace(sName, "_", "+")
  sName = Replace(sName, "/.", "/_")
  sName = Replace(sName, "/+", "/_")
  sName = Replace(sName, ".", "+")
  sName = Replace(sName, ":", "+")
  
  Return sName
  
End

Private Sub GetLanguage() As String
  
  Dim sLang As String = System.Language
  Dim iPos As Integer
  
  iPos = InStr(sLang, ".")
  If iPos Then sLang = Left$(sLang, iPos - 1)
  Return sLang
  
End

Private Function GetLocalHelp(hSymbol As CSymbolInfo) As String
    Dim hForm As Object
    Dim hEdit As Editor
    Dim sClass, sResult, s As String
    Dim iLine As Integer
    
    sClass = Project.FindPath(hSymbol.Class)
    hForm = Project.LoadFile(sClass)
    Try hEdit = hForm.Editor
    If Not hEdit Then Return  

    iLine = hSymbol.HelpLineNumber
     
    If hSymbol.HelpLineNumber Then 
      sResult &= "<hr>"
      Do
        s = LTrim(hEdit.Lines[iLine].Text)
        If Left(s, 2) <> "''" Then Break
        sResult &= Mid(s, 3) & "<br>"
        Inc iLine
        If iLine >= hEdit.Lines.Count Then Break
      Loop
      
      Return sResult
    Endif
End


Public Sub txtHelp_Enter()


  MouseOver = True
  
End


Public Sub txtHelp_Link(Path As String)
  Dim aRes As String[]
  
  If Left(Path, 6) = "file:/" Then
    aRes = Scan(Path, "file://*.*")
    Project.SavePosition()
    Project.OpenFile(Project.FindPath(aRes[0]), CInt(aRes[1]))
  Endif
  If Left(Path, 6) = "form:/" Then
    aRes = Scan(Path, "form://*.*")
    Project.SavePosition()
    Project.OpenForm(aRes[0],, aRes[1])
  Endif

End


Public Sub btnUndock_Click()

  Settings["/FHelp/Undock"] = btnUndock.Value
  FDebugInfo.UpdateHelpView
  
End


Public Sub txtHelp_Leave()

  MouseOver = False

End

Public Sub Form_Close()

  btnUndock.Value = False
  btnUndock_Click
  Stop Event

End
