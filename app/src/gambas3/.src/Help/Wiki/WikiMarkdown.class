' Gambas class file

Inherits Markdown

Public Sub Link(hLink As MarkdownLink)
  
  Dim sPath As String
  Dim sLink As String = hLink.Link
  Dim iPos As Integer
  
  If sLink Begins "/" Then
    sPath = sLink
  Else If sLink Begins "./" Then
    sPath = Wiki.Path &/ Mid$(sLink, 3)
  Else If sLink Begins "../" Then
    sPath = Wiki.MakeAbsolute(sLink, Wiki.Path)
  Else If Not sLink Then
    
    sLink = hLink.Text
    iPos = InStr(sLink, ".")
    If iPos Then
      hLink.Text = ""
      sPath = "/comp/gb" &/ Trim(Left$(sLink, iPos - 1)) &/ Trim(Mid$(sLink, iPos + 1))
    Endif
    
  Else
    
    If sLink Like "*://*" Then hLink.Blank = True
    
  Endif
  
  If Not sPath Then Return
  
  hLink.Query = Mid$(Wiki.LinkSuffix, 2)
  
  If Not hLink.Text Or If hLink.Text = hLink.Link Then
    
    If Wiki.IsImage(sPath) Then
      hLink.Html = "<img src=\"" & Html(MHelp.GetOfflinePath(sPath)) & "\" />"
      Return
    Endif
  
    hLink.Text = Wiki.GetPageTitle(sPath)
    
  Endif
  
  hLink.Link = Wiki.WikiRoot &/ sPath 
  
End

Private Sub MakeError(sMsg As String) As String[]

  Return ["*<span style=\"color:red;\">" & Html(sMsg) & "</span>*"]

End

Public Sub Command(sCommand As String) As String[]

  Dim aArg As String[]
  Dim sDir As String
  Dim aResult As String[]
  Dim I As Integer
  Dim iPos As Integer
  Dim sPath As String
  Dim sLetter, sLast As String
  Dim aDir As String[]
  Dim hComp As CComponent
  Dim hClass As CClassInfo
  Dim hSym As CSymbolInfo
  Dim sTitle As String
  Dim sClass As String
  
  aArg = Split(sCommand, " ", Chr$(34))
  sCommand = aArg[0]
  
  If sCommand = "index" Then
    
    If aArg.Count > 1 Then
      
      sDir = File.Dir(Wiki.GetPagePathFrom(aArg[1]))
      aResult = New String[]
      aDir = New String[]
      
      If IsDir(sDir) Then
        
        For Each sDir In Dir(sDir, "*", gb.Directory)
          sPath = "/" &/ aArg[1] &/ sDir
          If Not Wiki.ExistPage(sPath) Then Continue
          sTitle = Wiki.GetPageTitle(sPath)
          If aArg.Count > 2 And If Comp(Left(sTitle, Len(aArg[2])), aArg[2], gb.Language + gb.IgnoreCase) = 0 Then sTitle = LTrim(Mid$(sTitle, Len(aArg[2]) + 1))
          aDir.Add(sTitle & "\n[" & sPath & "]")
        Next
        
        If aDir.Count Then

          aResult.Add("[[ index")
          aResult.Add("==")
            
          aDir.Sort(gb.Natural + gb.IgnoreCase)
          
          For I = 0 To aDir.Max
            If I Then aResult.Add("==")
            iPos = InStr(aDir[I], "\n")
            sLetter = String.UCase(String.Left(aDir[I]))
            If sLetter <> sLast Then
              aResult.Add("<div class=\"letter\">" & sLetter & "</div>")
              sLast = sLetter
            Endif
            aResult.Add("--")
            aResult.Add(Mid$(aDir[I], iPos + 1) & "\\")
          Next
          
          aResult.Add("]]")
          
        Endif
        
      Endif
      
      Return aResult
      
    Endif
  
  Else If sCommand = "since" Or If sCommand = "only" Then
  
    Return ["<div class=\"since\">" & ("Since") & "&nbsp;" & Html(aArg[1]) & "</div>"]
  
  Else If sCommand = "classes" And If Wiki.Component Then
    
    hComp = CComponent.All[Wiki.Component]
    If hComp Then Return hComp.WikiGetClasses()

    Return MakeError(("This component does not exist."))
  
  Else If sCommand = "symbols" Then
    
    If aArg.Count >= 2 And If aArg[1] Then
      sClass = aArg[1]
    Else
      sClass = Wiki.Class
    Endif
    
    If sClass Then
      hComp = CComponent.Get(Wiki.Component)
      If hComp Then
        hClass = hComp[sClass]
        If hClass Then Return hClass.WikiGetSymbols()
      Endif
    Endif
    
    Return MakeError(sClass & ": " & ("This class does not exist."))
  
  Else If sCommand = "syntax" And If Wiki.Symbol Then
  
    hComp = CComponent.Get(Wiki.Component)
    If hComp Then
      hClass = hComp[Wiki.Class]
      If hClass Then 
        hSym = hClass.Symbols[Wiki.Symbol]
        If hSym Then Return hSym.WikiGetSyntax()
      Endif
    Endif
    
    Return MakeError(("This symbol does not exist."))

  Else If sCommand = "stat" Then
    
    Return Wiki.GetStat()

  Else If sCommand = "todo" Then
    
    Return "Unavailable in offline help."
    
  Else If sCommand = "changes" Then
    
    Return "Unavailable in offline help."

  Else
    
    Return ["**COMMAND: `" & sCommand & "`**"]
    
  Endif
  
End

Public Sub Enter(sClass As String) As String[]
  
  If sClass = "syntax" Then Me.EnableCode(False)
  
End

Public Sub Leave(sClass As String) As String[]
  
  If sClass = "syntax" Then Me.EnableCode(True)
  
End

Public Sub CheckURL(sURL As String, sMarkup As String) As String
  
  Dim sPath As String
  Dim iPos As Integer
  
  If sURL Begins "/wiki/" Then
    If sMarkup = "img" Then
      sPath = Mid$(sURL, 7)
      If sPath Ends "?v" Then sPath = Left(sPath, -2)
      Return MHelp.GetOfflinePath(sPath)
    Endif
  Endif
  
  Return sURL
  
End
