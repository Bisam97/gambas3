' Gambas class file

Private $hLogo As Image
Private $hText As Image

Public Sub _new(hDrawingArea As DrawingArea)
  
  Dim hSvgLogo As SvgImage
  Dim X As Integer
  
  If Not $hLogo Then 
    
    hSvgLogo = SvgImage.Load("img/logo/gambas.svg")
    $hLogo = New Image(hSvgLogo.Width, 256, Color.Transparent)
    Paint.Begin($hLogo)
    Paint.Scale(1.1, 1.1)
    hSvgLogo.Paint
    Paint.End
    
    $hText = New Image(hDrawingArea.Width, hDrawingArea.Height, Color.Transparent)
    Paint.Begin($hText)
    Paint.Font = Font["+12,Bold"]
    X = PaintText("Gambas", 8, 48, &HFFAF5F) 'Color.SelectedForeground)
    
    Paint.Font = Font["+8,Bold"]
    PaintText(Project.FullVersionWithRevision, X + 8, 32, &HFFAF5F) 'Color.SelectedForeground)
    Paint.Font = Font["+2"]
    PaintText("http://gambas.sourceforge.net", 12, 68, &HFFAF5F) 'Color.LightForeground)
    
    Paint.Font = Font["Italic,+1"]
    PaintText(("This program is published under\nthe GNU General Public License."), 12, 104, Color.SelectedForeground)
    Paint.End
    
  Endif

  Object.Attach(hDrawingArea, Me, "dwgWelcome")
  
End

Private Sub PaintText(sText As String, X As Integer, Y As Integer, iColor As Integer) As Integer
  
  Dim hColor As ColorInfo
  Dim XR As Integer
  
  Paint.Brush = Paint.Color(Color.RGB(0, 0, 0, 192))
  Paint.Text(sText, X + 6, Y + 6)
  Paint.Fill

  Paint.Brush = Paint.Color(Color.RGB(128, 128, 128))
  Paint.Text(sText, X - 1, Y - 1)
  Paint.Text(sText, X + 1, Y - 1)
  Paint.Text(sText, X + 1, Y + 1)
  Paint.Text(sText, X - 1, Y + 1)
  Paint.Fill

  Paint.Brush = Paint.Color(iColor)
  Paint.Text(sText, X, Y)
  XR = X + Paint.PathExtents.Width
  Paint.Fill

  Return XR
  
End


Public Sub dwgWelcome_Draw()

  Draw.Image($hLogo, Last.W - $hLogo.Width, -20)
  Draw.Image($hText, 0, 0)

End

