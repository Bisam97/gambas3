' Gambas class file

Public Sub btnClose_Click()

  Me.Close

End

Public Sub btnCopy_Click()

  Clipboard.Copy(txtInfo.Text) 

End

Private Sub FindLibraryRec(sDir As String, sPattern As String) As String
  
  Dim sSubDir As String
  Dim sLib As String
  Dim aDir As String[]
  
  Try aDir = Dir(sDir, sPattern)
  If aDir And If aDir.Count = 1 Then Return aDir[0]
  
  Try aDir = Dir(sDir, "*", gb.Directory)
  If Not Error Then
    For Each sSubDir In aDir
      sLib = FindLibraryRec(sDir &/ sSubDir, sPattern)
      If sLib Then Return sLib
    Next
  Endif
  
End


Public Sub Form_Open()

  Dim sTemp As String
  Dim sText As String
  Dim sLib As String
  Dim sFind As String
  
  txtInfo.Font = Font[Settings["/ConsoleFont", Project.DEFAULT_FONT]]

  sTemp = Temp$("report-ng")
  Try Mkdir sTemp
  sTemp &/= "report-ng.sh"
  Try Kill sTemp
  Copy "report-ng.sh" To sTemp
  'Shell "chmod a+x " & Shell$(sTemp) Wait
  Exec ["/bin/sh", sTemp] To sText

  sFind = FindLibraryRec("/usr/lib", "libQtCore.so.*.*.*")
  If sFind Then sLib &= "Qt4=" & sFind & "\n"
  sFind = FindLibraryRec("/usr/lib/", "libgtk-x11-2*.so.*.*.*")
  If sFind Then sLib &= "GTK+=" & sFind & "\n"
  
  If sLib Then
    sText &= "[Libraries]\n"
    sText &= sLib & "\n"
  Endif
  
  txtInfo.Text = sText

End
