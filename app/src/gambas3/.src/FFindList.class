' Gambas class file

Private $aFound As New Variant[][]
Private $hFont As Font
Private $eAngle As Float

Public Sub ClearFound()

  tvwFind.Rows.Count = 0
  $aFound.Clear
  lblNotFound.Hide
  tvwFind.Show
  
End

Public Sub AddFound(sName As String, iLine As Integer, iCol As Integer, iLen As Integer, sText As String)
  
  $aFound.Add([sName, iLine + 1, iCol, sText, iLen, False])
  If $aFound.Count > 1 Then 
    If $aFound[$aFound.Max][0] = $aFound[$aFound.Max - 1][0] Then
      $aFound[$aFound.Max][5] = $aFound[$aFound.Max - 1][5]
    Else
      $aFound[$aFound.Max][5] = Not $aFound[$aFound.Max - 1][5]
    Endif
  Endif
  tvwFind.Rows.Count = $aFound.Count
  
End

' PUBLIC SUB SelectFound(sName AS String, iLine AS Integer, iCol AS Integer, iLen AS Integer)
'   
'   DIM sKey AS String
'   
'   sKey = FindKey(sName)
'   IF NOT sKey THEN RETURN 
'   
'   sKey = KEY_FIND & sKey & "@" & Format(iLine, "000000") & "." & Format(iCol, "00000") & "." & iLen
'   TRY ProjectTree[sKey].Selected = TRUE
'   
' END

Public Sub _new()

  With tvwFind
    .Columns.Count = 4  
    .Columns[0].Width = Desktop.Scale * 16
    .Columns[0].Text = ("Class")
    .Columns[1].Width = Desktop.Scale * 8
    .Columns[1].Text = ("Line")
    .Columns[2].Width = Desktop.Scale * 8
    .Columns[2].Text = ("Column")
    .Columns[3].Text = ("Text")    
  End With
  
  ReadConfig

End

Public Sub Form_Open()

  Settings.Read(Me)
  MoveMag

End

Public Sub tvwFind_Data(Row As Integer, Column As Integer)

  With tvwFind.Data
    
    If Column = 3 Then .Font = $hFont 
    
    .Text = $aFound[Row][Column]

    If Row Then
      If Column = 0 Then 
        If $aFound[Row - 1][0] = .Text Then 
          .Text = ""
        Endif
      Else If Column = 1 Then
        If $aFound[Row - 1][0] = $aFound[Row][0] And $aFound[Row - 1][1] = $aFound[Row][1] Then 
          .Text = "-"
        Endif
      Endif
    Endif 
    
    .Background = If($aFound[Row][5], Color.LightBackground, Color.TextBackground)
    
  End With

End

Public Sub tvwFind_Select()

  Dim aFind As Variant[]
  Dim iLine As Integer
  Dim iCol As Integer
  Dim iLen As Integer 
  Dim hEdit As Editor
  Dim sPath As String

  If timSearch.Enabled Then Return
  If $aFound.Count = 0 Then Return
  
  aFind = $aFound[tvwFind.Row]
  
  sPath = Project.FindPath(aFind[0])
  If Not sPath Then Return
  
  Project.OpenFile(sPath)

  hEdit = Project.Files[sPath].Editor
  iLine = aFind[1] - 1
  iCol = aFind[2] - 1
  iLen = aFind[4]
  hEdit.Goto(iLine, iCol, True)
  hEdit.Select(iLine, iCol, iLine, iCol + iLen)
  
  'Project.ProjectTree[sSave].Selected = TRUE

End

Public Sub Form_Close()

  Settings.Write(Me)  

End

Public Sub ReadConfig()
  
  tvwFind.Font.Grade = - Settings["/GlobalFont", 0]
  $hFont = Font[Settings["/Editor/Font", Project.DEFAULT_FONT]]
  tvwFind.Refresh

End

' PUBLIC SUB panSearch_Arrange()
' 
'   btnCancel.Move(panSearch.Width - btnCancel.Width - Desktop.Scale, Desktop.Scale)
' 
' END

Private Sub MoveMag()
  
  picSearch.Move(16 + Cos($eAngle) * 8, 16 + Sin($eAngle) * 8 + btnCancel.Y + btnCancel.H + Desktop.Scale)
  
End


Public Sub timSearch_Timer()

  $eAngle += 0.4
  MoveMag

End

Public Sub Start()
  
  FMain.Enabled = False
  Me.Enabled = True
  ClearFound
  btnCancel.Text = ("Cancel")
  timSearch.Start
  
End

Public Sub Stop()
  
  timSearch.Stop
  btnCancel.Text = ("Close")
  If tvwFind.Rows.Count = 0 Then
    tvwFind.Hide
    lblNotFound.Show
  Endif
  FMain.Enabled = True
  
End

Public Sub btnCancel_Click()

  If timSearch.Enabled Then
    FFind.Cancel
  Else 
    Me.Close
  Endif

End
