' Gambas class file

Public Enum SORT_BY_DATE, SORT_BY_NAME, SORT_BY_PATH 

Static Public All As New CRecentFile[]
Static Public Sort As Integer

Static Private $dLastLoad As Date

Static Property Read OutOfDate As Boolean

Public Const MAX_RECENT As Integer = 64

Public Path As String
Public Date As Date

Public Sub _new(sPath As String, dDate As Date)
  
  If IsNull(dDate) Then Try dDate = Stat(sPath &/ ".startup").LastModified
  If IsNull(dDate) Then Try dDate = Stat(sPath &/ ".project").LastModified
  If IsNull(dDate) Then Return
  If Not Exist(sPath &/ ".project") Then Return

  Path = sPath
  {Date} = dDate
  
End

Public Sub _compare(hRecentFile As CRecentFile) As Integer
  
  Select Case Sort
    Case SORT_BY_DATE
      Return Sgn(hRecentFile.Date - {Date})
    Case SORT_BY_NAME
      Return Comp(File.Name(Path), File.Name(hRecentFile.Path), gb.Natural + gb.IgnoreCase)
    Case SORT_BY_PATH
      Return Comp(Path, hRecentFile.Path, gb.Natural + gb.IgnoreCase)
  End Select
  
End

Public Sub IsValid() As Boolean
  
  Return Path
  
End


Public Sub GetTitle(iSort As Integer) As String
  
  Select Case iSort
    Case SORT_BY_DATE
      Return GetDateTitle()
    Case SORT_BY_NAME
      Return String.UCase(String.Left(File.Name(Path)))
    Case SORT_BY_PATH
      Return Replace(File.Dir(Path), User.Home, "~")
  End Select
  
End
  
Private Sub GetDateTitle() As String

  Dim dToday As Date = Date(Now)
  Dim dDate As Date = Date({Date})
  Dim sText As String
  
  If dDate = dToday Then
    Return ("Today")
  Else If DateDiff(dDate, dToday, gb.Day) = 1 Then
    Return ("Yesterday")
  Else If DateDiff(dDate, dToday, gb.Day) = 2 Then
    Return ("Two days ago")
  Else If DateDiff(dDate, dToday, gb.Day) <= 7 Then
    Return ("This week")
  Else If Year(dToday) = Year(dDate) And If Month(dToday) = Month(dDate) Then
    Return ("This month")
  Else
    sText = Format(dDate, "mmmm yyyy")
    Return String.UCase(String.Left(sText)) & String.Mid$(sText, 2)
  Endif
  
End


Static Public Sub Clear()
  
  All.Clear
  Save
  
End

Static Public Sub Load()

  Dim nRecent As Integer
  Dim iInd As Integer
  Dim sPath As String
  Dim hRecentFile As CRecentFile

  If Not OutOfDate_Read() Then Return
  
  Settings.Reload
  nRecent = Settings["/Recent/Count", 0]
  
  All = New CRecentFile[]

  For iInd = 1 To nRecent
    sPath = Settings["/Recent/File[" & CStr(iInd) & "]"]
    If sPath Then
      hRecentFile = New CRecentFile(sPath, CDate(Settings["/Recent/Date[" & CStr(iInd) & "]"]))
      If hRecentFile.IsValid() Then
        All.Add(hRecentFile)
        If All.Count >= MAX_RECENT Then Break
      Endif
    Endif
  Next
  
  Try $dLastLoad = Stat(Settings.Path).LastModified
  
End

Static Public Sub Save()

  Dim iInd As Integer

  Load

  Settings.Clear("/Recent")

  Settings["/Recent/Count"] = All.Count

  Sort = SORT_BY_DATE
  All.Sort()

  For iInd = 0 To All.Max
    Settings["/Recent/File[" & CStr(iInd + 1) & "]"] = All[iInd].Path
    Settings["/Recent/Date[" & CStr(iInd + 1) & "]"] = CStr(All[iInd].Date)
  Next

  Settings.Save

  $dLastLoad = Stat(Settings.Path).LastModified

End

Static Public Sub Add(sPath As String)

  Dim iInd As Integer
  Dim nMax As Integer
  Dim hRecentFile As CRecentFile
  
  Load

  If Right$(sPath) = "/" Then sPath = Left$(sPath, -1)

  'sPath = "(" & File.BaseName(sPath) & ") " & File.Dir(sPath)
  Sort = SORT_BY_DATE
  All.Sort()

  While iInd < All.Count

    If All[iInd].Path = sPath Then
      All.Remove(iInd)
    Else
      Inc iInd
    Endif

  Wend

  hRecentFile = New CRecentFile(sPath, CStr(Now))
  If Not hRecentFile.IsValid() Then Return
  
  All.Add(hRecentFile, 0)

  'nMax = Settings["/Recent/Max", DEFAULT_MAX_RECENT]
  While All.Count > MAX_RECENT
    All.Remove(All.Max)
  Wend

  Save

End

Static Public Sub Get(Optional iSort As Integer) As CRecentFile[]
  
  Load
  
  Sort = iSort
  All.Sort()
  
  Return All
  
End

Static Public Sub Exit()
  
  All = Null
  
End

Static Private Function OutOfDate_Read() As Boolean

  If $dLastLoad And If Stat(Settings.Path).LastModified <= $dLastLoad Then Return
  Return True  

End
