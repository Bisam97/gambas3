' Gambas class file

Static Private $cTypeName As New Collection
Static Private $cNameType As New Collection(gb.IgnoreCase)

Public Name As String
Public Type As String
Public Kind As String
Public Signature As String
Public Value As String
Public Class As String
Public Component As String
Public NotPublic As Boolean
Public LineNumber As Integer

Private $sArgBefore As String
Private $sArgAfter As String
Private $iArg As Integer
Private $iShowArg As Integer

Static Public Sub _init()

  Dim sType As String

  $cTypeName["b"] = "Boolean"
  $cTypeName["c"] = "Byte"
  $cTypeName["h"] = "Short"
  $cTypeName["i"] = "Integer"
  $cTypeName["l"] = "Long"
  $cTypeName["d"] = "Date"
  $cTypeName["g"] = "Single"
  $cTypeName["f"] = "Float"
  $cTypeName["v"] = "Variant"
  $cTypeName["s"] = "String"
  $cTypeName["o"] = "Object"
  $cTypeName["p"] = "Pointer"

  For Each sType In $cTypeName
    $cNameType[sType] = $cTypeName.Key
  Next

End

Public Sub Copy(sType As String, sNewType As String) As CSymbolInfo
  
  Dim hSym As New CSymbolInfo
  
  With hSym
    .Name = Name
    .Type = Replace(Type, sType, sNewType)
    .Kind = Kind
    .Signature = Replace(Signature, sType, sNewType)
    .Value = Value
    .Class = Class
    .Component = Component
    .NotPublic = NotPublic
    .LineNumber = 0
  End With
  
  Return hSym
  
End


Public Function IsHidden() As Boolean

  If Left(Name) = "_" Then Return True
  Return InStr(Name, "_") > 0 And Kind <> "C"

End


Public Function IsStatic() As Boolean

  Return UCase(Kind) = Kind

End


Public Function GetIcon() As String


  Dim bStatic As Boolean
  Dim sIcon As String

  bStatic = UCase(Kind) = Kind

  Select Case LCase(Kind)

    Case "r"
      sIcon = "img/16" &/ If(bStatic, "s-property-ro.png", "property-ro.png")
    Case "p"
      sIcon = "img/16" &/ If(bStatic, "s-property.png", "property.png")
    Case "v"
      sIcon = "img/16" &/ If(bStatic, "s-variable.png", "variable.png")
    Case "c"
      sIcon = "img/16/constant.png"
    Case ":"
      sIcon = "img/16/event.png"
    Case "t"
      sIcon = "img/16/control.png"
    Case "m"
      sIcon = "img/16" &/ If(bStatic, "s-method.png", "method.png")

  End Select

  Return sIcon

End


Private Sub NextArg()
  
  Inc $iArg  
  
  If $iArg = $iShowArg Then 
    $sArgBefore = "<u>"
    $sArgAfter = "</u>"
  Else 
    $sArgBefore = ""
    $sArgAfter = ""
  Endif

End

Private Sub MakeArgBold(sCode As String, iShowArg As Integer) As String
  
  Dim sCar As String
  Dim iInd As Integer
  Dim aWait As New String[]
  Dim bString As Boolean
  Dim sSign As String
  Dim bOptional As Boolean
  
  If iShowArg < 0 Then Return sCode
  
  $iArg = -1
  $iShowArg = iShowArg
  NextArg
  sSign = $sArgBefore

  Do
    
    Inc iInd
    If iInd > Len(sCode) Then Break
    
    sCar = Mid$(sCode, iInd, 1)

    If bString Then
      sSign &= sCar
      If sCar = "\\" Then
        Inc iInd
        If iInd > Len(sCode) Then Break
        sSign &= Mid$(sCode, iInd, 1)
      Endif
      Continue
    Endif
    
    If sCar = Chr$(34) Then
      sSign &= sCar
      bString = Not bString
      Continue
    Endif
    
    If sCar = "(" Then
      sSign &= sCar
      aWait.Push(")")
    Else If sCar = "[" Then
      sSign &= sCar
      aWait.Push("]")
    Else If aWait.Count Then
      sSign &= sCar
      If sCar = aWait[aWait.Max] Then
        aWait.Pop
      Endif
    Else If Mid$(sCode, iInd, 9) = "OPTIONAL " Then
      iInd += 7
      If Not bOptional Then 
        sSign &= " ["
      Endif
      bOptional = True
    Else If Mid$(sCode, iInd, 2) = ", " Then
      sSign &= $sArgAfter 
      If Mid$(sCode, iInd, 11) = ", OPTIONAL " Then
        If Not bOptional Then 
          sSign &= " ["
          bOptional = True
        Endif 
        iInd += 9
      Endif
      sSign &= ", "
      NextArg
      sSign &= $sArgBefore
    Else If Mid$(sCode, iInd, 3) = "..." Then 
      If $iShowArg > $iArg Then
        sSign &= "<u>...</u>"
      Else 
        sSign &= "..."
      Endif 
      iInd += 2
    Else
      sSign &= sCar
    Endif
    
  Loop
  
  If bOptional Then
    sSign &= $sArgAfter & " ]"
  Else 
    sSign &= $sArgAfter
  Endif
  
  Return sSign  
  
End

Private Sub MakeSignature(sSign As String) As String
  
  Dim iInd As Integer
  Dim aWait As New String[]
  Dim bOptional As Boolean
  Dim sSym As String
  Dim sType As String

  Highlight.Analyze(sSign)
  sSign = ""
  
  For iInd = 0 To Highlight.Symbols.Max
  
    sSym = Highlight.Symbols[iInd]
    
    If sSym = "(" Then
      aWait.Push(")")
    Else If sSym = "[" Then
      aWait.Push("]")
    Else If aWait.Count Then
      If sSym = aWait[aWait.Max] Then
        aWait.Pop
      Endif
    Else 
    
      If iInd > 0 And If sSym <> "," Then Continue
    
      If iInd Then
        Inc iInd
        sSym = Highlight.Symbols[iInd]
      Endif
      
      If sSym = "..." Then
        sSign &= "."
        Break
      Endif
      
      If UCase(sSym) = "OPTIONAL" Then
        If Not bOptional Then
          sSign &= "["
          bOptional = True
        Endif
        Inc iInd
        sSym = Highlight.Symbols[iInd]
      Endif
    
      iInd += 2
      sType = Highlight.Symbols[iInd]
      If $cNameType.Exist(sType) Then
        sType = $cNameType[sType]
      Else
        sType &= ";"
      Endif
      sSign &= "(" & sSym & ")" & sType

    Endif
  
  Next
  
  If bOptional Then sSign &= "]"
  
  Return sSign
  
End


Public Function GetSignature(Optional iShowArg As Integer = -1) As String

  Dim iInd As Integer
  Dim sRes As String
  Dim sCar As String
  Dim iPos As Integer
  Dim bName As Boolean
  Dim bFirst As Boolean
  Dim sSign As String
  Dim sKeywordAs As String

  sSign = Signature
  sKeywordAs = If(Settings["/Editor/KeywordsUseUpperCase"], "AS", "As")
  
  If Left$(sSign) = "#" Then
    'sRes = MakeArgBold(Mid$(sSign, 2), iShowArg)
    'Debug Mid$(sSign, 2)
    sSign = MakeSignature(Mid$(sSign, 2))
    'Debug "--> "; sSign
  Endif
  
  bFirst = True
  
  $iArg = -1
  $iShowArg = iShowArg

  Do

    Inc iInd

    If iInd > Len(sSign) Then Break

    sCar = Mid$(sSign, iInd, 1)

    If sCar = "'" Or sCar = "(" Then

      If sCar = "'" Then
        iPos = InStr(sSign, "'", iInd + 1)
      Else
        iPos = InStr(sSign, ")", iInd + 1)
      Endif

      If iPos = 0 Then Break

      If bFirst Then
        bFirst = False
        NextArg
      Else
        sRes &= $sArgAfter & ", "
        NextArg
      Endif

      sRes &= $sArgBefore & Mid$(sSign, iInd + 1, iPos - iInd - 1) & " " & sKeywordAs & " "

      iInd = iPos
      bName = True

      Continue

    Endif

    If sCar = "<" Then

      iPos = InStr(sSign, ">", iInd + 1)
      If iPos = 0 Then Break
      iInd = iPos
      Continue

    Endif

    If sCar = "[" Then

      sRes &= $sArgAfter & "<font color=#808080>"
      $sArgAfter = ""
      Continue

    Endif

    If sCar = "]" Then

      sRes &= $sArgAfter & "</font>"
      $sArgAfter = ""
      Continue

    Endif

    If Not bName Then

      If bFirst Then
        bFirst = False
      Else
        sRes &= $sArgAfter & ", "
        NextArg
      Endif

      sRes &= $sArgBefore & "? AS " 
      bName = False

    Endif
    
    If sCar = "." Then
      sRes &= $sArgAfter & ", " 
      NextArg
      If $iShowArg >= 0 And If $iShowArg > $iArg Then sRes &= "<u>"
      sRes &= $sArgBefore & "..."
      If $iShowArg >= 0 And If $iShowArg > $iArg Then sRes &= "</u>"
      Continue
    Endif

    If (sCar >= "A" And sCar <= "Z") Or sCar = "." Then

      iPos = InStr(sSign, ";", iInd)
      If iPos = 0 Then iPos = Len(sSign) + 1

      sRes &= Mid$(sSign, iInd, iPos - iInd)

      iInd = iPos
      Continue

    Endif

    sRes &= GetType(sCar)

  Loop
  
  sRes &= $sArgAfter
    
  If Name = "_get" Or Name = "_put" Then
    sRes = "<b>[</b>" & sRes & "<b>]</b>"
  Else
    sRes = "<b>(</b>" & sRes & "<b>)</b>"
  Endif
  
  If Type Then sRes &= " " & sKeywordAs & " " & GetType(Type)

  Return sRes

End

Public Function GetDescription(Optional bBalise As Boolean) As String

  Dim bStatic As Boolean
  Dim sDesc As String

  bStatic = UCase(Kind) = Kind

  Select LCase(Kind)
  
    Case "r"
      sDesc = If(bStatic, "STATIC ", "") & "PROPERTY READ"
    Case "p"
      sDesc = If(bStatic, "STATIC ", "") & "PROPERTY"
    'CASE "v"
    '  sIcon = "img/16" &/ If(bStatic, "s-variable.png", "variable.png")
    Case "c"
      sDesc = "CONST"
    Case ":"
      sDesc = "EVENT"
    'CASE "t"
    '  sIcon = "img/16/control.png"
    Case "m"
      sDesc = If(bStatic, "STATIC ", "") & If(Type, "FUNCTION", "SUB")

  End Select  
  
  If Kind = ":" Then
    sDesc &= " " & Mid$(Name, 2) & " "
  Else
    sDesc &= " " & Name & " "
  Endif
  
  If InStr(":m", LCase(Kind)) Then 
    sDesc &= " " & GetSignature()
  Else
    If Type Then sDesc &= " AS " & GetType(Type, False)
  Endif
  
  Return sDesc
  
End


Static Private Function GetType(sType As String, Optional bBalise As Boolean) As String

  If Len(sType) = 1 Then
    sType = $cTypeName[sType]
  Else If Left$(sType) = "." Then
    'IF bBalise THEN
    '  sType = "<I>" & Mid$(sType, 2) & "</I>"
    'ELSE
    sType = "Object"
    'ENDIF
  Endif

  If bBalise Then
    Return "<B>" & sType & "</B>"
  Else
    Return sType
  Endif

End

Static Public Function TransformSignature(sSign As String, bBalise As Boolean) As String

  Dim iInd As Integer
  Dim sRes As String
  Dim sCar As String
  Dim iPos As Integer
  Dim bName As Boolean
  Dim bFirst As Boolean

  Dim sBold As String
  Dim sNoBold As String

  If bBalise Then
    sBold = "<B>"
    sNoBold = "</B>"
  Endif

  bFirst = True

  Do

    iInd = iInd + 1

    If iInd > Len(sSign) Then Break

    sCar = Mid$(sSign, iInd, 1)

    If sCar = "'" Or sCar = "(" Then

      If sCar = "'" Then
        iPos = InStr(sSign, "'", iInd + 1)
      Else
        iPos = InStr(sSign, ")", iInd + 1)
      Endif

      If iPos = 0 Then Break

      If bFirst Then
        bFirst = False
      Else
        sRes = sRes & sBold & "," & sNoBold & " "
      Endif

      sRes = sRes & Mid$(sSign, iInd + 1, iPos - iInd - 1) & " " & sBold & "AS" & sNoBold & " "

      iInd = iPos
      bName = True

      Continue

    Endif

    If sCar = "<" Then

      iPos = InStr(sSign, ">", iInd + 1)
      If iPos = 0 Then Break
      iInd = iPos
      Continue

    Endif

    If sCar = "[" Then

      sRes = sRes & " " & sBold & "[" & sNoBold
      Continue

    Endif

    If sCar = "]" Then

      sRes = sRes & " " & sBold & "] " & sNoBold
      Continue

    Endif

    If Not bName Then

      If bFirst Then
        bFirst = False
      Else
        sRes = sRes & sBold & "," & sNoBold & " "
      Endif

      sRes = sRes & sBold & "? AS " & sNoBold
      bName = False

    Endif

    If sCar >= "A" And sCar <= "Z" Then

      iPos = InStr(sSign, ";", iInd)
      If iPos = 0 Then iPos = Len(sSign) + 1

      sRes = sRes & Mid$(sSign, iInd, iPos - iInd)

      iInd = iPos
      Continue

    Endif

    sRes = sRes & GetType(sCar, bBalise)

  Loop

  Return sRes

End
