' Gambas class file

Static Private $hLogo As Image

Public Sub _new(hDrawingArea As DrawingArea)
  
  Dim hSvgLogo As SvgImage
  
  If Not $hLogo Then 
    hSvgLogo = SvgImage.Load("img/logo/gambas.svg")
    $hLogo = New Image(hSvgLogo.Width, 256, Color.Transparent)
    Paint.Begin($hLogo)
    Paint.Scale(1.1, 1.1)
    hSvgLogo.Paint
    Paint.End
  Endif

  Object.Attach(hDrawingArea, Me, "dwgWelcome")
  
End

Private Sub PaintText(sText As String, X As Integer, Y As Integer, iColor As Integer) As Integer
  
  Dim hColor As ColorInfo
  Dim XR As Integer
  
  Paint.Brush = Paint.Color(Color.RGB(0, 0, 0, 192))
  Paint.Text(sText, X + 6, Y + 6)
  Paint.Fill


  Paint.Brush = Paint.Color(Color.RGB(128, 128, 128))
  Paint.Text(sText, X - 1, Y - 1)
  Paint.Text(sText, X + 1, Y - 1)
  Paint.Text(sText, X + 1, Y + 1)
  Paint.Text(sText, X - 1, Y + 1)
  Paint.Fill

  Paint.Brush = Paint.Color(iColor)
  Paint.Text(sText, X, Y)
  XR = X + Paint.PathExtents.Width
  Paint.Fill

  Return XR
  
End


Public Sub dwgWelcome_Draw()

  Dim X As Integer

  Paint.Brush = Paint.Image($hLogo)
  Paint.Translate(Last.W - $hLogo.Width, -20)
  Paint.Rectangle(0, 0, $hLogo.Width, $hLogo.Height)
  Paint.Fill
  Paint.Reset

  Paint.Font = Font["+12,Bold"]
  X = PaintText("Gambas", 8, 48, Color.SelectedForeground)
  
  Paint.Font = Font["+9,Bold"]
  PaintText(Project.Version, X + 8, 32, Color.SelectedForeground)
  Paint.Font = Font["+2"]
  PaintText("http://gambas.sourceforge.net", 12, 68, Color.LightForeground)
  
  Paint.Font = Font["Bold,Italic,+2"]
  PaintText(("This program is published under\nthe GNU General Public License."), 12, 104, Color.SelectedForeground)

End

