' Gambas class file

Static Private $aTheme As String[] = ["amethyst", "amber", "emerald", "ruby", "sapphire", "visual", "obsidian", "quest", "quick"]
Static Private $aThemeName As String[] = [("Amethyst"), ("Amber"), ("Emerald"), ("Ruby"), ("Sapphire"), ("Visual"), ("Obsidian"), ("Quest"), ("Quick")]
Static Private $aBrowser As String[] = ["konqueror", "firefox", "epiphany", "seamonkey", "opera"]
Static Private $aTerminal As String[] = ["konsole", "gnome-terminal", "Terminal", "xterm"]

Private $cLast As Collection
Private $cCurrent As Collection
Private $bNoChange As Boolean
Private $aConfig As Variant[][]
Private $sStyle As String
Private $hDraw As DrawingArea

Private Sub ReadConfig()
  
  Dim aParam As Variant[]
  Dim hCtrl As Object
  Dim vVal As Variant
  
  For Each aParam In $aConfig
  
    hCtrl = aParam[0]
    vVal = Settings[aParam[1], aParam[2]]
    
    Select Case Object.Type(hCtrl)
    
      Case "FontChooser"
        hCtrl.SelectedFont = vVal
      Case "ComboBox"
        If IsBoolean(vVal) Then 
          hCtrl.Index = If(vVal, 1, 0)
        Else
          hCtrl.Index = vVal
        Endif
      Case "SpinBox"
        hCtrl.Value = vVal
      Default 
        Debug "Unmanaged type: "; Object.Type(hCtrl)
    
    End Select
  
  Next
  
End


Public Sub _new()

  Dim iInd As Integer
  Dim hHBox As HBox
  Dim hColor As DrawingArea
  Dim hButton As Button

  cmbTheme.List = $aThemeName.Copy().Sort(gb.Language)
  cmbTheme.Add(("Select a theme"), 0)
  cmbIconTheme.List = [("Desktop"), "Gnome", "KDE", "KDE4"]
  cmbBrowser.List = [("(Default)"), "Konqueror", "Firefox", "Epiphany", "SeaMonkey", "Opera"]
  cmbTerminal.List = [("(Default)"), "Konsole", "Gnome Terminal", "XFCE Terminal", "XTerm"]
  
  $cLast = MTheme.ReadSettings(Settings, "/Highlight", True)
  $cCurrent = MTheme.Copy($cLast)
  
  $aConfig = [
    [fntEditor, "/Editor/Font", Project.DEFAULT_FONT],
    [cmbGlobalFont, "/GlobalFont", 0],
    [cmbTabStripFont, "/TabStripFont", 0],
    [cmbToolbox, "/ToolboxSize", 0],
    [cmbSortProperty, "/SortProperties", 1],
    [txtTabSize, "/DefaultTabSize", 2],
    [cmbProcLimit, "/Editor/ProcedureLimit", 2],
    [cmbLineNumber, "/Editor/ShowLineNumbers", 0],
    [cmbShowChange, "/Editor/ShowChange", 1],
    [cmbShowCurrent, "/Editor/ShowCurrent", 1],
    [cmbFoldProc, "/Editor/Fold", 0],
    [txtRecent, "/Recent/Max", Project.DEFAULT_MAX_RECENT],
    [cmbOutput, "/QuietExternalCommands", 0],
    [cmbTooltip, "/ShowTooltip", 0],
    [cmbIntegratedHelp, "/PropertyHelp", 1],
    [cmbMinimize, "/MinimizeOnRun", 0],
    [cmbUpperCaseKeywords, "/Editor/KeywordsUseUpperCase", 0]]
  
  $bNoChange = True
  ReadConfig
  cmbIconTheme.Index = ["gnome", "kde"].Find(Settings["/Theme"]) + 1
  cmbBrowser.Index = $aBrowser.Find(Settings["/Browser"]) + 1
  cmbTerminal.Index = $aTerminal.Find(Settings["/Terminal"]) + 1
  $bNoChange = False
  
  For iInd = 0 To MTheme.ColorKeys.Max
  
    hHBox = New HBox(svwTheme)
    hHBox.Tag = MTheme.ColorKeys[iInd]
    hHBox.Height = 4 * Desktop.Scale
    hHBox.Spacing = 4
    hColor = New DrawingArea(hHBox) As "dwgStyle"
    hColor.Expand = True
    hButton = New Button(hHBox) As "btnStyle"
    hButton.Text = ("Define...")
    hButton.Width = Desktop.Scale * 16
  
  Next
  
  RefreshStyle
  
End

Public Sub Form_KeyPress()
  
  If Key.Code = Key.Esc Then Me.Close
  
End

Public Sub cmbTheme_Click()

  Dim cColor As Collection

  If cmbTheme.Index <= 0 Then Return

  $cCurrent = MTheme.ReadFile($aTheme[$aThemeName.Find(cmbTheme.Text)])
  RefreshEditor
  
End

Public Sub btnExport_Click()

  Dialog.Path = Settings["/FOption/ThemePath", System.User.Home &/ System.User.Name & ".gambas.theme"]
  Dialog.Filter = ["*.gambas.theme", ("Gambas highlight theme files")]
  Dialog.Title = ("Export a theme file")
  Dialog.AutoExt = True
  If Dialog.SaveFile() Then Return
  Settings["/FOption/ThemePath"] = Dialog.Path
  
  MTheme.WriteFile(Dialog.Path, $cCurrent)
  
End

Private Sub RefreshStyle()
  
  Dim hHBox As HBox
  
  svwTheme.Background = CStyle[$cCurrent["Background"]].Foreground
  
  For Each hHBox In svwTheme.Children
    hHBox.Children[0].Refresh
  Next  
  
End



Private Sub RefreshEditor()
  
  Dim hFile As Object

  If $bNoChange Then Return

  RefreshStyle
  
  MTheme.WriteSettings(Settings, "Highlight", $cCurrent)
  Settings["/Editor/Font"] = fntEditor.SelectedFont
  
  Project.RefreshBreakpointPicture
  
  For Each hFile In Project.Files
    If hFile Is FEditor Or hFile Is FTextEditor Then hFile.ReadConfig()
  Next  
  
  FOutput.ReadConfig
  FFindList.ReadConfig

End

Public Sub btnUndo_Click()

  $cCurrent = MTheme.Copy($cLast)
  cmbTheme.Index = 0
  RefreshEditor

End

Public Sub fntEditor_Change()

  RefreshEditor

End

Private Sub RefreshWindow()
  
  Dim hFile As Object
  
  If $bNoChange Then Return

  FProperty.ReadConfig
  FMain.ReadConfig
  FDebugInfo.ReadConfig
  FFindList.ReadConfig
  FFormStack.ReadConfig
  FToolBox.ReadConfig
  FDebugExpr.ReadConfig
  For Each hFile In Project.Files
    Try hFile.ReadConfig
  Next

End

Public Sub cmbGlobalFont_Click()

  Settings["/GlobalFont"] = cmbGlobalFont.Index
  RefreshWindow

End

Public Sub cmbTabStripFont_Click()

  Settings["/TabStripFont"] = cmbTabStripFont.Index
  RefreshWindow

End

Public Sub cmbTooltip_Click()

  Settings["/ShowTooltip"] = cmbTooltip.Index = 1
  Application.Tooltip.Enabled = cmbTooltip.Index = 1
  
End

Public Sub txtTabSize_Change()

  Settings["/DefaultTabSize"] = txtTabSize.Value

End

Public Sub cmbProcLimit_Click()

  Settings["/Editor/ProcedureLimit"] = cmbProcLimit.Index
  RefreshEditor

End

Public Sub cmbLineNumber_Click()

  Settings["/Editor/ShowLineNumbers"] = cmbLineNumber.Index = 1
  RefreshEditor

End

Public Sub cmbShowChange_Click()

  Settings["/Editor/ShowChange"] = cmbShowChange.Index = 1
  RefreshEditor

End

Public Sub cmbShowCurrent_Click()

  Settings["/Editor/ShowCurrent"] = cmbShowCurrent.Index = 1
  RefreshEditor

End

' Public Sub cmbCompletion_Click()
' 
'   Settings["/Editor/Completion"] = cmbCompletion.Index = 1
'   RefreshEditor
' 
' End

Public Sub btnImport_Click()

  Dialog.Path = Settings["/FOption/ThemePath", System.User.Home]
  Dialog.Filter = ["*.gambas.theme", ("Gambas highlight theme files")]
  Dialog.Title = ("Select a theme file")
  If Dialog.OpenFile() Then Return
  
  'Enhanced Dialog.OpenFile() is buggy 
  If Not Dialog.Path Or If IsDir(Dialog.Path) Then Return
  
  Settings["/FOption/ThemePath"] = Dialog.Path 
  
  $cCurrent = MTheme.ReadFile(Dialog.Path)
  cmbTheme.Index = 0
  RefreshEditor

End

' PUBLIC SUB cmbMascot_Click()
' 
'   Settings["/ShowMascot"] = cmbMascot.Index = 0
'   FGambas.Visible = cmbMascot.Index = 0
' 
' END

Public Sub cmbSortProperty_Click()

  Settings["/SortProperties"] = cmbSortProperty.Index = 1
  FProperty.ReadConfig

End

Private Sub NeedRestart(hCtrl As Control)
  
  Balloon.Warning(("You need to restart the application to see your changes."), hCtrl)  
  
End


Public Sub cmbIconTheme_Click()

  If cmbIconTheme.Index > 0
    Settings["/Theme"] = LCase(cmbIconTheme.Text)
  Else 
    Settings["/Theme"] = ""
  Endif
  
  NeedRestart(cmbIcontheme)
  
End

Public Sub cmbBrowser_Click()

  If cmbBrowser.Index > 0 Then 
    Settings["/Browser"] = $aBrowser[cmbBrowser.Index - 1]
  Else   
    Settings["/Browser"] = ""
  Endif
  
  Project.Browser = ""

End

Public Sub cmbTerminal_Click()

  If cmbTerminal.Index > 0 Then 
    Settings["/Terminal"] = $aTerminal[cmbTerminal.Index - 1]
  Else   
    Settings["/Terminal"] = ""
  Endif
  
End

' PUBLIC SUB cmbImageEditor_Click()
' 
'   IF cmbImageEditor.Index > 0 THEN 
'     Settings["/ImageEditor"] = LCase(cmbImageEditor.Text)
'   ELSE   
'     Settings["/ImageEditor"] = ""
'   ENDIF
'   
'   Project.ImageEditor = ""
' 
' END

Public Sub cmbToolbox_Click()

  Settings["/ToolboxSize"] = cmbToolbox.Index
  FToolBox.ReadConfig

End

Public Sub txtRecent_Change()

  Settings["/Recent/Max"] = txtRecent.Value
  NeedRestart(txtRecent)
  
End

Public Sub cmbOutput_Click()

  Settings["/QuietExternalCommands"] = cmbOutput.Index = 1

End

Public Sub btnClose_Click()

  Me.Close

End

' Public Sub cmbMessage_Click()
' 
'   Settings["/ShowMessages"] = cmbMessage.Index
'   FMain.ReadConfig
' 
' End

Public Sub cmbIntegratedHelp_Click()

  Settings["/PropertyHelp"] = cmbIntegratedHelp.Index = 1
  FProperty.CheckIntegratedHelpSetting
  
End

Public Sub cmbMinimize_Click()

  Settings["/MinimizeOnRun"] = cmbMinimize.Index = 1
  
End

Public Sub dwgStyle_Draw()
  
  Dim hDraw As DrawingArea = Last
  Dim sKey As String = hDraw.Parent.Tag
  Dim hStyle As CStyle = CStyle[$cCurrent[sKey]]
  Dim hStyle2 As CStyle
  Dim sName As String
  Dim iForeground As Integer
  Dim iBackground As Integer

  sName = MTheme.ColorNames[MTheme.ColorKeys.Find(sKey)]

  Draw.Font = Font[fntEditor.SelectedFont]
  If hStyle.Underline Then Draw.Font.Underline = True
  If hStyle.Italic Then Draw.Font.Italic = True
  
  If MTheme.IsBackgroundStyle(sKey) Then 
    iBackground = hStyle.Foreground
    iForeground = CStyle[$cCurrent["Normal"]].Foreground
  Else 
    iBackground = hStyle.Background
    iForeground = hStyle.Foreground
  Endif
  
  If iBackground <> Color.Default Then 
    Draw.FillColor = iBackground
    Draw.FillStyle = Fill.Solid
    Draw.LineStyle = Line.None
    Draw.Rect(0, 0, hDraw.Width, hDraw.Height)
  Endif
  Draw.Foreground = iForeground
  Draw.Text(sName, 4, 0, hDraw.Width - 8, hDraw.Height, Align.Normal)
  If hStyle.Bold Then Draw.Text(sName, 5, 0, hDraw.Width - 8, hDraw.Height, Align.Normal)
  
End

Public Sub btnStyle_Click()

  $hDraw = Last.Parent.Children[0]
  $sStyle = Last.Parent.Tag  
  mnuPopup.Popup
  
End

Public Sub mnuPopup_Show()
  
  With CStyle[$cCurrent[$sStyle]]
  
    mnuBold.Checked = .Bold
    mnuItalic.Checked = .Italic
    mnuUnderline.Checked = .Underline

    If MTheme.IsBackgroundStyle($sStyle) Then 
      mnuForeground.Hide
      mnuBackground.Checked = False
    Else
      mnuForeground.Show
      mnuBackground.Checked = .Background <> Color.Default
    Endif
  
  End With 
  
End

Private Sub UpdateStyle()
  
  Dim hStyle As New CStyle($cCurrent[$sStyle])
  
  With hStyle
    .Bold = mnuBold.Checked
    .Italic = mnuItalic.Checked
    .Underline = mnuUnderline.Checked
  End With
  
  $cCurrent[$sStyle] = hStyle.ToString()
  RefreshEditor
  
End

Public Sub mnuBold_Click()
  
  mnuBold.Checked = Not mnuBold.Checked
  UpdateStyle
  
End

Public Sub mnuItalic_Click()
  
  mnuItalic.Checked = Not mnuItalic.Checked
  UpdateStyle
  
End

Public Sub mnuUnderline_Click()
  
  mnuUnderline.Checked = Not mnuUnderline.Checked
  UpdateStyle
  
End

Public Sub mnuForeground_Click()
  
  Dim hStyle As New CStyle($cCurrent[$sStyle])
  Dim iColor As Integer = hStyle.Foreground
  
  If FColorChooser.Run("&H" & Hex$(iColor, 6) & "&") Then Return
  
  hStyle.Foreground = FColorChooser.Value
  $cCurrent[$sStyle] = hStyle.ToString()
  RefreshEditor
  
End

Public Sub mnuBackground_Click()
  
  Dim hStyle As New CStyle($cCurrent[$sStyle])
  Dim iColor As Integer = hStyle.Background
  Dim iDefault As Integer
  
  If MTheme.IsBackgroundStyle($sStyle) Then 
    mnuForeground_Click
    Return 
  Endif
  
  iDefault = CStyle[$cCurrent["Background"]].Foreground
  If iColor = Color.Default Then iColor = iDefault

  If FColorChooser.Run("&H" & Hex$(iColor, 6) & "&") Then Return
  
  If FColorChooser.Color = "" Or If FColorChooser.Value = iDefault Then 
    hStyle.Background = Color.Default
  Else
    hStyle.Background = FColorChooser.Value
  Endif
  $cCurrent[$sStyle] = hStyle.ToString()
  RefreshEditor
  
End

Public Sub cmbUpperCaseKeywords_Click()

  Dim hFile As Object

  Settings["/Editor/KeywordsUseUpperCase"] = cmbUpperCaseKeywords.Index = 1

  RefreshEditor

End

Public Sub cmbFoldProc_Click()

  Settings["/Editor/Fold"] = cmbFoldProc.Index = 1  

End

Public Sub Form_Open()

  Me.Center

End

