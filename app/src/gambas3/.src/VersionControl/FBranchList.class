' Gambas class file

Static Private $sBranch As String

Private Const MAX_ROW As Integer = 12

Private $sCurrent As String
Private $aBranches As String[]

Public Sub Init()

  'txtFilter.H = FMain.GetTabBarHeight()

  $sBranch = ""
  $aBranches = VersionControl.GetBranches(ByRef $sCurrent)
  
  If $aBranches.Count <= MAX_ROW Then 
    txtFilter.Hide
    sepFilter.Hide
  Else 
    txtFilter.Show
    sepFilter.Show
  Endif
  
  FillBranch()
  
  txtFilter.SetFocus
  
End

Private Sub FillBranch()

  Dim sBranch As String
  Dim sFilter As String
  Dim H As Integer
  
  sFilter = Trim(txtFilter.Text)
  lvwBranch.Clear
  
  For Each sBranch In $aBranches
    If sFilter And InStr(sBranch, sFilter) = 0 Then Continue
    With lvwBranch.Add(sBranch, sBranch)
      If sBranch = VersionControl.GetDefaultBranch() Or If sBranch = $sCurrent Then
        .Font = Application.Font
        If sBranch = VersionControl.GetDefaultBranch() Then .Font.Underline = True
        If sBranch = $sCurrent Then .Font.Bold = True
      Endif
    End With
  Next
  
  lvwBranch.Key = $sCurrent
  
  H = Max(Min(Desktop.Scale * 3 * MAX_ROW, lvwBranch.ScrollHeight), Desktop.Scale * 3)
  If txtFilter.Visible Then H += txtFilter.H + sepFilter.H
  Me.Parent.H = H + 2
  
End

Public Sub txtFilter_Filter()

  FillBranch()

End

Public Sub lvwBranch_Activate()

  VersionControl.SetBranch(lvwBranch.Current.Text)

End

Public Sub Form_KeyPress()

  If Key.Code = Key.Escape Then FMain.CloseBranchList

End

Public Sub txtFilter_KeyPress()

  If Not Key.Text Then Object.Raise(lvwBranch.Children[0].Proxy, "KeyPress")
  
End

Public Sub txtFilter_Activate()

  lvwBranch_Activate

End

Public Sub lvwBranch_GotFocus()

  txtFilter.SetFocus

End

Public Sub txtFilter_Click()

  lvwBranch_Activate

End
