' Gambas class file

Public CaseSensitive As Boolean
Public RegularExpression As Boolean
Public IgnoreStrings As Boolean
Public IgnoreComments As Boolean
Public WordOnly As Boolean
Public CurrentProcedure As Boolean
Public CurrentProcedureName As String
Public SearchString As String
Public ReplaceString As String
Public BrowseTimeStamp As Integer
'Private OnlySource As Boolean

Public InBrowse As Boolean

Private $hCurrent As Object
Private $sGrep As String
Private $bReplace As Boolean
Private $aBrowse As New CFindResult[]
Private $aExt As String[] = ["JPG", "JPEG", "BMP", "GIF", "PNG", "ICO", "XPM", "GAMBAS"]
Private $bCancel As Boolean

Private $cGrepCache As New Collection
Private $cRefreshBrowse As New Collection
Private $bMustRefreshBrowse As Boolean
Private $hFont As Font

Private gvwFind As GridView

Private Const GO_FORWARD As Integer = 0
Private Const GO_BACKWARD As Integer = 1
Private Const DO_REPLACE As Integer = 2
Private Const DO_ALL As Integer = 4

Public Sub _new()
  
  Me.Utility = Settings["/UseUtilityWindows", 1]
  
  gvwFind = FDebugInfo.GetSearchList()
  Object.Attach(gvwFind, Me, "gvwFind")
  
  ReadConfig

End

Public Sub Form_Open()

  Settings.Read(Me)
  
  chkCaseSensitive.Value = Settings["/FSearch/CaseSensitive", False]
  chkCurrentProcedure.Value = Settings["/FSearch/CurrentProcedure", False]
  chkHighlight.Value = Settings["/FSearch/Highlight", False]
  chkIgnoreComments.Value = Settings["/FSearch/IgnoreComments", False]
  chkIgnoreStrings.Value = Settings["/FSearch/IgnoreStrings", False]
  chkRegularExpression.Value = Settings["/FSearch/RegularExpression", False]
  chkWordOnly.Value = Settings["/FSearch/WordOnly", False]
  
End

Public Sub Form_Close()

  Settings.Write(Me)

  Settings["/FSearch/CaseSensitive"] = CBool(chkCaseSensitive.Value)
  Settings["/FSearch/CurrentProcedure"] = CBool(chkCurrentProcedure.Value)
  Settings["/FSearch/Highlight"] = CBool(chkHighlight.Value)
  Settings["/FSearch/IgnoreComments"] = CBool(chkIgnoreComments.Value)
  Settings["/FSearch/IgnoreStrings"] = CBool(chkIgnoreStrings.Value)
  Settings["/FSearch/RegularExpression"] = CBool(chkRegularExpression.Value)
  Settings["/FSearch/WordOnly"] = CBool(chkWordOnly.Value)

End

Public Sub ReadConfig()
  
  gvwFind.Font.Grade = - Settings["/TabStripFont", 0]
  $hFont = Font[Settings["/Editor/Font", Project.DEFAULT_FONT]]
  $hFont.Grade = gvwFind.Font.Grade
  gvwFind.Refresh

End

Private Sub UpdateTitle()

  Dim sWhere As String

  If $hCurrent Is FOutput Then
    sWhere = ("Console")
  Else If $hCurrent Then
    sWhere = $hCurrent.Title
  Else
    sWhere = ("Project")
  Endif
  
  Me.Title = ("Search & Replace") & " - " & sWhere
  
End


Public Sub Update(Optional bForce As Boolean)
  
  Dim hCtrl As Control
  Dim bCode As Boolean
  Dim hWindow As Window
  Dim bAllProject As Boolean
  
  If Not Me.Visible And If Not bForce Then Return
  
  Try hWindow = Application.ActiveControl.Window
  If hWindow = Me Then Return
  $hCurrent = hWindow
  
  If $hCurrent Is FOutput Then
  Else 
    $hCurrent = Project.ActiveForm
    If $hCurrent Then
      If $hCurrent Is FEditor Or If $hCurrent Is FTextEditor Then
        bCode = $hCurrent.Editor.Highlight <> Highlight.None
      Endif
    Else
      bAllProject = True
    Endif
  Endif
    
  'chkIgnoreStrings.Enabled = bCode Or bAllProject
  'chkIgnoreComments.Enabled = bCode Or bAllProject
  chkCurrentProcedure.Enabled = bCode
  
  btnNext.Visible = Not bAllProject
  btnPrevious.Visible = Not bAllProject
  btnReplace.Visible = Not bAllProject
  btnReplaceAll.Visible = Not bAllProject
  
  If bAllProject Then
    btnBrowse.Lower
  Else
    btnBrowse.Next = btnClose
  Endif
  
  'btnBrowse2.Visible = bAllProject
  
  UpdateTitle
  
End

Public Sub Form_Show()

  Update()

End

Public Sub Form_Activate()

  Update()
  RefreshBrowse()

End


Public Sub cmbSearch_Activate()

  btnNext.Value = True

End

Public Sub cmbReplace_Activate()

  btnReplace.Value = True

End

Private Sub ShowMessage(sMsg As String, hCtrl As Control)
  
  If Me.Visible Then
    Balloon.Info(sMsg, hCtrl)
  Else 
    Project.SetMessage(sMsg)
  Endif
  
End

' Private Sub GetCurrentPosition()
'   
'   Dim hFirst As Object
'   
'   If $hCurrent Then
'     $hPosition = $hCurrent.GetFindPosition()
'   Else
'     hFirst = Project.Get
'   Endif
'   
' End

Public Sub Grep_Read()
  
  Dim sData As String
  
  sData = Read #Last, Lof(Last)
  $sGrep &= sData
  
End

' Private Sub MakeResult(sResult As String) As CFindResult
'   
'   Dim aResult As String[]
'   Dim hResult As New CFindResult
'   
'   aResult = Split(sResult, ":")
'   
'   iLine = CInt(aResult[1]) - 1
'   If iLine < hPos.Line Then Continue
'   
'   iCol = 0
'   iPos = CInt(aResult[2])
'   While iPos > 0
'     If Mid$(sText, iPos, 1) = "\n" Then Break
'     Dec iPos
'     Inc iCol
'   Wend
'   
'   hResult = New CFindResult
'   hResult.Line = 
'   
' End

Private Sub GrepFile(hFile As Object) As CFindResult[]
  
  Dim sText As String
  Dim aGrep As String[]
  Dim aResult As New CFindResult[]
  Dim sFind As String
  Dim aFind As String[]
  Dim hResult As CFindResult
  Dim sTemp As String
  Dim iAge As Integer
  Dim hGrepResult As CGrepResult
  Dim sPath As String

  Try sText = hFile.GetText()
  If Error Then Return aResult
  
  sTemp = Temp$("grep")
  
  aGrep = ["grep", "-nobs"]
  If Not CaseSensitive Then aGrep.Add("-i")
  If WordOnly Then aGrep.Add("-w")
  If RegularExpression Then 
    aGrep.Add("-E")
  Else
    aGrep.Add("-F")
  Endif
  aGrep.Add("--")
  aGrep.Add(SearchString)
  aGrep.Add(sTemp)
  
  Try sPath = hFile.Path
  If sPath Then
    hGrepResult = $cGrepCache[sPath]
    
    If hGrepResult Then
      Try iAge = hFile.Age
      If iAge = hGrepResult.Age Then
        If aGrep.Join("\n") = hGrepResult.Command Then
          Return hGrepResult.Result
        Endif
      Endif
    Endif
  Endif
  
  'grep -nR -F -I -i -w -ob --exclude=*~ --exclude-dir=.svn -s -- "Date" * .src
  
  File.Save(sTemp, sText)
  
  Exec aGrep To $sGrep 'For Read Write As "Grep"
  ' Print #hProcess, sText;
  ' Close #hProcess
  ' Repeat
  '   Wait 0.01
  ' Until hProcess.State <> Process.Running

  For Each sFind In Split($sGrep, "\n", "", True)
    aFind = Split(sFind, ":")
    hResult = New CFindResult
    If hFile Is FOutput Then
      hResult.Path = ""
      hResult.File = "Console"
    Else
      hResult.Path = hFile.Path
      hResult.File = hFile.Name
    Endif
    hResult.Line = CInt(aFind[0])
    hResult.Pos = CInt(aFind[1])
    hResult.Bytes = Len(aFind[2])
    hResult.Length = String.Len(aFind[2])
    aResult.Add(hResult)
  Next
  
  Try iAge = hFile.Age
  If Not Error And If sPath Then
    hGrepResult = New CGrepResult
    With hGrepResult
      .Result = aResult.Copy()
      .Age = iAge
      .Path = sPath
      .Command = aGrep.Join("\n")
    End With
    $cGrepCache[sPath] = hGrepResult
  Endif
  
  Return aResult
  
End

Private Sub InitSearch() As Boolean
  
  SearchString = cmbSearch.Text
  ReplaceString = cmbReplace.Text
  
  Project.StoreCombo(cmbSearch)
  Project.StoreCombo(cmbReplace)
  
  CaseSensitive = chkCaseSensitive.Value
  IgnoreStrings = chkIgnoreStrings.Value
  IgnoreComments = chkIgnoreComments.Value
  RegularExpression = chkRegularExpression.Value
  WordOnly = chkWordOnly.Value
  CurrentProcedure = chkCurrentProcedure.Value
  
  If Not SearchString Then Return True
  
End

Private Sub DoReplace(aResult As CFindResult[], iInd As Integer, sReplace As String)

  Dim iAdd As Integer
  
  iAdd = Len(sReplace) - aResult[iInd].Bytes

  aResult.Remove(iInd)
  While iInd < aResult.Count
    aResult[iInd].Pos += iAdd
    Inc iInd
  Wend
  
End

Private Sub Run(iAction As Integer)
  
  Dim sResult As String
  Dim aResult As CFindResult[]
  Dim aFind As String[]
  Dim iLine As Integer
  Dim iCol As Integer
  Dim iCurrentPos, iPos, iInd As Integer
  Dim iTry As Integer
  Dim nReplace As Integer
  Dim hEditor As FEditor
  Dim sProc As String
  
  If InitSearch() Then Return
  
  If CurrentProcedure Then
    If $hCurrent Is FEditor Then
      hEditor = $hCurrent
      CurrentProcedureName = hEditor.GetProcAt(hEditor.Editor.Line)
    Endif
  Endif
  
  aResult = GrepFile($hCurrent)

DO_IT_AGAIN:
  
  iCurrentPos = $hCurrent.GetPosition()
  
  If iAction And DO_REPLACE And If nReplace = 0 Then
    If iAction And GO_BACKWARD Then
      Inc iCurrentPos
    Else
      Dec iCurrentPos
    Endif
  Endif

  If aResult.Count Then
  
    For iTry = 1 To aResult.Count
    
      If iAction And GO_BACKWARD Then
      
        For iInd = aResult.Max To 0 Step -1
          iPos = aResult[iInd].Pos
          If iPos < iCurrentPos Then Break
        Next
        
        If iPos >= iCurrentPos Then 
          iInd = aResult.Max
          iPos = aResult[iInd].Pos
        Endif
    
      Else
      
        For iInd = 0 To aResult.Max
          iPos = aResult[iInd].Pos
          If iPos > iCurrentPos Then Break
        Next
         
        If iPos <= iCurrentPos Then 
          iInd = 0
          iPos = aResult[iInd].Pos
        Endif
        
      Endif
    
      If Not $hCurrent.SetPosition(aResult[iInd], False) Then 
        If iAction And DO_REPLACE Then 
          $hCurrent.Replace(ReplaceString)
          DoReplace(aResult, iInd, ReplaceString)
        Endif
        Inc nReplace
        If (iAction And DO_ALL) = 0 Then Return
        Goto DO_IT_AGAIN
      Endif
      
      iCurrentPos = iPos
      
    Next
    
  Endif
  
  If iAction And DO_REPLACE And If iAction And DO_ALL Then
    If nReplace = 0 Then
      ShowMessage(("Search string cannot be found."), cmbReplace)
    Else If nReplace = 1 Then
      ShowMessage(("Search string replaced once."), cmbReplace)
    Else
      ShowMessage(Subst(("Search string replaced &1 times."), nReplace), cmbReplace)
    Endif
  Else
    ShowMessage(("Search string cannot be found."), cmbSearch)
  Endif
  
End

Private Sub UpdateSpan(iRow As Integer)
  
  Dim hResult As CFindResult
  Dim iLastFile, iLastLine As Integer
  
  If iRow >= $aBrowse.Count Then Return
  
  iLastFile = iRow
  iLastLine = iRow
  
  gvwFind[iRow, 0].RowSpan = 1
  gvwFind[iRow, 1].RowSpan = 1
  
  Inc iRow
  
  While iRow < $aBrowse.Count
    
    hResult = $aBrowse[iRow]
    
    If hResult.File <> $aBrowse[iLastFile].File Then
      iLastFile = iRow
      iLastLine = iRow
    Else If hResult.Line <> $aBrowse[iLastLine].Line Then
      iLastLine = iRow
    Endif
    gvwFind[iLastFile, 0].RowSpan = iRow - iLastFile + 1
    gvwFind[iLastLine, 1].RowSpan = iRow - iLastLine + 1
    
    Inc iRow
    
  Wend
  
End


Public Sub AddFound(hResult As CFindResult, Optional iIndex As Integer = -1)
  
  Dim iInd As Integer
  
  If iIndex < 0 Then
    iIndex = $aBrowse.Count
    $aBrowse.Add(hResult)
  Else
    $aBrowse.Add(hResult, iIndex)
  Endif
  
  If iIndex > 0 Then 
    If hResult.File = $aBrowse[iIndex - 1].File Then
      hResult.Dark = $aBrowse[iIndex - 1].Dark
    Else
      hResult.Dark = Not $aBrowse[iIndex - 1].Dark
    Endif
  Endif
  
  ' iInd = iIndex
  ' While iInd > 0
  '   If $aBrowse[iInd - 1].File <> hResult.File Then Break
  '   Dec iInd
  ' Wend
  ' gvwFind[iInd, 0].RowSpan = iIndex - iInd + 1
  ' 
  ' iInd = iIndex
  ' While iInd > 0
  '   If $aBrowse[iInd - 1].File <> hResult.File Then Break
  '   If $aBrowse[iInd - 1].Line <> hResult.Line Then Break
  '   Dec iInd
  ' Wend
  ' gvwFind[iInd, 1].RowSpan = iIndex - iInd + 1
  
End

Private Sub FindPathInBrowseResult(sPath As String) As Integer
  
  Dim iInd As Integer
  
  For iInd = 0 To $aBrowse.Max
    If $aBrowse[iInd].Path = sPath Then Return iInd
  Next
  
  Return -1
  
End


Private Sub BrowseFile(sPath As String, Optional iIndex As Integer = -1, Optional bRefresh As Boolean)
  
  Dim hFile As Object
  Dim aResult As CFindResult[]
  Dim iInd As Integer
  Dim hResult As CFindResult
  Dim iStart As Integer
  
  Try hFile = Project.LoadFile(sPath)
  If Not hFile Then Return
  
  If iIndex < 0 Then
    iStart = $aBrowse.Count
  Else
    iStart = iIndex
  Endif
  
  aResult = GrepFile(hFile)
  
  For iInd = 0 To aResult.Max
    hResult = aResult[iInd]
    If Not hFile.SetPosition(hResult, True) Then 
      hResult.Path = sPath
      AddFound(hResult, iIndex)
      If bRefresh Then 
        If iIndex >= 0 Then gvwFind.Rows[iIndex].Refresh
      Endif
      If iIndex >= 0 Then Inc iIndex
    Endif
  Next
  
  gvwFind.Rows.Count = $aBrowse.Count
  UpdateSpan(iStart)
  
End

Private Sub Browse(Optional bOnlySource As Boolean)
  
  Dim sDir As String
  Dim sFile As String
  Dim sPath As String
  Dim iPos As Integer
  Dim iLen As Integer
  Dim aFind As String[]
  Dim sName As String
  Dim sExt As String
  
  If InitSearch() Then Return
  'OnlySource = bOnlySource
  
  InBrowse = True
  
  CurrentProcedure = False
  BrowseTimeStamp = Project.TimeStamp
  
  FDebugInfo.ShowSearchList
  
  Inc Application.Busy
  FDebugInfo.EnableGUI(False)
  'tabSearch.Enabled = False
  
  'btnClose.Text = ("Cancel")
  $bCancel = False
  
  gvwFind.Rows.Count = 0
  gvwFind.Clear
  gvwFind.Rows.Height = Max(gvwFind.Font.Height, $hFont.Height)
  $aBrowse.Clear
  
  Inc FMain.NoMessage
  
  If bOnlySource Then
    sDir = Project.Dir &/ ".src"
  Else
    sDir = Project.Dir
  Endif
  
  For Each sFile In RDir(sDir)
  
    If Left(sFile) = "." And If Not (sFile Begins ".src/") Then Continue
    If Right(sFile) = "~" Then Continue
      
    sPath = sDir &/ sFile
  
    If IsDir(sPath) Then Continue
    If InStr(sPath, "/.svn/") Then Continue
    
    sExt = UCase(File.Ext(sFile))
    If $aExt.Exist(sExt) Then Continue
    
    sName = File.Name(sPath)
    If sName Begins "core." Then Continue
    If sName Begins "vgcore." Then Continue
    If sName Begins "callgrind.out." Then Continue
    If sName Begins "cachegrind.out." Then Continue
    
    If sFile Begins ".src/" Then
      FDebugInfo.SetSearchListInfo(sName)
    Else
      FDebugInfo.SetSearchListInfo(sFile)
    Endif
  
    BrowseFile(sPath)
    Wait 0.01
    If $bCancel Then Break
    
    'Debug sName
  Next
  
Finally
  
  Dec FMain.NoMessage
  
  If gvwFind.Rows.Count = 0 Then
    FDebugInfo.HideSearchList
  Endif

  'lblBrowse.Hide
  FDebugInfo.EnableGUI(True)
  'btnClose.Text = ("Close")
  Dec Application.Busy
  
  InBrowse = False

End



Public Sub btnNext_Click()

  Run(GO_FORWARD)

End

Public Sub btnPrevious_Click()

  Run(GO_BACKWARD)

End

Public Sub btnClose_Click()

  Me.Close

End

Public Sub Cancel()
  
  $bCancel = True
  
End


Public Sub FindNext()
  
  Update(True)
  Run(GO_FORWARD)
  
End

Public Sub FindPrevious()
  
  Update(True)
  Run(GO_BACKWARD)
  
End

Public Sub FindProject(sText As String)
  
  'WakeUp(False)
  cmbSearch.Text = sText
  Browse
  
End

Public Sub BrowseAgain()
  
  Browse
  
End


' Public Sub UpdateSearchString(sText As String)
'   
'   If sText <> cmbSearch.Text Then cmbSearch.Text = sText
'   
' End

Private Sub ShowString(Optional bHide As Boolean)

  Dim hForm As Object
  Dim bIgnoreCase As Boolean = chkCaseSensitive.Value = False
  Dim sStr As String
  
  If chkHighlight.Value And If Not bHide Then sStr = cmbSearch.Text
  
  For Each hForm In Project.Files
    Try hForm.OnShowString(sStr, bIgnoreCase)
  Next
  
End

Public Sub cmbSearch_Change()

  'If Not cmbSearch.Text Then Stop
  'FMain.UpdateSearchString(cmbSearch.Text)
  ShowString
  
End


Public Sub OnNewForm(hForm As Object)
  
  Try hForm.OnShowString(If(chkHighlight.Value, cmbSearch.Text, ""), chkCaseSensitive.Value = False)
  
End

Public Sub chkCaseSensitive_Click()

  ShowString

End

Public Sub chkHighlight_Click()

  ShowString

End


Public Sub btnReplace_Click()

  Run(GO_FORWARD + DO_REPLACE)

End

Public Sub btnReplaceAll_Click()

  Run(GO_FORWARD + DO_REPLACE + DO_ALL)

End

Public Sub WakeUp(bReplace As Boolean)
  
  Dim sFind As String

  $bReplace = bReplace
  Me.Show

  Update()
  
  Try sFind = $hCurrent.GetSelection()

  If $bReplace And If sFind Then
    cmbReplace.SelectAll
    cmbReplace.SetFocus
  Else
    cmbSearch.SelectAll
    cmbSearch.SetFocus
  Endif
  
  If sFind Then cmbSearch.Text = sFind
  ShowString
  
End

Public Sub Form_Hide()

  If Not FMain.Enabled Then $bCancel = True
  ShowString(True)

End

Public Sub btnBrowse_Click()

  Browse

End

Public Sub mnuBrowseAll_Click()
  
  Browse
  
End

Public Sub mnuBrowseSource_Click()
  
  Browse(True)
  
End



Public Sub gvwFind_Data(Row As Integer, Column As Integer)

  If Row > $aBrowse.Max Then Return

   With gvwFind.Data
    
    If Column = 3 Then .Font = $hFont 
    
    Select Case Column
      Case 0
        .Text = $aBrowse[Row].File
        .Alignment = Align.TopNormal
      Case 1
        .Text = $aBrowse[Row].Line + 1
        .Alignment = Align.TopRight
      Case 2
        .Text = $aBrowse[Row].Column + 1
        .Alignment = Align.TopRight
      Case 3
        .RichText = $aBrowse[Row].Text
        .Alignment = Align.TopNormal
    End Select

    ' If Row Then
    '   If Column = 0 Then 
    '     If $aBrowse[Row - 1][0] = .Text Then 
    '       .Text = ""
    '     Endif
    '   Else If Column = 1 Then
    '     If $aBrowse[Row - 1][0] = $aBrowse[Row][0] And $aBrowse[Row - 1][1] = $aBrowse[Row][1] Then 
    '       .Text = "-"
    '     Endif
    '   Endif
    ' Endif 
    
    .Background = If($aBrowse[Row].Dark, Color.LightBackground, Color.TextBackground)
    
  End With 

End

Public Sub gvwFind_Select()

  Dim hFind As CFindResult
  Dim sPath As String
  Dim hForm As Object

  If $aBrowse.Count = 0 Then Return
  If gvwFind.Row < 0 Then Return
  
  hFind = $aBrowse[gvwFind.Row]
  
  Try hForm = Project.OpenFile(hFind.Path)
  If Not hForm Then Return
  
  hForm.SetPosition(hFind, False)
  Project.Activate(hForm)

  gvwFind.ScrollX = 0
  
End

Public Sub Form_KeyPress()

  If Key.Code = Key.Esc Then 
    If InBrowse Then
      Cancel
    Else
      btnClose.Value = True
    Endif
  Endif

End

Private Sub ClearCache()
  
  $cGrepCache.Clear
  $cRefreshBrowse.Clear
  
End

Public Sub OnProjectChange()
  
  ClearCache
  
End

Public Sub OnFileChange(sPath As String)
  
  If Not $cGrepCache.Exist(sPath) Then Return
  
  $cRefreshBrowse[sPath] = True
  
  timRefresh.Stop
  If Me.Visible Then timRefresh.Start
  
End

Private Sub RefreshBrowse()
  
  Dim sPath As String
  Dim iIndex As Integer
  
  If $cRefreshBrowse.Count = 0 Then Return
  
  Object.Lock(gvwFind)
  
  For Each $cRefreshBrowse
    sPath = $cRefreshBrowse.Key
    
    $cGrepCache[sPath] = Null
    
    iIndex = FindPathInBrowseResult(sPath)
    If iIndex >= 0 Then
      While $aBrowse[iIndex].Path = sPath
        $aBrowse.Remove(iIndex)
        If iIndex >= $aBrowse.Count Then Break
      Wend
    Endif
    BrowseFile(sPath, iIndex)
    
  Next
  
  $cRefreshBrowse.Clear
  Object.Unlock(gvwFind)
  gvwFind.Refresh
  timRefresh.Stop
  BrowseTimeStamp = Project.TimeStamp
  
Catch
  
  Debug Error.Text
  Error Error.Backtrace.Join("\n")
  
End

Public Sub timRefresh_Timer()
  
  RefreshBrowse
  
End
