' Gambas class file

Private $aFound As New CFindResult[]
Private $hFont As Font
Private $eAngle As Float

Public Sub ClearFound()

  tvwFind.Rows.Count = 0
  $aFound.Clear
  lblNotFound.Hide
  tvwFind.Show
  
End

Public Sub AddFound(sName As String, iLine As Integer, iCol As Integer, iLen As Integer, sText As String)
  
  Dim hResult As New CFindResult
  Dim iInd As Integer
  
  hResult.File = sName
  hResult.Line = iLine + 1
  hResult.Column = iCol
  hResult.Text = sText
  hResult.Length = iLen
  
  $aFound.Add(hResult)
  If $aFound.Count > 1 Then 
    If hResult.File = $aFound[$aFound.Max - 1].File Then
      hResult.Dark = $aFound[$aFound.Max - 1].Dark
    Else
      hResult.Dark = Not $aFound[$aFound.Max - 1].Dark
    Endif
  Endif
  tvwFind.Rows.Count = $aFound.Count
  
  iInd = $aFound.Max
  While iInd > 0
    If $aFound[iInd - 1].File <> hResult.File Then Break
    Dec iInd
  Wend
  tvwFind[iInd, 0].RowSpan = $aFound.Max - iInd + 1
  
  iInd = $aFound.Max
  While iInd > 0
    If $aFound[iInd - 1].File <> hResult.File Then Break
    If $aFound[iInd - 1].Line <> hResult.Line Then Break
    Dec iInd
  Wend
  tvwFind[iInd, 1].RowSpan = $aFound.Max - iInd + 1
  
End

' PUBLIC SUB SelectFound(sName AS String, iLine AS Integer, iCol AS Integer, iLen AS Integer)
'   
'   DIM sKey AS String
'   
'   sKey = FindKey(sName)
'   IF NOT sKey THEN RETURN 
'   
'   sKey = KEY_FIND & sKey & "@" & Format(iLine, "000000") & "." & Format(iCol, "00000") & "." & iLen
'   TRY ProjectTree[sKey].Selected = TRUE
'   
' END

Public Sub _new()

  With tvwFind
    .Columns.Count = 4  
    .Columns[0].Width = Desktop.Scale * 16
    .Columns[0].Text = ("Class")
    .Columns[1].Width = Desktop.Scale * 8
    .Columns[1].Text = ("Line")
    .Columns[2].Width = Desktop.Scale * 8
    .Columns[2].Text = ("Column")
    .Columns[3].Text = ("Text")    
  End With
  
  ReadConfig

End

Public Sub Form_Open()

  Settings.Read(Me)
  MoveMag

End

Public Sub tvwFind_Data(Row As Integer, Column As Integer)

  With tvwFind.Data
    
    If Column = 3 Then .Font = $hFont 
    
    Select Case Column
      Case 0
        .Text = $aFound[Row].File
      Case 1
        .Text = $aFound[Row].Line
      Case 2
        .Text = $aFound[Row].Column
      Case 3
        .Text = $aFound[Row].Text
    End Select
    .Alignment = Align.TopNormal

    ' If Row Then
    '   If Column = 0 Then 
    '     If $aFound[Row - 1][0] = .Text Then 
    '       .Text = ""
    '     Endif
    '   Else If Column = 1 Then
    '     If $aFound[Row - 1][0] = $aFound[Row][0] And $aFound[Row - 1][1] = $aFound[Row][1] Then 
    '       .Text = "-"
    '     Endif
    '   Endif
    ' Endif 
    
    .Background = If($aFound[Row].Dark, Color.LightBackground, Color.TextBackground)
    
  End With

End

Public Sub tvwFind_Select()

  Dim hFind As CFindResult
  Dim iLine As Integer
  Dim iCol As Integer
  Dim iLen As Integer 
  Dim hEdit As Editor
  Dim sPath As String

  If timSearch.Enabled Then Return
  If $aFound.Count = 0 Then Return
  
  hFind = $aFound[tvwFind.Row]
  
  sPath = Project.FindPath(hFind.File)
  If Not sPath Then Return
  
  Project.OpenFile(sPath)

  hEdit = Project.Files[sPath].Editor
  iLine = hFind.Line - 1
  iCol = hFind.Column - 1
  iLen = hFind.Length
  hEdit.Goto(iLine, iCol, True)
  hEdit.Select(iLine, iCol, iLine, iCol + iLen)
  
  'Project.ProjectTree[sSave].Selected = TRUE

End

Public Sub Form_Close()

  Settings.Write(Me)  

End

Public Sub ReadConfig()
  
  tvwFind.Font.Grade = - Settings["/GlobalFont", 0]
  $hFont = Font[Settings["/Editor/Font", Project.DEFAULT_FONT]]
  tvwFind.Refresh

End

' PUBLIC SUB panSearch_Arrange()
' 
'   btnCancel.Move(panSearch.Width - btnCancel.Width - Desktop.Scale, Desktop.Scale)
' 
' END

Private Sub MoveMag()
  
  picSearch.Move(16 + Cos($eAngle) * 8, 16 + Sin($eAngle) * 8 + btnCancel.Y + btnCancel.H + Desktop.Scale)
  
End


Public Sub timSearch_Timer()

  $eAngle += 0.4
  MoveMag

End

Public Sub Start()
  
  FMain.Enabled = False
  Me.Enabled = True
  ClearFound
  btnCancel.Text = ("Cancel")
  timSearch.Start
  
End

Public Sub Stop()
  
  timSearch.Stop
  btnCancel.Text = ("Close")
  If tvwFind.Rows.Count = 0 Then
    tvwFind.Hide
    lblNotFound.Show
  Endif
  FMain.Enabled = True
  
End

' Public Sub btnCancel_Click()
' 
'   If timSearch.Enabled Then
'     FFind.Cancel
'   Else 
'     Me.Close
'   Endif
' 
' End
