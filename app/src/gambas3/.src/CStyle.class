' Gambas class file

Public Index As Integer
Public Foreground As Integer
Public Bold As Boolean
Public Underline As Boolean
Public Dotted As Boolean

Static Public Sub _get((Style) As String) As CStyle
  
  Dim hStyle As New CStyle(Style)
  Return hStyle
  
End


Public Sub _new(Optional (Style) As String)
  
  Reset
  Foreground = Color.Black
  If Style Then FromString(Style)
  
End


Public Sub Reset()

  Foreground = Color.Default
  Bold = False
  Underline = False
  Dotted = False
  
End


Public Sub FromString(sStyle As String)
  
  Dim sStr As String
  Dim iColor As Integer
  
  Reset
  
  For Each sStr In Split(sStyle)
    If Left(sStr) = "#" Then 
      iColor = Val("&H" & Mid$(sStr, 2) & "&") 
      Foreground = iColor
    Else If sStr = "Bold" Then 
      Bold = True
    Else If sStr = "Underline" Then
      Underline = True
      Dotted = False
    Else If sStr = "Dotted" Then
      Dotted = True
      Underline = False
    Else If sStr = "Normal" Then
      Bold = False
      Underline = False
      Dotted = False
    Endif
  Next
  
End

Public Sub ToString() As String

  Dim sStr As String
  
  sStr = "#" & Hex$(Foreground, 6)
  If Bold Then sStr &= ",Bold"
  'If Italic Then sStr &= ",Italic"
  If Underline Then sStr &= ",Underline"
  If Dotted Then sStr &= ",Dotted"
  
  Return sStr
  
End
