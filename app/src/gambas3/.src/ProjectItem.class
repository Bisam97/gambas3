' Gambas class file

Inherits UserControl

Private $hDrawingArea As DrawingArea
Private $sPath As String
Private $hProjectInfo As CProjectInfo
Private $hIcon As Picture

Private Const ICON_SIZE As Integer = 32
Private Const PADDING As Integer = 6

Private $bHighlight As Boolean

Property Background As Integer

Public Sub _new()
  
  $hDrawingArea = New DrawingArea(Me) As "DrawingArea"
  '$hDrawingArea.Mouse = Mouse.Pointing
  Me.H = ICON_SIZE + PADDING * 2
  
End

  
Public Sub SetPath(sPath As String)
  
  $sPath = sPath
  $hProjectInfo = New CProjectInfo(sPath)
  Me.Tag = sPath
  
  With $hProjectInfo
    
    $hIcon = Project.StretchIcon(.Icon, ICON_SIZE).Picture
    
    ' If .Icon.W = ICON_SIZE And If .Icon.H = ICON_SIZE Then
    '   $hIcon = .Icon
    ' Else
    '   $hIcon = .Icon.Image.Stretch(ICON_SIZE, ICON_SIZE).Picture
    ' Endif
    
  End With 
  
  $hDrawingArea.ToolTip = Replace($sPath, System.User.Home, "~")
  $hDrawingArea.Refresh
  
End

Public Sub DrawingArea_Draw()
  
  Dim hIcon As Picture
  Dim sTitle As String
  Dim X, W As Integer
  Dim sPath As String
  Dim bSelected As Boolean
  
  If Not $hProjectInfo Then Return
  
  bSelected = Me.Background = Color.SelectedBackground
  
  With $hProjectInfo
    
    Draw.Picture($hIcon, PADDING, PADDING)
    sTitle = .Title
    If Not sTitle Then sTitle = .Name
    sTitle = "<b>" & .Name & "</b> " & .Version
    sTitle &= Subst("<br><font size=\"-1\" color=\"&1\">", If(bSelected, "white", "gray")) & .Title
    If .Authors And If .Authors.Count Then
      If .Title Then sTitle &= " - "
      sTitle &= "<i>" & If(.Authors.Count = 1, ("Author"), ("Authors")) & " : " & .Authors.Join(", ") & "</i>"
    Endif
    sTitle &= "</font>"
    'Draw.Font.Bold = True
    X = ICON_SIZE + PADDING * 2
    W = Max(Me.W - X, Draw.RichTextWidth(sTitle))
    If bSelected Then Draw.Foreground = Color.SelectedForeground
    Draw.RichText(sTitle, X, 0, W, Desktop.Scale * 4, Align.TopLeft)
    
  End With
  
End

Public Sub DrawingArea_Enter()
  
  'If Me.Parent Is ListContainer Then
    If Me.Background = Color.Default Then 
      $hDrawingArea.Background = Color.LightBackground
      $bHighlight = True
    Endif
  'Endif
  
End

Public Sub DrawingArea_Leave()
  
  If $bHighlight Then $hDrawingArea.Background = Color.Default
  
End


Private Function Background_Read() As Integer

  Return Super.Background

End

Private Sub Background_Write(Value As Integer)

  Super.Background = Value
  $hDrawingArea.Background = Color.Default

End
