' Gambas class file

Inherits CFamily

Public Const Name As String = "WebForm"
'Public Const DropImageClass As String = "ReportImage"
'Public Const DropImageProperty As String = "Image"
Public Const EmbedContainer As String = "WebContainer"

Public Sub _new()

  Me.Background = Color.TextBackground
  
End


Public Sub GetIcon(hCCtrl As CControl) As String
  
  Dim sImage As String
  
  Select hCCtrl.Kind
  
    Case "WebHBox"
      sImage = "horizontal"
    
    Case "WebVBox", "WebMenu"
      sImage = "vertical"
    
    'Case "WebForm", "WebContainer"
    '  sImage = "vertical"
      
    Case Else
      sImage = Super.GetIcon(hCCtrl)
      
  End Select
  
  ' If hCCtrl.GetProperty("Invert") Then
  '   If Exist("img/family/" &/ sImage & "-invert.png") Then sImage &= "-invert"
  ' Endif
  
  Return sImage
  
End


Public Sub DrawControl(hCCtrl As CControl) As Boolean
  
  Dim iBorder As Integer
  Dim hCtrl As Control = hCCtrl.Control
  Dim hParent As CControl
  Dim sImage As String
  Dim DS As Integer
  Dim X As Integer
  Dim hImage As Image
  Dim sColor As Variant
  
  Select hCCtrl.Kind
  
    ' Case "WebMenu"
    '   
    '   sText = hCCtrl.GetProperty("Text")
    '   If Not sText Then sText = " "
    '   With Paint.TextSize(sText)
    '     
    '   End With
    ' 
    
    Case "WebMenuItem"
      
      DS = Desktop.Scale
      
      Paint.Background = Color.LightForeground
      Paint.LineWidth = 1
      Paint.Rectangle(0.5, 0.5, hCtrl.W - 1, hCtrl.H - 1)
      Paint.Stroke
      
      X = DS * 2 + Stock.GetSize("small")
      sImage = hCCtrl.GetProperty("Image")
      If sImage Then
        
        Try hImage = Image.Load(Project.Dir &/ ".public" &/ sImage)
        
        If hImage Then 
          Paint.DrawImage(hImage, DS, CInt(Paint.H - hImage.H) \ 2)
          X = DS + hImage.W + DS
        Endif
        
      Endif
      
      sColor = hCCtrl.GetProperty("Foreground")
      If sColor Then
        Paint.Background = FColorChooser.FromString(sColor)
      Else
        Paint.Background = Color.TextForeground
      Endif
      
      Paint.DrawText(hCCtrl.GetProperty("Text"), X, 0, Paint.W - X - DS, Paint.H, Align.Normal)
      Paint.DrawText(hCCtrl.GetProperty("Shortcut"), X, 0, Paint.W - X - DS, Paint.H, Align.Right)
      
      Return
    
    Case "WebContainer", "WebHBox", "WebVBox"
    
      Paint.Background = &HFFB0B0
    
    Case Else
      
      Return True

  End Select

  Paint.LineWidth = 1
  Paint.Rectangle(0.5, 0.5, hCtrl.W - 1, hCtrl.H - 1)
  Paint.Stroke
  
End

Public Sub ActionDialog(sAction As String) As Boolean

  Dim vColor As Variant
  
  Select Case sAction
    
    ' Case Like ".text-*"
    '   Return Super.ActionDialog(sAction)
      
    Case Like ".format-*"
      If FColorChooser.Run(, True) Then Return True
      vColor = FColorChooser.Color
      If Not IsNull(Val(vColor)) Then vColor = Val(vColor)
      Me.ActionValue = vColor
      
  End Select
  
End

Public Sub ActionExec(hCtrl As CControl, sAction As String)
  
  Select Case sAction
    
    ' Case Like ".text-*"
    '   Super.ActionExec(hCtrl, sAction)
      
    Case ".format-background"
      If hCtrl.HasProperty("Background") Then hCtrl.SetProperty("Background", Me.ActionValue)
    
    Case ".format-foreground"
      If hCtrl.HasProperty("Foreground") Then hCtrl.SetProperty("Foreground", Me.ActionValue)
    
  End Select
  
End

Public Sub SelectImage((sPropertyType) As String, sImage As String) As String

  Return FSelectIcon.Run(sImage, FSelectIcon.SELECT_IMAGE + FSelectIcon.SELECT_SVGIMAGE + FSelectIcon.SELECT_PUBLIC)
      
End
