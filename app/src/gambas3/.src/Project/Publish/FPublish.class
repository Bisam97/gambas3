' Gambas class file

Public Sub Run() As Boolean

  Return Not Me.ShowModal()

End

Public Sub btnOK_Click()

  Me.Close(True)

End

Public Sub btnCancel_Click()

  Me.Close

End

Public Sub btnPublish_Click()

  spnPublish.Enabled = True
  btnPublish.Hide
  btnCancel.Hide
  btnAbort.Show
  
  Publish.PublishSoftware(Me, txtScreenshot.Text, txtLogin.Text, txtPassword.Text, cmbGambasVersion.Text, tagEditor.GetTags(), lstRequire.List)
  
End

Public Sub SetMessage(sMsg As String)
  
  lblMessage.Text = sMsg
  
End


Public Sub OnPublishEnd()

  btnPublish.Show
  btnAbort.Hide
  btnCancel.Show
  spnPublish.Enabled = False

End

Public Sub btnAbort_Click()

  Publish.Abort

End

Public Sub txtScreenshot_Click()

  Dialog.Title = ("Select a screenshot of your project")
  Dialog.Filter = ["*.png;*.jpg;*.jpeg", ("Picture files")]
  Dialog.Path = txtScreenshot.Text
  If Dialog.OpenFile() Then Return
  
  txtScreenshot.Text = Dialog.Path

End

Public Sub Form_Open()

  Settings.Read(Me)

  cmbFarm.List = Settings["/Publish/FarmList"]
  
  cmbFarm.Text = Settings["/Publish/Farm"]
  If Not cmbFarm.Text Then cmbFarm.Text = cmbFarm[0].Text
  
  txtLogin.Text = Settings["/Publish/Login"]
  chkRememberPassword.Value = Settings["/Publish/RememberPassword", False]
  If chkRememberPassword.Value Then
    Try txtPassword.Text = Desktop.Passwords["/Publish"]
  Endif
  
  tagEditor.SetTags(Project.PublishTags)
  tagEditor.SetCompletion("CTagCompletion")
  
  lstRequire.List = Project.PublishDependencies
  
  cmbGambasVersion.Text = Project.PublishGambasVersion

End

Public Sub Form_Close()

  Settings.Write(Me)
  
  Settings["/Publish/Farm"] = cmbFarm.Text
  Settings["/Publish/Login"] = txtLogin.Text
  Settings["/Publish/RememberPassword"] = CBool(chkRememberPassword.Value)
  If chkRememberPassword.Value Then
    Try Desktop.Passwords["/Publish"] = txtPassword.Text
  Endif

  Project.PublishTags = tagEditor.GetTags()
  Project.PublishDependencies = lstRequire.List
  Project.PublishGambasVersion = cmbGambasVersion.Text
  Project.WriteProject

End


Public Sub GetFarm() As String
  
  Return cmbFarm.Text
  
End
