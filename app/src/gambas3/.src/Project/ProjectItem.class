' Gambas class file

Inherits UserControl

Event Click

Static Private $hSmallFont As Font

Property ShowPath As Boolean
Property Path As String
Property Highlight As Boolean
Property Background As Integer
Property Read IdealHeight As Integer
Property ShowAuthors As Boolean
Property Read Info As CProjectInfo
Public Group As String ' for examples

Private $hDrawingArea As DrawingArea
Private $sPath As String
Private $hProjectInfo As CProjectInfo
Private $hIcon As Picture
Private $bShowPath As Boolean
Private $bShowAuthors As Boolean

Private Const ICON_SIZE As Integer = 32
Private Const PADDING As Integer = 6

Private $bCanHighlight As Boolean = True
Private $bHighlight As Boolean
Private $bSquare As Boolean

Private $hObs As Observer

Private $iIdealH As Integer
Private $iIdealW As Integer

Public Sub _new(Optional bSquare As Boolean)
  
  $bSquare = bSquare
  $hDrawingArea = New DrawingArea(Me) As "DrawingArea"
  $hDrawingArea.Mouse = Mouse.Pointing
  $hDrawingArea.Focus = True
  
  $hObs = New Observer(Me) As "ProjectItem"
  
  If Not $hSmallFont Then
    $hSmallFont = Me.Font.Copy()
    $hSmallFont.Size *= 0.75
  Endif
  
End

  
Private Sub SetPath(sPath As String)
  
  $sPath = sPath
  $hProjectInfo = New CProjectInfo(sPath)
  $hIcon = Null
  
  $hDrawingArea.ToolTip = Replace($sPath, System.User.Home, "~")
  $iIdealW = 0
  Me.H = Me.IdealHeight
  Me.Refresh
  
End


Private Sub GetTitle() As String
  
  Dim sTitle As String
  Dim bSelected As Boolean
  
  bSelected = Me.Background = Color.SelectedBackground

  With $hProjectInfo
    sTitle = .Title
    If Not sTitle Then sTitle = .Name
    sTitle = "<b>" & .Name & "</b>&nbsp;" & .Version
    sTitle &= Subst("<br><font size=\"-1\" color=\"#&1\">", Hex$(If(bSelected, Color.SelectedForeground, Color.Gradient(Color.TextBackground, Color.TextForeground, 0.75)), 6)) & .Title
    If $bShowAuthors And If .Authors And If .Authors.Count Then
      If .Title Then sTitle &= " - "
      sTitle &= "<i>" & If(.Authors.Count = 1, ("Author"), ("Authors")) & " : " & .Authors.Join(", ") & "</i>"
    Endif
    sTitle &= "</font>"
    If $bShowPath Then sTitle &= Subst("<br><font size=\"-1\" color=\"&1\"><b>", If(bSelected, "white", "gray")) & Replace($sPath, System.User.Home, "~") & "</b></font>"
  End With
  
  Return sTitle

End


Public Sub DrawingArea_Draw()
  
  Dim sTitle As String
  Dim X, Y, W, H As Integer
  Dim bSelected As Boolean
  Dim sText As String
  
  If Not $hProjectInfo Then Return
  
  'If $bHighlight And If Not $hDrawingArea.Hovered Then
  '  DrawingArea_Leave
  '  Return
  'Endif
  
  If $bHighlight Then Draw.FillRect(0, 0, Draw.W, Draw.H, Color.LightBackground)
  
  bSelected = Me.Background = Color.SelectedBackground
  
  With $hProjectInfo
    
    If Not $hIcon Then
      $hIcon = Project.StretchIcon(.Icon, ICON_SIZE).Picture
    Endif
    
    Draw.Picture($hIcon, PADDING, PADDING)
    sTitle = GetTitle()
    'Draw.Font.Bold = True
    X = ICON_SIZE + PADDING * 2
    W = Min(Me.W - X, Draw.RichTextWidth(sTitle))
    If bSelected Then Draw.Foreground = Color.SelectedForeground
    
    Y = PADDING
    H = Draw.RichTextHeight(sTitle, W)
    Draw.RichText(sTitle, X, Y, W, H, Align.TopLeft)
    
    sText = Html$($hProjectInfo.Description)
    If sText Then
      Draw.Font = $hSmallFont
      Y += H
      W = Me.W - X - PADDING
      H = Draw.RichTextHeight(sText, W)
      Draw.RichText(sText, X, Y, W, H, Align.TopLeft)
    Endif
    
  End With
  
  If $hDrawingArea.HasFocus Then
    Draw.Foreground = Color.Gradient(Color.SelectedBackground, Me.Background)
    Draw.Rect(1, 1, Draw.W - 2, Draw.H - 2)
    'Paint.Begin(Draw.Device)
    'Paint.Rectangle(0, 0, Paint.W, Paint.H)
    'Paint.Brush = Paint.Color(Color.SetAlpha(Color.SelectedBackground, 192))
    'Paint.Fill
    'Paint.End
  Endif
  ' If $hDrawingArea.HasFocus Then
  '   Draw.Foreground = Color.SelectedBackground
  '   Draw.LineStyle = Line.Dot
  '   Draw.Rect(1, 1, Draw.W - 2, Draw.H - 2)
  ' Endif
  
End

Public Sub DrawingArea_Enter()
  
  If $bCanHighlight Then
    'If not $bHighlight Then 
      '$hDrawingArea.Background = Color.LightBackground
      $bHighlight = True
      $hDrawingArea.Refresh
    'Endif
  Endif
  
End

Public Sub DrawingArea_Leave()
  
  If $bHighlight Then 
    '$hDrawingArea.Background = Color.Default
    $bHighlight = False
    $hDrawingArea.Refresh
  Endif
  
End

Public Sub DrawingArea_KeyPress()

  If Key.Code = Key.Space Then Raise Click

End

Private Function Background_Read() As Integer

  Return Super.Background

End

Private Sub Background_Write(Value As Integer)

  Super.Background = Value
  $hDrawingArea.Background = Color.Default

End

Private Function ShowPath_Read() As Boolean

  Return $bShowPath

End

Private Sub ShowPath_Write(Value As Boolean)

  $bShowPath = Value
  Me.Refresh

End

Private Function Path_Read() As String

  Return $sPath

End

Private Sub Path_Write(Value As String)

  SetPath(Value)

End

Private Function Highlight_Read() As Boolean

  Return $bCanHighlight

End

Private Sub Highlight_Write(Value As Boolean)

  $bCanHighlight = Value
  $hDrawingArea.Mouse = If(Value, Mouse.Pointing, Mouse.Default)

End

Private Function IdealHeight_Read() As Integer
  
  Dim X, H As Integer

  If Not $hProjectInfo Then Return 0

  If $hProjectInfo.Description Then
    If Me.W = $iIdealW Then Return $iIdealH
  Else
    If $iIdealW Then Return $iIdealH
  Endif
  $iIdealW = Me.W

  X = ICON_SIZE + PADDING * 2
  'If $hProjectInfo Then
  '  sTitle = GetTitle()
    'H = $hDrawingArea.Font.RichTextHeight(sTitle, Min(Me.W - X, $hDrawingArea.Font.RichTextWidth(sTitle))) + PADDING * 2
    H = $hDrawingArea.Font.Height * 2 + PADDING * 2
  'Endif

  H = Max(H, X)
  If $bShowPath Then H += Desktop.Scale * 2
  
  If $hProjectInfo.Description Then
    H += $hSmallFont.RichTextHeight(Html($hProjectInfo.Description), Me.W - X - PADDING)
  Endif
  
  $iIdealH = H
  Return H
  
End

Private Function Info_Read() As CProjectInfo

  Return $hProjectInfo

End

Public Sub DrawingArea_MouseDown()
  
  If Mouse.Left Then Raise Click
  
End

Private Function ShowAuthors_Read() As Boolean

  Return $bShowAuthors

End

Private Sub ShowAuthors_Write(Value As Boolean)

  $bShowAuthors = Value
  $hDrawingArea.Refresh

End

Public Sub Match(sFilter As String) As Boolean

  If Not sFilter Then Return True
  With $hProjectInfo
    If .Name Like sFilter Then Return True
    If String.LCase(.Title) Like sFilter Then Return True
    If $bShowAuthors And If String.LCase(.Authors.Join(" ")) Like sFilter Then Return True
    If String.LCase(Group) Like sFilter Then Return True
  End With
  
End

Public Sub ProjectItem_BeforeArrange()
  
  If Not Me.Visible Then Return
  Me.H = Me.IdealHeight
  
End
