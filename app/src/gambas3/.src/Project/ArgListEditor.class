' Gambas class file

Export

Inherits UserControl

Event Click

Property Selected As Boolean

Private $hPanel As DrawingArea
Private $bSelected As Boolean
Private $bNoResizePanel As Boolean

Public Sub _new()
  
  Me._Arrangement = Arrange.Fill
  
  $hPanel = New DrawingArea(Me) As "Panel"
  $hPanel.Background = Color.TextBackground
  $hPanel.Foreground = Color.TextForeground
  $hPanel.Arrangement = Arrange.LeftRight
  $hPanel.Margin = True
  '$hPanel.Spacing = True
  
  Set(Null)
  
End

Private Sub Add(Optional sArg As String, Optional hAfter As ArgListBox)

  Dim hArg As ArgListBox
  
  hArg = New ArgListBox($hPanel) As "ArgListBox"
  hArg.Text = sArg
  
  'hArg = hArg.Previous
  'If hArg Then Try hArg.Text = hArg.Text
  If hAfter Then 
    hArg.Next = hAfter.Next
    hAfter.Next = hArg
  Endif
  
End

Public Sub Set(aArg As String[])

  Dim sArg As String
  
  $hPanel.Children.Clear
  
  $bNoResizePanel = True
  
  If aArg Then
    For Each sArg In aArg
      Add(sArg)
    Next
  Endif
  
  Add()
  
  $bNoResizePanel = False
  
  ResizePanel
  
End

Public Sub Get() As String[]
  
  Dim aArg As New String[]
  Dim I As Integer
  Dim hArg As ArgListBox
  
  For I = 0 To $hPanel.Children.Count - 1
    hArg = $hPanel.Children[I]
    aArg.Add(hArg.Text)
  Next
  
  While Not aArg[aArg.Max]
    aArg.Remove(aArg.Max)
    If aArg.Count = 0 Then Break
  Wend
  
  Return aArg
  
End


Private Sub ResizePanel()
  
  Dim H As Integer
  
  If $bNoResizePanel Then Return
  
  With $hPanel.Children[$hPanel.Children.Count - 1]
    H = .Y + .H + Desktop.Scale
  End With
  
  H = Max(H, Desktop.Scale * 5)
  
  $hPanel.H = H
  Me.H = H
  
End

Public Sub Panel_Arrange()
  
  ResizePanel
  
End

Public Sub SetFocus()
  
  Dim hArg As ArgListBox
  
  Try hArg = $hPanel.Children[$hPanel.Children.Count - 1]
  If Not hArg Or If hArg.Text Then Add()
  
  $hPanel.Children[$hPanel.Children.Count - 1].SetFocus
  
  Raise Click
  
End


Public Sub Panel_MouseDown()

  SetFocus
  
End

Private Function Selected_Read() As Boolean

  Return $bSelected

End

Private Sub Selected_Write(Value As Boolean)

  If $bSelected = Value Then Return
  $bSelected = Value
  $hPanel.Refresh

End

Public Sub Panel_Draw()
  
  If $bSelected Then
    Paint.Rectangle(1, 1, Paint.W - 2, Paint.H - 2)
    Paint.LineWidth = 2
    Paint.Background = Color.SelectedBackground
    Paint.Stroke
  Else
    Paint.Rectangle(0.5, 0.5, Paint.W - 1, Paint.H - 1)
    Paint.LineWidth = 1
    Paint.Background = Color.LightForeground
    Paint.Stroke
  Endif
  
End

Public Sub ArgListBox_GotFocus()
  
  Raise Click
  
End

Public Sub ArgListBox_Activate()

  Dim hArg As ArgListBox = Last
  
  If Not hArg.Next Then Add()
  If hArg.HasFocus Then Try hArg.Next.SetFocus
  
End

Public Sub ArgListBox_Insert()

  Dim hArg As ArgListBox = Last
  Dim sText As String
  Dim iPos As Integer
  
  sText = hArg.Text
  iPos = hArg.Pos
  hArg.Text = String.Left(sText, iPos)
  
  Add(String.Mid(sText, iPos + 1), hArg)
  If hArg.HasFocus Then
    hArg = hArg.Next
    hArg.SetFocus
    hArg.MoveBegin
  Endif
  
End


Public Sub ArgListBox_Cancel()
  
  Me.Selected = False
  Me.Parent.SetFocus
  
End


