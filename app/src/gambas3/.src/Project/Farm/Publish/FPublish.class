' Gambas class file

Public Sub Run() As Boolean

  Return Not Me.ShowModal()

End

Public Sub btnOK_Click()

  Me.Close(True)

End

Public Sub btnCancel_Click()

  Me.Close

End

Public Sub btnPublish_Click()

  Dim hReq As FarmRequest

  hReq = New FarmRequest(GetFarm(), txtLogin.Text, txtPassword.Text)
  hReq.PublishSoftware(txtScreenshot.Text, cmbGambasVersion.Text, tagEditor.GetTags(), lstRequire.List, txtURL.Text)
  hReq.WaitFor(("The project has been successfully published."), ("Unable to publish project."))
  
End

Public Sub txtScreenshot_Click()

  Dialog.Title = ("Select a screenshot of your project")
  Dialog.Filter = ["*.png;*.jpg;*.jpeg", ("Picture files")]
  Dialog.Path = txtScreenshot.Text
  If Dialog.OpenFile() Then Return
  
  txtScreenshot.Text = Dialog.Path

End

Public Sub Form_Open()

  Settings.Read(Me)

  cmbFarm.List = FarmRequest.GetFarms()
  chkRememberPassword.Value = Settings["/Publish/RememberPassword", False]
  
  cmbFarm.Text = Settings["/Publish/Farm"]
  cmbFarm.Index = 0
  
  tagEditor.SetTags(Project.PublishTags)
  
  lstRequire.List = Project.PublishDependencies
  
  cmbGambasVersion.Text = Project.PublishGambasVersion
  txtURL.Text = Project.PublishWebSite

End

Public Sub Form_Close()

  Settings.Write(Me)
  
  Settings["/Publish/Farm"] = cmbFarm.Text
  Settings["/Publish/RememberPassword"] = CBool(chkRememberPassword.Value)

  Project.PublishTags = tagEditor.GetTags()
  Project.PublishDependencies = lstRequire.List
  Project.PublishGambasVersion = cmbGambasVersion.Text
  Project.PublishWebSite = txtURL.Text
  Project.WriteProject

End


Public Sub GetFarm() As String
  
  Return cmbFarm.Text
  
End

Public Sub cmbFarm_Click()

  txtLogin.Text = Settings["/Publish" &/ cmbFarm.Text &/ "Login"]
  If chkRememberPassword.Value Then
    Try txtPassword.Text = Desktop.Passwords["/Publish" &/ cmbFarm.Text]
  Endif
  tagEditor.SetFarm(cmbFarm.Text)

End

Public Sub txtLogin_LostFocus()
  
  Settings["/Publish" &/ cmbFarm.Text &/ "Login"] = txtLogin.Text 
  
End


Public Sub txtPassword_LostFocus()

  If cmbFarm.Text And If chkRememberPassword.Value Then
    Try Desktop.Passwords["/Publish" &/ cmbFarm.Text] = txtPassword.Text
  Endif

End
