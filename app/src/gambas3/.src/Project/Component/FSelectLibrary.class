' Gambas class file

Static Public Path As String

Public Sub Run() As Boolean

  Return Not Me.ShowModal()

End

Public Sub btnOK_Click()

  Dim sKey As String

  sKey = cvwLibrary.Key
  If Not sKey Then Return
  If Not cvwLibrary.Current.ParentKey Then Return
  
  Path = ":" & sKey
  
  Me.Close(True)

End

Public Sub btnCancel_Click()

  Me.Close

End

Private Sub RefreshLibraries()

  Dim sDir As String
  Dim sVendor As String
  Dim sLib As String
  Dim bParent As Boolean
  Dim aLib As String[]
  Dim sKey As String
  Dim hPict As Picture

  cvwLibrary.Clear

  For Each sDir In [CLibraryInfo.UserPath, Component.Path]

  For Each sVendor In Dir(sDir, "*", gb.Directory).Sort(gb.Natural)
    bParent = False
    
    For Each sLib In Dir(sDir &/ sVendor, "*.gambas", gb.File).Sort(gb.Natural)
      
      sLib = File.BaseName(sLib)
      sKey = sVendor &/ sLib
      If cvwLibrary.Exist(sKey) Then Continue
      
      aLib = Split(sLib, ":")
      If aLib.Count <> 2 Then Continue
      
      If Not bParent Then
        cvwLibrary.Add(sVendor, sVendor, Picture["icon:/small/directory"]).Expanded = True
        bParent = True
      Endif

      If sDir = Component.Path Then 
        hPict = Picture["icon:/small/component"]
      Else
        hPict = Picture["icon:/small/book"]
      Endif

      cvwLibrary.Add(sKey, aLib[0], hPict, sVendor)[1] = aLib[1]
      
    Next
    
  Next
  
  If cvwLibrary.Count Then
    cvwLibrary.Show
    panNoLibrary.Hide
  Else
    cvwLibrary.Hide
    panNoLibrary.Show
  Endif
  
Next

End

Public Sub Form_Open()

  cvwLibrary.Columns.Count = 2
  cvwLibrary.Columns[0].Text = ("Library")
  cvwLibrary.Columns[0].Expand = True
  cvwLibrary.Columns[1].Text = ("Version")
  
  RefreshLibraries

End

Public Sub cvwLibrary_Select()

  If Not cvwLibrary.Current.ParentKey Then
    cvwLibrary.MoveChild
    cvwLibrary.Item.Selected = True
  Endif

End

Public Sub btnRemove_Click()

  If Not cvwLibrary.Key Then Return
  Try Kill CLibraryInfo.UserPath &/ cvwLibrary.Key
  RefreshLibraries

End

Public Sub cvwLibrary_Activate()

  btnOK.Value = True

End
