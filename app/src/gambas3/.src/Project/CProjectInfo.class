' Gambas class file

Property Read Name As String
Property Read Path As String
Property Read Title As String
Property Read Version As String
Property Read Description As String
Property Read Authors As String[]
Property Read Icon As String
Property Read Startup As String
Property Read Components As String[]

Private $sName As String
Private $sPath As String
Private $sTitle As String
Private $sVersion As String
Private $sDescription As String
Private $aAuthors As New String[]
Private $sIcon As String
Private $sStartup As String
Private $aComponents As New String[]
Private $bRead As Boolean

Private Sub GetProjectPath(sPath As String) As String
  
  Do
    If Exist(sPath &/ ".project") Then Return sPath
    sPath = File.Dir(sPath)
    If sPath = "/" Then Break
  Loop
  
Catch
  
End

Private Sub ReadProject()

  Dim hFile As File
  Dim sIcon As String
  Dim sLine As String
  Dim iPos As Integer
  Dim sKey As String
  Dim sVal As String
  
  If $bRead Then Return
  $bRead = True
  
  If Not $sPath Then Return
  
  $aComponents = New String[]
    
  hFile = Open $sPath &/ ".project"
  
  For Each sLine In hFile.Lines
    sLine = Trim(sLine)
    If Not sLine Then Continue 
    If Left(sLine) = "#" Then Continue

    iPos = InStr(sLine, "=")
    If iPos = 0 Then Continue

    sKey = Lower$(Trim(Left$(sLine, iPos - 1)))
    sVal = Trim(Mid$(sLine, iPos + 1))

    Select Case sKey
    
      Case "title"
        $sTitle = sVal

      Case "version"
        $sVersion = sVal
        
      Case "description"
        $sDescription = UnQuote(sVal)

      Case "authors"
        $aAuthors = Split(UnQuote(sVal), "\n")

      Case "icon"
        sIcon = sVal
        
      Case "startup"
        $sStartup = sVal
        
      Case "component"
        $aComponents.Add(sVal)

    End Select
  
  Next
  
  Close #hFile

  If sIcon Then
    $sIcon = $sPath &/ sIcon 'Project.StretchIcon(sPath &/ sIcon, 58)
  Else
    $sIcon = "img/logo/project.png"
  Endif  

End

Public Sub _new(sPath As String)
  
  $sPath = GetProjectPath(sPath)
  $sName = File.Name(sPath)

End

Public Sub GetTitle() As String
  
  If $sTitle Then Return $sTitle
  Return $sName
  
End

' Public Sub GetIcon(Optional iSize As Integer) As Image
'   
'   Dim hImage As Image = Image.Load(Icon)
'   
'   If iSize Then hImage = hImage.Stretch(iSize, iSize)
'   Return hImage
'   
' End

Private Function Name_Read() As String

  Return $sName

End

Private Function Path_Read() As String

  Return $sPath

End

Private Function Title_Read() As String

  ReadProject
  Return $sTitle

End

Private Function Version_Read() As String

  ReadProject
  Return $sVersion

End

Private Function Description_Read() As String

  ReadProject
  Return $sDescription

End

Private Function Authors_Read() As String[]

  ReadProject
  Return $aAuthors

End

Private Function Icon_Read() As String

  ReadProject
  Return $sIcon

End

Private Function Startup_Read() As String

  ReadProject
  Return $sStartup

End

Private Function Components_Read() As String[]

  ReadProject
  Return $aComponents

End
