' Gambas class file

Private $iBackground As Integer
Private $cIndex As New Collection
Private $bMany As Boolean
Private $hObject As CControl
Private $hForm As FForm

Private $iLast As Integer
Private $sLast As String
Private $sFocus As String
'Private $sFocusForm As String
Private $sType As String
Private $sOption As String
Private $hEditor As Object
Private $sOldVal As String
'Private $bFreeze As Boolean
Private $bButton As Boolean
Private $bIgnoreHide As Boolean
Private $bIgnoreCancel As Boolean
Private $bSort As Boolean
Private $bDisableIntegratedHelp As Boolean


Public Sub _new()
  
  Settings.Read(Me)
  
  grdProperty.Columns.Count = 2
  grdProperty.ScrollBar = Scroll.Vertical
  grdProperty.Columns.Resizable = True

End


Public Sub Form_Open()

  $iBackground = grdProperty.Background

  Settings.Read(spnHelp)
  ReadConfig

End


Public Sub Form_Close()

  Settings.Write(Me)
  Settings.Write(spnHelp)
  
End



Public Sub Form_Resize()

  Dim iWidth As Integer
  ' 
  ' lblMessage.Move(0, 0, ME.ClientW)
  ' 
  ' grdProperty.Move(0, 0, ME.ClientWidth, ME.ClientHeight)
  ' 
  ' IF grdProperty.Columns.Count < 2 THEN RETURN
  ' 
  iWidth = Min(128, grdProperty.ClientWidth \ 2)
  grdProperty.Columns[0].Width = iWidth
  grdProperty.Columns[1].Width = grdProperty.ClientWidth - iWidth - 4
  ' 'grdProperty.Columns.Width = 100 'ME.ClientWidth / 4
  ' 
  MoveProperty

  'CATCH $$ Non déréférencement en cas d'erreur

End


Public Sub RefreshAll()

  Dim cSelection As Collection
  Dim hCtrl As CControl
  Dim sClass As String
  Dim bDiff As Boolean
  Dim aProp As String[]
  Dim hProp As CPropertyInfo
  Dim iRow As Integer
  Dim sProp As String
  Dim sVal As String
  Dim hCtrlAlone As CControl
  Dim cKeyProp As Collection
  Dim iInd As Integer

'   DIM aColor AS NEW Integer[]
'   DIM sClassColor AS String
'   DIM hClass AS CClassInfo

  Balloon.Hide(grdProperty)

  Try $hForm = Project.ActiveForm
  If Error Or If Not $hForm Then
    HideAll
    Return 
  Endif 
  'IF NOT $hForm THEN RETURN 'GOTO _HIDE

  Try cSelection = $hForm.Selection
  If Not cSelection Then Return

  $cIndex.Clear
  $bMany = False
  HideProperty
  $hObject = Null

  If Not cSelection Then Goto _HIDE

  If cSelection.Count = 0 Then
    cSelection = New Collection
    cSelection.Add($hForm.Control[$hForm.Name], $hForm.Name)
  Endif

  $bMany = cSelection.Count > 1

  For Each hCtrl In cSelection

    If sClass = "" Then
      $hObject = hCtrl
      sClass = hCtrl.Kind
    Else If sClass <> hCtrl.Kind Then
      bDiff = True
    Endif

  Next
  
  UpdateLabel

  If bDiff Then sClass = "Control"

  'PRINT "Kind = "; sClass

  'cProp = CControl.PropertiesInOrder[sClass]
  Try aProp = CComponent.Classes[sClass].PropertyList
  If Not aProp Then
    Print "FProperty.RefreshAll: Cannot get property list of "; sClass
    aProp = New String[]
  Else
'     sClassColor = sClass
'     WHILE sClassColor
'       hClass = CComponent.Classes[sClassColor]
'       aColor.Add(hClass.PropertyList.Count, 0)
'       sClassColor = hClass.ParentComponent &/ hClass.Parent
'     WEND
  Endif

  grdProperty.Rows.Count = aProp.Count + 2
  grdProperty.Clear
  grdProperty.Rows.Height = -1

  grdProperty.Columns.Count = 2

  grdProperty[iRow, 0].Text = "Class"
  grdProperty[iRow, 1].Text = $hObject.Kind

  Inc iRow

  grdProperty[iRow, 0].Text = "Name"
  If $bMany Then
    grdProperty[iRow, 1].Text = ""
  Else
    grdProperty[iRow, 1].Text = $hObject.Name
  Endif
  
  Inc iRow

  If $bSort Then 
    aProp = aProp.Copy().Sort()
    iInd = aProp.Find(CPropertyInfo.EVENT_NAME)
    If iInd >= 0 Then
      aProp.Remove(iInd)
      aProp.Add(CPropertyInfo.EVENT_NAME, 0)
    Endif
  Endif

  For Each sProp In aProp

    'PRINT sProp

    hProp = CComponent.Classes[sClass].GetProperties()[sProp]
    If hProp.Hidden Then Continue

    $cIndex[sProp] = iRow

    grdProperty[iRow, 0].Text = sProp

    sVal = GetProperty(sProp)

    grdProperty[iRow, 0].Font = grdProperty.Font
    grdProperty[iRow, 0].Alignment = Align.TopNormal
    grdProperty[iRow, 1].Alignment = Align.TopNormal

    If CControl.WasDefaultValue Then 
      grdProperty[iRow, 0].Background = Color.TextBackground
      grdProperty[iRow, 1].Background = Color.TextBackground
      grdProperty[iRow, 0].Font.Bold = False
    Else
      grdProperty[iRow, 0].Background = Color.LightBackground
      grdProperty[iRow, 1].Background = Color.LightBackground
      grdProperty[iRow, 0].Font.Bold = True
    Endif

    grdProperty[iRow, 1].Text = sVal
    'grdProperty[iRow, 0].Padding = 4

    Inc iRow

  Next

  ' Can be lesser than predicted, as some properties may be hidden
  grdProperty.Rows.Count = iRow

  'grdProperty.Rows.Count = iRow

'   iRow = 3
'   WHILE aColor.Count
'     FOR iRow = iRow TO aColor[0] + 2
'       grdProperty[iRow, 0].
'     NEXT
'   WEND

  ' FOR iRow = 0 TO grdProperty.Rows.Count - 1
  '   grdProperty[iRow, 0].Font = grdProperty.Font
  '   grdProperty[iRow, 0].Font.Bold = TRUE
  '   grdProperty[iRow, 0].Alignment = Align.TopNormal
  '   grdProperty[iRow, 1].Alignment = Align.TopNormal
  ' NEXT

  Form_Resize
  panProperty.Show
  sepProperty.Show
  lblMessage.Hide
  'lblMessage.Ignore = TRUE
  
  'TODO save/restore last height
  CheckIntegratedHelpSetting
  
  grdProperty.Show
  lblName.Show
  'VSplit1.Show
  
  If Not $bMany Then
    FFormStack.SelectCurrent
  Endif 
  
  'If btnLock.Value Then 
  If $sFocus Then
    iRow = FindProperty($sFocus)
    If iRow >= 0 Then 
      If btnLock.Value Then grdProperty[iRow, 0].Picture = Picture["icon:/small/lock"]
      ShowProperty
    Endif
  Endif
  
  Return

_HIDE:

  HideAll

End



Public Sub HideAll()

  HideProperty
  grdProperty.Hide
  lblName.Hide
  spnHelp.Hide
  panProperty.Hide
  sepProperty.Hide
  lblMessage.Show
  $hForm = Null

End


Public Sub RefreshOne(hCtrl As CControl, sProp As String)

  Dim iRow As Integer

  If Not $bMany Then
    If $hObject <> hCtrl Then Return
  Endif

  If sProp = "Name" Then

    If $bMany Then Return
    grdProperty[1, 1].Text = $hObject.Name
    UpdateLabel

  Else

    If Not $cIndex.Exist(sProp) Then Return

    iRow = $cIndex[sProp]
    grdProperty[iRow, 1].Text = GetProperty(sProp)

    If CControl.WasDefaultValue Then 
      grdProperty[iRow, 0].Background = Color.TextBackground
      grdProperty[iRow, 1].Background = Color.TextBackground
      grdProperty[iRow, 0].Font.Bold = False
    Else
      grdProperty[iRow, 0].Background = Color.LightBackground
      grdProperty[iRow, 1].Background = Color.LightBackground
      grdProperty[iRow, 0].Font.Bold = True
    Endif

  Endif

  If sProp = $sLast Then
    CancelProperty
  Endif

End


Public Function SaveProperty() As Boolean

  Dim hTabStrip As TabStrip
  Dim vVal As Variant
  Dim hCtrl As CControl
  Dim sMsg As String

  If Not $hForm Or If Not Object.IsValid($hForm) Then Return
  If $hForm.ReadOnly Then Return
  If IsNull($hEditor) Then Return False

  vVal = $hEditor.Text
  If vVal = $sOldVal Then Return False

  Select $sType

    Case "Name"

      If IsNull(vVal) Then Goto _CANCEL

      If CControl.CheckName(vVal) Then
        sMsg = ("Forbidden characters in control name.")
        Goto _CANCEL
      Endif

      If $hObject.Rename(vVal) Then
        sMsg = ("This name is already in use.")
        Goto _CANCEL
      Endif

      RefreshOne($hObject, "Name")
      Return False

    Case "Group"

      If CControl.CheckName(vVal) Then
        sMsg = ("Forbidden characters in control group.")
        Goto _CANCEL
      Endif

    Case "i", "l", "b", "Range", "Position", "Dimension"

      vVal = Val(vVal)
      If IsNull(vVal) Then Goto _BAD

    Case "f"

      Try vVal = CFloat(vVal)
      If Error Then Goto _BAD

    Case "d"

      Try vVal = CDate(vVal)
      If Error Then Goto _BAD

    Case "Color"

      If Not IsNull(Val(vVal)) Then vVal = Val(vVal)

    Case "s", "Font", "Picture", "Constant", "List", "Path"
    
    Case "String[]", "Field[]"
    
      Try vVal = Split(vVal, "\n")
      If Error Then Goto _BAD

    Case Else ' Object

      'RETURN FALSE

  End Select

  $bIgnoreCancel = True

  If $bMany Then

    For Each hCtrl In $hForm.Selection
      If hCtrl.SetProperty($sLast, vVal) Then Goto _BAD
    Next

  Else

    If $hObject.SetProperty($sLast, vVal) Then Goto _BAD

  Endif

  $bIgnoreCancel = False

  $sOldVal = $hEditor.Text
  'DEBUG "$sOldVal =";; $sOldVal
  Return

_BAD:

  sMsg = ("Incorrect property value.")
  $bIgnoreCancel = False

_CANCEL:

  CancelProperty
  $hEditor.SetFocus
  If sMsg Then Balloon.Warning(sMsg, grdProperty, $hEditor.X - grdProperty.X + $hEditor.W / 2, $hEditor.Y - grdProperty.Y + $hEditor.H / 2)
  Return True

End


Public Sub HideProperty() As Boolean

  If $bIgnoreHide Then Return

  If $sLast Then

    If SaveProperty() Then Return True

    cmbProperty.Visible = False
    txtProperty.Visible = False
    btnProperty.Visible = False
    spnProperty.Visible = False

    ' If btnLock.Value Then
    '   $sFocus = $sLast
    ' Else
    '   $sFocus = ""
    ' Endif

    $sLast = ""
    $iLast = -1
    $hEditor = Null
    
  Endif

End


Private Sub MoveProperty()

  Dim iW As Integer

  If IsNull($hEditor) Then Return

  With grdProperty[grdProperty.Row, 1]

    iW = .Width '- 2
    If $bButton Then iW = iW - (.Height)

    $hEditor.Move(grdProperty.X + .X, grdProperty.Y + .Y, iW, .Height)
    $hEditor.Raise

  End With

  If $bButton Then
    btnProperty.Resize(txtProperty.Height, txtProperty.Height)
    btnProperty.Move(txtProperty.X + txtProperty.Width, txtProperty.Y)
    btnProperty.Raise
  Endif

End


Private Sub CancelProperty()

  If $bIgnoreCancel Then Return
  If IsNull($hEditor) Then Return

  $sOldVal = grdProperty[$iLast, 1].Text
  'DEBUG "$sOldVal =";; $sOldVal
  If $hEditor.Text <> $sOldVal Then
    If $hEditor Is SpinBox Then 
      Try $hEditor.Value = Val($sOldVal)
    Else
      $hEditor.Text = $sOldVal
    Endif
  Endif

End

Public Sub FocusOn(sFocus As String)
  
  ShowProperty(sFocus)
  
End

Private Sub FindProperty(sProp As String) As Integer

  Dim iRow As Integer
  
  If sProp Then
  
    For iRow = 0 To grdProperty.Rows.Count - 1
      If sProp = grdProperty[iRow, 0].Text Then
        Return iRow
      Endif
    Next
    
  Endif
  
  Return -1
  
End



Private Sub ShowProperty(Optional sFocus As String)

  Dim hProp As CPropertyInfo
  Dim sType As String
  Dim sOption As String
  Dim bButton As Boolean
  Dim sProp As String
  Dim sConst As String
  Dim aElt As String[]
  Dim sCtrl As String
  Dim aArg As String[]
  Dim hEditor As Object
  Dim hSymbol As CSymbolInfo
  Dim sHelp As String
  Dim iRow As Integer

  If Project.Running Then Return
  If Not $hForm Then Return 
  If $hForm.ReadOnly Then Return

  If $sLast Then HideProperty()

  If $sFocus Then sFocus = $sFocus

  If sFocus Then
    iRow = FindProperty(sFocus)
    If iRow >= 0 Then grdProperty.Row = iRow
  Endif

  If grdProperty.Row = 0 Then Return

  If grdProperty.Row = 1 Then

    If $hObject.Name = $hForm.Name Then Return
    If $bMany Then Return

    sProp = "Name"
    sType = "Name"

    If Not $bDisableIntegratedHelp Then 
      sHelp = ("The name of the control.")
    Endif 
    
  Else If grdProperty.Row = 2 Then

    If $hObject.Name = $hForm.Name Then Return

    sProp = CPropertyInfo.EVENT_NAME
    sType = "Group"

    If Not $bDisableIntegratedHelp Then 
      sHelp = ("The event group that the control belongs to.")
    Endif 

  Else

    sProp = grdProperty[grdProperty.Row, 0].Text
    hProp = CComponent.Classes[$hObject.Kind].GetProperties()[sProp]
    sType = hProp.Type
    sOption = hProp.Argument

    If Not $bDisableIntegratedHelp Then 
      
      If hProp.Name = CPropertyInfo.SCALE_NAME Then
        sHelp = ("If the form and controls dimensions must follow the size of the default font.")
      Else If hProp.Virtual Then 
        sHelp = ""
      Else
        hSymbol = CComponent.Classes[$hObject.Kind].Symbols[hProp.Name]    
        sHelp = MHelp.GetSymbolHelpText(MHelp.GetSymbolHelpPath(hSymbol))
      Endif
      
    Endif 
  Endif

  If sHelp Then lblHelp.Text = "<b>" & grdProperty[grdProperty.Row, 0].Text & "</b><br>" & sHelp

  $sLast = sProp
  $iLast = grdProperty.Row
  $sType = sType
  $sOption = sOption

  ' We assign $hEditor latter, so that SaveProperty can be called without any effect until the editor is initialized.
  hEditor = txtProperty
  txtProperty.ReadOnly = False

  Select Case $sType

    Case "i", "l", "f", "d", "v", "Name", "Group" ', "EventName"

    Case "s"
      bButton = True

    Case "String[]", "Field[]"
      bButton = True
      txtProperty.ReadOnly = True

    Case "Constant"

      If hProp.ConstantClass Then

        hEditor = cmbProperty
        cmbProperty.ReadOnly = True
        cmbProperty.Clear

        For Each sConst In hProp.ConstantList
          cmbProperty.Add(sConst)
        Next

        'cmbProperty.Sort

      Endif

    Case "b"

      hEditor = cmbProperty
      cmbProperty.ReadOnly = True
      cmbProperty.Clear
      cmbProperty.Add("False")
      cmbProperty.Add("True")

    Case "Font", "Picture", "Path", "Color"
      
      bButton = True
      txtProperty.ReadOnly = True

    Case "Range"
    
      hEditor = spnProperty
      aArg = Split(sOption, ";")
      Try spnProperty.MinValue = aArg[0]
      Try spnProperty.MaxValue = aArg[1]
      spnProperty.Step = 1
      'spnProperty.Suffix = ""
      If aArg.Count > 2 Then
        Try spnProperty.Step = aArg[2]
        If aArg.Count > 3 Then 
          'spnProperty.Suffix = " " & aArg[3]
        Endif
      Endif
      
    Case "Position"

      hEditor = spnProperty
      spnProperty.MinValue = -4096
      spnProperty.MaxValue = 4096
      spnProperty.Step = Desktop.Scale
      'spnProperty.Suffix = ""
      
    Case "Dimension"

      hEditor = spnProperty
      spnProperty.MinValue = 1
      spnProperty.MaxValue = 4096
      spnProperty.Step = Desktop.Scale
      'spnProperty.Suffix = ""
    
    Case "Table"
      bButton = True
      
    Case "Field"
    
    Case "Connection"

      hEditor = cmbProperty
      cmbProperty.ReadOnly = True
      cmbProperty.List = MConnection.GetConnections()
      cmbProperty.Add("", 0)
      
    Case "Menu"
      
      aElt = $hForm.GetTopLevelMenus()
      If aElt.Count = 0 Then Return
      
      hEditor = cmbProperty
      cmbProperty.ReadOnly = True
      aElt.Add("", 0)
      cmbProperty.List = aElt
    
    Case Else 'object
    
      hEditor = cmbProperty
      cmbProperty.ReadOnly = True
      cmbProperty.Clear
      cmbProperty.Add("(" & ("None") & ")")
      
      For Each sCtrl In $hForm.FindControlFromType($sType).Sort()
        cmbProperty.Add(sCtrl)
      Next

  End Select

  $hEditor = hEditor
  CancelProperty

  '$hEditor.Border = Border.None

  $bButton = bButton

  MoveProperty

  $hEditor.Visible = True
  btnProperty.Visible = bButton

  $hEditor.SetFocus

  If $hEditor = txtProperty Then
    'IF NOT txtProperty.ReadOnly THEN
      txtProperty.SelectAll
    'ENDIF
  Else If $hEditor = spnProperty Then 
    spnProperty.SelectAll
  Endif

End


Public Sub cmbProperty_Activate()

  SaveProperty

End


Public Sub cmbProperty_Click()

  SaveProperty

End


Public Sub txtProperty_Activate()

  SaveProperty

End


Public Sub txtProperty_Change()

  If $sLast = "Text" Then
    '$bFreeze = True
    SaveProperty
    '$bFreeze = False
  Endif

End


Public Sub txtProperty_KeyPress()

  If txtProperty.ReadOnly Then
    If $sType = "Font" Or If $sType = "Picture" Or If $sType = "String[]" Or If $sType = "Path" Or If $sType = "Color" Or If $sType = "Field[]" Then
      If Key.Code = Key.Delete Or Key.Code = Key.Backspace Then

        txtProperty.Text = ""
        SaveProperty

      Endif
    Endif
  Endif

End


Private Function GetFont(sFont As String) As String

  Dim bDefault As Boolean
  Dim aFont As String[]
  Dim iInd As Integer

  bDefault = Font[sFont].Name = Application.Font.Name

  If bDefault Then
    aFont = Split(sFont, ",")
    For iInd = 0 To aFont.Max
      If aFont[iInd] = Application.Font.Name Then 
        aFont[iInd] = ""
      Endif
    Next
    sFont = aFont.Join(",")
    While Left$(sFont) = ","
      sFont = Mid$(sFont, 2)
    Wend
    While Right$(sFont) = ","
      sFont = Left$(sFont, -1)
    Wend
  Endif

  Return sFont

End



Public Sub btnProperty_Click()

  Dim sDir As String
  Dim sVal As String
  Dim sSrc As String
  Dim sDest As String
  Dim sFont As String
  Dim sColor As String

  $bIgnoreHide = True

  Select $sType

    Case "s"
      If FText.Run($sOldVal) Then Goto _EXIT
      $hEditor.Text = FText.Value

    Case "String[]"
      If FList.Run(Split($sOldVal, "\n")) Then Goto _EXIT
      $hEditor.Text = FList.Value.Join("\n")

    Case "Font"

      'IF $sOldVal THEN
        sFont = $sOldVal
      'ELSE
      '  sFont = $hForm.Font.ToString()
      'ENDIF

      sFont = FFontChooser.Run(sFont, $sOption = "Fixed")
      If Not sFont Then Goto _EXIT

      $hEditor.Text = GetFont(sFont)

    Case "Color"
      If $sOldVal Then
        sColor = $sOldVal
      Else
        sColor = ""
      Endif

      If FColorChooser.Run(sColor) Then Goto _EXIT
      $hEditor.Text = FColorChooser.Color

    Case "Picture"

      sDest = FSelectIcon.Run($sOldVal) 'GetPicture()
      If Not sDest Then Goto _EXIT
      $hEditor.Text = sDest

    Case "Path"
      Dialog.Path = $sOldVal
      If Dialog.OpenFile() Then Goto _EXIT
      $hEditor.Text = Dialog.Path
      
    Case "Table"
      FTableChooser.Table = $sOldVal
      If FTableChooser.Run() Then Goto _exit
      $hEditor.Text = FTableChooser.Table

    Case "Field[]"
      If FList.Run(Split($sOldVal, "\n"), ("Edit field list")) Then Goto _EXIT
      $hEditor.Text = FList.Value.Join("\n")

  End Select

  SaveProperty

_EXIT:

  $bIgnoreHide = False

End


Public Sub grdProperty_DblClick()

  If cmbProperty.Visible Then
    If cmbProperty.ReadOnly Then
      cmbProperty.Index = (cmbProperty.Index + 1) Mod cmbProperty.Count
    Endif
  Else If btnProperty.Visible Then
    btnProperty_Click
  Endif

End


Private Function GetProperty(sProp As String) As String

  Dim sVal As String
  Dim hCtrl As CControl
  Dim bFirst As Boolean

  If Not $bMany Then Return $hObject.GetPropertyString(sProp)

  For Each hCtrl In $hForm.Selection

    If bFirst Then
      If sVal <> hCtrl.GetPropertyString(sProp) Then
        CControl.WasDefaultValue = False
        Return ""
      Endif
    Else
      sVal = hCtrl.GetPropertyString(sProp)
      bFirst = True
    Endif

  Next

  CControl.WasDefaultValue = True
  Return sVal

End


' PUBLIC SUB grdProperty_MouseDown(X AS Integer, Y AS Integer, Button AS Integer)
'
'   grdProperty.Raise
'
' END
'
'
Public Sub grdProperty_MouseUp()

  'DEBUG grdProperty[grdProperty.Row, 0].Text
  btnLock.Value = False
  ShowProperty

End


Public Sub grdProperty_Scroll()

  MoveProperty

End

Public Sub txtProperty_LostFocus()

  If SaveProperty() Then Return
  HideProperty

End

Public Sub ReadConfig()

  $bSort = Settings["/SortProperties", True]
  grdProperty.Font.Grade = - Settings["/GlobalFont", 0]
  txtProperty.Font = grdProperty.Font
  cmbProperty.Font = grdProperty.Font
  spnProperty.Font = grdProperty.Font
  panProperty.Font = grdProperty.Font
  MoveProperty
  RefreshAll

End

Public Sub CheckIntegratedHelpSetting()
  
  $bDisableIntegratedHelp = Not Settings["/PropertyHelp", True]
  If $bDisableIntegratedHelp Then 
    lblHelp.Text = ""
    spnHelp.Hide
  Else 
    spnHelp.Show
  Endif 
  
End

' PUBLIC SUB grdProperty_MouseMove()
' 
'   IF grdProperty.Find(Mouse.X, Mouse.Y) THEN RETURN
'   
'   DEBUG grdProperty.Item.Row;; grdProperty.Item.Column
' 
' END

Public Sub spnProperty_Change()

  SaveProperty  

End


Public Sub RefreshReadOnly()
  
  HideProperty
  
End

Private Sub UpdateLabel()

  Dim hObject As CControl = $hForm.Master
  
  If Not hObject Then hObject = $hForm.Control[$hForm.Name]
  lblName.Text = "<b>" & hObject.Name & "</b> " & hObject.Kind
  
  If Not $bDisableIntegratedHelp Then 
    lblHelp.Text = ""
  Endif 

End

Public Sub spnHelp_Resize()

  HideProperty

End

Public Sub Form_KeyPress()

  If Key.Code = Key.Escape Then
    HideProperty
  Endif  

End

Public Sub panProperty_Arrange()

  lblName.Move(4, (panProperty.H - lblName.H) \ 2, panProperty.ClientW - btnLock.W, panProperty.ClientH)
  btnLock.Move(panProperty.W - btnLock.W, (panProperty.H - btnLock.H) \ 2)

End

Public Sub btnLock_Click()

  Dim iRow As Integer

  If btnLock.Value Then 
    $sFocus = $sLast
    iRow = FindProperty($sFocus)
    If iRow >= 0 Then 
      grdProperty[iRow, 0].Picture = Picture["icon:/small/lock"]
    Else
      btnLock.Value = False
    Endif
  Else
    iRow = FindProperty($sFocus)
    If iRow >= 0 Then grdProperty[iRow, 0].Picture = Null
    $sFocus = ""
  Endif

End

Public Sub cmbProperty_KeyPress()

  If Key.Code = Key.Delete Or Key.Code = Key.Backspace Then

    cmbProperty.Text = ""
    SaveProperty

  Endif

End
