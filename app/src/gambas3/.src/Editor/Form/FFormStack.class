' Gambas class file

Static Private $hForm As FForm 'FForm
Static Private $cPict As New Collection

Private $bNoSelect As Integer

Private Sub GetForm() As Boolean
  
  $hForm = Null
  Try $hForm = Project.ActiveForm
  If Not $hForm Then 
    HideAll
    Return True
  Endif
  
End


Public Sub Form_Open()
  
  ReadConfig
  
End


Public Sub Form_Show()

  RefreshAll

End


Public Sub Form_Hide()

  $hForm = Null

End

Static Public Sub GetControlIcon(sKind As String) As Picture
  
  Dim sImg As String
  Dim hPict As Picture
  
  If sKind <> "Form" Then
    sImg = ".control" &/ LCase(sKind)
  Else
    sImg = "img/32/form.png"
  Endif

  hPict = $cPict[sImg]
  If Not hPict Then
    hPict = Picture[sImg]
    If Not hPict Then hPict = Picture["img/control/unknown.png"]
    hPict = hPict.Image.Stretch(16, 16).Picture  
    $cPict[sImg] = hPict
  Endif
  
  Return hPict
  
End

Private Sub FillTree(hCtrl As CControl, Optional sParent As String)

  Dim hChild As Control
  Dim hTab As Object
  Dim iTab As Integer
  Dim sKey As String
  Dim hPict As Picture

  If Not hCtrl Then Return

  If sParent Then
    'sImg = "img/control/" & LCase(hCtrl.Kind) & ".png"
    'sImg = ".control" &/ LCase(hCtrl.Kind)
    hPict = GetControlIcon(hCtrl.Kind)
  Else
    hPict = Picture["img/16/form.png"]
    'sImg = "img/32/form.png"
  Endif

  Inc $bNoSelect 
  Try tvwControl[hCtrl.Name].Delete
  Dec $bNoSelect
  tvwControl.Add(hCtrl.Name, hCtrl.Name, hPict, sParent)

  If Not hCtrl.IsContainer() Then Return

  'IF hCtrl.Kind = "TabStrip" THEN
  If hCtrl.IsMultiContainer() Then

    hTab = hCtrl.Control

    For iTab = 0 To hTab.Count - 1
      sKey = hCtrl.Name & "." & iTab
      tvwControl.Add(sKey, hTab[iTab].Text, GetControlIcon("TabStrip"), hCtrl.Name)
      For Each hChild In hTab[iTab].Children
        If Not hChild.Tag Then Continue
        FillTree($hForm.Control[hChild.Tag], sKey)
      Next
      tvwControl[sKey].Expanded = True
    Next

  Else

    For Each hChild In hCtrl.Control.Children
      If Not hChild.Tag Then Continue
      FillTree($hForm.Control[hChild.Tag], hCtrl.Name)
    Next

  Endif

  tvwControl[hCtrl.Name].Expanded = True

End

Public Sub RefreshReadOnly()
  
  If GetForm() Then Return

  panControl.Visible = Not $hForm.ReadOnly  
  
End

Public Sub RefreshAll(Optional sKey As String, Optional bForce As Boolean)

  If Not Me.Visible Then Return

  If GetForm() Then Return
  
  'IF NOT Project.IsForm($hForm) THEN RETURN

  ' If Not sKey Then
  '   Try sKey = $hForm.Master.Name
  ' Endif 
  
  If bForce Then $cPict.Clear

  tvwControl.Clear
  FillTree($hForm.Control[$hForm.Name])
  tvwControl.Show
  sepControl.Show
  lblMessage.Hide
  RefreshReadOnly
  SelectCurrent
  
  ' If sKey Then
  '   Object.Lock(tvwControl)
  '   Try tvwControl[sKey].Selected = True
  '   Object.Unlock(tvwControl)
  '   Try tvwControl[sKey].EnsureVisible
  ' Endif
  ' 
  ' $bNoSelect = False
  
End

Public Sub RefreshOne(sKey As String)
  
  If Not Me.Visible Then Return
  If GetForm() Then Return
  
  FillTree($hForm.Control[sKey])
  SelectCurrent
  'TRY tvwControl[sKey].Selected = TRUE
  'TRY tvwControl[sKey].EnsureVisible

End


Public Sub HideAll()

  tvwControl.Hide
  panControl.Hide
  sepControl.Hide
  lblMessage.Show

End


Public Sub btnDown_Click()

  Dim sKey As String

  sKey = tvwControl.Key
  Try $hForm.Control[tvwControl.Current.Text].MoveDown
  If Error Then Return
  RefreshAll(sKey)

End

Public Sub btnBottom_Click()

  Dim sKey As String

  sKey = tvwControl.Key
  Try $hForm.Control[tvwControl.Current.Text].Raise
  If Error Then Return
  RefreshAll(sKey)

End

Public Sub btnTop_Click()

  Dim sKey As String

  sKey = tvwControl.Key
  Try $hForm.Control[tvwControl.Current.Text].Lower
  If Error Then Return
  RefreshAll(sKey)

End

Public Sub btnUp_Click()

  Dim sKey As String

  sKey = tvwControl.Key
  Try $hForm.Control[tvwControl.Current.Text].MoveUp
  If Error Then Return
  RefreshAll(sKey)

End

Public Sub SelectCurrent()
  
  Dim sName As String
  
  If Not $hForm Then Return
  Try sName = $hForm.Master.Name
  If Not sName Then sName = $hForm.Name
  
  If tvwControl.Exist(sName) Then 
    Try tvwControl[sName].EnsureVisible
    Inc $bNoSelect 
    tvwControl[sName].Selected = True
    Dec $bNoSelect 
  Endif

End


Public Sub tvwControl_Select()
  
  If $bNoSelect Then Return

  If InStr(tvwControl.Current.Key, ".") Then 
    tvwControl.MoveParent
    Try $hForm.SelectControl(tvwControl.Item.Text)
  Else
    Try $hForm.SelectControl(tvwControl.Current.Text)
  Endif

End

Public Sub tvwControl_Collapse()

  tvwControl.Item.Expanded = True  

End


Public Sub ReadConfig()

  tvwControl.Font.Grade = - Settings["/GlobalFont", 0]
  MTheme.InitControl(tvwControl)
  RefreshAll

End

Public Sub tvwControl_Activate()

  FMain.ActivatePropertyTab

End
