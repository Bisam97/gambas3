' Gambas class file

Private $hEditor As Editor
Private $bUseHelp As Boolean
Private $bShowHelp As Boolean

Private $DX As Integer
Private $DY As Integer
Private $iMinW As Integer
Private $iMinH As Integer
Private $iMaxY As Integer
Private $sLastUrl As String
Private $hSymbol As CSymbolInfo
Private $X As Integer

Private Sub UpdateSignature(Optional hForm As FEditor)
  
  Dim iX, iY As Integer
  Dim hCont As Container
  Dim sIcon As String

  If $bShowHelp And $bUseHelp Then
    MHelp.InitWebViewWith(webHelp, MHelp.TYPE_SYMBOL, $hSymbol.Name, $hSymbol.Class, "noimage")
    sepHelp.Show
    panHelp.Show
    panRight.Show
    $iMinW = Max(128, lblNewSignature.W + 2 + panRight.W + btnHelp.W)
    $iMinH = Desktop.Scale * 24
    Me.Resize(Max(Me.W, $iMinW), Max(Me.H, $iMinH))
  Else
    sepHelp.Hide
    panHelp.Hide
    panRight.Hide
    If hForm Then
      Me.Resize(lblNewSignature.W + 2 + If($bUseHelp, btnHelp.W + panRight.W, 0), lblSignature.H + 2)
    Else
      Me.H = lblSignature.H + 2
    Endif
  Endif
  
  iY = $hEditor.Y + $hEditor.CursorY + $hEditor.LineHeight
  
  If (iY + Me.Height) > ($hEditor.Y + $hEditor.H) Then
    iY = $hEditor.Y + $hEditor.CursorY - Me.Height
    If iY < 0 Then
      Me.Height += iY
      iY = 0
    Endif
    panResizeBottom.Hide
    panResizeTop.Show
    sepHelp.Raise
    panSignature.Raise
    sIcon = If($bShowHelp, "down", "up")
  Else
    panResizeBottom.Show
    panResizeTop.Hide
    sepHelp.Raise
    panHelp.Raise
    sIcon = If($bShowHelp, "up", "down")
  Endif

  If $bUseHelp Then btnHelp.Picture = Picture["icon:/small/" & sIcon]

  hCont = $hEditor.Parent
  iX = $X
  Do
    If hCont Is Form Then Break
    iX += hCont.X
    iY += hCont.Y
    hCont = hCont.Parent
  Loop
  
  iX = Max(0, Min(iX, $hEditor.W - Me.Width))
  
  If Not hForm Then 
    Me.Move(iX, iY)
    Return
  Endif
  
  If Me.Parent = hForm And If iX = Me.X And iY = Me.Y And Me.Visible Then Return

  Me.Reparent(hForm, iX, iY)
  Me.Show
  Me.Raise
  $hEditor.SetFocus
  
End


Public Sub Open(hForm As FEditor, iX As Integer, hSymbol As CSymbolInfo, iArg As Integer, Optional bDoNotUseKey As Boolean)
  
  Dim sSign As String
  Dim sName As String

  If hForm.Editor <> $hEditor Then HideFrom($hEditor)
  $hEditor = hForm.Editor

  sSign = hSymbol.GetSignature(True, iArg)
  If Not sSign Then Return
  
  lblSignature.Font = $hEditor.Font
  lblNewSignature.Font = $hEditor.Font
  $bShowHelp = False 
  $bUseHelp = Settings["/ShowHelpInPopup", 1]
  btnHelp.Visible = $bUseHelp
  
  sName = hSymbol.Name
  If Left$(sName) = "_" Then sName = hSymbol.Class
  sSign = "<b>" & sName & "</b>" & sSign 
  
  lblNewSignature.W = Max(64, $hEditor.W - 64)
  lblNewSignature.Text = sSign
  lblNewSignature.Adjust
  'lblSignature.AutoResize = FALSE
  'lblSignature.W = lblNewSignature.W
  'DEBUG lblSignature.W;; hEditor.W
  
  $hSymbol = hSymbol
  $X = iX
  
  UpdateSignature(hForm)
  
  ' If $bShowHelp Then
  '   MHelp.InitWebViewWith(webHelp, MHelp.TYPE_SYMBOL, hSymbol.Name, hSymbol.Class, "noimage")
  '   sepHelp.Show
  '   webHelp.Show
  '   panRight.Show
  '   $iMinW = Max(128, lblNewSignature.W + 2 + panRight.W + btnHelp.W)
  '   $iMinH = Desktop.Scale * 24
  '   Me.Resize(Max(Me.W, $iMinW), Max(Me.H, $iMinH))
  ' Else
  '   sepHelp.Hide
  '   webHelp.Hide
  '   panRight.Hide
  '   Me.Resize(lblNewSignature.W + 2 + btnHelp.W, lblSignature.H + 2)
  ' Endif

  lblSignature.Text = sSign
  'lblSignature.Adjust
  
  
End


Public Function ManageKey() As Boolean
  
  Dim iInd As Integer
  
  Select Case Key.Code
  
    Case Key.Esc
      Me.Hide
      Return True
    
    'CASE Key.Backspace
      'ME.Hide
      
  End Select
  
End


Public Sub lblSignature_MouseDown()

  Me.Hide

End

Public Sub panResizeBottom_MouseDown()

  $DX = Me.W - Mouse.ScreenX
  $DY = Me.H - Mouse.ScreenY

End

Public Sub panResizeBottom_MouseMove()

  Dim W, H As Integer
  
  W = Max($iMinW, $DX + Mouse.ScreenX)
  H = Max($iMinH, $DY + Mouse.ScreenY)
  
  Me.Resize(W, H)

End

Public Sub panResizeTop_MouseDown()

  $DX = Me.W - Mouse.ScreenX
  $DY = Me.Y - Mouse.ScreenY
  $iMaxY = Me.Y + Me.H - $iMinH

End

Public Sub panResizeTop_MouseMove()

  Dim W, Y As Integer
  
  W = Max($iMinW, $DX + Mouse.ScreenX)
  Y = Min($iMaxY, $DY + Mouse.ScreenY)
  
  Me.Move(Me.X, Y, W, Me.H + Me.Y - Y)

End

Public Sub Form_Close()

  $hEditor = Null

End

Public Sub HideFrom(hEditor As Editor)
  
  If $hEditor <> hEditor Then Return
  $hEditor = Null
  Me.Hide
  
End

Public Sub VisibleFrom(hEditor As Editor) As Boolean

  Return $hEditor = hEditor And Me.Visible
  
End

Public Sub webHelp_GotFocus()

  $hEditor.SetFocus

End

Public Sub webHelp_Link(Url As String)

  $sLastUrl = Url

End

Public Sub webHelp_Error()

  If $sLastUrl Begins "gambas://" Then 
    webHelp.Stop
    MHelp.ManageSpecialLink($sLastUrl)
  Endif

End

Public Sub btnHelp_Click()

  $bShowHelp = Not $bShowHelp
  UpdateSignature

End
