' Gambas class file

Public Path As String
Public FormType As String

Public Master As CControl
Public Selection As New Collection
Public Control As New Collection(gb.Text)
Public AllMenus As New CControl[]

Public Container As Container

Property Read ReadOnly As Boolean

Private $bDoNotModify As Boolean
Private $bModify As Boolean
Private $bSelChange As Boolean
Private $bReadOnly As Boolean
Private $bActivate As Boolean
Private $bLocked As Boolean
Private $bScaled As Boolean
'PRIVATE $sType AS String

' Gestion de la souris

Private $iMode As Integer

Private Const MODE_NOTHING As Integer = 0
Private Const MODE_CREATE As Integer = 1
Private Const MODE_MOVE As Integer = 2
Private Const MODE_SELECT As Integer = 3

Private $sTool As String
Private $hCurrent As CControl
Private $X As Integer
Private $Y As Integer
Private $MX As Integer
Private $MY As Integer
Private $W As Integer
Private $H As Integer
Private $bMove As Boolean

Private $XS As Integer
Private $YS As Integer
Private $WS As Integer
Private $HS As Integer

Private Const MIN_WIDTH As Integer = 4
Private Const MIN_HEIGHT As Integer = 4

' Gestion de la sauvegarde

Private $sSave As String
Private $iSaveX As Integer
Private $iSaveY As Integer
'PRIVATE $iIndent AS INTEGER
Private $iSaveLevel As Integer
Private $bSelectNew As Boolean
Private $bDoNotArrange As Boolean
Private $cAction As Collection
Private $cToolbar As Collection

Private $hContainer As CControl
Private $iContX As Integer
Private $iContY As Integer

'PRIVATE CONST FORM_NAME AS String = "$"

'PRIVATE CONST COORD_CONTROL AS Integer = 0
Private Const COORD_CONTROL_TO_INSIDE As Integer = 1
Private Const COORD_INSIDE_TO_CONTROL As Integer = 2

Private Const MOVE_FIRST As Integer = 0
Private Const MOVE_LAST As Integer = 1
Private Const MOVE_NEXT As Integer = 2
Private Const MOVE_PREVIOUS As Integer = 3

Private $bAfterLock As Boolean

Private $bConvert As Boolean ' Convert from 2.0 to 3.0
Private $bNoWarning As Boolean ' Do not warn if a property does not exist in FromString()

Private $bBorder As Boolean

Public Sub _new(sPath As String)

  btnCloseWindow.Design = True
  btnMaxWindow.Design = True

  Path = sPath
  Me.Name = File.BaseName(sPath)

  FormType = "Form" 'File.Ext(sPath)
  
  Container = panBorder

End


Public Sub Reload()

  Dim sData As String
  Dim hCtrl As CControl

  sData = File.Load(Path)

  If Left$(sData, Len(Project.FORM_MAGIC_1)) = Project.FORM_MAGIC_1 Then
    $bConvert = True
  Else If Left$(sData, Len(Project.FORM_MAGIC_2)) = Project.FORM_MAGIC_2 Then
    $bConvert = True
  Else If Left$(sData, Len(Project.FORM_MAGIC)) <> Project.FORM_MAGIC Then
    FGambas.Warning(("Bad form file"))
    Return
  Endif

  UnSelectAll
  RefreshProperty
  
  If Control.Exist(Me.Name) Then 
    Control[Me.Name].Delete
    AllMenus.Clear
  Endif
  
  $bLocked = False
  
  $bDoNotModify = True
  $bAfterLock = False

  sData = Mid$(sData, Len(Project.FORM_MAGIC) + 1)
  FromString(sData)

  $bConvert = False
  $bModify = False

  UpdateSnap

  'SetLock($bAfterLock)
  If $bAfterLock Then Stop
  Action[".lock", Me].Value = $bAfterLock

  DrawTitle
  
  $bDoNotModify = False

End


Public Sub UpdateSnap()
  
  Dim hPict As Picture
  Dim hCtrl As CControl = Control[Me.Name]
  Dim iColor As Integer

  If Action[".grid", Me].Value Then

    hPict = New Picture(Project.Snap, Project.Snap)
    iColor = hCtrl.Control.Background
    If iColor = Color.Default Then iColor = Color.Background
    hPict.Fill(iColor)
    Draw.Begin(hPict)
    If (Color[iColor].Value > 128) Then
      iColor = Color.Black
    Else
      iColor = Color.White
    Endif
    Draw.ForeColor = iColor
    Draw.Point(0, 0)
    Draw.End
    hCtrl.Control.Picture = hPict
    
  Else
  
    hCtrl.Control.Picture = Null
    
  Endif
  
End


Private Sub FromString(sData As String, Optional hParent As CControl)

  Dim hCtrl As CControl
  Dim sName As String
  Dim sClass As String
  Dim sOldClass As String
  Dim iPos, iPos2, iPos3 As Integer
  Dim sLine As String
  Dim hData As CControl
  Dim sProperty As String
  Dim sValue As String
  Dim vValue As Variant
  Dim aVal As String[]
  Dim iInd As Integer
  Dim iLevel As Integer
  Dim bFirst As Boolean
  Dim sEventName As String
  Dim bBackport As Boolean
  Dim cCoord As String[]
  Dim eW, eH As Float

  hCtrl = hParent
  bFirst = True

  For Each sLine In Split(sData, "\n")

    sLine = Trim(sLine)

    'iPos = InStr(sData, gb.newLine)
    'If iPos = 0 Then
    '  sLine = Trim(sData)
    '  sData = ""
    'Else
    '  sLine = Trim(Left$(sData, iPos - 1))
    '  sData = Mid$(sData, iPos + 1)
    'Endif

    'PRINT "> "; sLine

    If Len(sLine) = 0 Then Continue
    If Left$(sLine, 1) = "#" Then 
      sLine = Mid$(sLine, 2)
      If sLine = "LOCKED" Then 
        $bAfterLock = True
        Continue
      Endif
    Endif

    If Left$(sLine, 1) = "{" Then

      sLine = Trim(Mid$(sLine, 2))
      iPos = InStr(sLine, " ")
      sName = Left$(sLine, iPos - 1)
      sClass = Trim(Mid$(sLine, iPos + 1))
      iPos = InStr(sClass, " ")
      If iPos Then
        sEventName = Trim(Mid$(sClass, iPos + 1))
        sClass = Trim(Left$(sClass, iPos - 1))
      Else
        sEventName = ""
      Endif

      If Left$(sClass) = "#" Then sClass = Mid$(sClass, 2)

      If IsNull(hCtrl) Then sName = Me.Name

      ' If sClass = "Image" Then
      '   sOldClass = sClass
      '   sClass = "PictureBox"
      ' Else If sClass = "TextView" Then 
      '   sOldClass = sClass
      '   sClass = "TextEdit"
      ' Else If sClass = "DateBox" Or sClass = "TimeBox" Then 
      '   sOldClass = sClass
      '   sClass = "ValueBox"
      ' Else
        sOldClass = "" 
      ' Endif

      hCtrl = CreateControl(sClass, hCtrl, sName)
      
      If sEventName Then hCtrl.SetProperty(CPropertyInfo.EVENT_NAME, sEventName)
      
      If sOldClass Then
        Print sOldClass; " -> "; sClass 
        If sOldClass = "TextView" Then 
          hCtrl.SetProperty("ReadOnly", True)
        Else If sOldClass = "DateBox" Then 
          hCtrl.SetProperty("Type", ValueBox.Date)
        Else If sOldClass = "TimeBox" Then           
          hCtrl.SetProperty("Type", ValueBox.Time)
        Endif
      Endif 
      
      Inc iLevel

    Else If Left$(sLine, 1) = "}" Then

      'IF hCtrl = hParent THEN RETURN
      Dec iLevel

      If iLevel = 0 Then
        If $bSelectNew Then
          hCtrl.Select(Me, bFirst)
          bFirst = False
        Endif
      Endif

      If hCtrl.IsMultiContainer() Then UpdateMultiContainer(hCtrl)
      
      hCtrl = hCtrl.Parent
      
      If iLevel = 0 And If Not hParent Then Return

    Else

      iPos = InStr(sLine, "=")

      If iPos Then

        sProperty = Trim(Left$(sLine, iPos - 1))
        sValue = Trim(Mid$(sLine, iPos + 1))

        vValue = Val(sValue)
        If IsNull(vValue) Then

          If Left$(sValue, 2) = "(" & Chr$(34) Then
            sValue = Mid$(sValue, 2, -1)
          Endif

          If Left$(sValue, 1) = Chr$(34) Then
            If sProperty = "Name" Then Continue
            vValue = Unquote(Mid$(sValue, 2, -1))
            If sClass = "ComboBox" Or sClass = "ListBox" Then 
              If sProperty = "List" Then 
                vValue = Split(vValue, "\n")
              Endif
            Endif
          Else If Left$(sValue) = "[" Then
            aVal = New String[]
            iPos = 0
            Do
              iPos = InStr(sValue, Chr$(34), iPos + 1)
              If iPos = 0 Then Break
              iPos2 = iPos
              Do
                iPos3 = InStr(sValue, "\\", iPos2 + 1)
                iPos2 = InStr(sValue, Chr$(34), iPos2 + 1)
                If iPos2 = 0 Then 
                  iPos2 = Len(sValue) + 1
                  Break 
                Endif
                If iPos3 = 0 Or If iPos3 > iPos2 Then Break
                iPos2 = iPos3 + 1
              Loop
              aVal.Add(Unquote(Mid$(sValue, iPos + 1, iPos2 - iPos - 1)))
              iPos = iPos2 + 1
            Loop
            vValue = aVal '.Join("\n")
          Else If Left$(sValue, 5) = "Font[" Then
            vValue = Mid$(sValue, 7, -2)
          Else If Left$(sValue, 8) = "Picture[" Then
            vValue = Mid$(sValue, 10, -2)
            'PRINT File.Dir(Project.Path) &/ Mid$(sValue, 9, -1)
            'vValue = Picture[File.Dir(Project.Path) &/ Mid$(sValue, 9, -1)]
          Else If UCase(sValue) = "TRUE" Then
            vValue = True
          Else If UCase(sValue) = "FALSE" Then
            vValue = False
          Else If Left$(sValue, 6) = "CDate(" Then
            vValue = CDate(Mid$(sValue, 8, -2))
          Else

            iPos = InStr(sValue, ".")
            If iPos Then
              vValue = Mid$(sValue, iPos + 1)
            Else
              Print "Bad property value "; sValue
              ' This is an object !
            Endif
            
            If sClass = "Form" Then
            
              If sProperty = "Border" Then
            
                Select Case vValue
                  Case "None"
                    vValue = False
                  Case "Resizable"
                    vValue = True
                    hCtrl.SetProperty("Resizable", True)
                  Case "Fixed"
                    vValue = True
                    hCtrl.SetProperty("Resizable", False)
                End Select
                
              Else If sProperty = "Type" Then
              
                If vValue = "Toolbar" Then vValue = "Utility"
                
              Endif
            
            Endif

          Endif

        Endif

        If hCtrl.SetProperty(sProperty, vValue) Then
          If Not $bNoWarning Then Print "Error: "; hCtrl.Kind; "."; sProperty; " = "; sValue
        Endif

      Else If Left$(sLine, 5) = "Move(" Then

        cCoord = Split(Mid$(sLine, 6, -1))
        'TRY PRINT cCoord[0]; ","; cCoord[1]; ","; cCoord[2]; ","; cCoord[3]
        Try hCtrl.Move(CInt(cCoord[0]) / Project.Snap * Desktop.Scale, CInt(cCoord[1]) / Project.Snap * Desktop.Scale, True)
        'TRY hCtrl.SetProperty("X", Val(cCoord[0]))
        'TRY hCtrl.SetProperty("Y", Val(cCoord[1]))
        If cCoord.Count >= 2 Then
          Try hCtrl.Resize(CInt(cCoord[2]) / Project.Snap * Desktop.Scale, CInt(cCoord[3]) / Project.Snap * Desktop.Scale, True)
        Endif

        If Error Then Print "Error: Syntax error: "; sLine

      Else If Left$(sLine, 11) = "MoveScaled(" Then

        cCoord = Split(Mid$(sLine, 12, -1))
        'TRY PRINT cCoord[0]; ","; cCoord[1]; ","; cCoord[2]; ","; cCoord[3]
        Try hCtrl.Move(CFloat(cCoord[0]) * Desktop.Scale, CFloat(cCoord[1]) * Desktop.Scale, True)
        'TRY hCtrl.SetProperty("X", Val(cCoord[0]))
        'TRY hCtrl.SetProperty("Y", Val(cCoord[1]))
        If cCoord.Count >= 2 Then
          eW = CFloat(cCoord[2])
          eH = CFloat(cCoord[3])
          If $bConvert Then
            If eW <= 3 Then Inc eW
            If eH <= 3 Then Inc eH
          Endif
          Try hCtrl.Resize(eW * Desktop.Scale, eH * Desktop.Scale, True)
        Endif

        If Error Then Print "Error: Syntax error: "; sLine

      Else 
      
        If Left$(sLine) <> "'" Then Print "Error: Syntax error: "; sLine

      Endif

    Endif

  Next

End

' Private Sub UnQuoteShorcut(sText As String, Optional bNoAmp As Boolean) As String
'   
'   If Not bNoAmp Then
'     sText = Replace(sText, "&&", Chr$(1))
'     sText = Replace(sText, "&", "")
'     sText = Replace(sText, Chr$(1), "&")
'   Endif
'   
'   sText = Trim(sText)
'   
'   'If Right(sText, 3) = "..." Then sText = Trim(Left(sText, -3))
'   
'   Return sText
'   
' End


Private Sub SaveAction()
  
  Dim hFile As File
  Dim cAction As Collection
  Dim cToolbar As Collection
  Dim sAction As String
  Dim aList As String[]
  Dim sText As String
  Dim sKey As String

  If $cAction.Count = 0 Then Return
  
  aList = New String[]
  For Each cAction In $cAction
    aList.Add($cAction.Key)
  Next
  aList.Sort

  'Try Mkdir Project.Dir &/ ".action"
  'hFile = Open Project.Dir &/ ".action" &/ LCase(Me.Name) & ".action" For Output Create
  
  $sSave &= "\n" & Project.ACTION_MAGIC & "\n\n{ Actions\n"
  
  For Each sAction In aList
  
    cAction = $cAction[sAction]
    'If Not cAction.Exist("Menu") Then Continue
    sText = cAction["Text"]
    If Not sText Then sText = cAction["ToolTip"]
    
    $sSave &= "  { Action " & sAction & "\n"
    $sSave &= "    Text = " & Quote(sText) & "\n"
    If cAction.Exist("Menu") Then $sSave &= "    Shortcut = " & Quote(cAction["Shortcut"]) & "\n"
    If cAction["Picture"] Then $sSave &= "    Picture = " & Quote(cAction["Picture"]) & "\n"
    $sSave &= "  }\n"
  
  Next
    
  $sSave &= "}\n"
  
  If $cToolbar.Count = 0 Then Return

  aList = New String[]
  For Each cToolbar In $cToolbar
    aList.Add($cToolbar.Key)
  Next
  aList.Sort

  $sSave &= "\n{ Toolbars\n"
  
  For Each sKey In aList
  
    cToolbar = $cToolbar[sKey]
    sText = cToolbar["Text"]
    If Not sText Then sText = cToolbar["ToolTip"]

    $sSave &= "  { Toolbar " & sKey & "\n"
    $sSave &= "    Text = " & Quote(sText) & "\n"
    $sSave &= "    List = " & Quote(cToolbar["List"]) & "\n"
    $sSave &= "    Default = " & Quote(cToolbar["Default"]) & "\n"
    $sSave &= "  }\n"
    
  Next
  
  $sSave &= "}\n"

End

Public Function Save(Optional bForce As Boolean) As Boolean

  If Project.ReadOnly Then Return
  
  If Not bForce Then 
    If Not $bModify Then Return
    If $bReadOnly Then Return
  Endif

  UnselectAll

  Save.Begin(Path)

  ResetSave

  AddLine(Project.FORM_MAGIC)
  If $bLocked Then AddLine("#LOCKED")
  AddLine()

  SaveOne(Control[Me.Name])
  SaveAction

  File.Save(Path, $sSave)

  $sSave = ""
  $bModify = False
  DrawTitle

  Save.End()

Catch

  Return Save.Error()

End


Public Sub AddLine(Optional sLig As String)

  Dim sAdd As String

  'IF Left$(sLig, 1) = "}" THEN $iIndent = $iIndent - 1

  sAdd = Space$($iSaveLevel * 2) & sLig
  'PRINT sAdd
  $sSave = $sSave & sAdd & gb.NewLine

  'IF Left$(sLig, 1) = "{" THEN $iIndent = $iIndent + 1

End


Public Function GetChildren(sName As String) As CControl[]

  Dim aList As New CControl[]
  Dim hCtrl As CControl
  Dim hCChild As CControl
  Dim hChild As Control
  Dim iTab As Integer
  Dim hMenu As Menu
  Dim hTab As Object 'TabStrip

  hCtrl = Control[sName]
  If IsNull(hCtrl) Then Return
  If Not hCtrl.IsContainer() Then Return

  If hCtrl.Kind = "Form" Then

    For Each hCChild In AllMenus
      aList.Add(hCChild)
    Next
    'cList.Insert(Menus)

    For Each hChild In hCtrl.Control.Children
      If Not hChild.Tag Then Continue
      If Control.Exist(hChild.Tag) Then aList.Add(Control[hChild.Tag])
    Next

  'ELSE IF hCtrl.Kind = "TabStrip" THEN
  Else If hCtrl.IsMultiContainer() Then

    hTab = hCtrl.Control

    For iTab = 0 To hTab.Count - 1

      For Each hChild In hTab[iTab].Children
        If Not hChild.Tag Then Continue
        If Control.Exist(hChild.Tag) Then aList.Add(Control[hChild.Tag])
      Next

    Next

  Else If hCtrl.Kind = "Menu" Then

    For Each hMenu In hCtrl.Control.Children
      aList.Add(Control[hMenu.Tag])
    Next

  Else

    For Each hChild In hCtrl.Control.Children
      If Not hChild.Tag Then Continue
      If Control.Exist(hChild.Tag) Then aList.Add(Control[hChild.Tag])
    Next

  Endif

  Return aList

End



Private Sub SaveOne(hCtrl As CControl)

  Dim hChild As Control
  Dim sLine As String
  Dim hMenu As Menu
  Dim hMenuCtrl As CControl
  Dim cProp As String[]

  Dim hTab As Object
  Dim hSubTab As Object
  Dim iTab As Integer
  Dim sVal As String
  Dim vVal As Variant
  Dim iArr As Integer
  Dim sText As String
  Dim sName As String
  Dim sGroup As String
  Dim sAction As String

  'hCtrl = Control[sName]
  'PRINT "SaveOne: hCtrl = "; hCtrl
  If IsNull(hCtrl) Then Return

  sName = hCtrl.Name
  If sName = Me.Name Then sName = FormType

  sGroup = hCtrl.GetProperty(CPropertyInfo.EVENT_NAME)

  If hCtrl.Virtual Then
    AddLine(Trim("{ " & sName & " #" & hCtrl.Kind & " " & sGroup))
  Else
    AddLine(Trim("{ " & sName & " " & hCtrl.Kind & " " & sGroup))
  Endif

  If $iSaveLevel = 0 Then
    cProp = hCtrl.GetEachProperty($iSaveX, $iSaveY, Not $bScaled)
  Else
    cProp = hCtrl.GetEachProperty(0, 0, Not $bScaled)
  Endif
  
  hCtrl.AddAction($cAction)

  Inc $iSaveLevel

  If sGroup Then 
    AddLine("Name = \"" & sName & "\"")
  Endif

  For Each sLine In cProp
    AddLine(sLine)
  Next

  If hCtrl.IsContainer() Then

    If Not $bDoNotArrange Then
    
      Try iArr = CComponent.Classes[hCtrl.Kind].Symbols["_Arrangement"].Value
  
      If Not Error Then
  
        If iArr = Arrange.Fill Then
          iArr = 0
          Try iArr = CComponent.Classes["Arrange"].Symbols[hCtrl.GetProperty("Arrangement")].Value
        Endif
  
        If iArr Then
  
          'IF hCtrl.Kind = "TabStrip" THEN
          If hCtrl.IsMultiContainer() Then
  
            hTab = hCtrl.Control
        
            For iTab = 0 To hTab.Count - 1
              ArrangeContainer(hCtrl.Control, iArr, False, iTab)
            Next
 
          Else
  
            ArrangeContainer(hCtrl.Control, iArr, False)
  
          Endif
  
        Endif
  
      Endif
      
    Endif

    If hCtrl.Kind = "Form" Then

      For Each hMenuCtrl In AllMenus
        SaveOne(hMenuCtrl)
      Next

      For Each hChild In hCtrl.Control.Children
        'PRINT "SaveOne? "; Object.Type(hChild);; hChild.Tag; " "; Control[hChild.Tag]
        SaveOne(Control[hChild.Tag])
      Next

    'ELSE IF hCtrl.Kind = "TabStrip" THEN
    Else If hCtrl.IsMultiContainer() Then

      hTab = hCtrl.Control

      For iTab = 0 To hTab.Count - 1

        AddLine("Index = " & CStr(iTab))
        sVal = Replace(hTab[iTab].Text, "\\", "\\\\")
        sVal = Replace(sVal, Chr$(34), "\\" & Chr$(34))
        sVal = Replace(sVal, gb.NewLine, "\\n")
        AddLine("Text = (" & Chr$(34) & sVal & Chr$(34) & ")")

        If hCtrl.Tag Then
          sVal = hCtrl.Tag[iTab]
          If sVal Then
            AddLine("Picture = Picture[" & Chr$(34) & sVal & Chr$(34) & "]")
          Endif
        Endif

        For Each hChild In hTab[iTab].Children
          SaveOne(Control[hChild.Tag])
        Next

      Next

      'AddLine("Index = " & CStr(hTab.Index))
      AddLine("Index = 0")

    Else If hCtrl.Kind = "Menu" Then

      For Each hMenu In hCtrl.Control.Children
        SaveOne(Control[hMenu.Tag])
      Next

    Else

      For Each hChild In hCtrl.Control.Children
        SaveOne(Control[hChild.Tag])
      Next
      
      If hCtrl.Kind = "ToolBar" Then
        hCtrl.AddToolbar($cToolbar, Control)
      Endif

    Endif

  Endif

  Dec $iSaveLevel

  AddLine("}")

End



Public Sub Control_Resize()

  Dim hCtrl As Control = Last

  With Control[Me.Name]
    If hCtrl <> .Control Then Return
    If hCtrl.Width <> .GetProperty("Width") Then .SetProperty("Width", hCtrl.Width)
    If hCtrl.Height <> .GetProperty("Height") Then .SetProperty("Height", hCtrl.Height)
  End With

End


'PUBLIC SUB Form_KeyPress(Ascii AS String, Code AS Integer, State AS Integer)
'
'  Project.Shortcut(Code, Ascii, State)
'
'END


Private $bInFormMove As Boolean

' PUBLIC SUB Form_Move()
' 
'   DIM X, Y AS Integer
' 
'   IF NOT LAST.Visible THEN RETURN
'   IF $bInFormMove THEN RETURN
' 
'   $bInFormMove = TRUE
' 
'   'DEBUG ME.Name;; ME.X;; ME.Y
' 
'   WITH Control[Name]
' 
'     X = .GetProperty("X") 
'     Y = .GetProperty("Y") 
'     
'     'DEBUG X;; Y;; "->";; ME.X;; ME.Y;; X <> ME.X;; Y <> ME.Y
'     
'     IF X <> ME.X THEN .SetProperty("X", ME.X, TRUE)
'     IF Y <> ME.Y THEN .SetProperty("Y", ME.Y, TRUE)
' 
'   END WITH
' 
'   $bInFormMove = FALSE
' 
' END


Public Sub Control_MouseDown()

  'PRINT "> Control_MouseDown"

  Dim X As Integer
  Dim Y As Integer

  Me.SetFocus

  X = Mouse.X
  Y = Mouse.Y

  $hCurrent = Control[Last.Tag]
  $sTool = FToolBox.GetTool()

  $X = Last.X
  $Y = Last.Y
  $MX = Last.ScreenX + X
  $MY = Last.ScreenY + Y

  'IF $hCurrent.Kind = "GridView" THEN
  '  PRINT "MouseDown: $X ="; $X; " $Y ="; $Y; " $MX ="; $MX; " $MY ="; $MY
  '  PRINT "X ="; X; " Y ="; Y
  'ENDIF

  If $sTool = "" Then

    If Mouse.Control Or $hCurrent.Name = Me.Name Then

      $XS = X
      $YS = Y

      $iMode = MODE_SELECT

      $W = 0
      $H = 0

      Goto FIN

    Else

      If Master <> $hCurrent Then

        If Not $hCurrent.Selected Then
          UnSelectAll
        Endif

        SelectCurrent(True)

      Endif

      If $bReadOnly Then Return

      $iMode = MODE_MOVE

    Endif

  Else

    If $bReadOnly Then Return

    FindContainer($hCurrent, X, Y, COORD_CONTROL_TO_INSIDE)
    $hCurrent = $hContainer
    X = $iContX
    Y = $iContY

    ' IF NOT $hCurrent.IsContainer() THEN
    '   X = X + $hCurrent.Control.X + $hCurrent.Control.Parent.ClientX
    '   Y = Y + $hCurrent.Control.Y + $hCurrent.Control.Parent.ClientY
    '   $hCurrent = $hCurrent.Parent
    '   IF $hCurrent.Kind = "ScrollView" THEN
    '     X = X - $hCurrent.Control.ScrollX
    '     Y = Y - $hCurrent.Control.ScrollY
    '   ENDIF
    ' ENDIF

    $iMode = MODE_CREATE

    $X = X
    $Y = Y

    '$hCurrent = CreateControl(, $sTool, $hCurrent)
  Endif

  RefreshProperty

FIN:
  'PRINT "< Control_MouseDown"

End

Public Sub Control_MouseMove()

  Dim X As Integer
  Dim Y As Integer
  Dim iDepX As Integer
  Dim iDepY As Integer
  Dim hCtrl As CControl
  Dim W As Integer
  Dim H As Integer
  Dim bMoveX As Boolean
  Dim bMoveY As Boolean
  Dim hParent As CControl

  If Not Mouse.Left Then Return

  'PRINT "Control_MouseMove  Mode ="; $iMode

  If Mouse.Shift Then CControl.SetGrid(False)

  X = Mouse.X
  Y = Mouse.Y

  If $iMode = MODE_CREATE Then

    If Last.Mouse <> Mouse.Cross Then

      Last.Mouse = Mouse.Cross

      hParent = $hCurrent
      $hCurrent = CreateControl($sTool, hParent)
      FFormStack.RefreshAll

      FindContainer(hParent, $X, $Y, COORD_INSIDE_TO_CONTROL)
      $X = $iContX
      $Y = $iContY

      ' $X = $X - hParent.Control.ClientX
      ' $Y = $Y - hParent.Control.ClientY
      ' 
      ' IF hParent.Kind = "ScrollView" THEN
      '   $X = $X + hParent.Control.ScrollX
      '   $Y = $Y + hParent.Control.ScrollY
      ' ENDIF

      $hCurrent.Move($X, $Y)
      $hCurrent.Resize(MIN_WIDTH, MIN_HEIGHT)
      $hCurrent.Control.Mouse = Mouse.Cross

    Endif

    W = Mouse.ScreenX - $MX
    If (W < 0) Then
      W = Abs(W)
      X = $X - W
      bMoveX = True
    Else
      X = $X
    Endif

    H = Mouse.ScreenY - $MY
    If (H < 0) Then
      H = Abs(H)
      Y = $Y - H
      bMoveY = True
    Else
      Y = $Y
    Endif

    If bMoveX Or bMoveY Then
      $hCurrent.Move(X, Y)
      If bMoveX Then W = W + X - $hCurrent.Control.X
      If bMoveY Then H = H + Y - $hCurrent.Control.Y
    Endif

    $hCurrent.Resize(Max(MIN_WIDTH, W), Max(MIN_HEIGHT, H))

  Else If $iMode = MODE_MOVE Then

    If Last = $hCurrent.Control Then

      Last.Mouse = Mouse.SizeAll

      With $hCurrent

        iDepX = Master.Control.X
        iDepY = Master.Control.Y

        Master.Move($X + Mouse.ScreenX - $MX, $Y + Mouse.ScreenY - $MY)

        iDepX = Master.Control.X - iDepX
        iDepY = Master.Control.Y - iDepY

        If iDepX <> 0 Or iDepY <> 0 Then
          For Each hCtrl In Selection
            If hCtrl <> Master Then
              hCtrl.Move(hCtrl.Control.X + iDepX, hCtrl.Control.Y + iDepY, True)
            Endif
          Next
        Endif

      End With

    Endif

  Else If $iMode = MODE_SELECT Then

    DrawRectSelect(Mouse.ScreenX - $MX, Mouse.ScreenY - $MY)

  Endif

  CControl.SetGrid(True)

End


Public Sub Control_MouseUp()

  Dim hCont As Container
  Dim hCtrl As CControl
  Dim hPrevious As CControl
  Dim sFocus As String

  'PRINT "Control_MouseUp  Mode ="; $iMode

  If $iMode = MODE_CREATE Then

    'LAST.Mouse = Mouse.Arrow

    UnSelectAll

    If Last.Mouse = Mouse.Cross Then
      $hCurrent.Control.Mouse = Mouse.Arrow
      SelectCurrent(True)
      sFocus = "Name"
    Endif

    FToolBox.SetTool()

  Else If $iMode = MODE_SELECT Then

    $WS = $W
    $HS = $H

    DrawRectSelect(0, 0)

    If Abs($WS) > 1 And Abs($HS) > 1 Then

      If Not $hCurrent.IsContainer() Then
        $XS = $XS + $hCurrent.Control.X
        $YS = $YS + $hCurrent.Control.Y
        $hCurrent = $hCurrent.Parent
      Endif

      If Selection.Count Then
        If Master.Parent <> $hCurrent Then
          UnselectAll
        Else If (Mouse.Control) = 0 Then
          UnselectAll
        Endif
      Endif

      SelectIn($hCurrent, $XS, $YS, $WS, $HS)

    Else

      If $hCurrent.Name = Me.Name Then

        UnselectAll

      Else If $hCurrent.Selected Then

        If Selection.Count > 1 And If Master = $hCurrent Then 
          For Each hCtrl In Selection
            If hCtrl = $hCurrent Then Break
            hPrevious = hCtrl
          Next
          If Not hPrevious Then 
            For Each hPrevious In Selection
            Next
          Endif
          hPrevious.Select(Me, True)
        Endif

        UnselectCurrent

      Else

        If Master = Null Then
          SelectCurrent(True)
        Else If $hCurrent.Parent = Master.Parent Then
          SelectCurrent
        Else
          UnSelectAll
          SelectCurrent(True)
        Endif

      Endif

    Endif

  Endif

  Last.Mouse = Mouse.Arrow
  $hCurrent = Null
  $iMode = MODE_NOTHING

  RefreshProperty
  If sFocus Then FProperty.FocusOn(sFocus)

End


Public Sub Control_Menu()

  CreateMenu
  mnuForm.Popup

End


' TabStrip

Private Sub UpdateMultiContainer(hCtrl As CControl)

  With hCtrl
    $bDoNotModify = True
    .SetProperty("Picture", .Tag[hCtrl.Control.Index])
    .SetProperty("Text", hCtrl.Control.Text)
    $bDoNotModify = False
  End With

End

Public Sub Control_Click()

  Dim hCtrl As Control = Last
  Dim hCCtrl As CControl = Control[hCtrl.Tag]
  
  If hCCtrl.IsMultiContainer() Then 
    UpdateMultiContainer(hCCtrl)
  Endif
  
End


' PUBLIC SUB Control_KeyPress()
'   
'   Form_KeyPress
'   
' END


Public Function CreateControl(sClass As String, hParent As CControl, Optional sName As String) As CControl

  Dim hCtrl As CControl

  If Len(sName) = 0 Then sName = GetName(sClass)
  
  If Control.Exist(sName) Then 
    While IsDigit(Right(sName))
      sName = Left(sName, -1)
    Wend
    sName = GetName(sName)
  Endif

  ' If the component is not loaded, then return null
  If Not CComponent.Classes.Exist(sClass) Then
    FGambas.Error("Component missing")
    hCtrl = New CControl(sName, "DrawingArea", hParent, Me)
  Else
    hCtrl = New CControl(sName, sClass, hParent, Me)
  Endif

  Control[sName] = hCtrl

  If sClass = "Menu" Then
    If hParent.Name = Me.Name Then
      AllMenus.Add(hCtrl)
    Endif
  Endif

  ResetClassScan

  Return hCtrl

  'PRINT "< CreateControl "; sName

End

Private Sub ResetClassScan()

  Try Project.Files[File.Dir(Path) &/ Me.Name & ".class"].Scan = Null

End



Public Sub AddControl(sClass As String, Optional hParent As CControl, Optional X As Integer, Optional Y As Integer) As CControl

  Dim W, H As Integer
  Dim hCtrl As CControl

  If $bReadOnly Then Return

  If Not hParent Then
  
    If Master Then
      If Master.IsContainer() Then
        hParent = Master
      Else
        hParent = Master.Parent
      Endif
    Else
      hParent = Control[Me.Name]
    Endif
    
  Endif

  hCtrl = CreateControl(sClass, hParent)

  If X <> 0 Or Y <> 0 Then hCtrl.Move(X, Y)

  With CComponent.Classes[sClass]
    W = .DefaultWidth
    H = .DefaultHeight
    If W = 0 Or H = 0 Then 
      W = 8 * Desktop.Scale
      H = 8 * Desktop.Scale
    Endif
    hCtrl.Resize(W, H)
  End With

  $hCurrent = hCtrl
  UnSelectAll
  SelectCurrent(True)
  RefreshProperty
  FProperty.FocusOn("Name")

  Return hCtrl

End



Private Sub RemoveControl(sName As String)

  'PRINT "> RemoveControl "; sName; " "; Control[sName]
  Control[sName].Control.Delete
  Control.Remove(sName)
  Modify
  'PRINT "< RemoveControl "; sName; " "; Control[sName]

End


Private Function GetName(sClass As String) As String

  Dim iNum As Integer
  Dim sName As String
  Dim hCtrl As CControl

  If Control.Exist(sClass) Then Inc iNum

  Do

    Inc iNum
    sName = sClass & Trim(CStr(iNum))

    If Not Control.Exist(sName) Then Exit

  Loop

  Return sName

End


Public Procedure UnSelectAll()

  Dim hCtrl As CControl

  For Each hCtrl In Selection
    hCtrl.UnSelect(Me, True)
  Next

  Selection.Clear
  Master = Null
  SelectionChange

End

Private Procedure SelectCurrent(Optional bMaster As Boolean)

  $hCurrent.Select(Me, bMaster)
  SelectionChange

End


Private Sub UnSelectCurrent()

  $hCurrent.UnSelect(Me)
  SelectionChange
  'RefreshProperty

End


Private Sub SelectIn(hParent As CControl, X As Integer, Y As Integer, W As Integer, H As Integer)

  Dim hChild As Control
  Dim hCtrl As CControl
  Dim bFirst As Boolean

  If W < 0 Then
    X = X + W
    W = - W
  Endif

  If H < 0 Then
    Y = Y + H
    H = - H
  Endif

  'PRINT hParent.Name; X; Y; W; H

  If W < 2 Or H < 2 Then Return

  X = X - hParent.Control.ClientX
  Y = Y - hParent.Control.ClientY

  If hParent.Kind = "ScrollView" Then

    X = X + hParent.Control.ScrollX
    Y = Y + hParent.Control.ScrollY

  Endif

  bFirst = True

  For Each hChild In hParent.Control.Children

    hCtrl = Control[hChild.Tag]

    If IsNull(hCtrl) Then Continue 'panel

    If hChild.X >= (X + W) Then Continue
    If hChild.Y >= (Y + H) Then Continue
    If (hChild.X + hChild.W) < X Then Continue
    If (hChild.Y + hChild.H) < Y Then Continue

    hCtrl.Select(Me, bFirst)
    $bSelChange = True
    bFirst = False

  Next

  If $bSelChange Then
    SelectionChange
  Endif

End


Private Sub DrawRectSelect(W As Integer, H As Integer)

  Dim X As Integer
  Dim Y As Integer

  If W = $W And H = $H Then Return

  If W <> 0 And H <> 0 Then
  
    $W = W
    $H = H

    X = $MX - panBorder.ScreenX
    Y = $MY - panBorder.ScreenY
    
    If W < 0 Then
      X += W
      W = - W
    Endif
    
    If H < 0 Then
      Y += H
      H = - H
    Endif

    panSelectN.Move(X, Y, W, 2)
    panSelectS.Move(X, Y + H - 2, W, 2)
    panSelectW.Move(X, Y, 2, H)
    panSelectE.Move(X + W - 2, Y, 2, H)
    panSelectN.Raise
    panSelectS.Raise
    panSelectW.Raise
    panSelectE.Raise
    panSelectN.Show
    panSelectS.Show
    panSelectW.Show
    panSelectE.Show

  Else
  
    panSelectN.Hide
    panSelectS.Hide
    panSelectW.Hide
    panSelectE.Hide
  
  Endif

  ' Draw.Begin(Me)
  ' Draw.Invert = True
  ' Draw.ForeColor = Color.White
  ' Draw.LineWidth = 1
  ' Draw.LineStyle = Line.Dot
  ' 
  ' X = $MX - Me.ScreenX
  ' Y = $MY - Me.ScreenY
  ' 
  ' If $W <> 0 And $H <> 0 Then Draw.Rect(X, Y, $W, $H)
  ' If W <> 0 And H <> 0 Then Draw.Rect(X, Y, W, H)
  ' 
  ' Draw.End
  ' 
  ' $W = W
  ' $H = H

End

Private Sub KillSelection()
  
  Dim hCtrl As CControl
  Dim cCopy As New CControl[]
  Dim hParent As CControl

  If Selection.Count Then

    hParent = Master.Parent

    For Each hCtrl In Selection
      cCopy.Add(hCtrl)
    Next

    UnSelectAll

    For Each hCtrl In cCopy
      hCtrl.Delete
    Next

    Modify

    hParent.Select(Me, True)

    ResetClassScan

  Endif
  
End


Public Sub DeleteSelection()

  If Selection.Count Then
    
    KillSelection
    
    SelectionChange
    RefreshProperty
    FFormStack.RefreshAll

  Endif

End


Public Sub GetSelection(Optional bAbsolute As Boolean) As String

  Dim hCtrl As CControl

  If Selection.Count = 0 Then Return

  ResetSave

  If Not bAbsolute Then

    $iSaveX = Master.GetProperty("X")
    $iSaveY = Master.GetProperty("Y")
  
    For Each hCtrl In Selection
  
      $iSaveX = Min($iSaveX, hCtrl.GetProperty("X"))
      $iSaveY = Min($iSaveY, hCtrl.GetProperty("Y"))
  
    Next
    
  Endif

  $bDoNotArrange = True
  For Each hCtrl In Selection
    SaveOne(hCtrl)
  Next
  $bDoNotArrange = False

  $iSaveX = 0
  $iSaveY = 0
  
  Return $sSave

End

Public Sub CopySelection()

  Dim sStr As String
  
  sStr = GetSelection()

  If sStr Then Clipboard.Copy(sStr, MMime.FORM)

End


Function CanPaste() As Boolean

  Return Clipboard.Format = MMime.FORM

End

Public Sub PutSelection(sData As String, hParent As CControl)
  
  If Not sData Then Return

  'PRINT Clipboard.Text
  $bSelectNew = True
  FromString(sData, hParent)
  $bSelectNew = False

  RefreshProperty
  FFormStack.RefreshAll
  
End


Public Sub PasteSelection()

  Dim hParent As CControl

  If Not CanPaste() Then Return

  If Selection.Count = 1 Then
    hParent = Master
  Else If Selection.Count > 1 Then
    hParent = Master.Parent
  Else
    hParent = Control[Me.Name]
  Endif

  If Not hParent.IsContainer() Then
    hParent = hParent.Parent
  Endif

  UnSelectAll

  PutSelection(Clipboard.Paste(MMime.FORM), hParent)

End



Private Sub RefreshProperty(Optional bForce As Boolean)

  If $bSelChange Or bForce Then
    RefreshMenu
    FProperty.RefreshAll
    $bSelChange = False
  Endif

End


Public Sub Modify(Optional bReset As Boolean)

  Dim hEditor As FEditor

  If Project.ReadOnly Then Return
  If $bDoNotModify Then Return

  If $bModify <> bReset Then Return

  $bModify = Not bReset
  DrawTitle

  If $bModify Then
    hEditor = GetEditor()
    If hEditor Then hEditor.Scan = Null
    Inc Project.TimeStamp
  Endif

End


Public Function IsModified() As Boolean

  Return $bModify

End


Private Sub DrawTitle()

  Project.DrawTitle(Me)

End



Private Sub DoRaise()

  Dim hCtrl As CControl

  For Each hCtrl In Selection
    hCtrl.Raise
  Next

  FFormStack.RefreshAll

End


Public Sub DoLower()

  Dim hCtrl As CControl

  For Each hCtrl In Selection
    hCtrl.Lower
  Next

  FFormStack.RefreshAll

End


Private Function GetSortKey(hCtrl As Control, iArr As Integer) As String

  Dim sKey As String

  Select Case iArr

    Case Arrange.Horizontal
      sKey = Format(hCtrl.X, "000000") & Format(hCtrl.W, "000000")
    Case Arrange.Vertical
      sKey = Format(hCtrl.Y, "000000") & Format(hCtrl.H, "000000")
    Case Arrange.TopBottom
      sKey = Format(hCtrl.X, "000000") & Format(hCtrl.Y, "000000") & Format(hCtrl.W, "000000") & Format(hCtrl.H, "000000")
    Case Arrange.LeftRight
      sKey = Format(hCtrl.Y, "000000") & Format(hCtrl.X, "000000") & Format(hCtrl.H, "000000") & Format(hCtrl.W, "000000")

  End Select

  Return sKey

End


Private Sub ArrangeContainer(hParent As Container, iArr As Integer, Optional bRec As Boolean = True, Optional iIndex As Integer = -1)

  Dim X As Integer
  Dim Y As Integer
  Dim aPos As New String[]
  Dim sPos As String
  Dim hCtrl As Control
  Dim hCCtrl As CControl
  Dim hCont As Container
  Dim aCtrl As New Control[]
  Dim hTab As Object

  If hParent Then
    If iIndex < 0 Then
      For Each hCtrl In hParent.Children
        If Not hCtrl.Tag Then Continue
        If Control.Exist(hCtrl.Tag) Then aCtrl.Add(hCtrl)
      Next
    Else ' Multicontainer
      hTab = hParent
      For Each hCtrl In hTab[iIndex].Children
        If Not hCtrl.Tag Then Continue
        If Control.Exist(hCtrl.Tag) Then aCtrl.Add(hCtrl)
      Next
    Endif
  Else If Selection.Count >= 2 Then
    For Each hCCtrl In Selection
      aCtrl.Add(hCCtrl.Control)
    Next
  Else
    ArrangeContainer(Control[Me.Name].Control, iArr)
    Return
  Endif

  If aCtrl.Count = 0 Then Return

  For Each hCtrl In aCtrl
    If bRec Then
      Try hCont = hCtrl
      If Not Error Then
        ArrangeContainer(hCtrl, iArr)
      Endif
    Endif
    aPos.Add(GetSortKey(hCtrl, iArr))
  Next

  If aPos.Count = 0 Then Return

  aPos.Sort(gb.Descent)

  For Each sPos In aPos
    For Each hCtrl In aCtrl
      If GetSortKey(hCtrl, iArr) = sPos Then
        hCtrl.Lower
        Break
      Endif
    Next
  Next

  FFormStack.RefreshAll
  
  Modify

End

Public Sub mnuSave_Click()

  Save

End

Private Sub ResetSave()

  $sSave = ""
  $iSaveX = 0
  $iSaveY = 0
  '$iIndent  = 0
  $iSaveLevel = 0
  $bScaled = Control[Me.Name].GetPropertyDefault(CPropertyInfo.SCALE_NAME)
  $cAction = New Collection
  $cToolbar = New Collection

End

Private Function GetEditor() As FEditor

  Dim sPath As String

  sPath = File.Dir(Path) &/ File.BaseName(Path) & ".class"
  Return Project.Files[sPath]

End

Private Sub GotoEventMethod(hCtrl As CControl, sEvent As String)

  Dim sPath As String
  Dim sGroup As String

  If Not hCtrl Then hCtrl = Control[Me.Name]

  sPath = File.Dir(Path) &/ File.BaseName(Path) & ".class"

  If hCtrl.Kind = "Form" Then
    sGroup = "Form"
  Else
    sGroup = hCtrl.GetGroup()
  Endif

  Project.OpenFile(sPath)

  Project.Files[sPath].GotoEvent(sGroup, sEvent,
    CSymbolInfo.TransformSignature(CComponent.Classes[hCtrl.Kind].Symbols[":" & sEvent].Signature, False))

End


Public Sub Control_DblClick()

  Dim sEvent As String
  Dim hCurrent As CControl
  Dim sGroup As String

  If Mouse.Control Or Mouse.Shift Then Return

  hCurrent = Control[Last.Tag]

  sEvent = CComponent.Classes[hCurrent.Kind].DefaultEvent
  If Not sEvent Then Return

  GotoEventMethod(hCurrent, sEvent)

End


Public Sub MenuControl_Click()

  'IF NOT LAST THEN STOP
  Last.Checked = False
  Control_DblClick

End


Private Sub CreateMenu()

  Dim hCtrl As CControl
  Dim hMenu As Menu
  Dim cCtrl As New String[]
  Dim sName As String
  Dim cSymbol As Collection
  Dim hSymbol As CSymbolInfo
  Dim sGroup As String
  Dim aChange As String[]

  mnuSelect.Children.Clear

  For Each hCtrl In Control
    sName = hCtrl.Name
    If sName <> Me.Name Then
      If hCtrl.Kind <> "Menu" Then
        cCtrl.Add(sName)
      Endif
    Endif
  Next

  mnuSelect.Enabled = cCtrl.Count

  cCtrl.Sort(gb.Text)

  For Each sName In cCtrl

    hMenu = New Menu(mnuSelect) As "mnuControl"
    hMenu.Text = sName

  Next

  mnuEvent.Visible = False
  mnuChange.Visible = False

  If Not $bReadOnly Then

    If Master Then
      cCtrl = CComponent.Classes[Master.Kind].Events
      sGroup = Master.GetGroup()
    Else
      cCtrl = CComponent.Classes[FormType].Events
      sGroup = FormType
    Endif

    If cCtrl Then

      mnuEvent.Children.Clear
      cSymbol = CComponent.GetClassSymbols(Me.Name)

      For Each sName In cCtrl

        hMenu = New Menu(mnuEvent) As "mnuEvent"
        hMenu.Text = sName

        hSymbol = cSymbol[sGroup & "_" & sName]
        If hSymbol Then
          If hSymbol.Kind = "m" Then hMenu.Checked = True
        Endif

      Next

      mnuEvent.Visible = True

    Endif
    
    mnuChange.Children.Clear
    
    If Selection.Count = 1 Then
    
      aChange = CComponent.GetChanges(Master.Kind)
      If aChange Then
        For Each sName In aChange
          If sName = Master.Kind Then Continue
          hMenu = New Menu(mnuChange) As "mnuChange"
          hMenu.Text = sName
          hMenu.Picture = FFormStack.GetControlIcon(sName)
          mnuChange.Show
        Next
      Endif
    
    Endif

    'mnuChange.Visible = mnuChange.Children.Count > 0
    
  Endif

  mnuStartup.Checked = Project.Startup = File.BaseName(Path)

  RefreshMenu

End


Sub RefreshMenu()

  Dim bOn As Boolean

  bOn = Not IsNull(Master)

  Action[".cut", Me].Enabled = bOn
  Action[".copy", Me].Enabled = bOn
  Action[".delete", Me].Enabled = bOn
  Action[".lower", Me].Enabled = bOn
  Action[".raise", Me].Enabled = bOn

  bOn = Selection.Count >= 2

  Action[".paste", Me].Enabled = CanPaste()
  
  mnuArrange.Visible = Not $bReadOnly
  Action[".save,.refresh,.cut,paste-form,.delete,.lower,.raise,.menu,.arrange*", Me].Visible = Not $bReadOnly

  bOn = bOn And Not $bReadOnly

  mnuAlign.Enabled = bOn
  mnuAlign.Visible = bOn  
  Action[".align-*", Me].Visible = bOn
  Action[".same-*", Me].Visible = bOn

  If Selection.Count = 1 And If Master.IsMultiContainer() Then
    mnuTab.Visible = True
    Action[".move-tab", Me].Visible = True
  Else 
    mnuTab.Visible = False
    Action[".move-tab", Me].Visible = False
  Endif

End


Sub SelectionChange()

  $bSelChange = True
  RefreshMenu

End



Public Sub mnuControl_Click()

  UnselectAll
  Control[Last.Text].Select(Me, True)
  RefreshProperty

End


Public Sub mnuEvent_Click()

  GotoEventMethod(Master, Last.Text)

End


Public Sub Rename(sNewName As String, sNewPath As String)

  Dim hCtrl As CControl

  hCtrl = Control[Me.Name]

  Me.Name = sNewName
  Path = sNewPath 'File.Dir(Path) &/ sNewName & "." & File.Ext(Path)

  $bDoNotModify = True
  hCtrl.Rename(sNewName)
  DrawTitle
  FProperty.RefreshAll
  $bDoNotModify = False

End

Public Sub mnuShowProperty_Click()

  FProperty.Show

End


Public Sub mnuShowCode_Click()

  Project.OpenFile(Me.Name)

End


Public Sub Control_Draw(Optional iIndex As Integer)

  'DIM hForm AS Form
'   DIM X AS Integer
'   DIM Y AS Integer
'   DIM SX AS Integer
'   DIM SY AS Integer
'   DIM SXF AS Integer
'   DIM SYF AS Integer
'   DIM iMod AS Integer
'   DIM DX AS Integer
'   DIM DY AS Integer
  'DIM hGrid AS Picture

  Dim hCtrl As Control = Last  
  Dim hCCtrl As CControl = Control[hCtrl.Tag]
  Dim hPict As Picture
  Dim W As Integer = 1
  
  If Not hCCtrl Then Return

  Select hCCtrl.Kind
  
    Case "HBox"
    
      Draw.FillX = 0
      Draw.FillY = 0
      If hCtrl.H >= 28 Then
        Draw.Tile(Picture["img/16/red-arrow-h.png"], 4, 4, hCtrl.W - 8, 8)
        Draw.Tile(Picture["img/16/red-arrow-h.png"], 4, hCtrl.H - 12, hCtrl.W - 8, 8)
      Else 
        Draw.Tile(Picture["img/16/red-arrow-h.png"], 4, hCtrl.H / 2 - 4, hCtrl.W - 8, 8)
      Endif
      Draw.Foreground = Color.Red
    
    Case "VBox"

      Draw.FillX = 0
      Draw.FillY = 0
      If hCtrl.W >= 28 Then
        Draw.Tile(Picture["img/16/red-arrow-v.png"], 4, 4, 8, hCtrl.H - 8)
        Draw.Tile(Picture["img/16/red-arrow-v.png"], hCtrl.W - 12, 4, 8, hCtrl.H - 8)
      Else 
        Draw.Tile(Picture["img/16/red-arrow-v.png"], hCtrl.W / 2 - 4, 4, 8, hCtrl.H - 8)
      Endif
      Draw.Foreground = Color.Red
    
    Case "HPanel"
    
      Draw.FillX = 0
      Draw.FillY = 0
      If hCtrl.H >= 28 Then
        Draw.Tile(Picture["img/16/red-arrow-r.png"], 4, 4, hCtrl.W - 8, 8)
        Draw.Tile(Picture["img/16/red-arrow-r.png"], 4, hCtrl.H - 12, hCtrl.W - 8, 8)
      Else 
        Draw.Tile(Picture["img/16/red-arrow-r.png"], 4, hCtrl.H / 2 - 4, hCtrl.W - 8, 8)
      Endif
      Draw.Foreground = Color.Red
    
    Case "VPanel"

      Draw.FillX = 0
      Draw.FillY = 0
      If hCtrl.W >= 28 Then
        Draw.Tile(Picture["img/16/red-arrow-c.png"], 4, 4, 8, hCtrl.H - 8)
        Draw.Tile(Picture["img/16/red-arrow-c.png"], hCtrl.W - 12, 4, 8, hCtrl.H - 8)
      Else 
        Draw.Tile(Picture["img/16/red-arrow-c.png"], hCtrl.W / 2 - 4, 4, 8, hCtrl.H - 8)
      Endif
      Draw.Foreground = Color.Red
    
    Case Else 

      'hPict = Picture["img/control" &/ "draw-" & LCase(hCCtrl.Kind) & ".png"]
      'IF NOT hPict THEN hPict = Picture["img/control" &/ LCase(hCCtrl.Kind) & ".png"]
      hPict = Picture[".control" &/ LCase(hCCtrl.Kind)]
      'Draw.ForeColor = Control[Name].Control.Foreground
      If hPict Then
        Draw.Picture(hPict, 4, 4)
        Draw.Text(hCtrl.Tag, 8 + hPict.Width, 4)
      Else
        Draw.Text(hCtrl.Tag, 4, 4)
      Endif
      Draw.Foreground = Color.Black
      
    End Select

  Draw.LineStyle = Line.None

  Draw.FillStyle = Fill.Solid
  Draw.FillColor = Draw.Foreground 'Color.White
  Draw.Rect(0, 0, hCtrl.W, W)
  Draw.Rect(hCtrl.W - W, 0, W, hCtrl.H)
  Draw.Rect(0, W, W, hCtrl.H - W)
  Draw.Rect(W, hCtrl.H - W, hCtrl.W - W * 2, W)

  ' Draw.FillStyle = Fill.Dense50
  ' Draw.FillColor = Draw.Foreground
  ' Draw.Rect(0, 0, hCtrl.W, W)
  ' Draw.Rect(hCtrl.W - W, 0, W, hCtrl.H)
  ' Draw.Rect(0, W, W, hCtrl.H - W)
  ' Draw.Rect(W, hCtrl.H - W, hCtrl.W - W * 2, W)

  'Draw.Rect(0, 0, LAST.W, LAST.H)

'   IF NOT Project.ShowGrid THEN RETURN
' 
'   'hForm = LAST.Parent
' 
'   'IF hForm.Picture THEN
'   '  Draw.Picture(hForm.Picture, 0, 0)
'   'ENDIF
' 
'   'PRINT Draw.Clip.X; Draw.Clip.Y; Draw.Clip.Width; Draw.Clip.Height
' 
'   'Project.Snap = Desktop.Scale
' 
'   DX = Desktop.Scale
'   WHILE (DX < 4)
'     DX = DX + Desktop.Scale
'   WEND
' 
'   DY = Desktop.Scale
'   WHILE (DY < 4)
'     DY = DY + Desktop.Scale
'   WEND
' 
' '   hGrid = NEW Picture(DX, DY, TRUE)
' '   Draw.Begin(hGrid)
' '   Draw.ForeColor = Color.White
' '   Draw.Point(0, 0)
' '   Draw.End
' 
'   SX = Draw.Clip.X
'   iMod = SX MOD DX
'   IF iMod THEN SX = SX + DX - iMod
' 
'   SY = Draw.Clip.Y
'   iMod = SY MOD DY
'   IF iMod THEN SY = SY + DY - iMod
' 
'   SXF = Draw.Clip.X + Draw.Clip.Width - 1
'   SYF = Draw.Clip.Y + Draw.Clip.Height - 1
' 
'   Draw.Invert = TRUE
' '   Draw.Tile(hGrid, Draw.Clip.X, Draw.Clip.Y, Draw.Clip.W, Draw.Clip.H)
' 
'   Draw.ForeColor = Color.White
'   FOR X = SX TO SXF STEP DX
'     FOR Y = SY TO SYF STEP DY
'       Draw.Point(X, Y)
'     NEXT
'   NEXT

End


Public Sub Refresh()

  UpdateSnap
  'Control[Name].Control.Refresh

End

Public Sub RefreshForComponent()

  Dim hCtrl As CControl
  
  For Each hCtrl In Control
    If hCtrl.Control Is DrawingArea Then
      If Not CComponent.Classes.Exist(hCtrl.Kind) Then
        Me.Delete
        Project.Files[Path] = Null
        Return
      Endif
      hCtrl.Control.Refresh
    Endif
  Next

End


' PUBLIC SUB Form_Hide()
' 
'   'DEBUG Name
'   Project.Deactivate(ME)
' 
' END


Public Sub Control_Data(Row As Integer, Column As Integer)

  If Row = 0 And Column = 0 Then
    Last.Data.Text = Control[Last.Tag].Name
    'LAST.Data.Picture = Picture["img/16/image.png"]
  Endif

End


Public Sub mnuSelectAll_Click()

  Dim hChild As Control
  Dim hCtrl As CControl
  Dim bFirst As Boolean
  Dim hParent As CControl
  
  If Master Then 
    hParent = Master
    If Not hParent.IsContainer() Then hParent = hParent.Parent
  Else 
    hParent = Control[Me.Name]
  Endif

  UnselectAll
  bFirst = True

  For Each hChild In hParent.Control.Children

    hCtrl = Control[hChild.Tag]

    If IsNull(hCtrl) Then Continue 'panel

    hCtrl.Select(Me, bFirst)
    $bSelChange = True
    bFirst = False

  Next

  If $bSelChange Then
    SelectionChange
    RefreshProperty
  Endif

End


Public Sub mnuUnselectAll_Click()

  If Master Then
    UnselectAll
    RefreshProperty
  Else 
    FMain.HidePanels
  Endif

End



Private Sub DoAlign(sKey As String)

  Dim iPos As Integer
  Dim hCtrl As CControl

  Select sKey
  
    Case ".align-top"

      iPos = Master.Control.Y
    
      For Each hCtrl In Selection
        hCtrl.Move(hCtrl.Control.X, iPos)
      Next
  
    Case ".align-bottom"
      
      iPos = Master.Control.Y + Master.Control.H
    
      For Each hCtrl In Selection
        hCtrl.Move(hCtrl.Control.X, iPos - hCtrl.Control.H)
      Next
      
    Case ".align-left"      
  
      iPos = Master.Control.X
    
      For Each hCtrl In Selection
        hCtrl.Move(iPos, hCtrl.Control.Y)
      Next
      
    Case ".align-right"
    
      iPos = Master.Control.X + Master.Control.W
    
      For Each hCtrl In Selection
        hCtrl.Move(iPos - hCtrl.Control.W, hCtrl.Control.Y)
      Next
      
    Case ".same-width"

      iPos = Master.Control.W
    
      For Each hCtrl In Selection
        hCtrl.Resize(iPos, hCtrl.Control.H)
      Next
      
    Case ".same-height"
    
      iPos = Master.Control.H
    
      For Each hCtrl In Selection
        hCtrl.Resize(hCtrl.Control.W, iPos)
      Next

  End Select 

End


Private Sub SetReadOnly()

  $bReadOnly = Project.ReadOnly Or Project.Running Or Stat(Path).Type = gb.Link Or $bLocked
  FFormStack.RefreshReadOnly
  RefreshProperty(True)

End

Private Sub SetLock(bLock As Boolean)

  If bLock = $bLocked Then Return 
  
  $bLocked = bLock
  
  If Not $bDoNotModify Then
    If Me.Save(True) Then
      $bLocked = Not bLock
      Return 
    Endif
  Endif
  
  SetReadOnly
  DrawTitle
  
End



Public Sub OnProjectChange()

  SetReadOnly

End

Public Sub OnProjectDebug()

  SetReadOnly

End

Public Sub LoadFile()
  
  Reload
  SetReadOnly
  
End


Public Function FindControlFromType(sType As String) As String[]
  
  Dim hCtrl As CControl
  Dim aCtrl As New String[]
  
  For Each hCtrl In Control
    If hCtrl.Kind = sType Then
      aCtrl.Add(hCtrl.Name)
    Endif
  Next  
  
  Return aCtrl
  
End

Private Sub MoveSelection(DX As Integer, DY As Integer, Optional bFree As Boolean)

  Dim hCtrl As CControl

  If $bReadOnly Then Return
  
  For Each hCtrl In Selection
    hCtrl.Move(hCtrl.Control.X + DX, hCtrl.Control.Y + DY, bFree)
  Next
  
End

Private Sub ResizeSelection(DW As Integer, DH As Integer, Optional bFree As Boolean)

  Dim hCtrl As CControl
  Dim W, H As Integer

  If $bReadOnly Then Return
  
  For Each hCtrl In Selection
    W = hCtrl.Control.W + DW
    H = hCtrl.Control.H + DH
    If W < 1 Or If H < 1 Then Return
  Next

  For Each hCtrl In Selection
    hCtrl.Resize(hCtrl.Control.W + DW, hCtrl.Control.H + DH, bFree)
  Next
  
End

Public Sub Form_KeyPress()

  Dim D As Integer
  Dim bFree As Boolean
  
  If Key.Shift Then
    D = 1
    bFree = True
  Else
    D = Desktop.Scale
    bFree = False
  Endif 

  Select Key.Code
    Case Key.Up
      If Key.Control Then 
        ResizeSelection(0, - D, bFree)
      Else
        MoveSelection(0, - D, bFree)
      Endif
      Stop Event
    Case Key.Down
      If Key.Control Then 
        ResizeSelection(0, D, bFree)
      Else
        MoveSelection(0, D, bFree)
      Endif
      Stop Event
    Case Key.Left
      If Key.Control Then 
        ResizeSelection(- D, 0, bFree)
      Else
        MoveSelection(- D, 0, bFree)
      Endif
      Stop Event
    Case Key.Right
      If Key.Control Then 
        ResizeSelection(D, 0, bFree)        
      Else
        MoveSelection(D, 0, bFree)
      Endif
      Stop Event
  End Select

End

Public Sub UpdateBorder()

  Dim hCtrl As CControl
  Dim H As Integer
  
  hCtrl = Control[Me.Name]
  If Not hCtrl Then Return

  With hCtrl.Control

    If hCtrl.GetPropertyDefault("Border") = False Then
    
      panTitle.Hide
      H = 0
      panBorder.Move(0, 0, .Width + 2, .Height + 2)
      $bBorder = False
      'panBorder.Border = Border.Plain
      hCtrl.Control.Move(1, 1)
    
    Else

      H = lblTitle.Font.Height("X") + 8 '+ 4
      btnCloseWindow.W = 18 'H - 4
      btnMaxWindow.W = 17 'H - 4
      imgIcon.W = H - 4
      panBorder.Move(0, H, .Width + 4, .Height + 2)
      $bBorder = True
      'panBorder.Border = Border.Raised
      panTitle.Resize(panBorder.W, H)
      panInside.Move(0, 0, panTitle.W, panTitle.H + 8)
      panTitle.Show
  
      hCtrl.Control.Move(2, 0)
      
    Endif 
    
    panBorder.Refresh
      
    panRight.Move(panBorder.W - panRight.W / 2, Max(0, ((.Height + H) - panRight.Height) / 2))
    panDown.Move(Max(0, (.Width - panDown.Width + panDown.H / 2) / 2), panBorder.H + H - panDown.H / 2)
    panRightDown.Move(panRight.X, panDown.Y)
    
    'panBorder.Lower
    panRight.Raise
    panDown.Raise
    panRightDown.Raise
    
  End With
  
End

Public Sub UpdateTitle()
  
  Dim hCtrl As CControl
  Dim sPict As String
  Dim hPict As Picture
  
  hCtrl = Control[Me.Name]
  If Not hCtrl Then Return

  sPict = hCtrl.GetProperty("Icon")
  If sPict Then

    lblTitle.Text = hCtrl.GetProperty("Text")

    Try hPict = Project.GetPicture(sPict)
    If hPict Then
      If hPict.Width <> 16 Or hPict.Height <> 16 Then hPict = hPict.Image.Stretch(16, 16, True).Picture
    Else  
      hPict = Picture["img/16/gambas.png"]
    Endif 

    imgIcon.Picture = hPict
    imgIcon.Show

  Else

    lblTitle.Text = hCtrl.GetProperty("Text")
    imgIcon.Hide

  Endif

  btnMaxWindow.Visible = hCtrl.GetPropertyDefault("Resizable")  
  
  If hCtrl.GetPropertyString("Type") = "Utility" Then
    lblTitle.Font.Grade = -2
  Else
    lblTitle.Font.Grade = 0
  Endif
  
  UpdateBorder 
  
End



' 
' 
' PUBLIC SUB Activate(OPTIONAL hWindow AS Window)
'   
'   DIM hOld AS CWindow
'   DIM hWin AS CWindow
'   
'   IF $aWindow.Count THEN 
'   
'     IF hWindow THEN
'       TRY tabWorkspace.Index = GetIndex(hWindow)
'       RETURN
'     ENDIF
'   
'     hWin = $aWindow[tabWorkspace.Index]
'     hOld = $hCurrent
'   
'     $hCurrent = hWin
'     
'     IF $hCurrent.Resizable THEN
'       $hCurrent.Window.Move(0, 0)
'       MoveHandle
'       svwWorkspace.Raise
'     ELSE
'       panWorkspace.Raise
'     ENDIF
'     'WITH tabWorkspace
'     '  $hCurrent.Move(.X + .ClientX, .Y + .ClientY, .ClientW, .ClientH)
'     'END WITH
'     
'     WITH $hCurrent.Window
'       .Show
'       .Raise
'       .SetFocus
'     END WITH
'     
'     IF hOld AND IF hOld <> hWin THEN 
'       hOld.Window.Hide
'       'WAIT 0.2
'     ENDIF
'     
'     UpdateTitle
'     
'   ENDIF
'   
'   ME.Parent._Activate
'   
' END

' PUBLIC SUB Form_Resize()
' 
'   IF tabWorkspace.Visible THEN
'     tabWorkspace.Move(0, 0, ME.ClientW, ME.ClientH)
'     WITH tabWorkspace
'       panTitle.Move(.ClientX, .ClientY, .ClientW, panTitle.H)
'       svwWorkspace.Move(.ClientX, panTitle.H + .ClientY, .ClientW, .ClientH - panTitle.H)
'       panWorkspace.Move(.ClientX, panTitle.H + .ClientY, .ClientW, .ClientH - panTitle.H)
'     END WITH 
'     panWorkspace.BackColor = Color.Background
'   ELSE
'     svwWorkspace.Move(0, 0, ME.ClientW, ME.ClientH)
'     panWorkspace.Move(0, 0, ME.ClientW, ME.ClientH)
'     panWorkspace.BackColor = Color.Gray
'     panWorkspace.Raise
'   ENDIF
' 
' END
' 
' 
Public Sub Handle_MouseDown()
  $bMove = True  
  $X = Mouse.ScreenX
  $Y = Mouse.ScreenY
  $W = Control[Me.Name].Control.W
  $H = Control[Me.Name].Control.H
  
  'DEBUG Mouse.ScreenX;; Mouse.ScreenY
  'DEBUG $X;; $Y

End

Public Sub Handle_MouseMove()

  Dim W, H, G As Integer
  Dim hPanel As Panel

  If Not $bMove Then Return
  
  hPanel = Last

  With Control[Me.Name].Control

    W = .W
    H = .H

    'DEBUG "(";; W;; H;; ") + (";; Mouse.ScreenX;; Mouse.ScreenY;; ") -> ";

    If hPanel <> panRight Then H = $H + Mouse.ScreenY - $Y
    If hPanel <> panDown Then W = $W + Mouse.ScreenX - $X
    
    W = Max(1, W)
    H = Max(1, H)

    If Not Mouse.Shift Then    
      G = Project.Snap
      If G Then
        W = Max(1, Int(W / G + 0.5)) * G
        H = Max(1, Int(H / G + 0.5)) * G
      Endif
    Endif 
    
    'PRINT #File.Err, "(";; W;; H;; ")"
    
    .Resize(W, H)
  
  End With
  
  '$bMove = FALSE
  'UpdateBorder
  '$bMove = TRUE

End

Public Sub Handle_MouseUp()

  $bMove = False  

End


Public Sub Action_Activate(Key As String) As Boolean
  
  Select Case Key
  
    Case ".menu"
      FMenu.Run(Me)
  
    Case ".delete"
      DeleteSelection
    
    Case ".copy"
      CopySelection
      UnSelectAll
    
    Case ".paste"
      PasteSelection

    Case ".cut"
      CopySelection
      DeleteSelection
      
    Case ".align-left", ".align-right", ".align-top", ".align-bottom", ".same-width", ".same-height"
      DoAlign(Key)
      
    Case ".arrange-horizontal"
      ArrangeContainer(Null, Arrange.Horizontal)

    Case ".arrange-vertical"
      ArrangeContainer(Null, Arrange.Vertical)

    Case ".arrange-row"
      ArrangeContainer(Null, Arrange.Row)

    Case ".arrange-column"
      ArrangeContainer(Null, Arrange.Column)
      
    Case ".raise"
      DoRaise
      
    Case ".lower"
      DoLower
      
    Case ".grid"
      UpdateSnap

    Case ".move-tab-first"
      MoveTab(Master, MOVE_FIRST)

    Case ".move-tab-last"
      MoveTab(Master, MOVE_LAST)

    Case ".move-tab-previous"
      MoveTab(Master, MOVE_PREVIOUS)

    Case ".move-tab-next"
      MoveTab(Master, MOVE_NEXT)
      
    Case ".lock"
      SetLock(Action[".lock", Me].Value)
      
    Default 
      Return True
      
  End Select 
  
End

Public Sub Form_Activate()

  mnuForm.Enabled = True

End

Public Sub Form_Deactivate()

  mnuForm.Enabled = False

End

Public Sub mnuReload_Click()

  If IsModified() Then
    If Message.Warning(("The form has been modified.\n\nAll your changes will be lost."), ("Reload"), ("Cancel")) <> 1 Then Return
  Endif

  Reload

End

Public Sub Control_Drag()

  Dim hCtrl As CControl

  If $bReadOnly Then  
    Stop Event 
    Return 
  Endif

  If Drag.Format = MMime.DATA Then
    Drag.Show(Last)
    Return
  Else If Drag.Format = MMime.CONTROL Then 
    hCtrl = Control[Last.Tag]
    If Not hCtrl.IsContainer() Then 
      hCtrl = hCtrl.Parent
    Endif
    Drag.Show(hCtrl.Control)
    Return 
  Endif

Catch

  Stop Event

End

Public Sub Control_Drop()
  
  Dim sPath As String
  Dim hCtrl As CControl
  Dim hParent As CControl
  Dim hImage As Image
  Dim X, Y, W, H As Integer
  Dim sClass As String
  
  hParent = Control[Last.Tag]
  X = Drag.X
  Y = Drag.Y
  
  If Drag.Format = MMime.DATA Then
  
    sPath = Drag.Data
    Try hImage = Image.Load(sPath)
    If Not hImage Then Return
    
    If Last.Tag <> Me.Name And If hParent.HasProperty("Picture") Then
      hParent.SetProperty("Picture", Mid$(sPath, Len(Project.Dir) + 2)) 
      Return
    Endif
  
    FindContainer(hParent, X, Y, COORD_INSIDE_TO_CONTROL)
    hParent = $hContainer
    X = $iContX
    Y = $iContY
  
    ' IF NOT hParent.IsContainer() THEN
    '   X += hParent.Control.X
    '   Y += hParent.Control.Y
    '   hParent = hParent.Parent
    ' ENDIF
    ' X -= hParent.Control.ClientX
    ' Y -= hParent.Control.ClientY
  
    hCtrl = CreateControl("PictureBox", hParent)
    hCtrl.Move(Max(0, X - hImage.Width / 2), Max(0, Y - hImage.Height / 2))
    hCtrl.Resize(hImage.Width, hImage.Height)
    hCtrl.SetProperty("Picture", Mid$(sPath, Len(Project.Dir) + 2))
    
    $hCurrent = hCtrl
    UnSelectAll
    SelectCurrent(True)
    RefreshProperty
    
  Else If Drag.Format = MMime.CONTROL Then

    ' IF NOT hParent.IsContainer() THEN
    '   X += hParent.Control.X
    '   Y += hParent.Control.Y
    '   hParent = hParent.Parent
    ' ENDIF
    
    FindContainer(hParent, X, Y, COORD_INSIDE_TO_CONTROL)
    hParent = $hContainer
    X = $iContX
    Y = $iContY

    $hCurrent = AddControl(Drag.Data, hParent, X, Y)
    FToolBox.SetTool()

  Endif
  
  FFormStack.RefreshAll
  
End

Public Sub panTitle_Drag()

  Control_Drag  

End

Public Sub panTitle_Drop()
  
  Dim sPath As String = Drag.Data
  Dim hImage As Image
  Dim hCtrl As CControl = Control[Me.Name]
  
  Try hImage = Image.Load(sPath)
  If Not hImage Then Return
  
  hCtrl.SetProperty("Icon", Mid$(sPath, Len(Project.Dir) + 2))
  
End


Private Sub FindContainer(hCtrl As CControl, X As Integer, Y As Integer, Optional iMode As Integer)
  
  If Not hCtrl.IsContainer() Then
    X += hCtrl.Control.X
    Y += hCtrl.Control.Y
    hCtrl = hCtrl.Parent
    If iMode = COORD_CONTROL_TO_INSIDE Then
      X += hCtrl.Control.ClientX
      Y += hCtrl.Control.ClientY
    Endif
    If iMode = COORD_CONTROL_TO_INSIDE Then
      If hCtrl.Kind = "ScrollView" Then
        X = X - hCtrl.Control.ScrollX
        Y = Y - hCtrl.Control.ScrollY
      Endif
    Endif
  Endif   

  If iMode = COORD_INSIDE_TO_CONTROL Then 
    X -= hCtrl.Control.ClientX
    Y -= hCtrl.Control.ClientY
    If hCtrl.Kind = "ScrollView" Then
      X = X + hCtrl.Control.ScrollX
      Y = Y + hCtrl.Control.ScrollY
    Endif    
  Endif

  $hContainer = hCtrl
  $iContX = X
  $iContY = Y
  
End

Private Function ReadOnly_Read() As Boolean

  Return $bReadOnly  

End

Private Sub MoveTab(hCtrl As CControl, iWhere As Integer)
  
  Dim hTabStrip As Object = hCtrl.Control
  Dim iIndex As Integer
  Dim hChild As Control
  Dim aChildSrc As New Control[]
  Dim aChildDest As New Control[]
  
  Select Case iWhere
    Case MOVE_FIRST
      'Object.Lock(hTabStrip)
      While hTabStrip.Index > 0
        MoveTab(hCtrl, MOVE_PREVIOUS)
      Wend
      'Object.UnLock(hTabStrip)
      'Control_Click
      Return
    Case MOVE_LAST
      'Object.Lock(hTabStrip)
      While hTabStrip.Index < (hTabStrip.Count - 1)
        MoveTab(hCtrl, MOVE_NEXT)
      Wend
      'Object.UnLock(hTabStrip)
      'Control_Click
      Return
    Case MOVE_NEXT
      iIndex = hTabStrip.Index + 1
    Case MOVE_PREVIOUS
      iIndex = hTabStrip.Index - 1
  End Select  
  
  If iIndex < 0 Or If iIndex >= hTabStrip.Count Or If iIndex = hTabStrip.Index Then Return 
  
  Swap hCtrl.Tag[iIndex], hCtrl.Tag[hTabStrip.Index]
  Swap hTabStrip.Text, hTabStrip[iIndex].Text
  Swap hTabStrip.Picture, hTabStrip[iIndex].Picture

  For Each hChild In hTabStrip[iIndex].Children
    aChildDest.Add(hChild)
  Next
  
  For Each hChild In hTabStrip.Children
    aChildSrc.Add(hChild)
  Next
  
  For Each hChild In aChildDest
    hChild.Reparent(hTabStrip)
  Next
  
  hTabStrip.Index = iIndex
  For Each hChild In aChildSrc
    hChild.Reparent(hTabStrip)
  Next
  
  Modify
  
End

Public Sub SelectControl(sControl As String)

  UnselectAll
  Control[sControl].Select(Me, True)
  RefreshProperty

End

Public Sub panBorder_Draw()

  If $bBorder Then
    Draw.Style.Panel(0, -4, panBorder.W, panBorder.H + 4, Border.Raised)
  Else
    Draw.Style.Panel(0, 0, panBorder.W, panBorder.H, Border.Plain)
  Endif

End

Public Sub mnuChange_Click()
  
  Dim sNewClass As String
  Dim hParent As CControl
  Dim sStr As String
  Dim hCtrl As CControl
  Dim sName As String
  Dim sClass As String
  Dim sFind As String
  Dim iPos As Integer 
  
  If Selection.Count <> 1 Then Return
  
  sNewClass = Last.Text

  sStr = GetSelection(True)
  
  sClass = Master.Kind
  If Master.Virtual Then sClass = "#" & sClass
  
  sFind = "{ " & Master.Name & " " & sClass
  iPos = InStr(sStr, sFind)
  If iPos = 0 Then Return
  
  sStr = Left$(sStr, iPos - 1) & "{ " & Master.Name & " " & sNewClass & Mid$(sStr, iPos + Len(sFind))
  
  hParent = Master.Parent
  
  KillSelection
  $bNoWarning = True
  PutSelection(sStr, hParent)
  $bNoWarning = False

  SelectionChange
  RefreshProperty
  FFormStack.RefreshAll
  
End

Public Sub mnuStartup_Click()
  
  If mnuStartup.Checked Then Return
  mnuStartup.Checked = True
  Project.DefineStartup(Path)
  
End
