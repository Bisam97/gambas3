' Gambas class file

Static Private $sPath As String
Static Private $sLast As String
Static Private $bAnother As Boolean

Static Public InAnotherWindow As Boolean

Private frmProjectItem As FProjectItem

Public Sub Run(Optional bAnother As Boolean) As String

  $bAnother = bAnother
  If Me.ShowModal() Then Return $sPath

End

Public Sub _new()
  
  frmProjectItem = New FProjectItem(panProject)
  
End


Public Sub btnOK_Click()

  Dim sPath As String
  
  If tabProject.Index = 0 Then
    sPath = GetProjectPath(dchProject.Value)
  Else If tabProject.Index = 1 Then
    Try sPath = lstRecent.Current.Tag
  Else If tabProject.Index = 2 Then
    Try sPath = lstExample.Current.Tag
  Endif

  If NOT sPath Then Return

  $sPath = sPath
  'Settings["/FOpenProject/Path"] = $sPath
  'tabProject.Index = 0
  lstRecent.Clear
  InAnotherWindow = chkOther.Value  
  
  Me.Close(True)

End

Public Sub btnCancel_Click()

  Me.Close

End


Public Sub Form_Open()

  $sLast = ""
  Settings.Read(Me)
  Settings.Read(dchProject)
  Settings.Read(splProject)
  'dchProject.Value = Settings["/FOpenProject/Path"]
  dchProject_Change
  tabProject_Click

  chkOther.Value = False
  InAnotherWindow = False
  chkOther.Visible = $bAnother
  chkSortRecent.Value = Settings["/FOpenProjet/SortRecent", False]  

End

Public Sub Form_Close()

  Settings.Write(Me)  
  Settings.Write(splProject)
  Settings.Write(dchProject)
  Settings["/FOpenProject/SortRecent"] = chkSortRecent.Value
  
End

Public Sub dchProject_Icon(Path As String)

  If Exist(Path &/ ".project") Then 
    dchProject.Icon = Project.GetIcon(Path)    
  Endif  

End

Private Sub GetProjectPath(sPath As String) As String
  
  Do
    If Exist(sPath &/ ".project") Then Return sPath
    sPath = File.Dir(sPath)
    If sPath = "/" Then Break
  Loop
  
Catch
  
End


Public Sub dchProject_Change()

  Dim sPath As String
  
  fvwProject.ShowHidden = dchProject.ShowHidden  
  fvwProject.Dir = dchProject.Value

  sPath = GetProjectPath(dchProject.Value)
  
  If NOT sPath Then
    $sLast = ""
    sdpProject.Hide
    Return 
  Endif
  
  If sPath = $sLast Then Return
  
  $sLast = sPath
  
  frmProjectItem.SetPath(sPath)
  
  sdpProject.Show

End

Public Sub fvwProject_Activate()

  dchProject.Value = fvwProject.Dir &/ fvwProject.Current

End

Public Sub fvwProject_Icon(Path As String)

  If Exist(Path &/ ".project") Then 
    fvwProject.Icon = Project.GetIcon(Path, 16)    
  Endif    

End

Private Sub FillRecent()
  
  Dim sRecent As String
  Dim hProjectItem As FProjectItem
  Dim hFirst As FProjectItem
  Dim aRecent As String[]
  Dim bHighlight As Boolean
  
  If lstRecent.Children.Count Then Return

  Inc Application.Busy

  panRecent.Arrangement = Arrange.None
  lstRecent.Hide

  aRecent = Project.GetRecentFiles(chkSortRecent.Value)

  For Each sRecent In aRecent
    hProjectItem = New FProjectItem(lstRecent)
    If bHighlight Then hProjectItem.Background = Color.LightBackground
    bHighlight = NOT bHighlight
    If NOT hFirst Then hFirst = hProjectItem
    hProjectItem.SetPath(sRecent)
  Next
  
  If hFirst Then lstRecent.Select(hFirst)
  
  ' Workaround a scrollview bug that does not show the scrollbars.
  lstRecent.Show 
  panRecent.Arrangement = Arrange.Vertical
  
  Dec Application.Busy
  
End

Public Sub tabProject_Click()

  If tabProject.Index = 1 Then 
    FillRecent
  Else If tabProject.Index = 2 Then 
    FillExample
  Endif  

End

Public Sub lstRecent_Activate()
  
  btnOK.Value = True
  
End

Private Sub FillExample()

  Dim sExample As String
  Dim sDir As String
  Dim sLastDir As String
  Dim hDrawingArea As DrawingArea
  Dim hProjectItem As FProjectItem
  Dim hLabel As Label
  Dim bHighlight As Boolean
  
  If lstExample.Children.Count Then Return

  Inc Application.Busy

  For Each sExample In Project.GetExamples()
  
    sDir = File.Dir(sExample)
    If sDir <> sLastDir Then
      hDrawingArea = New DrawingArea(lstExample) As "dwgExample"
      With hDrawingArea
        .Enabled = False
        .Tag = Project.ExampleTitle[sDir]
        .Font = Font["Bold,+2"]
        .H = .Font.Height() + If(sLastDir, 16, 8)
        .Foreground = Color.SelectedBackground
      End With
      bHighlight = False
      sLastDir = sDir
    Endif
    
    hProjectItem = New FProjectItem(lstExample)
    If bHighlight Then hProjectItem.Background = Color.LightBackground
    bHighlight = NOT bHighlight
    hProjectItem.SetPath(Project.EXAMPLES_DIR &/ sExample)
  
  Next

  lstExample.Show ' Workaround a scrollview bug that does not show the scrollbars.

  Dec Application.Busy

End

Public Sub dwgExample_Draw()
  
  Dim hDrawingArea As DrawingArea = Last
  
  With hDrawingArea
    Draw.Font = .Font
    Draw.Foreground = .Foreground
    'Draw.Line(0, .H / 2 - 1, .W - 1, .H / 2 - 1)
    'Draw.Transparent = FALSE
    Draw.Text(.Tag, 4, 0, .W - 16, .H - 4, Align.BottomNormal)
    Draw.Line(4, .H - 4, .W - 9, .H - 4)
  End With
  
End

Public Sub dchProject_Activate()

  btnOK.Value = True  

End


Public Sub chkSortRecent_Click()

  If tabProject.Index = 1 Then 
    lstRecent.Clear
    FillRecent
  Endif

End

Public Sub OnUpdateRecent()
  
  chkSortRecent_Click
  
End


Public Sub lstExample_Activate()

  btnOK.Value = True

End
