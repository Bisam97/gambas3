' Gambas class file

Static Private $aSupport As String[]
Static Private $iWait As Integer
Static Private $iSupport As Integer

Private $aUser As New CUser[]
Private $hBack As Picture
Private $iStep As Integer
Private $iFirst As Integer

Private Const NSTEP As Integer = 100

Static Public Sub Run()

  Dim hForm As FAbout

  hForm = New FAbout
  hForm.ShowModal

End


Public Sub Form_Open()

  Dim sText As String

  txtAuthor.BackColor = svwAuthor.BackColor

  GetVersion
  GetAuthor
  GetSupport

  InitUser
  
  $hBack = New Picture(dwgAbout.W, dwgAbout.H)
  $hBack.Fill(Color.White)

  lblMessage.Text = "© 2000-" & Year(Now) & " Benoît Minisini"
  
End


Private Sub GetVersion()

  lblVersion.Text = Project.Version
  lblVersion.X = lblGambas.X + lblGambas.Font.Width(lblGambas.Text)

End


Public Sub btnClose_Click()

  Me.Close

End


Private Sub RemoveLink(sStr As String) As String
  
  Dim iPos As Integer
  Dim iPos2 As Integer
  
  Do

    iPos = InStr(sStr, "<a href=")
    If iPos = 0 Then Break

    iPos2 = InStr(sStr, ">", iPos)
    If iPos2 = 0 Then iPos2 = Len(sStr) + 1
    sStr = Left(sStr, iPos - 1) & Mid(sStr, iPos2 + 1)
    
  Loop
  
  Return Replace(sStr, "</a>", "")
  
End


Private Sub GetAuthor()

  Dim hFic As File
  Dim sText As String
  Dim sLig As String
  Dim nLig As Integer
  Dim sMail As String
  Dim sCountry As String

  hFic = Open "authors.txt"

  While Not Eof(hFic)

    Line Input #hFic, sLig
    If Not sLig Then Continue
    If Left(sLig) = "-" Then Continue

    Line Input #hFic, sMail
    Line Input #hFic, sCountry
    
    sText &= "<p><font size=+1><b>" & sLig & "</b>"
    If sCountry Then sText &= " (" & sCountry & ")"
    sText &= "</font></p><blockquote>\n"
    
    sText &= "<u>" & sMail & "</u><br>"

    While Not Eof(hFic)

      Line Input #hFic, sLig
      If Not sLig Then Continue
      If Left(sLig) = "-" Then Break
      
      sLig = RemoveLink(sLig)
      If Right(sLig) <> "." Then sLig &= "."
      sText &= sLig & "<br>"

    Wend

    'IF Right$(sText, 4) = "<br>" THEN sText = Left$(sText, -4)

    sText = sText & "</blockquote>\n"

  Wend

  Close #hFic

  txtAuthor.RichText = sText
  'nLig * txtAuthor.Font.Height("a") * 0.95 + 256

Finally

  txtAuthor.Height = txtAuthor.TextHeight '+ 256
  'PRINT txtAuthor.TextHeight

Catch

  txtAuthor.RichText = Error.Text

End

Private Sub GetSupport()

  Dim iInd As Integer

  $aSupport = Split(Trim(File.Load("support.txt")), "\n")

  For iInd = 0 To $aSupport.Count - 1

    $aSupport[iInd] = "<b>" & $aSupport[iInd] & "...</b>"

  Next

  $aSupport.Add("<b>" & ("Thanks to") & "...</b>", 0)

End


Public Sub Form_Show()

  txtAuthor.Height = txtAuthor.TextHeight + 256

End

Public Sub btnGift_Click()

  Project.OpenWebPage("https://www.paypal.com/xclick/business=gambas%40users.sourceforge.net&no_note=1&tax=0&currency_code=EUR")

End


Private Sub InitUser()
  
  Dim hFile As File
  Dim sLine As String
  Dim hUser As CUser
  Dim iPos As Integer
  Dim iGray As Integer
  Dim iInd As Integer
  Dim hPict As Picture
  Dim W, H As Integer
  Dim nTask As Integer
  Dim bTrans As Boolean
  
  hFile = Open "support.txt" 
  
  While Not Eof(hFile)
    Line Input #hFile, sLine
    sLine = Trim(sLine)
    If Not sLine Then Continue
    iPos = InStr(sLine, " ")
    If iPos = 0 Then Continue
    
    hUser = New CUser
    hUser.Name = Trim(Mid$(sLine, iPos + 1))
    hUser.Score = CInt(Left$(sLine, iPos - 1))
    'iGray = Int(Rnd(0, 128))
    iGray = 64
    hUser.Color = Color.RGB(iGray, iGray, iGray)
    $aUser.Add(hUser)
    
  Wend
  
  Close #hFile
  
  hFile = Open "authors.txt" 
  
  While Not Eof(hFile)
    Line Input #hFile, sLine
    sLine = Trim(sLine)
    If Not sLine Then Continue
    If Left$(sLine, 4) = "----" Then
      
      hUser = New CUser
      Line Input #hFile, sLine
      iPos = InStr(sLine, "(")
      If iPos Then sLine = Mid$(sLine, iPos + 1, -1)
      hUser.Name = sLine
      
      Line Input #hFile, sLine ' Mail
      Line Input #hFile, sLine ' country

      nTask = 0
      bTrans = False
      Do
        Line Input #hFile, sLine
        sLine = Trim(sLine)
        If Not sLine Then Break
        Inc nTask
        If InStr(sLine, "translation") = 0 Then bTrans = True
      Loop
      
      If nTask = 1 And If bTrans Then
        hUser.Score = 6
        iGray = Int(Rnd(64, 128))
        hUser.Color = Color.RGB(&H95 * iGray / 128, &HC1 * iGray / 128, &HE6 * iGray / 128)
        hUser.Bold = True
      Else
        hUser.Score = 12
        hUser.Color = Color.HSV(Int(Rnd(36)) * 10, Int(Rnd(192, 256)), 255)
        hUser.Bold = True
      Endif
      
      $aUser.Add(hUser)
      
    Endif
    
  Wend
  
  Close #hFile

  For Each hUser In $aUser
    With hUser
      Draw.Begin(dwgAbout)
      Draw.Font.Bold = .Bold
      Draw.Font.Size = GetFontSize(hUser)
      W = Draw.TextWidth(.Name)
      H = Draw.TextHeight(.Name)
      Draw.End

      hPict = New Picture(W, H)
      hPict.Fill(Color.White)
      Draw.Begin(hPict)
      Draw.Font.Bold = .Bold
      Draw.Font.Size = GetFontSize(hUser)
      
      ' If .Bold Then
      '   Draw.ForeColor = Color.Black
      '   Draw.Text(.Name, 0, 0, W, H)
      '   Draw.Text(.Name, 2, 0, W, H)
      '   Draw.Text(.Name, 0, 2, W, H)
      '   Draw.Text(.Name, 2, 2, W, H)
      ' Endif
      
      Draw.ForeColor = .Color 'Color.Black
      Draw.Text(.Name, 0, 0, W, H)
      Draw.End
      .Image = hPict.Image
      .Image.MakeTransparent
    End With
  Next

  For iInd = 1 To $aUser.Count
    iPos = Int(Rnd(0, $aUser.Count))
    Swap $aUser[0], $aUser[iPos]
  Next

End

Private Sub GetFontSize(hUser As CUser) As Float
  
  Return 6 + hUser.Score * 0.8
  
End



Private Sub DrawText(iIndex As Integer) As Boolean
  
  Dim hUser As CUser
  Dim iInd As Integer
  Dim X As Float
  Dim Y As Float
  Dim hPict As Picture
  Dim eSize As Float
  Dim W, H As Integer
  Dim iStep As Integer
  Dim eBlend As Float
  Dim hImage As Image

  hUser = $aUser[iIndex Mod $aUser.Count]
  With hUser

    iStep = $iStep - iIndex * 40
    If iStep < 0 Then Return
    If iStep > (NSTEP * 10) Then Return True

    If iStep = 0 Then
    
      Draw.Font.Bold = .Bold
      Draw.Font.Size = GetFontSize(hUser)
      W = Draw.TextWidth(.Name)
      H = Draw.TextHeight(.Name)
      .XD = Int(Rnd(0, dwgAbout.W - W - 0))
      .YD = Int(Rnd(0, dwgAbout.H - H - 0))
      
    Endif
  
    If iStep < NSTEP Then
  
      eSize = GetFontSize(hUser) * iStep / NSTEP
    
      If eSize >= 1 Then
      
        ' Draw.Font.Bold = True
        ' Draw.Font.Size = eSize
        ' W = Draw.TextWidth(.Name)
        ' H = Draw.TextHeight(.Name)
        ' X = dwgAbout.W / 2 + (.XD - dwgAbout.W / 2) * iStep / NSTEP + 0.5
        ' Y = dwgAbout.H / 2 + (.YD - dwgAbout.H / 2) * iStep / NSTEP + 0.5
        ' Draw.ForeColor = Color.White
        ' Draw.Text(.Name, X + 1, Y + 1, W, H)
        ' Draw.Text(.Name, X + 1, Y - 1, W, H)
        ' Draw.Text(.Name, X - 1, Y + 1, W, H)
        ' Draw.Text(.Name, X - 1, Y - 1, W, H)
        ' Draw.ForeColor = .Color 'Color.Black
        ' Draw.Text(.Name, X, Y, W, H)

        W = .Image.W * iStep / NSTEP + 0.5
        H = .Image.H * iStep / NSTEP + 0.5
        X = dwgAbout.W / 2 + (.XD - dwgAbout.W / 2) * iStep / NSTEP + 0.5
        Y = dwgAbout.H / 2 + (.YD - dwgAbout.H / 2) * iStep / NSTEP + 0.5
        Draw.Image(.Image.Stretch(W, H, True), X, Y)
      
      Endif
    
    Else If iStep < (10 * NSTEP) Then
    
      Draw.Font.Bold = .Bold
      Draw.Font.Size = GetFontSize(hUser)
      W = Draw.TextWidth(.Name)
      H = Draw.TextHeight(.Name)
      X = .XD 'dwgAbout.W / 2 + (.XD - dwgAbout.W / 2) * iStep / NSTEP
      Y = .YD 'dwgAbout.H / 2 + (.YD - dwgAbout.H / 2) * iStep / NSTEP
      
      ' If .Bold Then
      '   Draw.ForeColor = Color.Black
      '   Draw.Text(.Name, X + 1, Y + 1, W, H)
      '   Draw.Text(.Name, X + 1, Y - 1, W, H)
      '   Draw.Text(.Name, X - 1, Y + 1, W, H)
      '   Draw.Text(.Name, X - 1, Y - 1, W, H)
      ' Endif
      
      eBlend = (iStep - NSTEP) / (9 * NSTEP)
      Draw.ForeColor = Color.Mix(.Color, Color.White, eBlend)
      'Draw.ForeColor = Color.RGB(Color[.Color].Red + (&HFF - Color[.Color].Red) * eBlend, Color[.Color].Green + (&HFF - Color[.Color].Green) * eBlend, Color[.Color].Blue + (&HFF - Color[.Color].Blue) * eBlend)
      Draw.Text(.Name, X, Y, W, H)
      
    Else

      If iIndex = $iFirst Then Inc $iFirst

    Endif

    Inc iStep
  
  End With
  
End


Public Sub timAbout_Timer()

  Dim iInd As Integer
  Dim iFirst As Integer

  Draw.Begin(dwgAbout)
  Draw.FillColor = Color.White
  Draw.FillStyle = Fill.Solid
  Draw.LineStyle = Line.None
  Draw.Rect(0, 0, dwgAbout.W, dwgAbout.H)
  'Draw.Picture($hBack, 0, 0)  
  
  iFirst = $iFirst
  For iInd = 0 To 40 
    If DrawText(iFirst + iInd) Then Break
  Next
  
  Draw.End
  
  Inc $iStep

  If iInd = 0 Then '$iFirst >= $aUser.Count Then
    $hBack.Fill(Color.White)
    'InitUser
    $iStep = 0
    $iFirst = 0
  Endif

End

Public Sub timAuthor_Timer()

  Dim Y As Integer

  Y = svwAuthor.ScrollY
  Inc svwAuthor.ScrollY
  If Y = svwAuthor.ScrollY Then
    svwAuthor.ScrollY = 0
  Endif

End
