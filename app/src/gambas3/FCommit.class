' Gambas class file

Private $sChange As String

Private Const DELIM_CHANGE As String = ("This line and the following will be ignored")

Public Sub Run(sChange As String) As Boolean

  $sChange = sChange
  Return Not Me.ShowModal()

End

Public Sub Form_Open()
  
  txtName.Text = Settings["/VersionControlUser"]
  If Not txtName.Text Then 
    txtName.SetFocus
  Else 
    txtPassword.SetFocus
  Endif 
  
  FTextEditor.ReadConfigEditor(edtChange)
  edtChange.Text = "\n---- " & DELIM_CHANGE & " ----\n\n" & $sChange
  
End


Public Sub btnOK_Click()

  Dim sChange As String
  Dim iPos As Integer
  Dim bRes As Boolean

  If Not Trim(txtName.Text) Then 
    Balloon.Warning(("Please enter the versioning user name"), txtName)
    txtName.SetFocus
    Return 
  Endif
  
  Settings["/VersionControlUser"] = Trim(txtName.Text)
  
  sChange = Trim(edtChange.Text)
  iPos = InStr(sChange, "---- " & DELIM_CHANGE)
  If iPos Then sChange = Left(sChange, iPos - 1)

  Inc Application.Busy
  bRes = VersionControl.Commit(Settings["/VersionControlUser"], txtPassword.Text, sChange)
  Dec Application.Busy
  
  If bRes Then
    Message.Error(("The project could not be committed."))
  Endif

  Me.Close(True)

End

Public Sub btnCancel_Click()

  Me.Close

End


Public Sub edtChange_Cursor()

  lblCursor.Text = CStr(edtChange.Line + 1) & ":" & CStr(edtChange.Column + 1)

End

Public Sub btnCut_Click()

  edtChange.Cut

End

Public Sub btnCopy_Click()

  edtChange.Copy

End

Public Sub btnPaste_Click()

  edtChange.Paste

End

Public Sub btnUndo_Click()

  edtChange.Undo

End

Public Sub btnRedo_Click()

  edtChange.Redo

End

Public Sub btnIndent_Click()

  edtChange.Indent

End

Public Sub btnUnindent_Click()

  edtChange.Unindent

End
