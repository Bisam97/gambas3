' Gambas class file

Static Public Workspace As Workspace
'STATIC PUBLIC Config AS Config
Static Public Project As String

Private $sKey As String
Private $hServer As CServer
Private $hConn As CConnection
Private $sName As String
Private $sTable As String
Private $bDefault As Boolean

Private $sCryptKey As String

' STATIC PUBLIC SUB MoveRandom(hForm AS Window)
'
'   DIM iW AS Integer
'   DIM iH AS Integer
'
'   iW = Min(hForm.W, FMain.Container.W)
'   iH = Min(hForm.H, FMain.Container.H)
'
'   hForm.Move(Int(Rnd(FMain.Container.W - iW)), Int(Rnd(FMain.Container.H - iH)), iW, iH)
'
' END


Private Sub FindLocalServer()

  Dim hServer As CServer
  Dim sTemp As String

  sTemp = Temp$

  Shell "pgrep postmaster > " & sTemp Wait

  If Stat(sTemp).Size Then
    hServer = New CServer("postgresql")
  Endif

  Shell "pgrep mysqld > " & sTemp Wait

  If Stat(sTemp).Size Then
    hServer = New CServer("mysql")
  Endif

  If Exist(Component.Path &/ "lib.gb.db.sqlite.la") Then
    hServer = New CServer("sqlite")
  Endif

  Kill sTemp

  RefreshServer

End



' STATIC PUBLIC SUB Main()
'
'   FindLocalServer
'
'   Form = NEW FMain
'   Form.Show
'
' END

Private Sub LoadServer() As Boolean

  Dim hServer As CServer
  Dim iNbr As Integer
  Dim iInd As Integer
  Dim sType As String
  Dim sHost As String
  Dim sUser As String
  Dim sPassword As String
  Dim sKey As String

  $sCryptKey = FPassword.Run()
  If Not $sCryptKey Then
    Me.Close
    Return True
  Endif
  If $sCryptKey = "CONT" Then Return

  'iNbr = Config.ReadInteger("/Connection/Count", 0)
  iNbr = Settings["/Connection/Count", 0]

  For iInd = 1 To iNbr

    sKey = "/Connection/#" & CStr(iInd)

    'sType = Config.ReadString(sKey &/ "Type")
    'sHost = Config.ReadString(sKey &/ "Host")
    'sUser = Config.ReadString(sKey &/ "User")
    'sPassword = Config.ReadString(sKey &/ "Password")
    sType = Settings[sKey &/ "Type"]
    sHost = Settings[sKey &/ "Host"]
    sUser = Settings[sKey &/ "User"]
    sPassword = Crypt(FromHexaString(Settings[sKey &/ "Password"]))

    hServer = New CServer(sType, sHost, sUser, sPassword)

  Next

End


Private Sub SaveServer()

  Dim hServer As CServer
  Dim iInd As Integer
  Dim sKey As String

  If Not $sCryptKey Then Return

  'Config.WriteInteger("/Connection/Count", CServer.All.Count)
  Settings["/Connection/Count"] = CServer.All.Count

  For Each hServer In CServer.All

    Inc iInd

    sKey = "/Connection/#" & CStr(iInd)

    'Config.WriteString(sKey &/ "Type", hServer.Handle.Type)
    'Config.WriteString(sKey &/ "Host", hServer.Handle.Host)
    'Config.WriteString(sKey &/ "User", hServer.Handle.Login)
    'Config.WriteString(sKey &/ "Password", hServer.Handle.Password)
    With hServer.Handle
      Settings[sKey &/ "Type"] = .Type
      Settings[sKey &/ "Host"] = .Host
      Settings[sKey &/ "User"] = .Login
      Settings[sKey &/ "Password"] = ToHexaString(Crypt(.Password))
    End With

  Next

End



Private Sub RefreshServer()

  Dim hServer As CServer
  Dim sKey As String

  Inc Application.Busy

  For Each hServer In CServer.All
    sKey = "S" & CServer.All.Key
    If Not tvwBase.Exist(sKey) Then
      tvwBase.Add(sKey, hServer.Name, Picture["img/16/host.png"])
      tvwBase.Add(sKey & "/", ("Cannot connect to server"), Picture["img/16/quit.png"], sKey)
    Endif
  Next

  tvwBase.MoveFirst
  While tvwBase.Available
    If Not CServer.All.Exist(Mid$(tvwBase.Item.Key, 2)) Then
      tvwBase.Item.Delete
      tvwBase.MoveFirst
    Else
      tvwBase.MoveNext
    Endif
  Wend

  Dec Application.Busy

End

' PUBLIC SUB Form_Resize()
' 
'   DEBUG ME.ClientW;; ME.ClientH
'   
'   'splBase.Move(0, 0, ME.ClientW, ME.ClientH)
' 
' END

Public Sub mnuQuit_Click()

  Me.Close

End

Public Sub Form_Close() 

  'Config.SaveWindow(ME, "/FMain")
  Settings.Write(Me)
  Settings["/FMain/Splitter"] = splBase.Layout
  'Config.SaveSplitterPos(splBase, "FMain", "Splitter")
  SaveServer

  If CConnection.RemoveAll() Then 
    Stop Event
    Return
  Endif
  
  CServer.RemoveAll

  'Container = NULL
  '$hConn = NULL
  '$hServer = NULL
  'Config = NULL
  'IF Message.Question(("Do you really want to quit ?"), "Yes", "No") = 1 THEN
    'CConnection.All = NULL
    'CServer.All = NULL
  'ELSE
  '  RETURN TRUE
  'ENDIF

End

Private Sub RefreshUser(sParent As String)

  Dim hServer As CServer
  Dim sName As String
  Dim sKey As String
  Dim sIcon As String

  Inc Application.Busy

  If tvwBase[sParent].Children = 0 Then
    tvwBase.MoveParent
    sParent = tvwBase.Item.Key
  Endif

  hServer = CServer.All[Split(Mid$(sParent, 2), "/")[0]]

  tvwBase[sParent].Clear

  For Each sName In hServer.Users
    sKey = "U" & hServer.Key &/ sName
    If hServer.IsAdmin(sName) Then
      sIcon = "admin.png"
    Else
      sIcon = "user.png"
    Endif
    tvwBase.Add(sKey, sName, Picture["img/16" &/ sIcon], sParent)
  Next

  Dec Application.Busy

End


Public Sub RefreshDatabase(sParent As String)

  Dim hServer As CServer
  Dim sKey As String
  Dim sName As String
  Dim cKey As New Collection

  Inc Application.Busy

  While Left$(sParent) <> "S"
    tvwBase.MoveTo(sParent)
    tvwBase.MoveParent
    If Not tvwBase.Available Then Return
    sParent = tvwBase.Item.Key
  Wend

  hServer = CServer.All[Split(Mid$(sParent, 2), "/")[0]]

  For Each sName In hServer.Databases
    sKey = "D" & hServer.Key &/ sName
    cKey[sKey] = True
    If Not tvwBase.Exist(sKey) Then
      tvwBase.Add(sKey, sName, Picture["img/16/database.png"], sParent)
    Endif
    If tvwBase[sKey].Children = 0 And Not tvwBase[sKey].Expanded Then
      tvwBase.Add(sKey &/ "/", ("Cannot connect to database"), Picture["img/16/quit.png"], sKey)
    Endif
  Next

  tvwBase.MoveTo(sParent)
  tvwBase.MoveChild

  While tvwBase.Available

    If Left$(tvwBase.Item.Key) = "D" Then
      If Not cKey.Exist(tvwBase.Item.Key) Then
        tvwBase.Item.Delete
        Continue
      Endif
    Endif

    tvwBase.MoveNext
  Wend

  Dec Application.Busy

End


Public Sub RefreshTable(sParent As String)

  Dim hConn As CConnection
  Dim sName As String
  Dim aKey As String[]
  Dim sKey As String
  Dim bSystem As Boolean

  Inc Application.Busy

  While Left$(sParent) <> "D"
    tvwBase.MoveTo(sParent)
    tvwBase.MoveParent()
    sParent = tvwBase.Item.Key
  Wend

  aKey = Split(Mid$(sParent, 2), "/")

  hConn = CConnection.Get(CServer.All[aKey[0]], aKey[1])

  tvwBase[sParent].Clear

  For Each sName In hConn.Tables
    sKey = "T" & hConn.Key &/ sName
    tvwBase.Add(sKey, sName, Picture["img/16/table.png"], sParent)
  Next

  If hConn.ShowSystemTables Then
    For Each sName In hConn.SystemTables
      sKey = "T" & hConn.Key &/ sName
      tvwBase.Add(sKey, sName, Picture["img/16/system-table.png"], sParent)
    Next
  Endif

  Dec Application.Busy

End


Public Sub tvwBase_Expand()

  Dim sKey As String
  Dim sParent As String
  Dim sChild As String
  Dim sErr As String
  Dim sName As String
  Dim hServer As CServer
  Dim hConn As CConnection
  Dim aKey As String[]
  Dim sRemove As String

  sParent = tvwBase.Item.Key
  tvwBase.MoveChild
  sChild = tvwBase.Item.Key

  If Right$(sChild) = "/" Then

    Inc Application.Busy

    sRemove = sChild

    Select Case Left$(sParent)

      Case "S"

        hServer = CServer.All[Mid$(sParent, 2)]
        sErr = hServer.Exec()
        If sErr Then
          Dec Application.Busy
          tvwBase[sParent].Expanded = False
          Message.Error(("Cannot connect to database") & "\n\n" & sErr)
          Return
        Endif

        tvwBase[sParent].Picture = Picture["img/16/host-open.png"]

        sKey = "G" & hServer.Key
        tvwBase.Add(sKey, ("Users"), Picture["img/16/users.png"], sParent)
        tvwBase.Add(sKey & "/", "",, sKey)

        RefreshDatabase(sKey)

      Case "G"

        RefreshUser(sParent)

      Case "D"

        aKey = Split(Mid$(sParent, 2), "/")
        hConn = CConnection.Get(CServer.All[aKey[0]], aKey[1])

        sErr = hConn.Open()
        If sErr Then
          Dec Application.Busy
          tvwBase[sParent].Expanded = False
          Message.Error(Subst(("Cannot connect to database '&1'"), hConn.Name) & "\n\n" & sErr)
          Return
        Endif

        tvwBase[sParent].Picture = Picture["img/16/database-open.png"]

        RefreshTable(sParent)

    End Select

    Try tvwBase.Remove(sRemove)
    Dec Application.Busy

  Endif

End

Public Sub mnuNew_Click()

  If FServer.Run() Then Return
  RefreshServer

End

Public Sub mnuRefresh_Click()

  tvwBase.Clear
  CConnection.RemoveAll

  RefreshServer

End

Public Sub tvwBase_Menu()

  Dim sKey As String
  Dim bEnabled As Boolean
  Dim aKey As String[]
  Dim bVisible As Boolean

  If Not tvwBase.Current Then
    mnuConnection.Popup
    Return
  Endif

  sKey = tvwBase.Current.Key
  aKey = Split(Mid$(sKey, 2), "/")
  $sKey = sKey

  Select Case Left$(sKey)

    Case "S", "G"

      $hServer = CServer.All[aKey[0]]

      bVisible = tvwBase["S" & $hServer.Key].Expanded
      mnuRefreshServer.Visible = Not tvwBase.Exist("S" & $hServer.Key & "/")
      mnuCreateDatabase.Visible = bVisible
      mnuCreateUser.Visible = bVisible
      mnuOpenConnection.Visible = Not bVisible

      mnuPasteDatabase2.Visible = bVisible And Clipboard.Format = "text/x-gambas-database"

      mnuServer.Popup

    Case "D", "Z"

      $hServer = CServer.All[aKey[0]]
      $sName = aKey[1]

      $hConn = CConnection.All[Mid$(sKey, 2)]

      mnuOpenDatabase.Visible = IsNull($hConn)
      mnuCreateTable.Visible = Not Null?($hConn)
      mnuRequest.Visible = Not Null?($hConn)
      mnuImport.Visible = Not Null?($hConn)
      mnuRefreshDatabase.Visible = Not Null?($hConn)
      mnuCloseDatabase.Visible = Not Null?($hConn)
      mnuMakeCode.Visible = Project And Not Null?($hConn)
      mnuSepEncoding.Visible = Not Null?($hConn)
      mnuSepEncoding2.Visible = Not Null?($hConn)
      'mnuCopyDatabase.Visible = NOT IsNull($hConn)

      With mnuUseDatabaseEncoding
        .Visible = Not Null?($hConn)
        If Not IsNull($hConn) Then
          'DEBUG $hConn.Name
          .Checked = $hConn.UseEncoding
          .Text = Subst(("&Use encoding (&1)"), $hConn.Handle.Charset)
        Endif
      End With

      With mnuShowSystemTables
        .Visible = Not Null?($hConn)
        If Not IsNull($hConn) Then
          .Checked = $hConn.ShowSystemTables
        Endif
      End With

      mnuPasteDatabase.Visible = Clipboard.Format = "text/x-gambas-database"
      mnuPasteTable2.Visible = Clipboard.Format = "text/x-gambas-table" And Not IsNull($hConn)

      mnuDatabase.Popup

    Case "U"

      $hServer = CServer.All[aKey[0]]
      $sName = aKey[1]

      If $bDefault Then
        mnuEditUser_Click
      Else
        mnuUser.Popup
      Endif

    Case "T"

      $hServer = CServer.All[aKey[0]]
      $sName = aKey[1]
      $sTable = aKey[2]
      $hConn = CConnection.Get($hServer, $sName)

      tvwBase.MoveTo(sKey)
      tvwBase.MoveParent()
      bEnabled = Left$(tvwBase.Item.Key) <> "Z"
      mnuDeleteTable.Enabled = bEnabled
      'mnuEditTable.Enabled = bEnabled
      mnuRenameTable.Enabled = bEnabled

      mnuPasteTable.Visible = Clipboard.Format = "text/x-gambas-table"

      If $bDefault Then
        mnuOpenTable_Click
      Else
        mnuTable.Popup
      Endif

  End Select

  $bDefault = False

End

Public Sub mnuCreateUser_Click()

  If FUser.Run($hServer) Then Return

  RefreshUser($sKey)

End

Public Sub mnuEditUser_Click()

  If FUser.Run($hServer, $sName) Then Return

  RefreshUser($sKey)

End

Public Sub mnuDeleteUser_Click()

  If Message.Delete(Subst(("Do you really want to delete the user '&1' ?"), $sName), ("Delete"), ("Cancel")) = 2 Then Return

  If $hServer.DeleteUser($sName) Then Return

  RefreshUser($sKey)

End

Public Sub tvwBase_Activate()

  If tvwBase.Current.Children = 0 Then
    $bDefault = True
    tvwBase_Menu
  Endif

End

Public Sub mnuCloseServer_Click()

  If Message.Delete(Subst(("Do you really want to remove the following server ?\n\n&1"), $hServer.Name), ("Remove"), ("Cancel")) = 2 Then Return

  CServer.Remove($hServer)
  RefreshServer

End

Public Sub mnuCreateDatabase_Click()

  If FDatabase.Run($hServer) Then Return

  RefreshDatabase($sKey)

End

Public Sub mnuDeleteDatabase_Click()

  If Message.Delete(Subst(("Do you really want to delete the database '&1' ?\n\nBE CAREFUL ! All your data will be lost."), $sName), ("Delete"), ("Cancel")) = 2 Then Return

  If $hServer.DeleteDatabase($sName) Then Return

  RefreshDatabase($sKey)

End

Public Sub mnuEditTable_Click()

  $hConn.OpenTable($sTable, False)

End

Public Sub mnuOpenTable_Click()

  $hConn.OpenTable($sTable, True)

End

Public Sub mnuAbout_Click()

  FAbout.Run

End

Public Sub mnuCreateTable_Click()

  Dim aType As String[]
  Dim sTable As String

  If $hConn.Handle.Type = "mysql" Then
    'aType = [ "InnoDB", "BDB", "HEAP", "ISAM", "MERGE", "MRG_MYISAM", "MYISAM" ]
    aType = ["MyISAM", "MERGE", "HEAP", "InnoDB", "BDB", "ISAM"]
  Endif

  If FNewTable.Run("", "InnoDB", aType) Then Return

  If $hConn.CreateTable(FNewTable.Name, FNewTable.Type) Then Return

  RefreshTable($sKey)

End

Public Sub mnuImport_Click()
  
  Dialog.Title = ("Select the CSV file to import")
  Dialog.Filter = ["*.csv", ("CSV files"), "*", ("All files")]
  Dialog.Path = Settings["/Import/Path"]
  If Dialog.OpenFile() Then Return
  Settings["/Import/Path"] = Dialog.Path
  FImport.Run($hConn, Dialog.Path)
  mnuRefreshDatabase_Click
  
End


Public Sub mnuDeleteTable_Click()

  If Message.Delete(Subst(("Do you really want to delete the table '&1' ?"), $sTable), ("Delete"), ("Cancel")) <> 1 Then Return

  If $hConn.DeleteTable($sTable) Then Return

  RefreshTable($sKey)

End


Public Sub mnuRefreshDatabase_Click()

  $hConn.RefreshTree
  'RefreshTable($sKey)

End


Public Sub mnuRequest_Click()

  $hConn.OpenRequest()

End


Public Sub mnuRefreshServer_Click()

  $hServer.Close
  $hServer.Exec
  tvwBase.Remove("S" & $hServer.Key)
  RefreshServer

Catch

  Message(("Cannot refresh connection.") & "\n\n" & Error.Text)

End


Public Sub mnuCloseDatabase_Click()

  If $hConn.Close() Then Return

  With tvwBase[$sKey]
    .Expanded = False
    .Clear()
    .Picture = Picture["img/16/database.png"]
  End With

  CConnection.Remove($hConn)
  RefreshDatabase("S" & $hServer.Key)

Catch

  Message(("Cannot close database.") & "\n\n" & Error.Text)

End


' PUBLIC SUB mnuWindow_Show()
'
'   DIM hWin AS Window
'   DIM iInd AS Integer
'   DIM hMenu AS Menu
'
'   mnuWindow.Children.Clear
'
'   IF wrkBase.Children.Count = 0 THEN
'     hMenu = NEW Menu(mnuWindow)
'     hMenu.Text = ("No window")
'     hMenu.Enabled = FALSE
'     RETURN
'   ENDIF
'
'   hMenu = NEW Menu(mnuWindow) AS "mnuCascade"
'   hMenu.Text = ("&Cascade")
'
'   hMenu = NEW Menu(mnuWindow) AS "mnuTile"
'   hMenu.Text = ("&Tile")
'
'   hMenu = NEW Menu(mnuWindow)
'
'   FOR EACH hWin IN wrkBase.Children
'     INC iInd
'     hMenu = NEW Menu(mnuWindow) AS "mnuOneWindow"
'     hMenu.Text = "&" & CStr(iInd) & " " & hWin.Text
'     hMenu.Checked = hWin = wrkBase.ActiveWindow
'     hMenu.Tag = hWin
'   NEXT
'
' END


Public Sub mnuOneWindow_Click()

  Last.Tag.Show

End


' PUBLIC SUB mnuCascade_Click()
'
'   wrkBase.Arrange(wrkBase.Cascade)
'
' END
'
'
' PUBLIC SUB mnuTile_Click()
'
'   wrkBase.Arrange(wrkBase.Tile)
'
' END
'

' STATIC PUBLIC SUB Main()
'   
'   Application.Font = Font["sans,6"]
'   FMain.Show
'   
' END


Public Sub Form_Open()

  'IF Application.Args.Count = 2 THEN
  '  IF Application.Args[1] = "-debug" THEN
  '    DB.Debug = TRUE
  '  ENDIF
  'ENDIF

  'DB.Debug = TRUE

  'Config.LoadWindow(ME, "/FMain")
  Workspace = wrkBase

  If LoadServer() Then Return
  RefreshServer

  Settings.Read(Me)
  'Config.LoadSplitterPos(splBase, "/FMain", "Splitter")
  splBase.Layout = Settings["/FMain/Splitter"]

  If Application.Args.Count >= 2 Then
    Project = Application.Args[1]
  Endif

End


Public Sub mnuUseDatabaseEncoding_Click()

  'DEBUG $hConn.Name
  mnuUseDatabaseEncoding.Checked = Not mnuUseDatabaseEncoding.Checked
  $hConn.UseEncoding = mnuUseDatabaseEncoding.Checked

End


Public Sub mnuShowSystemTables_Click()

  Last.Checked = Not Last.Checked
  $hConn.ShowSystemTables = Last.Checked
  RefreshTable($sKey)

End


Public Sub mnuScan_Click()

  FindLocalServer

End

Public Sub mnuKillAll_Click()

  If Message.Delete(("Do you really want to remove all servers ?"), ("Remove all"), ("Cancel")) <> 1 Then Return

  CServer.RemoveAll
  RefreshServer

End

Public Sub mnuOpenConnection_Click()

  tvwBase[$sKey].Expanded = True

End

Public Sub mnuOpenDatabase_Click()

  tvwBase[$sKey].Expanded = True

End

Public Sub mnuPasteDatabase_Click()

  Dim aData As String[]

  aData = Split(Clipboard.Paste("text/x-gambas-database"), "\n")

  With FPasteDatabase
    .SrcKey = aData[0]
    .SrcName = aData[1]
    .SrcDatabase = aData[2]
    .DestKey = $hServer.Key
    .DestName = $hServer.Name
  End With

  If Not FPasteDatabase.ShowModal() Then Return

  RefreshDatabase("S" & $hServer.Key)

End

Public Sub mnuPasteTable_Click()

  Dim aData As String[]

  aData = Split(Clipboard.Paste("text/x-gambas-table"), "\n")

  With FPasteTable
    .SrcKey = aData[0]
    .SrcName = aData[1]
    .SrcDatabase = aData[2]
    .SrcTable = aData[3]
    .DestKey = $hServer.Key
    .DestName = $hServer.Name
    .DestDatabase = $sName
  End With

  If Not FPasteTable.ShowModal() Then Return

  $hConn.RefreshTree
  'RefreshTable("D" & $hServer.Key &/ $sName)

End

Public Sub mnuCopyDatabase_Click()

  Dim sData As String

  sData = $hServer.Key & "\n" & $hServer.Name & "\n" & $sName

  Clipboard.Copy(sData, "text/x-gambas-database")

End

Public Sub mnuCopyTable_Click()

  Dim sData As String

  sData = $hServer.Key & "\n" & $hServer.Name & "\n" & $sName & "\n" & $sTable

  Clipboard.Copy(sData, "text/x-gambas-table")

End

Public Sub mnuMakeCode_Click()

  With FCode
    .Server = $hServer
    .Database = $sName
    .ShowDialog
  End With

End


Static Public Function GetProjectIcon(sPath As String, iSize As Integer) As Picture

  Dim hFile As File
  Dim sLig As String
  Dim hImage As Image
  Dim hPict As Picture

  Open sPath &/ ".project" For Read As #hFile

  While Not Eof(hFile)
    Line Input #hFile, sLig
    If Left$(sLig, 5) = "Icon=" Then
      sPath = sPath &/ Mid$(sLig, 6)
      Try hImage = Image.Load(sPath)
      Break
    Endif
  Wend

  Close #hFile

Finally

  If Not hImage Then
    hImage = Picture["icon:/48/gambas"].Image
  Endif

  Return hImage.Stretch(iSize, iSize, True).Picture

End


Private Function Crypt(sStr As String) As String

  Dim iInd As Integer
  Dim iPos As Integer
  Dim sRes As String

  For iInd = 1 To Len(sStr)
    Inc iPos
    If iPos > Len($sCryptKey) Then iPos = 1
    sRes = sRes & Chr$(Asc(sStr, iInd) Xor Asc($sCryptKey, iPos) Xor 13)
  Next

  Return sRes

End


Private Function ToHexaString(sStr As String) As String

  Dim iInd As Integer
  Dim sRes As String

  For iInd = 1 To Len(sStr)
    sRes = sRes & Hex$(Asc(sStr, iInd), 2)
  Next

  Return sRes

End


Private Function FromHexaString(sStr As String) As String

  Dim iInd As Integer
  Dim sRes As String

  For iInd = 1 To Len(sStr) Step 2
    sRes = sRes & Chr$(Val("&H" & Mid$(sStr, iInd, 2)))
  Next

  Return sRes

End

Public Sub mnuDebug_Click()

  Inc DB.Debug
  mnuDebug.Checked = DB.Debug

End

' Public Sub Form_KeyPress()
' 
'   If Key.Alt And If Key.Code = Key["M"] Then Me.Menus.Visible = Not Me.Menus.Visible
' 
' End
