' Gambas class file

Public Connection As CConnection

Private $rData As Result
Private $sCharset As String
Private $sPath As String

Public Sub _new(hConn As CConnection)

  Connection = hConn
  $sCharset = Connection.GetCharset()

  RefreshTitle

  'FMain.MoveRandom(ME)

  btnNew_Click

  splRequest.Layout = "500,500"

End


Public Sub EncodingChange()

  $sCharset = Connection.GetCharset()
  tbvData.Refresh

End


Private Sub RefreshTitle()

  Dim sTitle As String

  If $sPath Then
    sTitle = File.Name($sPath)
  Else
    sTitle = ("SQL request")
  Endif

  sTitle = Connection.Server.Name & " - " & Connection.Name & " - " & sTitle

  Me.Title = sTitle

End


Private Sub ReadData()

  Dim hField As ResultField
  Dim sField As String
  Dim iInd As Integer
  Dim iLen As Integer
  Dim sReq As String
  Dim iPos As Integer

  Inc Application.Busy

  lblRequest.Text = ("No records.")

  tbvData.Rows.Count = 0

  sReq = Trim(txtRequest.Text)

  For Each sReq In Split(sReq, ";", Chr$(34) & "'")

    sReq = Trim(sReq)

    While InStr("#;'", Left$(sReq))
      iPos = InStr(sReq, "\n")
      If iPos Then
        sReq = Trim(Mid$(sReq, iPos + 1))
      Else
        sReq = ""
      Endif
    Wend

    If Not sReq Then Continue

    'PRINT "Exec: "; sReq
    $rData = Connection.Handle.Exec(sReq)

  Next

  If Not $rData Then

    panData.Visible = False

  Else

    tbvData.Columns.Count = $rData.Fields.Count

    For Each hField In $rData.Fields

      With hField

        tbvData.Columns[iInd].Text = .Name
        tbvData.Columns[iInd].Width = Connection.WidthFromType(tbvData, .Type, .Length, .Name)

      End With

      Inc iInd
    Next

    tbvData.Rows.Count = $rData.Count

    If $rData.Count = 1 Then
      lblCount.Text = ("One record")
    Else If $rData.Count > 1 Then
      lblCount.Text = Subst(("&1 records"), $rData.Count)
    Endif

    panData.Visible = $rData.Count > 0

  Endif

Finally

  Dec Application.Busy

Catch

  lblRequest.Text = ("Cannot exec request.") & "\n\n" & Error.Text
  panData.Hide

End


Public Sub tbvData_Data(Row As Integer, Column As Integer)

  $rData.MoveTo(Row)

  If $sCharset Then
    tbvData.Data.Text = Conv(Str($rData[tbvData.Columns[Column].Text]), $sCharset, Desktop.Charset)
  Else
    tbvData.Data.Text = Str($rData[tbvData.Columns[Column].Text])
  Endif

Catch

  tbvData.Data.Text = Str($rData[tbvData.Columns[Column].Text])

End


Public Sub btnNew_Click()

  tbvData.Rows.Count = 0
  lblRequest.Text = ("Enter your request...")
  panData.Hide
  $rData = Null
  txtRequest.Text = ""
  txtRequest.SetFocus

End

Public Sub btnRun_Click()

  ReadData

End

' PUBLIC SUB splRequest_Resize()
'
'   WITH panRequest
'     tbvData.Move(0, 0, .ClientW, .ClientH)
'     lblRequest.Move(8, 8, .ClientW - 16, .ClientH - 16)
'   END WITH
'
' END

Public Sub btnLoad_Click()

  If $sPath Then Dialog.Path = $sPath
  Dialog.Filter = ["*.sql", ("SQL script files"), "*", ("All files")]
  If Dialog.OpenFile() Then Return

  $sPath = Dialog.Path
  txtRequest.Text = File.Load($sPath)
  RefreshTitle

Catch

  Message.Error(("Cannot load SQL script file.") & "\n\n" & Error.Text)

End

Public Sub btnSave_Click()

  If Not Trim(txtRequest.Text) Then Return

  Dialog.Filter = ["*.sql", ("SQL script files"), "*", ("All files")]
  Dialog.Path = $sPath
  If Dialog.SaveFile() Then Return

  $sPath = Dialog.Path
  File.Save($sPath, txtRequest.Text)
  RefreshTitle

Catch

  Message.Error(("Cannot save SQL script file.") & "\n\n" & Error.Text)

End


Public Sub btnClose_Click()

  Me.Close

End


Public Sub txtRequest_KeyPress()

  If Key.Code = Key["F5"] Then btnRun_Click

End

Private Function GetData(sSep As String) As String

  Dim sTemp As String
  Dim hFile As File
  Dim bFirst As Boolean
  Dim bAll As Boolean
  Dim bOK As Boolean
  Dim hField As ResultField

  Inc Application.Busy

  sTemp = Temp$

  hFile = Open sTemp For Create

  For Each hField In $rData.Fields

    If Not bFirst Then
      bFirst = True
    Else
      Print #hFile, sSep;
    Endif

    Print #hFile, Chr$(34); hField.Name; Chr$(34);

  Next

  Print #hFile

RETRY:

  For Each $rData

    If Not bAll Then
      If Not tbvData.Rows[$rData.Index].Selected Then Continue
    Endif

    bOK = True
    bFirst = False

    For Each hField In $rData.Fields

      If Not bFirst Then
        bFirst = True
      Else
        Print #hFile, sSep;
      Endif

      If hField.Type = gb.String Then
        Print #hFile, Chr$(34); Replace(Str($rData[hField.Name]), Chr$(34), Chr$(34) & Chr$(34)); Chr$(34);
      Else
        Print #hFile, Chr$(34); Str($rData[hField.Name]); Chr$(34);
      Endif

    Next

    Print #hFile

  Next

  If Not bAll Then
    If Not bOK Then
      bAll = True
      Goto RETRY
    Endif
  Endif

  Close #hFile

  Dec Application.Busy

  Return sTemp

End


Public Sub btnSaveData_Click()

  Dim sTemp As String

  'Dialog.Path = FMain.Config.ReadString("/Request/Save", "/home/benoit")
  Dialog.Path = Settings["/Request/Save"]
  'DEBUG Dialog.Path
  Dialog.Filter = ["*.csv", ("CSV files"), "*", ("All files")]

  If Dialog.SaveFile() Then Return

  If Not File.Ext(Dialog.Path) Then
    Dialog.Path = File.Dir(Dialog.Path) &/ File.BaseName(Dialog.Path) & ".csv"
  Endif

  Settings["/Request/Save"] = Dialog.Path 

  sTemp = GetData(Chr$(9))
  Try Kill Dialog.Path
  Copy sTemp To Dialog.Path
  Kill sTemp

Catch

  Message.Error(("Cannot export data.") & "\n\n" & Error.Text)
  Try Kill sTemp

End

Public Sub btnCopyData_Click()

  Dim sTemp As String

  sTemp = GetData(",")
  Clipboard.Copy(File.Load(sTemp))
  Kill sTemp

End

Public Sub Form_GotFocus()

  txtRequest.SetFocus  

End
