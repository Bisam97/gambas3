' Gambas class file

STATIC PUBLIC All AS NEW Object[]

PUBLIC Name AS String
PUBLIC LastName AS String
PUBLIC Mail AS String
PUBLIC Country AS String
PUBLIC Works AS NEW String[]
PUBLIC Translator AS Boolean
PUBLIC Developer AS Boolean
PUBLIC Translation AS NEW String[]

PUBLIC SUB _new(hFile AS File)
  
  DIM sLine AS String
  DIM iPos AS Integer
  
  LINE INPUT #hFile, sLine
  IF Left(sLine) = "-" THEN LINE INPUT #hFile, sLine
  Name = Trim(sLine)
  LINE INPUT #hFile, Mail
  LINE INPUT #hFile, Country

  WHILE NOT Eof(hFile)
    LINE INPUT #hFile, sLine
    sLine = Trim(sLine)
    IF NOT sLine THEN CONTINUE
    IF Left(sLine) = "-" THEN BREAK 
    IF Right(sLine) = "." THEN sLine = Left(sLine, -1)
    IF InStr(sLine, "translation") THEN 
      Translator = TRUE
      Translation.Add(Trim(Replace(sLine, "translation", "")))
    ELSE 
      Works.Add(sLine)
      Developer = TRUE
    ENDIF
  WEND 
  
  iPos = InStr(Name, " ")
  IF iPos THEN 
    LastName = Mid$(Name, iPos + 1)
  ELSE 
    LastName = Name
  ENDIF
  
  All.Add(ME)
  
END

PUBLIC SUB _compare(hAuthor AS CAuthor) AS Integer
  
  RETURN String.Comp(LastName, hAuthor.LastName, gb.Language + gb.Text)
  
END

PRIVATE SUB GetMail(sMail AS String) AS String
  
  sMail = Replace(sMail, "@", "_AT_")
  sMail = Replace(sMail, ".", "_DOT_")
  RETURN sMail
  
END

PUBLIC SUB GetLink() AS String
  
  RETURN "<a href=\"mailto:" & GetMail(Mail) & "\">" & Name & "</a>"
  
END
