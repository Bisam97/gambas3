' Gambas class file

Static Public All As New Collection
Static Private $iKey As Integer
Static Public WhoImplement As New Collection

Public Key As String
Public Name As String

Public Require As New String[]
Public Exclude As New String[]
Public Implement As New String[]
Public Need As New String[]
Public SortKey As Integer

Static Private Sub InitComponent(sFile As String, bUser As Boolean)
  
  Dim hComponent As CComponent
  Dim hFic As File
  Dim sInfo As String
  Dim sLig As String
  Dim sKey As String

  Dim iPos As Integer
  
  Dim sLib As String
  Dim iKey As Integer
  Dim sGroup As String

  If bUser Then
    hFic = Open Component.UserPath &/ sFile 
  Else
    hFic = Open Component.Path &/ sFile 
  Endif

  Line Input #hFic, sLig
  sLig = Trim(sLig)

  If sLig = "[Component]" Then

    hComponent = New CComponent
    hComponent.Key = File.BaseName(sFile)
    hComponent.Name = hComponent.Key '$cName[hComponent.Key]

    While Not Eof(hFic)

      Line Input #hFic, sLig
      sLig = Trim(sLig)

      If Not sLig Then Continue
      If Left(sLig) = ";" Or Left(sLig) = "'" Then Continue

      If sLig = "[Component]" Then Break

      If Left(sLig) = "[" And If Right(sLig) = "]" Then
        sGroup = Mid$(sLig, 2, -1)
        Continue
      Endif

      iPos = InStr(sLig, "=")
      If iPos = 0 Then Continue

      sKey = Left$(sLig, iPos - 1)
      sLig = Mid$(sLig, iPos + 1)

      With hComponent

        If sKey = "Key" Then
          .Key = sLig
        Else If sKey = "Name" Then 
        Else If sKey = "Require" Or sKey = "Requires" Then
          .Require = Split(LCase(sLig))
        Else If sKey = "Exclude" Or sKey = "Excludes" Then
          .Exclude = Split(LCase(sLig))
        Else If sKey = "Implement" Or sKey = "Implements" Then
          .Implement = Split(LCase(sLig))
        Else If sKey = "Need" Or sKey = "Needs" Then
          .Need = Split(LCase(sLig))
        ' ELSE IF sKey = "Alpha" THEN
        '   .State = UNSTABLE
        ' ELSE IF sKey = "State" THEN 
        '   .State = CInt(sLig)
        Endif

      End With

    Wend

    'hComponent.User = bUser
  
    If hComponent.Key Then
      All[hComponent.Key] = hComponent
    Endif

  Endif

  Close #hFic
  
Catch 

  Debug "Unable to read component file:";; sFile
  
End


Static Public Sub _init()

  Dim aComp As String[]
  Dim sFile As String

  aComp = Dir(Component.Path, "*.component").Sort()
  For Each sFile In aComp
    InitComponent(sFile, False)
  Next

  If Exist(Component.UserPath) Then
    aComp = Dir(Component.UserPath, "*.component").Sort()
    For Each sFile In aComp
      InitComponent(sFile, True)
    Next
  Endif
  
  SortComponents
  
  'Force gb.gui en premier lieu
  WhoImplement["form"] = ["gb.gui"]
  GetWhoImplement
'PRINT "ok"
End

Static Private Sub GetWhoImplement()

  Dim sComp As String
  Dim s As String
  Dim hComp As CComponent

  For Each hComp In CComponent.All
  
    For Each s In hComp.Implement
    
      If WhoImplement[s] = Null Then WhoImplement[s] = New String[]
      If Not WhoImplement[s].Exist(hComp.Name) Then WhoImplement[s].Add(hComp.Name)
      
    Next 
    
  Next

End

Static Public Sub Exit()

  All = Null

End

Public Sub CalcSortKey()

  Dim sComp As String
  Dim sNeed As String
  Dim hComp As CComponent

  'DEBUG ">>>";; Key

  If SortKey = 0 Then 

    If Require.Count Or If Need.Count Then

      For Each sComp In Require
        hComp = All[sComp]
        If Not hComp Then Error "gbs3: warning: cannot find component "; sComp; " required by "; Me.Name
        If hComp <> Me Then hComp.CalcSortKey
      Next
  
      For Each sNeed In Need
        For Each hComp In All
          If hComp.Implement.Find(sNeed) >= 0 Then
            hComp.CalcSortKey
          Endif
        Next
      Next

    Endif
    
    Inc $iKey
    SortKey = $iKey

  Endif
  
  'DEBUG "<<<";; Key;; "=";; SortKey

End

Static Private Sub SortComponents()

  Dim iMax As Integer
  Dim hComponent As CComponent
  Dim cNewAll As New Collection
  Dim iInd As Integer

  For Each hComponent In All
    hComponent.CalcSortKey
    iMax = Max(iMax, hComponent.SortKey)
    'PRINT hComponent.Key;; hComponent.SortKey
  Next

  For iInd = 1 To iMax
    For Each hComponent In All
      If hComponent.SortKey = iInd Then
        cNewAll.Add(hComponent, hComponent.Key)
        'DEBUG hComponent.Key;; hComponent.SortKey
      Endif
    Next
  Next

  All = cNewAll

End
