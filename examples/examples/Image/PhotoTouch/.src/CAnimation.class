' Gambas class file

Private $hObject As Object
Private $sProperty As String
Private $fTarget As Float
Private $fTime As Float
Private $hTimer As Timer

Public Sub _new(hObject As Control, sProperty As String, fTarget As Float, iTime As Integer)
  
  $hObject = hObject
  $sProperty = sProperty
  $fTarget = fTarget
  $fTime = Timer + iTime / 1000
  
  $hTimer = New Timer As "Timer"
  $hTimer.Delay = 50
  $hTimer.Start
  
End

Public Sub Timer_Timer()
  
  Dim fValue As Float
  Dim iSign As Integer
  
  fValue = Object.GetProperty($hObject, $sProperty)
  
  iSign = Sgn($fTarget - fValue)
  fValue += ($fTarget - fValue) / (1000 * ($fTime - Timer) / $hTimer.Delay)
  If Sgn($fTarget - fValue) <> iSign Then
    Object.SetProperty($hObject, $sProperty, $fTarget)
    {Stop}
  Else
    Object.SetProperty($hObject, $sProperty, fValue)
  Endif
  
Catch
  
  {Stop}
  
End

Public Sub Stop()
  
  $hTimer = Null
  
End
