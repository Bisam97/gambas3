' Gambas class file

Private $hPlayer As MediaPlayer
Private $hOutput As MediaControl
Private $aVisualisation As MediaControl[]

Private $fPos As Float
Private $fLength As Float
Private $sInfo As String
Private $iVisualisation As Integer
Private $fVolume As Float
Private $bSuspend As Boolean

Public Sub _new()
  
  Application.MainWindow = Me
  
End

Private Sub AddVisualisation(sType As String, sTitle As String)
  
  Dim hVisualisation As MediaControl
  
  If sType Then
    hVisualisation = New MediaControl($hPlayer, sType)
    hVisualisation.Tag = sTitle
  Endif
  
  $aVisualisation.Add(hVisualisation)
  
Catch
  
  Error sType; ": "; Error.Text
  
End

Private Sub MakeMediaPlayer()
  
  $hPlayer = New MediaPlayer As "MediaPlayer"
  
  $hOutput = New MediaControl($hPlayer, "ximagesink")  
  $hPlayer.Video.Output = $hOutput
  
  $aVisualisation = New MediaControl[]
  AddVisualisation("", "")
  AddVisualisation("goom", "Goom")
  AddVisualisation("libvisual_bumpscope", "Bump")
  AddVisualisation("libvisual_corona", "Corona")
  ' Buggy infinite
  'AddVisualisation("libvisual_infinite", "Infinite")
  AddVisualisation("libvisual_jakdaw", "Jakdaw")
  AddVisualisation("libvisual_jess", "Jess")
  AddVisualisation("monoscope", "Mono")
  AddVisualisation("libvisual_oinksie", "Oinksie")
  AddVisualisation("libvisual_lv_scope", "Scope")
  AddVisualisation("spacescope", "Space")
  AddVisualisation("spectrascope", "Spectra")
  AddVisualisation("synaescope", "Synae")
  AddVisualisation("wavescope", "Wave")
  
  $iVisualisation = 0
  UpdateVisualisation
  
End

Public Sub GetButton(sKey As String) As CButton
  
  Return FControl.Controls["#" & sKey]
  
End


Public Sub CreateButtons(aButton As String[], hParent As Container)
  
  Dim sKey As String
  Dim sImg As String
  Dim hPanel As Panel
  Dim hButton As CButton
  Dim iPos As Integer
  
  For Each sKey In aButton
    
    If sKey = "<->" Then
      hPanel = New Panel(hParent)
      hPanel.Expand = True
      hPanel.Resize(8, 48)
    Else If sKey = "-" Then
      hPanel = New Panel(hParent)
      hPanel.Resize(8, 48)
    Else
      
      sImg = sKey
      iPos = InStr(sImg, "#")
      If iPos Then sImg = Left(sImg, iPos - 1)
      
      hButton = New CButton(hParent) As "Button"
      hButton.Resize(48, 48)
      hButton.Image = Image.Load(sImg & ".png")
      hButton.Tag = sKey
      hButton.Name = "#" & sKey
      'If cTooltip Then hButton.Tooltip = cTooltip[sImg]
      'If $cShortcut Then hButton.Shortcut = $cShortcut[sImg]
      '$cButton[hButton.Tag] = hButton
    Endif
  Next
  
End


Public Sub Form_Open()

  Dim sButton As String
  Dim hButton As CButton
  Dim hPanel As Panel
  
  MakeMediaPlayer
  
  FControl.Show
  
  RefreshVolume
  
End

Public Sub Form_KeyPress()
  
  If Key.Code = Key.Escape Then Action("quit")
  
End

Private Sub Action(sAction As String)

  Dim fPos As Float
  Dim iState As Integer

  Select sAction
    
    Case "eject"
      Dialog.Title = ("Select a media file")
      If Not Dialog.OpenFile() Then 
        Action("stop")
        $hPlayer.Subtitles.Enabled = False
        $hPlayer.URL = Media.URL(Dialog.Path)
        If Exist(File.SetExt(Dialog.Path, "srt")) Then 
          $hPlayer.Subtitles.URL = Media.URL(File.SetExt(Dialog.Path, "srt"))
          $hPlayer.Subtitles.Enabled = True
        Endif
        UpdateSubtitle
        FControl.SetTitle(File.Name(Dialog.Path))
        Action("play")
      Endif
      
    Case "subtitle"
      'If $hPlayer.State = Media.Playing Or If $hPlayer.State = Media.Paused Then
      '  $hPlayer.Subtitles.Enabled = Not $hPlayer.Subtitles.Enabled
      '  UpdateSubtitle
      'Else
        Dialog.Title = ("Select a subtitle file")
        If Not Dialog.OpenFile() Then 
          $hPlayer.Subtitles.URL = Media.URL(Dialog.Path)
          $hPlayer.Subtitles.Enabled = True
          UpdateSubtitle
        Endif
      'Endif
      
    Case "play"
      SuspendScreenSaver
      $hOutput.SetWindow(dwgVideo) ', panLeft.X + panLeft.W + 8, panLeft.Y + 8, Me.W - panLeft.X - panLeft.W - 16, Me.H - panLeft.Y - 16)
      Try $hPlayer.Play
      FControl.SetMinOpacity(0)
      dwgVideo.Mouse = Mouse.Blank
      timTime.Start
      
    Case "stop"
      ResumeScreenSaver
      $hPlayer.Stop
      $fLength = 0
      timTime.Stop
      dwgVideo.Mouse = Mouse.Default
      FControl.SetMinOpacity(80)
      
    Case "pause"
      ResumeScreenSaver
      $hPlayer.Pause
      dwgVideo.Mouse = Mouse.Default
      timTime.Stop
      
    Case "fullscreen"
      Me.FullScreen = Not Me.FullScreen
      Me.Maximized = Not Me.FullScreen
      
    Case "volume"
      $hPlayer.Audio.Mute = Not $hPlayer.Audio.Mute
      RefreshVolume
      
    Case "visualisation"
      
      iState = $hPlayer.State
      If iState <> Media.Null And If iState <> Media.Ready Then
        fPos = $hPlayer.Position
        $hPlayer.Stop
        $hPlayer.Close
        timTime.Stop
        'FadeOut
      Endif
      
      Inc $iVisualisation
      If $iVisualisation >= $aVisualisation.Count Then $iVisualisation = 0
      UpdateVisualisation
      
      If iState <> Media.Null And If iState <> Media.Ready Then
        $hPlayer.Pause
        $hPlayer.Position = fPos
        $hPlayer.State = iState
        If iState = Media.Playing Then timTime.Start
        'FadeIn
      Endif
    
    Case "quit"
      Me.Close
    
  End Select

End

Public Sub Button_Click()
  
  Action(Last.Tag)
  
End

' Public Sub Form_Arrange()
' 
'   panToolbar.Move(0, Me.H - panToolbar.H, Me.W, panToolbar.H)
' 
' End

Public Sub timTime_Timer()

  $fPos = $hPlayer.Position
  If $fLength = 0 Then $fLength = $hPlayer.Duration
  FControl.SetInfo(Format(CDate(($fPos + 0.5) / 86400), "hh:nn:ss") & " / " & Format(CDate($fLength / 86400), "hh:nn:ss"))
  
End

Public Sub GetLength() As Float
  
  Return $fLength
  
End

Public Sub GetPos() As Float
  
  Return $fPos
  
End

Public Sub SetPos(fPos As Float)
  
  If $hPlayer.State = Media.Paused Or If $hPlayer.State = Media.Playing Then
    If $fLength Then
      $fPos = fPos * $fLength
      '$hPlayer.Pause
      'FadeOut
      $hPlayer.Position = $fPos
      '$hPlayer.Play
      'FadeIn
    Endif
  Endif
  
End

Public Sub Form_Resize()

  FControl.Move(0, Me.H - FControl.H, Me.W, FControl.H)

End

Public Sub Form_Close()

  ResumeScreenSaver
  CAnimation.Exit

End

Public Sub Button_MouseWheel()
  
  $hPlayer.Audio.Mute = False
  
  If Mouse.Delta > 0 Then
    $hPlayer.Audio.Volume = Min(1, (Sqr($hPlayer.Audio.Volume) + 0.05) ^ 2)
  Else
    $hPlayer.Audio.Volume = Max(0, (Sqr($hPlayer.Audio.Volume) - 0.05) ^ 2)
  Endif
  
  RefreshVolume
  
End

Private Sub RefreshVolume()
  
  Dim sImage As String
  Dim fVolume As Float
  
  With GetButton("volume")
    
    If $hPlayer.Audio.Mute Then
      sImage = "mute"
      .Text = ""
    Else
      fVolume = Sqr($hPlayer.Audio.Volume)
      .Text = Format(fVolume, "0%")
      sImage = "volume-" & Min(3, CInt(fVolume * 4))
    Endif
    
    .Image = Image.Load(sImage & ".png")
    
  End With
  
End

Private Sub UpdateSubtitle()

  GetButton("subtitle").Text = If($hPlayer.Subtitles.Enabled, "ON", "")

End

Private Sub UpdateVisualisation()

  Dim hVis As MediaControl = $aVisualisation[$iVisualisation]

  $hPlayer.Video.Visualisation = hVis
  If hVis Then
    GetButton("visualisation").Text = hVis.Tag
    $hOutput.SetWindow(dwgVideo)
  Else
    GetButton("visualisation").Text = ""
    $hOutput.SetWindow(Null)
  Endif

End

Public Sub dwgVideo_Draw()

  If $hPlayer.State = Media.Null Then
    Draw.Font = Font["+5"]
    Draw.Foreground = Color.Gray
    Draw.RichText(("<h1>MediaPlayer</h1>A media player example based on GStreamer and Gambas.<br><i>Made by Beno√Æt Minisini.</i>"), 0, 0, dwgVideo.Width, dwgVideo.Height, Align.Center)
  Endif

End

Public Sub MediaPlayer_End()
  
  Action("stop")
  
End

Private Sub FadeOut()
  
  Dim I As Integer
  
  $fVolume = $hPlayer.Audio.Volume
  
  Do
    Debug $hPlayer.Audio.Volume 
    $hPlayer.Audio.Volume = Max(0, $hPlayer.Audio.Volume - 0.05)
    If $hPlayer.Audio.Volume = 0 Then Break
    Sleep 0.01
  Loop
  
End

Private Sub FadeIn()
  
  Dim I As Integer
  
  Do
    Debug $hPlayer.Audio.Volume 
    $hPlayer.Audio.Volume = Min($fVolume, $hPlayer.Audio.Volume + 0.05)
    If $hPlayer.Audio.Volume >= $fVolume Then Break
    Sleep 0.01
  Loop
  
End

Private Sub SuspendScreenSaver()
  
  If $bSuspend Then Return
  Desktop.ScreenSaver.Suspend(Me)
  
End

Private Sub ResumeScreenSaver()
  
  If Not $bSuspend Then Return
  Desktop.ScreenSaver.Resume(Me)
  
End
