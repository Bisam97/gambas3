' Gambas class file

'Static Private $aZoom As Float[]

Private $sStatus As String
Private $iZoom As Integer
Private $iHidden As Integer

' Static Public Sub _init()
'   
'   Dim iInd As Integer
'   
'   $aZoom = New Float[17]
'   
'   For iInd = 0 To $aZoom.Max
'     $aZoom[iInd] = 2 ^ (- (iInd - $aZoom.Max / 2) / 4)
'   Next
'   
' End

Public Sub Form_Open()

  CreateView()
  btnZoomNormal_Click
  txtURL.SetFocus
  tabBrowser_Click
  'txtURL.Text = "http://gambas.sourceforge.net"
  'txtURL_Activate

End

Private Sub GetView() As WebView
  
  Return tabBrowser[tabBrowser.Index].Children[0]
  
End

Private Sub IsLastCurrentView() As Boolean
  
  Dim hView As WebView = GetView()
  Return hView = Last
  
End

Public Sub WebView_Link(Url As String)

  If Not IsLastCurrentView() Then Return
  lblStatus.Text = Url

End

Public Sub WebView_Status(Status As String)

  If Not IsLastCurrentView() Then Return
  $sStatus = Status
  lblStatus.Text = Status

End

Public Sub WebView_Load(Progress As Float)

  Dim hIcon As Picture

  If Not IsLastCurrentView() Then Return

  pgbLoad.Value = Progress
  If Progress = 0 Then
    panLoad.Show
    lblStatus.Text = "Loading " & GetView().Url & "..."
    'WebView1.Mouse = Mouse.Wait
  Else If Progress = 1 Then
    panLoad.Hide
    lblStatus.Text = $sStatus
    txtURL.Text = GetView().Url
    hIcon = GetView().Icon
    Me.Icon = hIcon
    'WebView1.Mouse = Mouse.Default
  Endif

End

Public Sub WebView_Error()

  If Not IsLastCurrentView() Then Return

  lblStatus.Text = "Unable to load page"
  pgbLoad.Hide

End

Public Sub btnGo_Click()

  Dim sText As String = txtURL.Text
  
  If Not (sText Like "http://*") Then sText = "http://" & sText

  GetView().Url = sText

End

Public Sub txtURL_Activate()

  btnGo.Value = True

End

Public Sub btnBack_Click()

  GetView().Back

End

Public Sub btnForward_Click()

  GetView().Forward

End

Public Sub btnStop_Click()

  GetView().Stop

End

Public Sub btnReload_Click()

  GetView().Reload

End

Public Sub btnZoomIn_Click()

  GetView().Zoom = Round(GetView().Zoom / 1.25, -2)

End

Public Sub btnZoomOut_Click()

  GetView().Zoom = Round(GetView().Zoom * 1.25, -2)

End

Public Sub btnZoomNormal_Click()

  GetView().Zoom = 1

End

Public Sub WebView_Title()

  If Not IsLastCurrentView() Then Return
  Me.Title = GetView().Title & " - Gambas WebKit"
  tabBrowser[tabBrowser.Index].Text = GetView().Title

End

Public Sub WebView_Icon()

  If Not IsLastCurrentView() Then Return
  Me.Icon = GetView().Icon

End

Public Sub btnClear_Click()

  txtURL.Clear
  txtURL.SetFocus

End

Public Sub WebView_NewWindow(Modal As Boolean)

  CreateView()

  Last.NewView = GetView()
  
End

Private Sub CreateView()
  
  Dim iLast As Integer = tabBrowser.Count - 1
  Dim hView As WebView

  Object.Lock(tabBrowser)
  Inc tabBrowser.Count
  tabBrowser[iLast + 1].Picture = tabBrowser[iLast].Picture
  tabBrowser[iLast + 1].Text = tabBrowser[iLast].Text
  tabBrowser[iLast].Text = ""
  tabBrowser[iLast].Picture = Null
  tabBrowser.Index = iLast
  hView = New WebView(tabBrowser) As "WebView"
  Object.Unlock(tabBrowser)
  tabBrowser_Click
  
End


Public Sub tabBrowser_Click()

  Dim iLast As Integer = tabBrowser.Count - 1
  Dim hView As WebView

  If tabBrowser.Index = iLast Then 
    CreateView()
  Else
    hView = GetView()
    Me.Title = hView.Title & " - Gambas WebKit"
    tabBrowser.Text = hView.Title
    Me.Icon = hView.Icon
    txtURL.Text = hView.Url
    lblStatus.text = ""
  Endif
  
End

Public Sub btnClose_Click()

  GetView().Delete

  Object.Lock(tabBrowser)
  tabBrowser[tabBrowser.Index].Text = "[Deleted]"
  ' tabBrowser[].Visible does not work yet...
  tabBrowser[tabBrowser.Index].Enabled = False
  Object.UnLock(tabBrowser)
  tabBrowser_Click
  Inc $iHidden
  
  ' If (tabBrowser.Count - $iHidden) = 1 Then
  '   tabBrowser.Count = 1
  '   $iHidden = 0
  '   tabBrowser_Click
  ' Endif
  
End
