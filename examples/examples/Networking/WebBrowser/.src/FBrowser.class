' Gambas class file

'Static Private $aZoom As Float[]

Private $sStatus As String
Private $iZoom As Integer
Private $iHidden As Integer

' Static Public Sub _init()
'   
'   Dim iInd As Integer
'   
'   $aZoom = New Float[17]
'   
'   For iInd = 0 To $aZoom.Max
'     $aZoom[iInd] = 2 ^ (- (iInd - $aZoom.Max / 2) / 4)
'   Next
'   
' End

Public Sub Form_Open()

  WebSettings.IconDatabase.Path = File.Dir(File.Dir(Temp$()))

  CreateView()
  btnZoomNormal_Click
  txtURL.SetFocus
  tabBrowser_Click
  'txtURL.Text = "http://gambas.sourceforge.net"
  'txtURL_Activate

End

Private Sub GetView() As WebView
  
  Return tabBrowser[tabBrowser.Index].Children[0]
  
End

Private Sub IsLastCurrentView() As Boolean
  
  Dim hView As WebView = GetView()
  Return hView = Last
  
End

Public Sub WebView_Link(Url As String)

  If Not IsLastCurrentView() Then Return
  lblStatus.Text = Url

End

Public Sub WebView_Status()

  If Not IsLastCurrentView() Then Return
  lblStatus.Text = GetView().Status

End

Public Sub WebView_Progress()

  GetView().Status = "Loading..."
  If Not IsLastCurrentView() Then Return

  lblStatus.Text = GetView().Status
  pgbLoad.Value = GetView().Progress
  panLoad.Show

End

Public Sub WebView_Error()

  GetView().Status = "Unable to load page"
  If Not IsLastCurrentView() Then Return

  lblStatus.Text = GetView().Status
  pgbLoad.Hide

End

Public Sub WebView_Load()

  Dim iInd As Integer

  GetView().Status = ""
  If Not IsLastCurrentView() Then Return

  lblStatus.Text = ""
  txtURL.Text = GetView().Url
  pgbLoad.Hide
  
  ' Debug GetView().Frame
  ' For iInd = 0 To GetView().Frame.Children.Count - 1
  '   Debug "["; iInd; "] "; GetView().Frame.Children[iInd]
  ' Next

End

Public Sub btnGo_Click()

  Dim sText As String = txtURL.Text
  
  If Not (sText Like "http://*") Then sText = "http://" & sText

  GetView().Url = sText

End

Public Sub txtURL_Activate()

  btnGo.Value = True

End

Public Sub btnBack_Click()

  GetView().Back

End

Public Sub btnForward_Click()

  GetView().Forward

End

Public Sub btnStop_Click()

  GetView().Stop

End

Public Sub btnReload_Click()

  GetView().Reload

End

Public Sub btnZoomIn_Click()

  GetView().Zoom = Round(GetView().Zoom * 1.25, -2)

End

Public Sub btnZoomOut_Click()

  GetView().Zoom = Round(GetView().Zoom / 1.25, -2)

End

Public Sub btnZoomNormal_Click()

  GetView().Zoom = 1

End

Public Sub WebView_Title()

  Dim hView As WebView = Last
  tabBrowser[hView.Tag].Text = hView.Title
  If Not IsLastCurrentView() Then Return
  Me.Title = hView.Title & " - Gambas WebKit"

End

Public Sub WebView_Icon()

  Dim hView As WebView = Last
  tabBrowser[hView.Tag].Picture = hView.Icon
  If Not IsLastCurrentView() Then Return
  Me.Icon = hView.Icon

End

Public Sub btnClear_Click()

  txtURL.Clear
  txtURL.SetFocus

End

Public Sub WebView_NewWindow(Modal As Boolean)

  CreateView()

  Last.NewView = GetView()
  
End

Private Sub CreateView()
  
  Dim iLast As Integer = tabBrowser.Count - 1
  Dim hView As WebView

  Object.Lock(tabBrowser)
  Inc tabBrowser.Count
  tabBrowser[iLast + 1].Picture = tabBrowser[iLast].Picture
  tabBrowser[iLast + 1].Text = tabBrowser[iLast].Text
  tabBrowser[iLast].Text = ""
  tabBrowser[iLast].Picture = Null
  tabBrowser.Index = iLast
  hView = New WebView(tabBrowser) As "WebView"
  hView.Tag = tabBrowser.Index
  Print WebSettings.Font[WebSettings.FixedFont]
  WebSettings.Font[WebSettings.FixedFont] = "Inconsolata"
  Object.Unlock(tabBrowser)
  tabBrowser_Click
  
End


Public Sub tabBrowser_Click()

  Dim iLast As Integer = tabBrowser.Count - 1
  Dim hView As WebView

  If tabBrowser.Index = iLast Then 
    CreateView()
  Else
    hView = GetView()
    Me.Title = hView.Title & " - Gambas WebKit"
    tabBrowser.Text = hView.Title
    Me.Icon = hView.Icon
    tabBrowser.Picture = hView.Icon
    txtURL.Text = hView.Url
    lblStatus.Text = hView.Status
    pgbLoad.Value = hView.Progress
    If hView.Progress > 0 And If hView.Progress < 1 Then
      panLoad.Show
    Else
      panLoad.Hide
    Endif
  Endif
  
End

Public Sub btnClose_Click()

  GetView().Delete
  
  Object.Lock(tabBrowser)
  tabBrowser[tabBrowser.Index].Delete
  Object.UnLock(tabBrowser)
  If tabBrowser.Index = (tabBrowser.Count - 1) And If tabBrowser.Index > 0 Then
    tabBrowser.Index = tabBrowser.Index - 1
  Else
    tabBrowser_Click
  Endif
  
  ' If (tabBrowser.Count - $iHidden) = 1 Then
  '   tabBrowser.Count = 1
  '   $iHidden = 0
  '   tabBrowser_Click
  ' Endif
  
End

Public Sub WebView_Auth()
  
  Dim hView As WebView = Last
  
  If Not FAuth.Run(hView.Auth.Url, hView.Auth.Realm) Then
 
    hView.Auth.User = FAuth.User
    hView.Auth.Password = FAuth.Password
    'Debug hView.Auth.Url;; hView.Auth.User;; hView.Auth.Password
    
  Endif 
  
End
