' Gambas class file

'Static Private $aZoom As Float[]

Private $sStatus As String
Private $iZoom As Integer
Private $iHidden As Integer
Private $sLastLink As String

' Static Public Sub _init()
'   
'   Dim iInd As Integer
'   
'   $aZoom = New Float[17]
'   
'   For iInd = 0 To $aZoom.Max
'     $aZoom[iInd] = 2 ^ (- (iInd - $aZoom.Max / 2) / 4)
'   Next
'   
' End

Public Sub Form_Open()

  WebSettings.IconDatabase.Path = File.Dir(File.Dir(Temp$()))
  WebSettings.Font[WebSettings.FixedFont] = "Monospace"
  WebSettings[WebSettings.PluginsEnabled] = True
  WebSettings[WebSettings.JavascriptEnabled] = True
  WebSettings[WebSettings.JavaEnabled] = True

  CreateView()
  btnZoomNormal_Click
  txtURL.SetFocus
  tabBrowser_Click
  'txtURL.Text = "http://gambas.sourceforge.net"
  'txtURL_Activate

End

Private Sub GetView() As WebView
  
  Return tabBrowser[tabBrowser.Index].Children[0]
  
End

Private Sub IsLastCurrentView() As Boolean
  
  Dim hView As WebView = GetView()
  Return hView = Last
  
End

Public Sub WebView_Link(Url As String)

  If Not IsLastCurrentView() Then Return
  $sLastLink = Url
  lblStatus.Text = Url

End

Public Sub WebView_Status()

  If Not IsLastCurrentView() Then Return
  lblStatus.Text = GetView().Status

End

Public Sub WebView_Progress()

  GetView().Status = "Loading..."
  If Not IsLastCurrentView() Then Return

  lblStatus.Text = GetView().Status
  pgbLoad.Value = GetView().Progress
  panLoad.Show

End

Public Sub WebView_Error()

  GetView().Status = "Unable to load page: " & $sLastLink
  If Not IsLastCurrentView() Then Return

  lblStatus.Text = GetView().Status
  pgbLoad.Hide

End

Public Sub WebView_Load()

  Dim iInd As Integer
  Dim hView As WebView = GetView()

  hView.Status = ""
  If Not IsLastCurrentView() Then Return

  lblStatus.Text = ""
  txtURL.Text = hView.Url
  pgbLoad.Hide
  
  tabBrowser[hView.Tag].Picture = hView.Icon
  ' Debug GetView().Frame
  ' For iInd = 0 To GetView().Frame.Children.Count - 1
  '   Debug "["; iInd; "] "; GetView().Frame.Children[iInd]
  ' Next

End

Public Sub btnGo_Click()

  Dim sText As String = txtURL.Text
  
  If Not (sText Like "http://*") Then sText = "http://" & sText

  GetView().Url = sText

End

Public Sub txtURL_Activate()

  btnGo.Value = True

End

Public Sub btnBack_Click()

  GetView().Back

End

Public Sub btnForward_Click()

  GetView().Forward

End

Public Sub btnStop_Click()

  GetView().Stop

End

Public Sub btnReload_Click()

  Dim hView As WebView = GetView()

  hView.Reload

End

Public Sub btnZoomIn_Click()

  GetView().Zoom = Round(GetView().Zoom * 1.25, -2)

End

Public Sub btnZoomOut_Click()

  GetView().Zoom = Round(GetView().Zoom / 1.25, -2)

End

Public Sub btnZoomNormal_Click()

  GetView().Zoom = 1

End

Public Sub WebView_Title()

  Dim hView As WebView = Last
  tabBrowser[hView.Tag].Text = hView.Title
  If Not IsLastCurrentView() Then Return
  Me.Title = hView.Title & " - Gambas WebKit"

End

Public Sub WebView_Icon()

  Dim hView As WebView = Last
  tabBrowser[hView.Tag].Picture = hView.Icon
  If Not IsLastCurrentView() Then Return
  Me.Icon = hView.Icon

End

Public Sub btnClear_Click()

  txtURL.Clear
  txtURL.SetFocus

End

Public Sub WebView_NewWindow(Modal As Boolean)

  CreateView()

  Last.NewView = GetView()
  
End

Private Sub CreateView()
  
  Dim iLast As Integer = tabBrowser.Count - 1
  Dim hView As WebView

  Object.Lock(tabBrowser)
  Inc tabBrowser.Count
  tabBrowser[iLast + 1].Picture = tabBrowser[iLast].Picture
  tabBrowser[iLast + 1].Text = tabBrowser[iLast].Text
  tabBrowser[iLast].Text = ""
  tabBrowser[iLast].Picture = Null
  tabBrowser.Index = iLast
  hView = New WebView(tabBrowser) As "WebView"
  hView.Tag = tabBrowser.Index
  Print WebSettings.Font[WebSettings.FixedFont]
  Object.Unlock(tabBrowser)
  tabBrowser_Click
  
End


Public Sub tabBrowser_Click()

  Dim iLast As Integer = tabBrowser.Count - 1
  Dim hView As WebView

  If tabBrowser.Index = iLast Then 
    CreateView()
  Else
    hView = GetView()
    If hView.Title Then
      Me.Title = hView.Title & " - Gambas WebKit"
    Else
      Me.Title = "Gambas WebKit"
    Endif
    tabBrowser.Text = hView.Title
    Me.Icon = hView.Icon
    tabBrowser.Picture = hView.Icon
    txtURL.Text = hView.Url
    lblStatus.Text = hView.Status
    pgbLoad.Value = hView.Progress
    If hView.Progress > 0 And If hView.Progress < 1 Then
      panLoad.Show
    Else
      panLoad.Hide
    Endif
  Endif
  
End

Public Sub btnDownloadList_Click()

  FDownloadList.Show

End

Public Sub WebView_Auth()
  
  Dim hView As WebView = Last
  
  If Not FAuth.Run(hView.Auth.Url, hView.Auth.Realm) Then
 
    hView.Auth.User = FAuth.User
    hView.Auth.Password = FAuth.Password
    'Debug hView.Auth.Url;; hView.Auth.User;; hView.Auth.Password
    
  Endif 
  
End

Public Sub WebView_Click(Frame As WebFrame)
  
  Debug Frame.Name;; Frame.Url
  
End

Public Sub WebView_NewFrame(Frame As WebFrame)
  
  Debug Frame.Name
  
End

Public Sub WebView_Download(Download As WebDownload)
  
  Dialog.Path = System.User.Home &/ File.Name(Download.Url)
  If Not Dialog.SaveFile() Then 
    Download.Path = Dialog.Path
    FDownloadList.AddDownload(Download)
    FDownloadList.Show
  Endif
  
End

Public Sub WebView_MouseDown()
  
  Dim hView As WebView = Last
  Dim hTest As WebHitTest = hView.HitTest(Mouse.X, Mouse.Y)
  Dim sMsg As String
  
  If hTest.Document Then sMsg &= "DOCUMENT "
  If hTest.Link Then sMsg &= "LINK "
  If hTest.Image Then sMsg &= "IMAGE "
  If hTest.Selected Then sMsg &= "SELECTED "
  If hTest.Editable Then sMsg &= "EDITABLE "
  Debug sMsg; hTest.Url
  
End

Public Sub tabBrowser_Close(Index As Integer)

  Dim hView As WebView
  
  Try hView = tabBrowser[Index].Children[0]
  If Not hView Then Return
  
  hView.Delete
  
  Object.Lock(tabBrowser)
  tabBrowser[Index].Delete
  Object.UnLock(tabBrowser)
  If Index = tabBrowser.Index Then
    If tabBrowser.Index = (tabBrowser.Count - 1) And If tabBrowser.Index > 0 Then
      tabBrowser.Index = tabBrowser.Index - 1
    Else
      tabBrowser_Click
    Endif
  Endif  

End
