' Gambas class file

Private $sLast As String
Private $MX As Integer
Private $MY As Integer

Public Sub Form_Open()

  Me.Font = Font["64"]
  Me.Width = Me.Font.TextWidth("00:00:00") + 32
  Me.Height = Me.Font.Height + 8
  Redraw
End

Public Sub Timer1_Timer()

  Redraw

End

Private Sub Redraw()

  Dim hPict As Picture
  Dim sText As String
  Dim iInd As Integer

  sText = Str(Time)
  If sText = $sLast Then Return

  hPict = New Picture(Me.Width, Me.Height, True)

  Draw.Begin(hPict)
  Draw.Font = Font["64"]
  Draw.LineWidth = 4

  For iInd = 8 To 0 Step -1
    Draw.Foreground = Color.RGB(&h43 - iInd * &h43 / 8, &hC7 - iInd * &hC7 / 8, &hFF - iInd * &HFF / 8)
    Draw.Text(sText, 16 + iInd, iInd)
    Draw.Rect(iInd + 4, iInd + 4, hPict.Width - 14, hPict.Height - 14)
  Next

  Draw.End

  Me.Picture = hPict
'   Me.Mask = True
  $sLast = sText

End

Public Sub Form_KeyPress()

  If Key.Code = Key["Esc"] Then Me.Close

End

Public Sub Form_MouseDown()

  $MX = Mouse.ScreenX - Me.X
  $MY = Mouse.ScreenY - Me.Y

End

Public Sub Form_MouseMove()

  Me.Move(Mouse.ScreenX - $MX, Mouse.ScreenY - $MY)
  
End
