' Gambas class file

Private $sExec As String

Private $aTutorial As String[][] = [
  ["Example1", "Setting Up An OpenGL Window"],
  ["Example2", "Your First Polygon"],
  ["Example3", "Adding Color"],
  ["Example4", "Rotation"],
  ["Example5", "3D Shapes"],
  ["Example6", "Texture Mapping"],
  ["Example7", "Texture Filters, Lighting & Keyboard Control"],
  ["Example8", "Blending"],
  ["Example9", "Moving Bitmaps In 3D Space"],
  ["Example10", "Loading And Moving Through A 3D World"],
  ["Example11", "Flag Effect (Waving Texture)"]]
  

Public Sub Form_Open()

  Dim aTutorial As String[]
  Dim iInd As Integer
  
  For Each aTutorial In $aTutorial
    Inc iInd
    cmbTutorial.Add(CStr(iInd) & " - " & aTutorial[1])
  Next
  cmbTutorial_Click

End

Public Sub btnRun_Click()

  If Not $sExec Then
    Shell "rm -rf /tmp/NeHeTutorial" Wait
    Shell "cp -R " & Shell$(System.Path &/ "share/gambas3/examples/OpenGL/NeHeTutorial") & " /tmp/NeHeTutorial" Wait
    Shell "cd /tmp/NeHeTutorial; gbc3 -agt; gba3" Wait
    $sExec = "/tmp/NeHeTutorial/NeHeTutorial.gambas"
    If Not Exist($sExec) Then
      Message.Error("Unable to compile the NeHeTurotial example inside the <tt>/tmp</tt> directory.")
      $sExec = ""
      Return
    Endif
  Endif

  Exec [$sExec, $aTutorial[cmbTutorial.Index][0], $aTutorial[cmbTutorial.Index][1]]

End

Public Sub btnQuit_Click()

  Me.Close

End

Public Sub cmbTutorial_Click()

  txtCode.Text = File.Load(System.Path &/ "share/gambas3/examples/OpenGL/NeHeTutorial" &/ ".src" &/ $aTutorial[cmbTutorial.Index][0] & ".module")
  txtCode.Pos = 0

End
